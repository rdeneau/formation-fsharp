---
marp: true
html: true
theme: soat
title: 'Formation F‚ôØ 5.0 ‚Ä¢ Pattern matching'
footer: 'Formation F‚ôØ 5.0 ‚Ä¢ Pattern matching'
paginate: true
---

<!-- _class: title invert -->

# Formation F‚ôØ 5.0

## *Pattern matching*

### D√©cembre 2021

---

<!-- _class: speaker invert -->

# About me

## Romain DENEAU

- SOAT depuis 2009
- Senior Developer C‚ôØ F‚ôØ TypeScript
- Passionn√© de Craft
- Auteur sur le blog de SOAT

[![social-network](./themes/soat/pictos/social_twitter.png) DeneauRomain](https://twitter.com/DeneauRomain)

[![social-network](./themes/soat/pictos/social_github.png) rdeneau](https://github.com/rdeneau)

---

<!-- _class: purple agenda invert lead -->

![bg right:30% h:300](./themes/soat/pictos/SOAT_pictos_formation.png)

## Sommaire

- Patterns
- Match expression
- Active patterns

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_architecture.png)

# 1.

## G√©n√©ralit√©s : *Patterns*

---

# Patterns

*Patterns* = r√®gles pour d√©tecter la structure de donn√©es en entr√©e

Utilis√©s abondamment en F‚ôØ
‚Üí Dans *match expression*, *let binding* de valeurs et de param√®tres de fonctions
‚Üí Tr√®s pratiques pour manipuler les types alg√©briques F‚ôØ (tuple, record, union)
‚Üí Composables : supporte plusieurs niveaux d'imbrication
‚Üí Assemblables par ET/OU logiques
‚Üí Supporte les litt√©raux : `1.0`, `"test"`...

---

# Wildcard Pattern

Repr√©sent√© par `_`, seul ou combin√© avec un autre *pattern*

Toujours vrai
‚Üí A placer en dernier dans une *match expression*

‚ö†Ô∏è Toujours chercher en 1er √† traiter exhaustivement/explicitement tous les cas
¬†¬†¬†¬† Quand impossible, utiliser alors le `_`

```fs
match option with
| Some 1 -> ...
| _ -> ...              // ‚ö†Ô∏è Non exhaustif

match option with
| Some 1 -> ...
| Some _ | None -> ...  // üëå \+ exhaustif
```

---

# Constant Pattern

D√©tecte constantes, `null` et litt√©raux de nombre, `char`, `string`, `enum`

```fs
[<Literal>]
let Three = 3   // Constante

let is123 num = // int -> bool
    match num with
    | 1 | 2 | Three -> true
    | _ -> false
```

‚òù **Notes :**

- Le pattern de `Three` est aussi class√© en tant que *Identifier Pattern* üìç
- Pour le matching de `null`, on parle aussi de *Null Pattern*

---

# Identifier Pattern

D√©tecte les *cases* d'un type union ainsi que leur √©ventuel contenu

```fs
type PersonName =
    | FirstOnly of string
    | LastOnly  of string
    | FirstLast of string * string

let classify personName =
    match personName with
    | FirstOnly _ -> "First name only"
    | LastOnly  _ -> "Last name only"
    | FirstLast _ -> "First and last names"
```

---

# Variable Pattern

Assigne la valeur d√©tect√©e √† une "variable" pour l'utiliser ensuite

Exemple : variables `firstName` et `lastName` ci-dessous

```fs
type PersonName =
    | FirstOnly of string
    | LastOnly  of string
    | FirstLast of string * string

let confirm personName =
    match personName with
    | FirstOnly (firstName) -> printf "May I call you %s?" firstName
    | LastOnly  (lastName) -> printf "Are you Mr. or Ms. %s?" lastName
    | FirstLast (firstName, lastName) -> printf "Are you %s %s?" firstName lastName
```

---

# Variable Pattern (2)

‚ö†Ô∏è On ne peut pas lier √† plusieurs reprises vers la m√™me variable

```fs
let elementsAreEqualKo tuple =
    match tuple with
    | (x,x) -> true  // üí• Error FS0038: 'x' est li√© √† deux reprises dans ce mod√®le
    | (_,_) -> false
```

Solutions : utiliser 2 variables puis v√©rifier l'√©galit√©

```fs
// 1. Guard clauseüìç
let elementsAreEqualOk = function
    | (x,y) when x = y -> true
    | (_,_) -> false

// 2. D√©construction
let elementsAreEqualOk' (x, y) = x = y
```

---

## Champs nomm√©s de *case* d'union

Plusieurs possibilit√©s :
‚ë† Pattern "anonyme" du tuple complet
‚ë° Pattern d'un seul champ par son nom ‚Üí `Field = value`
‚ë¢ Pattern de plusieurs champs par leur nom ‚Üí `F1 = v1; F2 = v2`

```fs
type Shape =
    | Rectangle of Height: int * Width: int
    | Circle of Radius: int

let describe shape =
    match shape with
    | Rectangle (0, _)                                              // ‚ë†
    | Rectangle (Height = 0)            -> "Flat rectangle"         // ‚ë°
    | Rectangle (Width = w; Height = h) -> $"Rectangle {w} √ó {h}"   // ‚ë¢
    | Circle radius                     -> $"Circle ‚àÖ {2*radius}"
```

---

# Alias Pattern

`as` permet de nommer un √©l√©ment dont le contenu est d√©construit

```fs
let (x, y) as coordinate = (1, 2)
printfn "%i %i %A" x y coordinate  // 1 2 (1, 2)
```

üí° Marche aussi dans les fonctions :

```fs
type Person = { Name: string; Age: int }

let acceptMajorOnly ({ Age = age } as person) =
    if age < 18 then None else Some person
```

---

# OR et AND Patterns

Permettent de combiner deux patterns *(nomm√©s `P1` et `P2` ci-apr√®s)*
‚Ä¢ `P1 | P2` ‚Üí P1 ou P2. Ex : `Rectangle (0, _) | Rectangle (_, 0)`
‚Ä¢ `P1 & P2` ‚Üí P1 et P2. Utilis√© surtout avec *active patterns* üìç

üí° Utiliser la m√™me variable :

```fs
type Upload = { Filename: string; Title: string option }

let titleOrFile ({ Title = Some name } | { Filename = name }) = name

titleOrFile { Filename = "Report.docx"; Title = None }            // Report.docx
titleOrFile { Filename = "Report.docx"; Title = Some "Report+" }  // "Report+"
```

---

# Parenthesized Pattern

Usage des parenth√®ses `()` pour grouper des patterns, pour g√©rer la pr√©c√©dence

```fs
type Shape = Circle of Radius: int | Square of Side: int

let countFlatShapes shapes =
    let rec loop rest count =
        match rest with
        | (Square (Side = 0) | (Circle (Radius = 0))) :: tail -> loop tail (count + 1) // ‚ë†
        | _ :: tail -> loop tail count
        | [] -> count
    loop shapes 0
```

‚òù **Note :** la ligne ‚ë† ne compilerait sans faire `() :: tail` 

---

# Parenthesized Pattern (2)

‚ö†Ô∏è Les parenth√®ses compliquent la lecture
üí° Essayer de s'en passer quand c'est possible

```fs
let countFlatShapes shapes =
    let rec loop rest count =
        match rest with
        | Circle (Radius = 0) :: tail
        | Square (Side = 0) :: tail
          -> loop tail (count + 1)
        // [...]
```

---

# Construction Patterns

Reprennent syntaxe de construction d'un type pour le d√©construire

- Cons et List Patterns
- Array Pattern
- Tuple Pattern
- Record Pattern

---

# Cons et List Patterns

‚âÉ Inverses de 2 types de construction d'une liste, avec m√™me syntaxe

*Cons Pattern* : `head :: tail` ‚Üí d√©compose une liste *(avec >= 1 √©l√©ment)* en :
‚Ä¢ *Head* : 1er √©l√©ment
‚Ä¢ *Tail* : autre liste avec le reste des √©l√©ments - peut √™tre vide

*List Pattern* : `[items]` ‚Üí d√©compose une liste en 0..N √©l√©ments
‚Ä¢ `[]` : liste vide
‚Ä¢ `[x]` : liste avec 1 √©l√©ment mis dans la variable `x`
‚Ä¢ `[x; y]` : liste avec 2 √©l√©ments mis dans les variables `x` et `y`
‚Ä¢ `[_; _]` : liste avec 2 √©l√©ments ignor√©s

üí° `x :: []` ‚â° `[x]`, `x :: y :: []` ‚â° `[x; y]`...

---

# Cons et List Patterns (2)

La *match expression* par d√©faut combine les 2 patterns :
‚Üí Une liste est soit vide `[]`, soit compos√©e d'un item et du reste : `head :: tail`

Les fonctions r√©cursives parcourant une liste utilise le pattern `[]` pour stopper la r√©cursion :

```fs
let rec printList l =
    match l with
    | head :: tail ->
        printf "%d " head
        printList tail     // R√©cursion sur le reste
    | [] -> printfn ""     // Fin de r√©cursion : liste parcourue enti√®rement
```

---

# Array Pattern

Syntaxe: `[| items |]` pour 0..N items entre `;`

```fs
let length vector =
    match vector with
    | [| x |] -> x
    | [| x; y |] -> sqrt (x*x + y*y)
    | [| x; y; z |] -> sqrt (x*x + y*y + z*z)
    | _ -> invalidArg (nameof vector) $"Vector with more than 4 dimensions not supported"
```

‚òù Il n'existe pas de pattern pour les s√©quences, vu qu'elles sont *"lazy"*.

---

# Tuple Pattern

Syntaxe : `items` ou `(items)` pour 2..N items entre `,`

üí° Pratique pour pattern matcher plusieurs valeurs en m√™me temps

```fs
type Color = Red | Blue
type Style = Background | Text

let css color style =
    match color, style with
    | Red, Background -> "background-color: red"
    | Red, Text -> "color: red"
    | Blue, Background -> "background-color: blue"
    | Blue, Text -> "color: blue"
```

---

# Record Pattern

Syntaxe : `{ Fields }` pour 1..N `Field = variable` entre `;`
‚Üí Pas oblig√© de sp√©cifier tous les champs du Record
‚Üí En cas d'ambigu√Øt√©, qualifier le champ : `Record.Field`

üí° Marche aussi pour les param√®tres d'une fonction :

```fs
type Person = { Name: string; Age: int }

let displayMajority { Age = age; Name = name } =
    if age >= 18
    then printfn "%s is major" name
    else printfn "%s is minor" name

let john = { Name = "John"; Age = 25 }
displayMajority john // John is major
```

---

# Record Pattern (2)

‚ö†Ô∏è **Rappel :** il n'y a pas de pattern pour les *Records* anonymes !

```fs
type Person = { Name: string; Age: int }

let john = { Name = "John"; Age = 25 }
let { Name = name } = john  // üëå val name : string = "John"

let john' = {| john with Civility = "Mister" |}
let {| Name = name' |} = john'  // üí•
```

---

# Type Test Pattern

Syntaxe : `?: Subtype`

Usage : avec une hi√©rarchie de types

```fs
open System.Windows.Forms

let RegisterControl (control: Control) =
    match control with
    | :? Button as button -> button.Text <- "Registered."
    | :? CheckBox as checkbox -> checkbox.Text <- "Registered."
    | :? Windows -> invalidArg (nameof control) "Window cannot be registered"
    | _ -> ()
```

---

# Type Test Pattern - Bloc `try`/`with`

On rencontre fr√©quemment ce pattern dans les blocs `try`/`with` :

```fs
try
    printfn "Difference: %i" (42 / 0)
with
| :? DivideByZeroException as x -> 
    printfn "Fail! %s" x.Message
| :? TimeoutException -> 
    printfn "Fail! Took too long"
```

---

# Type Test Pattern - Boxing

Le *Type Test Pattern* ne marche qu'avec des types r√©f√©rences.
‚Üí Pour un type valeur ou inconnu, il faut le convertir en objet *(a.k.a boxing)*

```fs
let isIntKo = function :? int -> true | _ -> false
// üí• Error FS0008: test de type au moment de l'ex√©cution du type 'a en int...

let isInt x =
    match box x with
    | :? int -> true
    | _ -> false
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_etudeCas.png)

# 2.

## *Match Expression*

---

# Match expression

Similaire √† une expression `switch` en C‚ôØ 8.0 en \+ puissant gr√¢ce aux patterns

Syntaxe :

```fs
match test-expression with
| pattern1 [ when condition ] -> result-expression1
| pattern2 [ when condition ] -> result-expression2
| ...
```

Renvoie le r√©sultat de la 1√®re branche dont le pattern "match" `test-expression`

‚òù **Note :** toutes les branches doivent renvoyer le m√™me type !

---

# Match expression - Exhaustivit√©

Un `switch` doit toujours d√©finir un cas par d√©faut *(cf. pattern *wildcard* `_`)*
Sinon : warning √† la compilation, risque de üí• `MatchFailureException` au runtime

Pas n√©cessaire dans une *match expression* si les branches couvrent tous les cas
car le compilateur v√©rifie leur exhaustivit√© et les branches "mortes"

```fs
let fn x =
    match x with
    | Some true  -> "ok"
    | Some false -> "ko"
    | None       -> ""
    | _          -> "?"  // ‚ö†Ô∏è Warning FS0026: Cette r√®gle n'aura aucune correspondance
```

---

# Match expression - Exhaustivit√© (2)

‚òù **Conseil :** \+ les branches sont exhaustives, \+ le code est explicite et s√ªr

Exemple : matcher tous les cases d'un type union permet de g√©rer
l'ajout d'un case par un warning √† la compilation :
`Warning FS0025: Crit√®res sp√©ciaux incomplets dans cette expression`

- D√©tection d'un ajout accidentel
- Identification du code √† changer pour g√©rer le nouveau case

---

# Match expression - Guard

Syntaxe : `pattern1 when condition`
Usage : raffiner un pattern, via contraintes sur des variables

```fs
let classifyBetween low top value =
    match value with
    | x when x < low -> "Inf"  // üí° Alternative : `_ when value < low`
    | x when x = low -> "Low"
    | x when x = top -> "Top"
    | x when x > top -> "Sup"
    | _ -> "Between"

let test1 = 1 |> classifyBetween 1 5  // "Low"
let test2 = 6 |> classifyBetween 1 5  // "Sup"
```

üí° La *guard* n'est √©valu√©e que si le pattern est satisfait.

---

# Match expression - Guard et Pattern OR

Le pattern OR a une *precedence/priorit√©* plus √©lev√©e que la *guard* :

```fs
type Parity = Even of int | Odd of int

let parityOf value =
    if value % 2 = 0 then Even value else Odd value

let hasSquare square value =
    match parityOf square, parityOf value with
    | Even x2, Even x
    | Odd  x2, Odd  x
        when x2 = x*x -> true  // üëà Porte sur les 2 patterns pr√©c√©dents
    | _ -> false

let test1 = 2 |> hasSquare 4  // true
let test2 = 3 |> hasSquare 9  // true
```

---

# Match function

Syntaxe :

```fs
function
| pattern1 [ when condition ] -> result-expression1
| pattern2 [ when condition ] -> result-expression2
| ...
```

Equivalent √† une lambda prenant un param√®tre implicite qui est "match√©" :

```fs
fun value ->
    match value with
    | pattern1 [ when condition ] -> result-expression1
    | pattern2 [ when condition ] -> result-expression2
    | ...
```

---

# Match function - Int√©r√™ts

1. Dans pipeline

```fs
value
|> is123
|> function
    | true  -> "ok"
    | false -> "ko"
```

2. √âcriture \+ succincte d'une fonction

```fs
// ‚ö†Ô∏è Param√®tre implicite => peut rendre le code \+ difficile √† comprendre !
let is123 = function
    | 1 | 2 | 3 -> true
    | _ -> false
```

---

# Match function - Limites

‚ö†Ô∏è Param√®tre implicite => peut rendre le code \+ difficile √† comprendre !

Exemple : fonction d√©clar√©e avec d'autres param√®tres eux explicites
‚Üí On peut se tromper sur le nombre de param√®tres et leur ordre :

```fs
let classifyBetween low high = function  // üëà 3 param√®tres : low, high + 1 implicite
    | x when x < low  -> "Inf"
    | x when x = low  -> "Low"
    | x when x = high -> "High"
    | x when x > high -> "Sup"
    | _ -> "Between"

let test1 = 1 |> classifyBetween 1 5  // "Low"
let test2 = 6 |> classifyBetween 1 5  // "Sup"
```

---

<!-- _footer: '' -->

# Fonction `fold`

Fonction associ√©e √† un type union et masquant la logique de *matching*
Prend N+1 param√®tres pour un type union avec N *cases* `CaseI of 'DataI`
‚Üí N fonctions `'DataI -> 'T` (1 / *case*), avec `'T` le type de retour de `fold`
‚Üí En dernier, l'instance du type union √† matcher

```fs
type [<Measure>] C
type [<Measure>] F

type Temperature =
    | Celsius     of float<C>
    | Fahrenheint of float<F>

module Temperature =
    let fold mapCelsius mapFahrenheint temperature : 'T =
        match temperature with
        | Celsius x     -> mapCelsius x      // mapCelsius    : float<C> -> 'T
        | Fahrenheint x -> mapFahrenheint x  // mapFahrenheint: float<F> -> 'T
```

---

# Fonction `fold` : utilisation

```fs
module Temperature =
    // ...
    let [<Literal>] FactorC2F = 1.8<F/C>
    let [<Literal>] DeltaC2F = 32.0<F>

    let celsiusToFahrenheint x = (x * FactorC2F) + DeltaC2F  // float<C> -> float<F>
    let fahrenheintToCelsius x = (x - DeltaC2F) / FactorC2F  // float<F> -> float<C>

    let toggleUnit temperature =
        temperature |> fold
            (celsiusToFahrenheint >> Fahrenheint)
            (fahrenheintToCelsius >> Celsius)

let t1 = Celsius 100.0<C>
let t2 = t1 |> Temperature.toggleUnit  // Fahrenheint 212.0
```

---

# Fonction `fold` : int√©r√™t

`fold` masque les d√©tails d'impl√©mentation du type

Par exemple, on peut ajouter un *case* `Kelvin` et n'impacter que `fold`,
pas les fonctions qui l'appellent comme `toggleUnit` :

```fs
type [<Measure>] C
type [<Measure>] F
type [<Measure>] K  // üåü

type Temperature =
    | Celsius     of float<C>
    | Fahrenheint of float<F>
    | Kelvin      of float<K>  // üåü

// ...
```

---

# Fonction `fold` : int√©r√™t (2)

```fs
// ...
module Temperature =
    let fold mapCelsius mapFahrenheint temperature : 'T =
        match temperature with
        | Celsius x     -> mapCelsius x      // mapCelsius: float<C> -> 'T
        | Fahrenheint x -> mapFahrenheint x  // mapFahrenheint: float<F> -> 'T
        | Kelvin x      -> mapCelsius (x * 1.0<C/K> + 273.15<C>)  // üåü

Kelvin 273.15<K>
|> Temperature.toggleUnit
|> Temperature.toggleUnit
// Celsius 0.0<C>
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_alEchelle.png)

# 3.

## *Active Patterns*

---

# Limitations du *Pattern Matching*

Nombre limit√© de patterns

Impossibilit√© de factoriser l'action de patterns avec leur propre guard
‚Üí `Pattern1 when Guard1 | Pattern2 when Guard2 -> do` üí•
‚Üí `Pattern1 when Guard1 -> do | Pattern2 when Guard2 -> do` üòï

Patterns ne sont pas des citoyens de 1√®re classe
*Ex : une fonction ne peut pas renvoyer un pattern*
‚Üí Juste une sorte de sucre syntaxique

Patterns interagissent mal avec un style OOP

---

# Origine des *Active Patterns*

> üîó [*Extensible pattern matching via a lightweight language extension*](https://www.microsoft.com/en-us/research/publication/extensible-pattern-matching-via-a-lightweight-language-extension/)
> ‚ÑπÔ∏è Publication de 2007 de Don Syme, Gregory Neverov, James Margetson

Int√©gr√© √† F‚ôØ 2.0 (2010)

üí° **Id√©es**

- Permettre le *pattern matching* sur d'autres structures de donn√©es
- Faire de ces nouveaux patterns des citoyens de 1√®re classe

---

# *Active Patterns* - Syntaxe

Syntaxe g√©n√©rale : `let (|Cases|) [arguments] valueToMatch = expression`

1. **Fonction** avec un nom sp√©cial d√©fini dans une "banane" `(|...|)`
2. Ensemble de 1..N **cases** o√π ranger `valueToMatch`

üí° Sorte de fonction *factory* d'un **type union** "anonyme", d√©fini *inline*

---

# *Active Patterns* - Types

Il existe 4 types d'active patterns :

1. Pattern total simple
2. Pattern total multiple
3. Pattern partiel
4. Pattern param√©tr√©

üí° *Partiel* et *total* indique la faisabilit√© du ¬´ rangement dans le(s) case(s) ¬ª
¬†¬†¬†¬†¬†de la valeur en entr√©e
¬†¬†¬†¬†¬†‚Üí **Partiel** : il n'existe pas toujours une case correspondante
¬†¬†¬†¬†¬†‚Üí **Total** : il existe forc√©ment une case correspondante ‚Üí pattern exhaustif

---

# Active pattern total simple

*A.k.a Single-case Total Pattern*

Syntaxe : `let (|Case|) [...parameters] value = Case [data]`
Usage : d√©construction en ligne

```fs
// Avec param√®tre => pas tr√®s lisible üòï
let (|Default|) = Option.defaultValue  // 'T -> 'T option -> 'T

let (Default "unknown" name1) = Some "John"  // name1 = "John"
let (Default "unknown" name2) = None         // name2 = "unknown"

// Sans param√®tre => mieux üëå
let (|ValueOrUnknown|) = Option.defaultValue "unknown"  // 'T option -> 'T

let (ValueOrUnknown name1) = Some "John"  // name1 = "John"
let (ValueOrUnknown name2) = None         // name2 = "unknown"
```

---

# Active pattern total simple (2)

Autre exemple : extraction de la forme polaire d'un nombre complexe

```fs
open System.Numerics

let (|Polar|) (x : Complex) =
    Polar (x.Magnitude, x.Phase)

let multiply (Polar (m1, p1)) (Polar (m2, p2)) =  // Complex -> Complex -> Complex
    Complex(m1 + m2, p1 + p2)
```

Sans l'active pattern, c'est un autre style mais de lisibilit√© √©quivalente :

```fs
let multiply x y =
    Complex (x.Magnitude + y.Magnitude, x.Phase + y.Phase)
```

---

<!-- _footer: '' -->

# Active pattern total multiple

*A.k.a Multiple-case Total Pattern*

Syntaxe : `let (|Case1|...|CaseN|) value = CaseI [dataI]`
‚òù Pas de param√®tre possible‚ùó

```fs
// R√©-√©criture d'un exemple pr√©c√©dent

// ‚ùå type Parity = Even of int | Odd of int
// ‚ùå let parityOf value = if value % 2 = 0 then Even value else Odd value

let (|Even|Odd|) x =  // int -> Choice<int, int>
    if x % 2 = 0 then Even x else Odd x

let hasSquare square value =
    // ‚ùå match parityOf square, parityOf value with
    match square, value with
    | Even x2, Even x | Odd x2, Odd x when x2 = x*x -> true
    | _ -> false
```

---

<!-- _footer: '' -->

# Active pattern partiel

Syntaxe : `let (|Case|_|) value = Some Case | Some data | None`
‚Üí Renvoie type `'T option` si *Case* comprend des donn√©es, sinon `unit option`
‚Üí Pattern matching est non exhaustif ‚Üí il faut un cas par d√©faut

```fs
let (|Integer|_|) (x: string) = // (x: string) -> int option
    match System.Int32.TryParse x with
    | true, i -> Some i
    | false, _ -> None

let (|Float|_|) (x: string) = // (x: string) -> float option
    match System.Double.TryParse x with
    | true, f -> Some f
    | false, _ -> None

let detectNumber = function
    | Integer i -> $"Integer {i}"   // detectNumber "10"
    | Float f -> $"Float {f}"       // detectNumber "1,1" = "Float 1,1" (en France)
    | s -> $"NaN {s}"               // detectNumber "abc" = "NaN abc"
```

---

# Active pattern partiel param√©tr√©

Syntaxe : `let (|Case|_|) ...arguments value = Some Case | Some data | None`

Exemple 1 : ann√©e bissextile = multiple de 4 mais pas 100 sauf 400

```fs
let (|DivisibleBy|_|) factor x =  // (factor: int) -> (x: int) -> unit option
    match x % factor with
    | 0 -> Some DivisibleBy
    | _ -> None

let isLeapYear year =  // (year: int) -> bool
    match year with
    | DivisibleBy 400 -> true
    | DivisibleBy 100 -> false
    | DivisibleBy   4 -> true
    | _               -> false
```

---

# Active pattern partiel param√©tr√© (2)

Exemple 2 : Expression r√©guli√®re

```fs
let (|Regexp|_|) pattern value =  // string -> string -> string list option
    let m = System.Text.RegularExpressions.Regex.Match(value, pattern)
    if not m.Success || m.Groups.Count < 1 then
        None
    else
        [ for g in m.Groups -> g.Value ]
        |> List.tail // drop "root" match
        |> Some
```

---

# Active pattern partiel param√©tr√© (3)

Exemple : Couleur hexad√©cimale

```fs
let hexToInt hex =  // string -> int // E.g. "FF" -> 255
    System.Int32.Parse(hex, System.Globalization.NumberStyles.HexNumber)

let (|HexaColor|_|) = function  // string -> (int * int * int) option
    // üí° Utilise l'active pattern pr√©c√©dent
    // üí° La Regex recherche 3 groupes de 2 chars √©tant un chiffre ou une lettre A..F
    | Regexp "#([0-9A-F]{2})([0-9A-F]{2})([0-9A-F]{2})" [ r; g; b ] ->
        Some <| HexaColor ((hexToInt r), (hexToInt g), (hexToInt b))
    | _ -> None

match "#0099FF" with
| HexaColor (r, g, b) -> $"RGB: {r}, {g}, {b}"
| otherwise -> $"'{otherwise}' is not a hex-color"
// "RGB: 0, 153, 255"
```

---

# R√©cap‚Äô des types d‚Äôactive patterns

| Type           | Syntaxe                    | Signature                            |
|----------------|----------------------------|--------------------------------------|
| Total multiple | `let (ÔΩúCase1ÔΩú‚Ä¶ÔΩúCaseNÔΩú) x`  | `'T -> Choice<'U1, ‚Ä¶, 'Un>`          |
| Total simple   | `let (ÔΩúCaseÔΩú) x`           | `'T -> 'U`                           |
| Partiel simple | `let (ÔΩúCaseÔΩú_ÔΩú) x`         | `'T -> 'U option`                    |
| ... param√©tr√©  | `let (ÔΩúCaseÔΩú_ÔΩú) p1 ‚Ä¶ pN x` | `'P1 -> ‚Ä¶ -> 'Pn -> 'T -> 'U option` |

---

# Comprendre un active pattern

> Comprendre comment utiliser un active pattern...
> peut s'av√©rer un vrai **jonglage intellectuel** !

üëâ Explications en utilisant les exemples pr√©c√©dents

---

# Comprendre un active pattern total

- Active pattern total ‚âÉ Fonction *factory* d'un type union "anonyme"
- Usage : idem pattern matching d'un type union normal

```fs
// Single-case
let (|Cartesian|) (x: Complex) = Cartesian (x.Real, x.Imaginary)

let Cartesian (r, i) = Complex (1.0, 2.0)  // r = 1.0, i = 2.0

// Double-case
let (|Even|Odd|) x = if x % 2 = 0 then Even else Odd

let parityOf = function  // int -> string
    | Even -> "Pair"
    | Odd  -> "Impair"
```

---

# Comprendre un active pattern partiel

‚òù Bien distinguer les √©ventuels param√®tres des √©ventuelles donn√©es

Examiner la signature de l'active pattern : `[...params ->] value -> 'U option`
‚Ä¢ Les 1..N-1 param√®tres = param√®tres de l'active pattern
‚Ä¢ Son retour : `'U option` ‚Üí donn√©es de type `'U` ; si `'U` = `unit` ‚Üí pas de donn√©e

√Ä l'usage : `match value with Case [params] [data]`
‚Ä¢ `Case params` ‚âÉ **application partielle**, donnant active pattern sans param√®tre
‚Ä¢ `CaseWithParams data` ‚âÉ d√©construction d'un case de type union

---

# Comprendre un active pattern partiel (2)

1. `let (|Integer|_|) (s: string) : int option`
   ‚Üí Usage `match s with Integer i`, avec `i: int` donn√©e en sortie

2. `let (|DivisibleBy|_|) (factor: int) (x: int) : unit option`
   ‚Üí Usage `match year with DivisibleBy 400`, avec `400` le param√®tre `factor`

3. `let (|Regexp|_|) (pattern: string) (value: string) : string list option`
   ‚Üí Usage `match s with Regexp "#([0-9...)" [ r; g; b ]`
   ‚Üí Avec `"#([0-9...)"` le param√®tre `pattern`
   ‚Üí Et `[ r; g; b ]` la liste en sortie d√©compos√©e en 3 cha√Ænes

---

# Exercice : fizz buzz avec active pattern

R√©-√©crire ce fizz buzz en utilisant un active pattern `DivisibleBy`

```fs
let isDivisibleBy factor number =
    number % factor = 0

let fizzBuzz = function
    | i when i |> isDivisibleBy 15 -> "FizzBuzz"
    | i when i |> isDivisibleBy  3 -> "Fizz"
    | i when i |> isDivisibleBy  5 -> "Buzz"
    | other -> string other

[1..15] |> List.map fizzBuzz
// ["1"; "2"; "Fizz"; "4"; "Buzz"; "Fizz";
//  "7"; "8"; "Fizz"; "Buzz"; "11";
//  "Fizz"; "13"; "14"; "FizzBuzz"]
```

---

<!-- _footer: '' -->

# Fizz buzz avec active pattern : solution

```fs
let isDivisibleBy factor number =
    number % factor = 0

let (|DivisibleBy|_|) factor number =
    if number |> isDivisibleBy factor
    then Some DivisibleBy // üí° Ou `Some ()`
    else None

let fizzBuzz = function
    | DivisibleBy 3 &
      DivisibleBy 5 -> "FizzBuzz"  // üí° Ou `DivisibleBy 15`
    | DivisibleBy 3 -> "Fizz"
    | DivisibleBy 5 -> "Buzz"
    | other -> string other

[1..15] |> List.map fizzBuzz
// ["1"; "2"; "Fizz"; "4"; "Buzz"; "Fizz";
//  "7"; "8"; "Fizz"; "Buzz"; "11";
//  "Fizz"; "13"; "14"; "FizzBuzz"]
```

---

<!-- _footer: '' -->

# Fizz buzz avec active pattern : alternative

```fs
let isDivisibleBy factor number =
    number % factor = 0

let boolToOption b =
    if b then Some () else None

let (|Fizz|_|) number = number |> isDivisibleBy 3 |> boolToOption
let (|Buzz|_|) number = number |> isDivisibleBy 5 |> boolToOption

let fizzBuzz = function
    | Fizz & Buzz -> "FizzBuzz"
    | Fizz -> "Fizz"
    | Buzz -> "Buzz"
    | other -> string other
```

‚Üí Les 2 solutions se valent. C'est une question de style / de go√ªt personnel.m

---

# Cas d'utilisation des actives patterns

1. Factoriser une guard *(cf. exercice pr√©c√©dent du fizz buzz)*
2. Wrapper une m√©thode de la BCL *(cf. `(|Regexp|_|)` et ci-dessous)*
3. Am√©liorer l'expressivit√©, aider √† comprendre la logique *(cf. apr√®s)*

```fs
let (|ParsedInt|UnparsableInt|) (input: string) =
    match input with
    | _ when fst (System.Int32.TryParse input) -> ParsedInt(int input)
    | _ -> UnparsableInt

let addOneOrZero = function
    | ParsedInt i -> i + 1
    | UnparsableInt -> 0

let v1 = addOneOrZero "1"  // 2
let v2 = addOneOrZero "a"  // 0
```

---

# Expressivit√© gr√¢ce aux actives patterns

```fs
type Movie = { Title: string; Director: string; Year: int; Studio: string }

module Movie =
    let private boolToOption b =
        if b then Some () else None

    let (|Director|_|) director movie =
        movie.Director = director |> boolToOption

    let (|Studio|_|) studio movie =
        movie.Studio = studio |> boolToOption

    let private matchYear comparator year movie =
        (comparator movie.Year year) |> boolToOption

    let (|After|_|) = matchYear (>)
    let (|Before|_|) = matchYear (<)
    let (|In|_|) = matchYear (=)
```

---

# Expressivit√© gr√¢ce aux actives patterns (2)

```fs
open Movie

let ``Is anime rated 10/10`` = function
    | ((After 2001 & Before 2007) | In 2014) & Studio "Bones"
    | Director "Hayao Miyazaki" -> true
    | _ -> false
```

---

<!-- _footer: '' -->

# Active pattern : citoyen de 1√®re classe

Un active pattern ‚âÉ fonction avec des m√©tadonn√©es
‚Üí Citoyen de 1√®re classe :

```fs
// 1. Renvoyer un active pattern depuis une fonction
let (|Hayao_Miyazaki|_|) movie =
    (|Director|_|) "Hayao Miyazaki" movie

// 2. Prendre un active pattern en param√®tre -- Un peu tricky 
let firstItems (|Ok|_|) list =
    let rec loop values = function
        | Ok (item, rest) -> loop (item :: values) rest
        | _ -> List.rev values
    loop [] list

let (|Even|_|) = function
    | item :: rest when (item % 2) = 0 -> Some (item, rest) | _ -> None

let test = [0; 2; 4; 5; 6] |> firstItems (|Even|_|)  // [0; 2; 4]
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_diplome.png)

# 4.

## Le¬†¬†¬† R√©cap‚Äô

---

# R√©cap‚Äô - Pattern matching

- Brique fondamentale de F‚ôØ
- Combine "comparaison de structure de donn√©es" et "d√©construction"
- S'utilisent presque partout :
  - `match expression` et bloc `function`
  - bloc `try/with`
  - `let binding`, y.c. param√®tre de fonction
- Peut s'abstraire en fonction `fold` associ√©e √† un type union

---

# R√©cap‚Äô - Patterns

| Pattern                            | Exemple                           |
|------------------------------------|-----------------------------------|
| Constant ‚Ä¢ Identifier ‚Ä¢ Wilcard    | `1`, `Color.Red` ‚Ä¢ `Some 1` ‚Ä¢ `_` |
| *Collection* : Cons ‚Ä¢ List ‚Ä¢ Array | `head :: tail` ‚Ä¢ `[1; 2]`         |
| *Product type* : Record ‚Ä¢ Tuple    | `{ A = a }` ‚Ä¢ `a, b`              |
| Type Test                          | `:? Subtype`                      |
| *Logique* : OR, AND                | `1 \| 2`, `P1 & P2`               |
| Variables ‚Ä¢ Alias                  | `head :: _` ‚Ä¢ `(0, 0) as origin`  |

\+ Les guards `when` dans les match expressions

---

# R√©cap‚Äô - Active Patterns

- Extension du pattern matching
- Bas√©s sur fonction + metadata ‚Üí Citoyens de 1√®re classe
- 4 types : total simple/multiple, partiel (simple), param√©tr√©
- Un peu tricky √† comprendre mais on s'habitue vite
- S'utilisent pour :
  - Ajouter de la s√©mantique sans recourir aux types union
  - Simplifier / factoriser des guards
  - Wrapper des m√©thodes de la BCL
  - Extraire un ensemble de donn√©es d'un objet
  - ...

---

# Compl√©ments

üìú Match expressions
https://fsharpforfunandprofit.com/posts/match-expression/

üìú Domain modelling et pattern matching
https://fsharpforfunandprofit.com/posts/roman-numerals/

üìú Recursive types and folds *(6 articles)*
https://fsharpforfunandprofit.com/series/recursive-types-and-folds/

üìπ A Deep Dive into Active Patterns
https://www.youtube.com/watch?v=Q5KO-UDx5eA
https://github.com/pblasucci/DeepDiveAP

---

# Exercices

Les exercices suivants sur https://exercism.org/tracks/fsharp 
peuvent se r√©soudre avec des active patterns :

- Collatz Conjecture *(easy)*
- Darts *(easy)*
- Queen Attack *(medium)*
- Robot Name *(medium)*

---

<!-- _class: end invert lead-->

# Merci üôè
