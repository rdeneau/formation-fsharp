---
marp: true
html: true
theme: soat
title: 'Formation Fâ™¯ 5.0 â€¢ Les fonctions'
footer: 'Formation Fâ™¯ 5.0 â€¢ Les fonctions'
paginate: true
---

<!-- _class: title invert -->

# Formation Fâ™¯ 5.0

## *Les fonctions*

### DÃ©cembre 2021

---

<!-- _class: speaker invert -->

# About me

## Romain DENEAU

- SOAT depuis 2009
- Senior Developer Câ™¯ Fâ™¯ TypeScript
- PassionnÃ© de Craft
- Auteur sur le blog de SOAT

[![social-network](./themes/soat/pictos/social_twitter.png) DeneauRomain](https://twitter.com/DeneauRomain)

[![social-network](./themes/soat/pictos/social_github.png) rdeneau](https://github.com/rdeneau)

---

<!-- _class: purple agenda invert lead -->

![bg right:30% h:300](./themes/soat/pictos/SOAT_pictos_formation.png)

## Sommaire

- Signature des fonctions
- Fonctions
- OpÃ©rateurs
- Interop avec BCL .NET

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_probleme.png)

# 1.

## Signature des fonctions

---

# ProblÃ¨mes avec `void` en Câ™¯

`void` oblige Ã  faire du spÃ©cifique = 2 fois + de boulot ğŸ˜ 

- 2 types de dÃ©lÃ©guÃ©s : `Action` vs `Func<T>`
- 2 types de tÃ¢ches : `Task` vs `Task<T>`

Exemple :

```cs
interface ITelemetry
{
  void Run(Action action);
  T Run<T>(Func<T> func);

  Task RunAsync(Func<Task> asyncAction);
  Task<T> RunAsync<T>(Func<Task<T>> asyncFunc);
}
```

---

# De `void` Ã  `Void`

â˜ Le problÃ¨me avec `void`, c'est que ce n'est ni un type, ni une valeur.

ğŸ’¡ Si on avait un type "`Void`", un *Singleton* du type :

```cs
public class Void
{
    public static readonly Void Instance = new Void();

    private Void() {}
}
```

---

# De `void` Ã  `Void` (2)

On peut dÃ©finir les *helpers* suivants pour convertir vers `Void` :

```cs
public static class VoidExtensions
{
    // Action -> Func<Void>
    public static Func<Void> AsFunc(this Action action)
    {
        action();
        return Void.Instance;
    }

    // Func<Task> -> Func<Task<Void>>
    public async static Func<Task<Void>> AsAsyncFunc(this Func<Task> asyncAction)
    {
        await asyncAction();
        return Void.Instance;
    }
}
```

---

# Simplification de `ITelemetry`

On peut Ã©crire une implÃ©mentation par dÃ©faut (Câ™¯ 8) pour 2 des 4 mÃ©thodes :

```cs
interface ITelemetry
{
    void Run(Action action) =>
        Run(action.AsFunc());

    T Run<T>(Func<T> func);

    Task RunAsync(Func<Task> asyncAction) =>
        RunAsync(asyncAction.AsAsyncFunc());

    Task<T> RunAsync<T>(Func<Task<T>> asyncFunc);
}
```

---

# `Void` s'appelle `Unit` en Fâ™¯

En Fâ™¯, pas de fonction `void` mais des fonctions avec type de retour `Unit` / `unit`.

`unit` a une seule instance (d'oÃ¹ son nom), notÃ©e `()`
â†’ UtilisÃ©e en tant que derniÃ¨re expression d'une fonction *"void"* :

```fs
let voidFunction arg =
    // ...
    ()
```

---

# Fonctions sans paramÃ¨tre

`unit` sert aussi Ã  modÃ©liser des fonctions sans paramÃ¨tre :

```fs
let oneParam arg = ...
let noParam () = ... // ğŸ‘ˆ Avec
let noParam2() = ... // ğŸ‘ˆ ou sans espace
```

ğŸ’¡ IntÃ©rÃªt de la notation `()` : on dirait une fonction Câ™¯.

âš ï¸ **Attention :** on a vite fait d'oublier les `()` !

- Oubli dans la dÃ©claration â†’ simple valeur plutÃ´t que fonction
- Oubli dans l'appel â†’ alias de la fonction sans l'exÃ©cuter

---

# Fonction `ignore`

En Fâ™¯, tout est expression mais on peut insÃ©rer des expressions de type `unit`, par un exemple un `printf` avant de renvoyer la valeur

ProblÃ¨me : quand on appelle une fonction `save` pour enregistrer en base mais elle renvoie la valeur `true` ou `false` qu'on veut ignorer.

Solution : utiliser la fonction `ignore` de signature `'a -> unit`.
â†’ Qqsoit la valeur fournie en paramÃ¨tre, elle l'ignore et renvoie `()`.

```fs
let save entity = true

let a =
    save "bonjour" // âš ï¸ Warning FS0020: Le rÃ©sultat de cette expression a le type 'bool' et est implicitement ignorÃ©.
    save "bonjour" |> ignore // ğŸ‘Œ
    "ok"
```

---

# Signature d'une fonction en Fâ™¯

Notation flÃ©chÃ©e :

- Fonction Ã  0 paramÃ¨tre : `unit -> TResult`
- Fonction Ã  1 paramÃ¨tre : `T -> TResult`
- Fonction Ã  2 paramÃ¨tres : `T1 -> T2 -> TResult`
- Fonction Ã  3 paramÃ¨tres : `T1 -> T2 -> T3 -> TResult`

â“ **Quiz** : Pourquoi plusieurs `->` plutÃ´t que des `,` ? Cela indique quoi ?

---

# Curryfication

Syntaxe des fonctions Fâ™¯ : paramÃ¨tres sÃ©parÃ©s par des espaces
â†’ Indique que les fonctions sont curryfiÃ©es
â†’ D'oÃ¹ les `->` dans la signature entre les paramÃ¨tres

```fs
let fn () = result         // unit -> TResult
let fn arg = ()            // T    -> unit
let fn arg = result        // T    -> TResult

let fn x y = (x, y)        // T1 -> T2 -> (T1 * T2)

// Equivalents, explicitement curryfiÃ©s :
let fn x = fun y -> (x, y) // 1. Avec une lambda
let fn x =                 // 2. Avec une sous fonction
    let fn' y = (x, y)     // N.B. `x` vient du scope
    fn'
```

---

# Curryfication - Compilation .NET

â˜ Fonction curryfiÃ©e compilÃ©e en mÃ©thode avec paramÃ¨tres tuplifiÃ©s
â†’ Vue comme mÃ©thode normale quand consommÃ©e en Câ™¯

Exemple : Fâ™¯ puis Ã©quivalent Câ™¯ *(version simplifiÃ©e de [SharpLab](https://sharplab.io/#v2:DYLgZgzgNAJiDUAfAtgexgV2AUwAQEFcBeAWAChdLccAXXAQxhlwA9cBPY13eD8q6tjoA3esAx4iuAEy5EAPgZNcARnJA===))* :

```fs
module A =
    let add x y = x + y
    let value = 2 |> add 1
```

```cs
public static class A
{
    public static int add(int x, int y) => x + y;
    public static int value => 3;
}
```

---

# Conception unifiÃ©e des fonctions

Le type `unit` et la curryfication permettent de concevoir les fonctions simplement comme :

- **Prend un seul paramÃ¨tre** de type quelconque
  - y compris `unit` pour une fonction "sans paramÃ¨tre"
  - y compris une autre fonction *(callback)*
- **Renvoie une seule valeur** de type quelconque
  - y compris `unit` pour une fonction "ne renvoyant rien"
  - y compris une autre fonction

ğŸ‘‰ **Signature universelle** d'une fonction en Fâ™¯ : `'T -> 'U`

---

# Ordre des paramÃ¨tres

Pas le mÃªme ordre entre Câ™¯ et Fâ™¯

- Dans mÃ©thode extension Câ™¯, l'objet `this` est le 1er paramÃ¨tre
  - Ex : `items.Select(x => x)`
- En Fâ™¯, "l'objet" est plutÃ´t le **dernier paramÃ¨tre** : style *data-last*
  - Ex : `List.map (fun x -> x) items`

Style *data-last* favorise :

- Pipeline : `items |> List.map square |> List.sum`
- Application partielle : `let sortDesc = List.sortBy (fun i -> -i)`
- Composition de fonctions appliquÃ©es partiellement jusqu'au param "*data*"
  - `(List.map square) >> List.sum`

---

# Ordre des paramÃ¨tres (2)

âš ï¸ Friction avec BCL .NET car plutÃ´t *data-first*

â˜ Solution : wrapper dans une fonction avec params dans ordre sympa en Fâ™¯

```fs
let startsWith (prefix: string) (value: string) =
    value.StartsWith(prefix)
```

ğŸ’¡ **Tips** : utiliser `Option.defaultValue` plutÃ´t que `defaultArg` avec les options

- Fonctions font la mÃªme chose mais params `option` et `value` sont inversÃ©s
- `defaultArg option value` : param `option` en 1er ğŸ˜•
- `Option.defaultValue value option` : param `option` en dernier ğŸ‘

---

# Ordre des paramÃ¨tres (3)

De mÃªme, prÃ©fÃ©rer mettre **en 1er** les paramÃ¨tres les \+ statiques
= Ceux susceptibles d'Ãªtre prÃ©dÃ©finis par application partielle

Ex : "dÃ©pendances" qui seraient injectÃ©es dans un objet en Câ™¯

ğŸ‘‰ Application partielle = moyen de simuler l'injection de dÃ©pendances

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_alEchelle.png)

# 2.

## LesÂ Â  fonctions

---

# Binding d'une fonction

`let f x y = x + y + 1`

- Binding rÃ©alisÃ© avec mot clÃ© `let`
- Associe Ã  la fois un nom (`f`) et les paramÃ¨tres (`x` et `y`)
- Annotation de type optionnelle pour paramÃ¨tres et/ou retour
  - `let f (x: int) (y: int) : int = ...`
  - Sinon, infÃ©rence de type, avec possible gÃ©nÃ©ralisation auto
- DerniÃ¨re expression â†’ valeur de retour de la fonction
- Possible dÃ©finition de sous-fonctions (non gÃ©nÃ©riques)

---

# Fonction gÃ©nÃ©rique

- Dans beaucoup de cas, infÃ©rence marche avec gÃ©nÃ©ralisation auto
  - `let listOf x = [x]` â†’ `(x: 'a) -> 'a list`
- Annotation explicite de params gÃ©nÃ©riques
  - `let f (x: 'a) = ...` (pas besoin de faire `f<'a>` grÃ¢ce au `'` ğŸ‘)
- Annotation explicite avec infÃ©rence du type gÃ©nÃ©rique
  - `let f (list: list<_>) = ...`

---

# Fonction anonyme / (Expression) Lambda

Expression dÃ©finissant une fonction

Syntaxe : `fun parameter1 parameter2 etc -> expression`

â˜ **Ã€ noter :**

- Mot clÃ© `fun` obligatoire
- FlÃ¨che fine `->` (Java) â‰  flÃ¨che grasse `=>` (Câ™¯, Js)

---

# Fonctions anonymes - Quelques usages

### **1.** En argument d'une *high-order function*

- Pour Ã©viter de devoir dÃ©finir une fonction nommÃ©e
- RecommandÃ©e pour une fonction courte, pour que cela reste lisible

```fs
[1..10] |> List.map (fun i -> i + 1) // ğŸ‘ˆ () autour de la lambda

// Versus en passant par une fonction nommÃ©e
let add1 i = i + 1
[1..10] |> List.map add1
```

âš ï¸ Lambda inutile : `List.map (fun x -> f x)` â‰¡ `List.map f`

---

### **2.** Dans un *let binding* avec infÃ©rence

- Pour rendre explicite quand la fonction renvoie une fonction
- Sorte de curryfication manuelle
- Ã€ utiliser avec parcimonie

```fs
let add x y = x + y                     // Version normale, curryfiÃ©e automatiquement
let add' x = fun y -> x + y             // Idem avec une sous lambda
let add'' = fun x -> (fun y -> x + y)   // Idem en totalement "lambda-isÃ©e"
```

---

### **3.** Dans un *let binding* annotÃ©

- Signature de la fonction prÃ©-dÃ©finie sous forme d'un type
- Type "fonction" s'utilise un peu comme une `interface` Câ™¯
  - Pour contraindre implÃ©mentation Ã  suivre signature
  - Ex : *Domain modelling made functional* par Scott Wlaschin

```fs
type Add = int -> int -> int

let add: Add = fun x y -> x + y // ğŸ‘ˆ Signature finale avec param nommÃ©s : (x: int) -> (y: int) -> int
```

---

# Fonction anonyme `function`

- Mot clÃ© `function` permet aussi de dÃ©finir une fonction anonyme
- Syntaxe abrÃ©gÃ©e Ã©quivalente Ã  `fun x -> match x with`
- Prend 1 paramÃ¨tre qui est implicite

```fs
let ouiNon x =
  match x with
  | true  -> "Oui"
  | false -> "Non"

// RÃ©Ã©crit avec `function`
let ouiNon = function
  | true  -> "Oui"
  | false -> "Non"
```

â˜ Pas de cas d'usage spÃ©cifique. Son emploi est une question de goÃ»t.

---

# DÃ©construction de paramÃ¨tres

- Comme en JavaScript, on peut dÃ©construire *inline* un paramÃ¨tre
- C'est Ã©galement une faÃ§on d'indiquer le type du paramÃ¨tre
- Le paramÃ¨tre apparaÃ®t sans nom dans la signature

Exemple avec un type *Record* ğŸ“

```fs
type Person = { Name: string; Age: int }

let name { Name = x } = x     // Person -> string
let age { Age = x } = x       // Person -> int
let age' person = person.Age  // Equivalent explicite

let bob = { Name = "Bob"; Age = 18 } // Person
let bobAge = age bob // int = 18
```

---

# DÃ©construction de paramÃ¨tres (2)

On parle aussi de *pattern matching*
â†’ Mais je prÃ©fÃ¨re rÃ©server ce terme pour l'usage de `match x with ...`

DÃ©construction pas adaptÃ©e pour un type union avec plusieurs cas ğŸ“
â†’ Ex : liste Fâ™¯ *(soit vide `[]`, soit valeur + sous-liste `head::tail`)*
ğŸ’¡ **Solution :** faire un *pattern matching* de tous les cas de l'union

```fs
let printFirstItem (x::_) = // 'a list -> unit
//                  ~~~~  Warning FS0025: CritÃ¨res spÃ©ciaux incomplets dans cette expression.
   printfn $"first element: {x}"

let printFirstItemOk = function
    | x::_ -> printfn $"first element: {x}"
    | []   -> printfn "none"
```

---

# ParamÃ¨tre tuple

- Comme en Câ™¯, on peut vouloir regrouper des paramÃ¨tres d'une fonction
  - Par soucis de cohÃ©sion, quand ces paramÃ¨tres forment un tout
  - Pour Ã©viter le *code smell* [long parameter list](https://refactoring.guru/smells/long-parameter-list)
- On peut les regrouper dans un tuple et mÃªme le dÃ©construire

```fs
// V1 : trop de paramÃ¨tres
let f x y z = ...

// V2 : paramÃ¨tres regroupÃ©s dans un tuple
let f params =
    let (x, y, z) = params
    ...

// V3 : idem avec tuple dÃ©construit sur place
let f (x, y, z) = ...
```

---

# ParamÃ¨tre tuple (2)

- `f (x, y, z)` ressemble furieusement Ã  une mÃ©thode Câ™¯ !
- La signature signale le changement : `(int * int * int) -> TResult`
  - La fonction n'a effectivement plus qu'1! paramÃ¨tre plutÃ´t que 3
  - Perte possibilitÃ© application partielle de chaque Ã©lÃ©ment du tuple

â˜ **Conclusion** :

- Resister Ã  la tentation d'utiliser tout le temps un tuple *(car familier - Câ™¯)*
- RÃ©server cet usage quand c'est pertinent de regrouper les paramÃ¨tres
  - Sans pour autant dÃ©clarer un type spÃ©cifique pour ce groupe

---

# Fonction rÃ©cursive

- Fonction qui s'appelle elle-mÃªme
- Syntaxe spÃ©ciale avec mot clÃ© `rec` sinon erreur `FS0039: â€¦ is not defined`
- TrÃ¨s courant en Fâ™¯ pour remplacer les boucles `for`
  - Car c'est souvent + facile Ã  concevoir

Exemple : trouver nb Ã©tapes pour atteindre 1 dans la [suite de Syracuse](https://fr.wikipedia.org/wiki/Conjecture_de_Syracuse) / Collatz

```fs
let rec steps (n: int) : int =
    if n = 1       then 0
    elif n % 2 = 0 then 1 + steps (n / 2)
    else                1 + steps (3 * n + 1)
```

---

# *Tail recursion*

- Type de rÃ©cursivitÃ© oÃ¹ l'appel rÃ©cursif est la derniÃ¨re instruction
- DÃ©tectÃ© par le compilateur et optimisÃ© sous forme de boucle
  - Permet d'Ã©viter les `StackOverflow`
- ProcÃ©dÃ© classique pouvant rendre tail rÃ©cursif :
  - Ajouter un param "accumulateur", comme `fold`/`reduce`

```fs
let steps (number: int) : int =
    let rec loop count n = // ğŸ‘ˆ `loop` = nom idiomatique de ce type de fonction interne rÃ©cursive
        if n = 1       then count
        elif n % 2 = 0 then loop (count + 1) (n / 2)      // ğŸ‘ˆ Dernier appel : `loop`
        else                loop (count + 1) (3 * n + 1)  // ğŸ‘ˆ idem
    loop 0 number // ğŸ‘ˆ Lancement de la boucle avec 0 comme valeur initiale pour `count`
```

---

# Fonctions mutuellement rÃ©cursives

- Fonctions qui s'appellent l'une l'autre
- Doivent Ãªtre dÃ©clarÃ©es ensemble :
  - 1Ã¨re fonction indiquÃ©e comme rÃ©cursive avec `rec`
  - autres fonctions ajoutÃ©es Ã  la dÃ©claration avec `and`

```fs
// âš ï¸ Algo un peu alambiquÃ© servant juste d'illustration

let rec Even x =        // ğŸ‘ˆ Mot clÃ© `rec`
    if x = 0 then true
    else Odd (x-1)      // ğŸ‘ˆ Appel Ã  `Odd` dÃ©finie + bas
and Odd x =             // ğŸ‘ˆ Mot clÃ© `and`
    if x = 0 then false
    else Even (x-1)     // ğŸ‘ˆ Appel Ã  `Even` dÃ©finie + haut
```

---

# Surcharge / *overload* de fonctions

âš ï¸ Pas possible de surcharger une fonction

ğŸ’¡ Noms diffÃ©rents :

- `List.map (mapping: 'T -> 'U) list`
- `List.mapi (mapping: (index: int) -> 'T -> 'U) list`

ğŸ’¡ ImplÃ©mentation via fonction template ğŸ‘‡

---

# Fonction template

Permet de crÃ©er des "surcharges" spÃ©cialisÃ©es :

```fs
type ComparisonResult = Bigger | Smaller | Equal

// Fonction template, 'private' pour la "cacher"
let private compareTwoStrings (comparison: StringComparison) string1 string2 =
    let result = System.String.Compare(string1, string2, comparison)
    if result > 0 then
        Bigger
    else if result < 0 then
        Smaller
    else
        Equal

// Application partielle du paramÃ¨tre 'comparison'
let compareCaseSensitive   = compareTwoStrings StringComparison.CurrentCulture
let compareCaseInsensitive = compareTwoStrings StringComparison.CurrentCultureIgnoreCase
```

---

# Fonction template (2)

â˜ Emplacement du paramÃ¨tre de spÃ©cialisation :

- En Câ™¯, en dernier :

```cs
String.Compare(String, String, StringComparison)
String.Compare(String, String)
```

- En Fâ™¯, en premier pour permettre application partielle :

```fs
compareTwoStrings    : StringComparison -> String -> String -> ComparisonResult
compareCaseSensitive :                     String -> String -> ComparisonResult
```

---

# Organisation des fonctions

3 faÃ§ons d'organiser les fonctions = 3 endroits oÃ¹ les dÃ©clarer :

- *Module* : fonction dÃ©clarÃ©e dans un module ğŸ“
- *Nested* : fonction dÃ©clarÃ©e Ã  l'intÃ©rieur d'une valeur / fonction
  - ğŸ’¡ Encapsuler des helpers utilisÃ©s juste localement
  - â˜ ParamÃ¨tres de la fonction chapeau accessibles Ã  fonction *nested*
- *Method* : fonction dÃ©finie comme mÃ©thode dans un type *(next slide)*

---

# MÃ©thodes

- DÃ©finies avec mot-clÃ© `member` plutÃ´t que `let`
- Choix du *self-identifier* : `this`, `me`, `_`...
- ParamÃ¨tres sont au choix :
  - TuplifiÃ©s : style OOP
  - CurryfiÃ©s : style FP

---

## MÃ©thodes - Exemple

```fs
type Product = { SKU: string; Price: float } with  // ğŸ‘ˆ `with` nÃ©cessaire pour l'indentation
    // Style avec tuplification et `this`          // Alternative : `{ SKU...}` Ã  la ligne
    member this.TupleTotal(qty, discount) =
        (this.Price * float qty) - discount

    // Style avec curryfication et `me`
    member me.CurriedTotal qty discount = // ğŸ‘ˆ `me` dÃ©signe le "this"
        (me.Price * float qty) - discount // ğŸ‘ˆ `me.Price` pour accÃ©der Ã  la propriÃ©tÃ© `Price`
```

---

<!-- _footer: '' -->

# Fonction *vs* MÃ©thode

| FonctionnalitÃ©                    | Fonction  | MÃ©thode                  |
|-----------------------------------|-----------|--------------------------|
| Nommage                           | camelCase | PascalCase               |
| Curryfication                     | âœ… oui    | âœ… si non tuplifiÃ©s       |
| ParamÃ¨tres nommÃ©s                 | âŒ non    | âœ… si tuplifiÃ©s           |
| ParamÃ¨tres optionnels             | âŒ non    | âœ… si tuplifiÃ©s           |
| Surcharge / *overload*            | âŒ non    | âœ… si tuplifiÃ©s           |
| InfÃ©rence Ã  l'usage               | âœ… oui    | âŒ non                    |
| En argument d'une *high-order fn* | âœ… oui    | âŒ non, lambda nÃ©cessaire |
| Support du `inline`               | âœ… oui    | âœ… oui                    |
| RÃ©cursive                         | âœ… si `rec` | âœ… oui                  |

---

# Fonctions standards

DÃ©finies dans `FSharp.Core` automatiquement importÃ©

## Conversion

- `box`, `tryUnbox`, `unbox` : *boxing* et (tentative de) *unboxing*
- `byte`, `char`, `decimal`, `float`, `int`, `string` : conversion en `byte`, `char`, ...
- `enum<'TEnum>` : conversion en l'enum spÃ©cifiÃ©

---

## Math

- `abs`, `sign` : valeur absolue, signe (-1 si < 0...)
- `(a)cos(h)`, `(a)sin`, `(a)tan` : (co)sinus/tangente (inverse/hyperbolique)
- `ceil`, `floor`, `round` : arrondi (inf, sup)
- `exp`, `log`, `log10` : exponentielle, logarithme...
- `pown x (n: int)` : *power* = `x` Ã  la puissance `n`
- `sqrt` : *square root* / racine carrÃ©e

---

## Autres

- `compare a b : int`: renvoie -1 si a < b, 0 si =, 1 si >
- `hash` : calcul le hash (code)
- `max`, `min` : maximum et minimum de 2 valeurs comparables
- `ignore` : pour "avaler" une valeur et obtenir `unit`
- `id` : next slide ğŸ‘‡

---

## Fonction `id`

DÃ©finition `let id x = x` â€¢ Signature : `(x: 'T) -> 'T`
â†’ Fonction Ã  un seul paramÃ¨tre d'entrÃ©e
â†’ Qui ne fait que renvoyer ce paramÃ¨tre

Pourquoi une telle fonction â“
â†’ Nom `id` = abrÃ©viation de `identity`
â†’ ZÃ©ro / Ã‰lÃ©ment neutre de la composition des fonctions

| OpÃ©ration          | IdentitÃ© | Exemple                        |
|--------------------|----------|--------------------------------|
| Addition `+`       | `0`      | `0 + 5` â‰¡ `5 + 0` â‰¡ `5`        |
| Multiplication `*` | `1`      | `1 * 5` â‰¡ `5 * 1` â‰¡ `5`        |
| Composition `>>`   | `id`     | `id >> fn` â‰¡ `fn >> id` â‰¡ `fn` |

---

## Fonction `id` - Cas d'utilisation

Avec une *high-order function* faisant 2 choses :
â€¢ 1 opÃ©ration
â€¢ 1 mapping de valeur via param `'T -> 'U`

Ex : `List.collect fn list` = flatten + mapping

Comment faire juste l'opÃ©ration et pas de mapping ?

- `list |> List.collect (fun x -> x)` ğŸ‘
- `list |> List.collect id` ğŸ‘
- â˜ Meilleure alternative : `List.concat list` ğŸ’¯

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_software.png)

# 3.

## LesÂ Â Â Â Â  opÃ©rateurs

---

# OpÃ©rateur

Est dÃ©fini comme une fonction

- OpÃ©rateur unaire : `let (~symbols) = ...`
- OpÃ©rateur binaire : `let (symbols) = ...`
- *Symbols* = combinaison de `% & * + - . / < = > ? @ ^ | ! $`

2 faÃ§ons d'utiliser les opÃ©rateurs

- En tant qu'opÃ©rateur â†’ infixe `1 + 2` ou prÃ©fixe `-1`
- En tant que fonction â†’ chars entre `()` : `(+) 1 2` â‰¡ `1 + 2`

---

# OpÃ©rateurs standards

Ã‰galement dÃ©finis dans `FSharp.Core`

- OpÃ©rateurs arithmÃ©tiques : `+`, `-`...
- OpÃ©rateurs de pipeline
- OpÃ©rateurs de composition

---

# OpÃ©rateurs *Pipe*

OpÃ©rateurs binaires, placÃ©s entre une valeur simple et une fonction

- Appliquent la valeur Ã  la fonction = Passe la valeur en argument
- Permettent d'Ã©viter la mise entre parenthÃ¨ses / prÃ©cÃ©dence
- âˆƒ plusieurs *pipes*
  - *Pipe right* `|>` : le *pipe* "classique"
  - *Pipe left* `<|` a.k.a. *pipe* inversÃ©
  - *Pipe right 2* `||>`
  - Etc.

---

## OpÃ©rateur *Pipe right* `|>`

Inverse l'ordre entre fonction et valeur : `val |> fn` â‰¡ `fn val`

- Ordre naturel "sujet verbe", comme appel mÃ©thode d'un objet (`obj.M(x)`)
- *Pipeline* : enchaÃ®ner appels de fonctions, sans variable intermÃ©diaire
- Aide infÃ©rence d'objet. Exemple :

```fs
let items = ["a"; "bb"; "ccc"]

let longestKo = List.maxBy (fun x -> x.Length) items  // âŒ Error FS0072
//                                   ~~~~~~~~

let longest = items |> List.maxBy (fun x -> x.Length) // âœ… Renvoie "ccc"
```

---

## OpÃ©rateur *Pipe left* `<|`

`fn <| expression` â‰¡ `fn (expression)`
â€¢ â˜ Usage un peu moins courant que `|>`
â€¢ âœ… Avantage mineur : permet d'Ã©viter des parenthÃ¨ses
â€¢ âŒ InconvÃ©nient majeur : se lit de droite Ã  gauche
Â Â Â  â†’ Inverse du sens lecture naturel en anglais et ordre exÃ©cution

```fs
printf "%i" 1+2          // ğŸ’¥ Erreur
printf "%i" (1+2)        // Avec parenthÃ¨ses
printf "%i" <| 1+2       // Avec pipe inversÃ©
```

---

## OpÃ©rateur *Pipe left* `<|` (2)

### Quid d'une expression telle que `x |> fn <| y` â“

ExÃ©cutÃ©e de gauche Ã  droite :
`(x |> fn) <| y` â‰¡ `(fn x) <| y` â‰¡ `fn x y`

- En thÃ©orie : permettrait d'utiliser `fn` en position infixÃ©e
- En pratique : difficile Ã  lire Ã  cause du double sens de lecture â—

ğŸ‘‰ Conseil : **Ã€ Ã‰VITER**

---

## OpÃ©rateur *Pipe right 2* `||>`

`(x, y) |> fn` â‰¡ `fn x y`

â€¢ Pour passer 2 arguments Ã  la fois, sous la forme d'un tuple
â€¢ Usage peu frÃ©quent, par exemple avec `fold` pour passer liste & seed

```fs
let items = [1..5]

// ğŸ˜• On peut manquer le 0 au bout (le seed)
let sumOfEvens = items |> List.fold (fun acc x -> if x % 2 = 0 then acc + x else acc) 0

let sumOfEvens' =
    (0, items)
    ||> List.fold (fun acc x -> if x % 2 = 0 then acc + x else acc)

// ğŸ’¡ Remplacer lambda par fonction nommÃ©e
let addIfEven acc x = if x % 2 = 0 then acc + x else acc
let sumOfEvens'' = items |> List.fold addIfEven 0
```

---

# OpÃ©rateur *Compose* `>>`

OpÃ©rateurs binaires, placÃ©s **entre deux fonctions**
â†’ Le rÃ©sultat de la 1Ã¨re fonction servira d'argument Ã  la 2e fonction

`f >> g` â‰¡ `fun x -> g (f x)` â‰¡ `fun x -> x |> f |> g`

ğŸ’¡ Peut se lire Â« `f` ensuite `g` Â»

âš ï¸ Les types doivent correspondre : `f: 'T -> 'U` et `g: 'U -> 'V`
â†’ On obtient une fonction de signature `'T -> 'V`

```fs
let add1 x = x + 1
let times2 x = x * 2

let add1Times2 x = times2(add1 x) // ğŸ˜• Style explicite mais + chargÃ©
let add1Times2' = add1 >> times2  // ğŸ‘ Style concis
```

---

# OpÃ©rateur *Compose* inverse `<<`

Sert rarement, sauf pour retrouver un ordre naturel des termes

Exemple avec opÃ©rateur `not` (qui remplace le `!` du Câ™¯) :

```fs
let Even x = x % 2 = 0

// Pipeline classique
let Odd x = x |> Even |> not

// RÃ©Ã©crit avec composition inverse
let Odd = not << Even
```

---

# *Pipe* `|>` ou *Compose* `>>` ?

#### *Compose* `let h = f >> g`

- Composition de 2 fonctions `f` et `g`
- Renvoie une nouvelle fonction
- Les fonctions `f` et `g` ne sont exÃ©cutÃ©es que lorsque `h` l'est

#### *Pipe* `let result = value |> f`

- Juste une syntaxe diffÃ©rente pour passer un argument
- La fonction `f` est :
  - ExÃ©cutÃ©e si elle n'a qu'1! param â†’ `result` est une valeur
  - AppliquÃ©e partiellement sinon â†’ `result` est une fonction

---

# Style *Point-free*

A.k.a *Programmation tacite*

Fonction dÃ©finie par composition ou application partielle ou avec `function`
â†’ **ParamÃ¨tre implicite**, d'oÃ¹ le Â« sans-point Â» (dans l'espace)

```fs
let add1 x = x + 1                // (x: int) -> int
let times2 x = x * 2              // (x: int) -> int
let add1Times2 = add1 >> times2   // int -> int â€¢ x implicite â€¢ Par composition

let isEven x = x % 2 = 0
let evens list = List.filter isEven list // (list: int list) -> int list
let evens' = List.filter isEven // int list -> int list â€¢ Par application partielle

let greet name age = printfn $"My name is {name} and I am %d{age} years old!" // name:string -> age:int -> unit
let greet' = printfn "My name is %s and I am %d years old!" // (string -> int -> unit)
```

---

# Style *Point-free* - Pros/Cons âš–ï¸

### âœ… Avantages

Style concis â€¢ Abstraction des paramÃ¨tres, opÃ¨re au niveau fonctions

### âŒ InconvÃ©nients

Perd le nom du paramÃ¨tre devenu implicite dans la signature
â†’ Sans importance si la fonction reste comprÃ©hensible :
Â Â Â Â â€¢ Nom du param non significatif (ex. `x`)
Â Â Â Â â€¢ Type du param et nom de la fonction suffisent
â†’ DÃ©conseillÃ© pour une API publique

---

# Style *Point-free* - Limite ğŸ›‘

Marche mal avec fonctions gÃ©nÃ©riques :

```fs
let isNotEmptyKo = not << List.isEmpty          // ğŸ’¥ Error FS0030: Restriction de valeur
let isNotEmpty<'a> = not << List.isEmpty<'a>    // ğŸ‘Œ Avec annotation
let isNotEmpty' list = not (List.isEmpty list)  // ğŸ‘Œ Style explicite
```

ğŸ”— [https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/conventions
#partial-application-and-point-free-programming](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/conventions#partial-application-and-point-free-programming)

---

# Fonction `inline`

ğŸ’¡ MÃªme principe que les refactos *Inline Method*, *Inline Variable*

Indique au compilateur de *"inliner"* la fonction
â†’ Tous les appels Ã  cette fonction seront remplacÃ©s par le corps de la fonction
â†’ Usage typique : petite fonction/opÃ©rateur de "sucre syntaxique"

```fs
// See https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/prim-types.fs
let inline (|>) x f = f x
let inline ignore _ = ()

let t = true |> ignore
     ~= ignore true // AprÃ¨s inline du pipe
     ~= ()          // AprÃ¨s inline de ignore
```

---

# OpÃ©rateurs personnalisÃ©s

2 possibilitÃ©s :

- Surcharge d'opÃ©rateurs
- CrÃ©ation d'un nouvel opÃ©rateur

---

# Surcharge d'opÃ©rateurs

En gÃ©nÃ©ral, concerne un type spÃ©cifique
â†’ Surcharge dÃ©finie Ã  l'intÃ©rieur du type associÃ© *(comme en Câ™¯)*

```fs
type Vector = { X: int; Y: int } with
    // OpÃ©rateur unaire (cf ~ et 1! param) d'inversion d'un vecteur
    static member (~-) (v: Vector) =
        { X = -v.X
          Y = -v.Y }

    // OpÃ©rateur binaire d'addition de 2 vecteurs
    static member (+) (a: Vector, b: Vector) =
        { X = a.X + b.X
          Y = a.Y + b.Y }

let v1 = -{ X=1; Y=1 } // { X = -1; Y = -1 }
let v2 = { X=1; Y=1 } + { X=1; Y=3 } // { X = 2; Y = 4 }
```

---

# CrÃ©ation d'un nouvel opÃ©rateur

- DÃ©finition plutÃ´t dans un module ou dans un type associÃ©
- Cas d'usage classique : alias fonction existante, utilisÃ© en infixe

```fs
// "OR" Composition of 2 functions (fa, fb) which return an optional result
let (<||>) fa fb x =
    match fa x with
    | Some v -> Some v // Return value produced by (fa x) call
    | None   -> fb x   // Return value produced by (fb x) call

// Functions: int -> string option
let tryMatchPositiveEven x = if x > 0 && x % 2 = 0 then Some $"Even {x}" else None
let tryMatchPositiveOdd x = if x > 0 && x % 2 <> 0 then Some $"Odd {x}" else None
let tryMatch = tryMatchPositiveEven <||> tryMatchPositiveOdd

tryMatch 0;; // None
tryMatch 1;; // Some "Odd 1"
tryMatch 2;; // Some "Even 2"
```

---

# Symboles autorisÃ©s dans un opÃ©rateur

**OpÃ©rateur unaire "tilde"**
â†’ `~` suivi de `+`, `-`, `+.`, `-.`, `%`, `%%`, `&`, `&&`

**OpÃ©rateur unaire "snake"**
â†’ Plusieurs `~`, ex : `~~~~`

**OpÃ©rateur unaire "bang"**
â†’ `!` suivi combinaison de `!`, `%`, `&`, `*`, `+`, `.`, `/`, `<`, `=`, `>`, `@`, `^`, `|`, `~`, `?`
â†’ Sauf `!=` (!=) qui est binaire

**OpÃ©rateur binaire**
â†’ Toute combinaison de `!`, `%`, `&`, `*`, `+`, `.`, `/`, `<`, `=`, `>`, `@`, `^`, `|`, `~`, `?`
â†’ qui ne correspond pas Ã  un opÃ©rateur unaire

---

# Symboles Ã  l'usage

Tout opÃ©rateur s'utilise tel quel
â— Sauf opÃ©rateur unaire "tilde" : s'utilise sans le `~` initial

| OpÃ©rateur    | DÃ©claration         | Usage     |
|--------------|---------------------|-----------|
| Unaire tilde | `let (~&&) x = â€¦`   | `&&x`     |
| Unaire snake | `let (~~~) x = â€¦`   | `~~~x`    |
| Unaire bang  | `let (!!!) x = â€¦`   | `!!!x`    |
| Binaire      | `let (<Ë†>) x y = â€¦` | `x <Ë†> y` |

â˜ Espace obligatoire entre `(` et `*` pour distinguer d'un commentaire `(* *)`
â†’ `let ( *+ ) x y = x * y + y` âœ…

---

# OpÃ©rateur ou fonction ?

## OpÃ©rateur infixe *vs* fonction

ğŸ‘ **Pour** : ordre naturel de lecture (gauche â†’ droite) â€¢ Ã©viter parenthÃ¨ses
Â Â Â Â Â â†’ `1 + 2 * 3` *vs* `multiply (add 1 2) 3`

âš ï¸ **Contre** : opÃ©rateur folklorique (ex : `@!`) moins comprÃ©hensible que
Â Â Â Â Â fonction dont le nom utilise le **langage du domaine**

## Utiliser opÃ©rateur en tant que fonction

ğŸ‘ Application partielle : `(+) 1` â‰¡ `fun x -> x + 1` â€¢ `let isPositive = (<) 0`
ğŸ’¡ Composition : `(~-) >> ((*) 2)` â‰¡ `fun x -> -x * 2`

---

# Pipeline avec mÃ©thode d'instance

Exemple : appeler `ToLower()` d'une `string`

- Via lambda : `"MyString" |> (fun x -> x.ToLower())`
- Idem via fonction nommÃ©e telle que :
  - `String.toLower` de la librairie [FSharpPlus](https://fsprojects.github.io/FSharpPlus//reference/fsharpplus-string.html)
  - `"MyString" |> String.toLower`
- ~~Pipeline~~ Valeur intermÃ©diaire : `let low = "MyString".ToLower()`

---

# MÃ©moÃ¯sation

ğŸ’¡ **IdÃ©e :** rÃ©duire le temps de calcul d'une fonction

â“ **Comment :** mise en cache des rÃ©sultats
â†’ Au prochain appel avec mÃªmes arguments, renverra rÃ©sultat en cache

ğŸ‘‰ **En pratique :** fonction `memoizeN` de la librairie [FSharpPlus](https://fsprojects.github.io/FSharpPlus/reference/fsharpplus-memoization.html#memoizeN)

âš ï¸ **Attention :** Comme tout optimisation, Ã  utiliser quand le besoin se fait sentir et en validant (mesurant) que cela marche sans dÃ©sagrÃ©ment annexe.

â˜ Ne pas confondre avec expression `lazy` *(slide suivante)*

---

# Lazy expression

Sucre syntaxique pour crÃ©er un objet .NET `Lazy<'T>` Ã  partir d'une expression
â†’ Expression pas Ã©valuÃ©e immÃ©diatement mais qu'Ã  la 1Ã¨re demande _([Thunk](https://en.wikipedia.org/wiki/Thunk))_
â†’ IntÃ©ressant pour amÃ©liorer performance sans trop complexifier le code

```fs
let printAndForward x = printfn $"{x}"; x

let a = lazy (printAndForward "a")

let b = printAndForward "b"
// > b

printfn $"{a.Value} et {b}"
// > a
// > a et b

printfn $"{a.Value} et c"
// > a et c
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_architecture.png)

# 4.

## Interop avec BCL

BCL = Base Class Library .NET

---

# Appel Ã  une mÃ©thode de la BCL

- Syntaxe similaire au tuple - Ex : `System.String.Compare("a","b")`
  - MÃ©thode **pas** curryfiÃ©e â†’ Tous les arguments doivent Ãªtre spÃ©cifiÃ©s
- Mais ne marche pas avec un vrai tuple Fâ™¯

```fs
System.String.Compare("a", "b") // âœ…
System.String.Compare ("a","b") // âœ…

System.String.Compare "a" "b"   // âŒ
System.String.Compare "a","b"   // âŒ

let tuple = ("a","b")
System.String.Compare tuple     // âŒ
```

---

# ParamÃ¨tre `out` - En Câ™¯

`out` utilisÃ© pour avoir plusieurs valeurs en sortie
â†’ Ex : `Int32.TryParse`, `Dictionary<,>.TryGetValue` :

```cs
if (int.TryParse(maybeInt, out var value))
    Console.WriteLine($"It's the number {value}.");
else
    Console.WriteLine($"{maybeInt} is not a number.");
```

---

# ParamÃ¨tre `out` - En Fâ™¯

PossibilitÃ© de consommer la sortie sous forme de tuple ğŸ‘

```fs
  match System.Int32.TryParse maybeInt with
  | true, i  -> printf $"It's the number {value}."
  | false, _ -> printf $"{maybeInt} is not a number."
```

ğŸ’¡ Fonctions Fâ™¯ `tryXxx` s'appuient plutÃ´t sur le type `Option<T>` ğŸ“

---

# Instancier une classe avec `new` ?

| Classe                   | Utiliser `new`             |
|--------------------------|----------------------------|
| Quelconque               | Optionnel - Non recommandÃ© |
| ImplÃ©mente `IDisposable` | Obligatoire                |

```fs
let myvar = MyClass(12)       // ğŸ‘
let myvar2 = new MyClass(234) // âš ï¸ Marche mais pas idiomatique

// IDisposable
let f = FileStream("hello.txt", FileMode.Open)     // âš ï¸ Compiler warning
use f = new FileStream("hello.txt", FileMode.Open) // â˜ `use` plutÃ´t que `let`
```

---

# Appel d'une mÃ©thode surchargÃ©e

- Compilateur peut ne pas comprendre quelle surcharge est appelÃ©e
- Astuce : faire appel avec argument nommÃ©

```fs
let createReader fileName =
    new System.IO.StreamReader(path=fileName) // ğŸ‘ˆ Param `path` â†’ `filename` infÃ©rÃ© en `string`

let createReaderByStream stream =
    new System.IO.StreamReader(stream=stream) // ğŸ‘ˆ Param `stream` de type `System.IO.Stream`
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# 5.

## ğŸ” Quiz

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# Question 1

#### Comment dÃ©finir la valeur de retour `v` d'une fonction `f` ?

**A.** Il suffit de nommer la valeur `result`

**B.** Faire `return v`

**C.** `v` constitue la derniÃ¨re ligne de `f`

â± 10â€™â€™

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_solution.png)

# RÃ©ponse 1

#### Comment dÃ©finir la valeur de retour `v` d'une fonction `f` ?

**A.** Il suffit de nommer la valeur `result` âŒ

**B.** Faire `return v` âŒ

**C.** `v` constitue la derniÃ¨re ligne de `f` âœ…

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# Question 2

#### Comment Ã©crire fonction `add` prenant 2 `string`s et renvoyant un `int`

**A.** `let add a b = a + b`

**B.** `let add (a: string) (b: string) = (int a) + (int b)`

**C.** `let add (a: string) (b: string) : int = a + b`

â± 20â€™â€™

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_solution.png)

# RÃ©ponse 2

#### Comment Ã©crire fonction `add` prenant 2 `string`s et renvoyant un `int`

**A.** `let add a b = a + b` âŒ
Â Â Â Â Â |Â  Mauvais type infÃ©rÃ© pour `a` et `b` : `int`

**B.** `let add (a: string) (b: string) = (int a) + (int b)` âœ…
Â Â Â Â Â |Â  Il faut spÃ©cifier le type de `a` et `b`.
Â Â Â Â Â |Â  Il faut les convertir en `int`.
Â Â Â Â Â |Â  Le type de retour `int` peut Ãªtre infÃ©rÃ©.

**C.** `let add (a: string) (b: string) : int = a + b`
Â Â Â Â Â |Â  Ici, `+` concatÃ¨ne les `string`s.

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# Question 3

#### Que fait le code `add >> multiply` ?

**A.** CrÃ©er un pipeline

**B.** DÃ©finir une fonction

**C.** CrÃ©er une composition

â± 10â€™â€™

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_solution.png)

# RÃ©ponse 3

#### Que fait le code `add >> multiply` ?

**A.** CrÃ©er un pipeline âŒ

**B.** DÃ©finir une fonction âŒ

**C.** CrÃ©er une composition âœ…

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# Question 4

#### Retrouvez le nom de ces fonctions [Core](https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/)

#### **A.** `let ? _ = ()`

#### **B.** `let ? x = x`

#### **C.** `let ? f x = f x`

#### **D.** `let ? x f = f x`

#### **E.** `let ? f g x = g (f x)`

â± 60â€™â€™ Â  ğŸ’¡ Il peut s'agir d'opÃ©rateurs

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_solution.png)

# RÃ©ponse 4

**A.** `let inline ignore _ = ()`
Â Â Â Â Â â†’ **Ignore** : [prim-types.fs#L459](https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/prim-types.fs#L459)

**B.** `let id x = x`
Â Â Â Â Â â†’ **Identity** : [prim-types.fs#L4831](https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/prim-types.fs#L4831)

**C.** `let inline (<|) func arg = func arg`
Â Â Â Â Â â†’ **Pipe Left** : [prim-types.fs#L3914](https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/prim-types.fs#L3914)

**D.** `let inline (|>) arg func = func arg`
Â Â Â Â Â â†’ **Pipe Right** : [prim-types.fs#L3908](https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/prim-types.fs#L3908)

**E.** `let inline (>>) func1 func2 x = func2 (func1 x)`
Â Â Â Â Â â†’ **Compose Right** : [prim-types.fs#L3920](https://github.com/dotnet/fsharp/blob/main/src/fsharp/FSharp.Core/prim-types.fs#L3920)

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# Question 5. Que signifie ces signatures ?

#### Combien de paramÃ¨tres ? De quel type ? Type du retour ?

#### **A.** `int -> unit`

#### **B.** `unit -> int`

#### **C.** `string -> string -> string`

#### **D.** `('T -> bool) -> 'T list -> 'T list`

â± 60â€™â€™

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_solution.png)

# RÃ©ponse 5. Que signifie ces signatures ?

**A.** `int -> unit`
1 paramÃ¨tre `int` - pas de valeur renvoyÃ©e

**B.** `unit -> int`
aucun paramÃ¨tre - renvoie un `int`

**C.** `string -> string -> string`
2 paramÃ¨tres `string` - renvoie une `string`

**D.** `('T -> bool) -> 'T list -> 'T list`
2 paramÃ¨tres : un prÃ©dicat et une liste - renvoie une liste
â†’ Fonction `filter`

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# Question 6. Signature de `h` ?

```fs
let f x = x + 1
let g x y = $"%i{x} + %i{y}"
let h = f >> g
```

#### **A.** `int -> int`

#### **B.** `int -> string`

#### **C.** `int -> int -> string`

#### **D.** `int -> int -> int`

â± 30â€™â€™ Â  ğŸ’¡ `%i{a}` indique que `a` est un `int`

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_solution.png)

# RÃ©ponse 6. Signature de `h` ?

#### **C.** `int -> int -> string` âœ…

`let f x = x + 1` â†’ `f: (x: int) -> int`
Â» `1` â†’ `int` â†’ `x: int` â†’ `x + 1: int`

`let g x y = $"{+x} + {+y}"` â†’ `(x: int) -> (y: int) -> string`
Â» `%i{x}` â†’ `int`
Â» `$"..."` â†’ `string`

`let h = f >> g`
Â» `h` peut s'Ã©crire `let h x y = g (f x) y`
Â» MÃªme `x` que `f` â†’ `int`, mÃªme `y` que `g` â†’ `int`

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_idee.png)

# Annexe 6. Signature de `h` ?

```fs
let f x = x + 1
let g x y = $"%i{x} + %i{y}"
let h = f >> g
```

â˜ **Conseil :** Ã©viter d'utiliser `>>` avec des fonctions d'aritÃ©
diffÃ©rente *(ici 1 pour `f`, 2 pour `g`)* car ce n'est pas lisible

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_question.png)

# Question 7. Combien vaut `f 2` ?

```fs
let f = (-) 1;
f 2 // ?
```

#### **A.** `1`

#### **B.** `3`

#### **C.** `-1`

â± 10â€™â€™

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_solution.png)

# RÃ©ponse 7. Combien vaut `f 2` ?

```fs
let f = (-) 1
f 2 // ?
```

#### **C.** `-1`

Contre-intuitif : on s'attend Ã  ce que f dÃ©crÃ©mente de 1.

On comprend en Ã©crivant `f` ainsi :
`let f x = 1 - x`

ğŸ’¡ La fonction qui dÃ©crÃ©mente de 1 peut s'Ã©crire :
`let f = (+) -1` ou `let f x = x - 1`

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_diplome.png)

# 6.

## LeÂ Â Â  RÃ©capâ€™

---

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_note.png)

Ã‡a en fait des choses juste sur les fonctions ğŸ˜‰

- Signature avec notation flÃ©chÃ©e
- Signature universelle `T -> U` grÃ¢ce Ã  type `Unit` et curryfication
- Fonction gÃ©nÃ©rique, fonction anonyme/lambda
- Fonction rÃ©cursive et *tail recursion*
- DiffÃ©rences entre fonctions et mÃ©thodes
- Fonctions et opÃ©rateurs standards, dont `|>`, `>>`
- Surcharger ou crÃ©er opÃ©rateur
- Notation *point-free*
- InteropÃ©rabilitÃ© avec la BCL

---

<!-- _class: end invert lead-->

# Merci ğŸ™
