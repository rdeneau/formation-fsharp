---
marp: true
html: true
theme: soat
title: 'Formation Fâ™¯ 5.0 â€¢ OrientÃ©-objet'
footer: 'Formation Fâ™¯ 5.0 â€¢ OrientÃ©-objet'
paginate: true
---

<!-- Chars: â‘  â‘¡ â‘¢  â’¶ â’· â’¸  â†’ â€¢ -->
<!-- Chars-Math : â‰ƒ â‰  â‰¡ âˆ€ âˆƒ âˆˆ âˆ… âˆ‘ âˆ â€¦ â”†ï½œ -->
<!-- Emoji : â˜ ğŸ‘‰ ğŸ‘Œ ğŸ‘ ğŸ‘ ğŸ’¡ âš ï¸ ğŸ˜• âœ… âŒ â— ğŸ’¥ -->
<!-- https://emojipedia.org -->

<!-- _class: title invert -->

# Formation Fâ™¯ 5.0

## *OrientÃ©-objet*

### DÃ©cembre 2021

---

<!-- _class: speaker invert -->

# About me

## Romain DENEAU

- SOAT depuis 2009
- Senior Developer Câ™¯ Fâ™¯ TypeScript
- PassionnÃ© de Craft
- Auteur sur le blog de SOAT

[![social-network](./themes/soat/pictos/social_twitter.png) DeneauRomain](https://twitter.com/DeneauRomain)

[![social-network](./themes/soat/pictos/social_github.png) rdeneau](https://github.com/rdeneau)

---

# Introduction

En Fâ™¯, orientÃ©-objet parfois \+ pratique que style fonctionnel.

Briques permettant l'orientÃ©-objet en Fâ™¯ :

1. Membres
   - MÃ©thodes, propriÃ©tÃ©s, opÃ©rateurs
   - Consistent Ã  attacher des fonctionnalitÃ©s directement dans le type
   - Permettent d'encapsuler l'Ã©tat (en particulier mutable) de l'objet
   - S'utilisent avec la notation "pointÃ©e" `my-object.my-member`
2. Interfaces et classes
   - Supports de l'abstraction par hÃ©ritage

---

<!-- _class: purple agenda invert lead -->

![bg right:30% h:300](./themes/soat/pictos/SOAT_pictos_formation.png)

## Sommaire

1. Membres : mÃ©thodes, propriÃ©tÃ©s, opÃ©rateurs
2. Extensions de type
3. Classe, structure
4. Interface
5. Expression objet

---

# Polymorphisme

4e pilier de l'orientÃ©-objet

En fait, il existe plusieurs polymorphismes. Les principaux :

1. Par sous-typage : celui Ã©voquÃ© classiquement avec l'orientÃ©-objet
   â†’ Type de base dÃ©finissant membres abstraits ou virtuels
   â†’ Sous-types en hÃ©ritant et implÃ©mentant ces membres
2. Ad hoc/overloading â†’ surcharge de membres de mÃªme nom
3. ParamÃ©trique â†’ gÃ©nÃ©riques en Câ™¯, Java, TypeScript
4. Structurel/duck-typing â†’ SRTP en Fâ™¯, typage structurel en TypeScript
5. Higher-kinded â†’ classes de type en Haskell

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_roles_grey.png)

# 1.

## LesÂ Â  Membres

---

# Membres

Ã‰lÃ©ments complÃ©mentaires dans dÃ©finition d'un type *(classe, record, union)*

- *(Ã‰vÃ©nement)*
- MÃ©thode
- PropriÃ©tÃ©
- PropriÃ©tÃ© indexÃ©e
- Surcharge d'opÃ©rateur

---

# Membres statiques et d'instance

Membre statique : `static member member-name ...`

Membre d'instance :
â†’ Membre concret : `member self-identifier.member-name ...`
â†’ Membre abstrait : `abstract member member-name : type-signature`
â†’ Membre virtuel = nÃ©cessite 2 dÃ©clarations
Â Â Â  1. Membre abstrait
Â Â Â  2. ImplÃ©mentation par dÃ©faut : `default self-identifier.member-name ...`
â†’ Surcharge d'un membre virtuel : `override self-identifier.member-name ...`

â˜ `member-name` en PascalCase *(convention .NET)*
â˜ Pas de membre `protected` ou `private` !

---

# *Self identifier*

En Câ™¯, Java, TypeScript : `this`
En VB : `Me`
En Fâ™¯ : au choix â†’ `this`, `self`, `me`, n'importe quel *identifier* valide...

DÃ©finissable de 3 maniÃ¨res complÃ©mentaires :

1. Pour le constructeur primaire : avec `as` â†’ `type MyClass() as self = ...`
2. Pour un membre : `member me.Introduce() = printfn $"Hi, I'm {me.Name}"`
3. Pour un membre ne l'utilisant pas : avec `_` â†’ `member _.Hi() = printfn "Hi!"`

---

# Appeler un membre

ğŸ’¡ MÃªmes rÃ¨gles quand Câ™¯

Appeler un membre statique
â†’ PrÃ©fixer par le nom du type : `type-name.static-member-name`

Appeler un membre d'instance Ã  l'intÃ©rieur du type
â†’ PrÃ©fixer avec *self-identifier* : `self-identifier.instance-member-name`

Appeler un membre d'instance depuis l'extÃ©rieur
â†’ PrÃ©fixer avec le nom de l'instance : `instance-name.instance-member-name`

---

# MÃ©thode

MÃ©thode â‰ƒ Fonction attachÃ©e directement Ã  un type

2 formes de dÃ©claration des paramÃ¨tres :

1. ParamÃ¨tres curryfiÃ©s = Style FP
2. ParamÃ¨tres en tuple = Style OOP
   - Meilleure interop avec Câ™¯
   - Seul mode autorisÃ© pour les constructeurs
   - Support des paramÃ¨tres nommÃ©s, optionnels, en tableau
   - Support des surcharges *(overloads)*

---

<!-- _footer: '' -->

# MÃ©thode (2)

```fs
// (1) Forme en tuple (la + classique)
type Product = { SKU: string; Price: float } with
    member this.TupleTotal(qty, discount) =
        (this.Price * float qty) - discount  // (A)

// (2) Forme currifiÃ©e
type Product' =
    { SKU: string; Price: float }
    member me.CurriedTotal qty discount =
        (me.Price * float qty) - discount  // (B)
```

â˜ `with` nÃ©cessaire en â‘  mais pas en â‘¡ Ã  cause de l'indentation
Â Â Â  â†’ `end` peut terminer le bloc commencÃ© avec `with`

â˜ `this.Price` â’¶ et `me.Price` â’·
Â Â Â  â†’ AccÃ¨s Ã  l'instance via le *self-identifier* dÃ©fini par le membre

---

<!-- _footer: '' -->

# Arguments nommÃ©s

Permet d'appeler une mÃ©thode tuplifiÃ©e en spÃ©cifiant le nom des paramÃ¨tres :

```fs
type SpeedingTicket() =
    member _.SpeedExcess(speed: int, limit: int) =
        speed - limit

    member x.CalculateFine() =
        if x.SpeedExcess(limit = 55, speed = 70) < 20 then 50.0 else 100.0
```

Pratique pour :
â†’ Clarifier un usage pour le lecteur ou le compilateur (en cas de surcharges)
â†’ Choisir l'ordre des arguments
â†’ Ne spÃ©cifier que certains arguments, les autres Ã©tant optionnels

â˜ Les arguments *aprÃ¨s un argument nommÃ©* sont forcÃ©ment nommÃ©s eux-aussi

---

# ParamÃ¨tres optionnels

Permet d'appeler une mÃ©thode tuplifiÃ©e sans spÃ©cifier tous les paramÃ¨tres.

ParamÃ¨tre optionnel :
â€¢ DÃ©clarÃ© avec `?` devant son nom â†’ `?arg1: int`
â€¢ Dans le corps de la mÃ©thode, wrappÃ© dans une `Option` â†’ `arg1: int option`
Â Â Â â†’ On peut utiliser `defaultArg` pour indiquer la **valeur par dÃ©faut**
Â Â Â â†’ Mais la valeur par dÃ©faut n'apparaÃ®t pas dans la signature !

Lors de l'appel de la mÃ©thode, l'argument est spÃ©cifiable au choix :
â€¢ Directement dans son type â†’ `M(arg1 = 1)`
â€¢ WrappÃ© dans une `Option` si nommÃ© avec prÃ©fixe `?` â†’ `M(?arg1 = Some 1)`

â˜ Autre syntaxe pour interop .NET : `[<Optional; DefaultParameterValue(...)>] arg`

---

# ParamÃ¨tres optionnels : exemples

```fs
type DuplexType = Full | Half

type Connection(?rate: int, ?duplex: DuplexType, ?parity: bool) =
    let duplex = defaultArg duplex Full
    let parity = defaultArg parity false
    let defaultRate = match duplex with Full -> 9600 | Half -> 4800
    let rate = defaultArg rate defaultRate
    do printfn "Baud Rate: %d â€¢ Duplex: %A â€¢ Parity: %b" rate duplex parity

let conn1 = Connection(duplex = Full)
let conn2 = Connection(?duplex = Some Half)
let conn3 = Connection(300, Half, true)
```

â˜ Noter le *shadowing* des paramÃ¨tres par des variables de mÃªme nom
`let parity (* bool *) = defaultArg parity (* bool option *) Full`

---

# Tableau de paramÃ¨tres

Permet de spÃ©cifier un nombre variable de paramÃ¨tres de mÃªme type
â†’ Via attribut `System.ParamArray` sur le **dernier** argument de la mÃ©thode

```fs
open System

type MathHelper() =
    static member Max([<ParamArray>] items) =
        items |> Array.max

let x = MathHelper.Max(1, 2, 4, 5)  // 5
```

ğŸ’¡ Ã‰quivalent en Câ™¯ de `public static T Max<T>(params T[] items)`

---

# Appeler mÃ©thode Câ™¯ *TryXxx()*

â“ Comment appeler en Fâ™¯ une mÃ©thode Câ™¯ `bool TryXxx(args, out T outputArg)` ?
*(Exemple : `int.TryParse`, `IDictionnary::TryGetValue`)*

- ğŸ‘ Utiliser Ã©quivalent Fâ™¯ de `out outputArg` mais utilise mutation ğŸ¤®
- âœ… Ne pas spÃ©cifier l'argument `outputArg`
  - Change le type de retour en tuple `bool * T`
  - `outputArg` devient le 2e Ã©lÃ©ment de ce tuple

```fs
  match System.Int32.TryParse text with
  | true, i  -> printf $"It's the number {value}."
  | false, _ -> printf $"{text} is not a number."
```

---

# Appeler mÃ©thode *Xxx(tuple)*

â“ Comment appeler une mÃ©thode dont 1er param est lui-mÃªme un tuple ?!

Essayons :

```fs
let friendsLocation = Map.ofList [ (0,0),"Peter" ; (1,0),"Jane" ]
// Map<(int * int), string>
let peter = friendsLocation.TryGetValue (0,0)
// ğŸ’¥ error FS0001: expression censÃ©e avoir le type `int * int`, pas `int`
```

ğŸ’¡ Explications : `TryGetValue(0,0)` = appel mÃ©thode en mode tuplifiÃ©
â†’ SpÃ©cifie 2 paramÃ¨tres, `0` et `0`.
â†’ `0` est un `int` alors qu'on attend un tuple `int * int` !

---

# Appeler mÃ©thode *Xxx(tuple)* - Solutions

1. ğŸ˜• Doubles parenthÃ¨ses, mais syntaxe confusante
   - `friendsLocation.TryGetValue((0,0))`
2. ğŸ˜• *Backward pipe*, mais confusant aussi
   - `friendsLocation.TryGetValue <| (0,0)`
3. âœ… Utiliser une fonction plutÃ´t qu'une mÃ©thode
   - `friendsLocation |> Map.tryFind (0,0)`

---

# MÃ©thode *vs* Fonction

| FonctionnalitÃ©         | Fonction | MÃ©thode currifiÃ©e | MÃ©thode tuplifiÃ©e |
|------------------------|----------|-------------------|-------------------|
| Application partielle  | âœ… oui    | âœ… oui             | âŒ non             |
| Arguments nommÃ©s       | âŒ non    | âŒ non             | âœ… oui             |
| ParamÃ¨tres optionnels  | âŒ non    | âŒ non             | âœ… oui             |
| Tableau de paramÃ¨tres  | âŒ non    | âŒ non             | âœ… oui             |
| Surcharge / *overload* | âŒ non    | âŒ non             | âœ… oui Â â‘           |

â‘  Si possible, prÃ©fÃ©rer paramÃ¨tres optionnels Ã  surcharge

---

# MÃ©thode *vs* Fonction (2)

| FonctionnalitÃ©        | Fonction      | MÃ©thode statique | MÃ©thode d'instance |
|-----------------------|---------------|------------------|--------------------|
| Nommage               | camelCase     | PascalCase       | PascalCase         |
| Support du `inline`   | âœ… oui         | âœ… oui            | âœ… oui              |
| RÃ©cursive             | âœ… si `rec`    | âœ… oui            | âœ… oui              |
| InfÃ©rence de `x` dans | `f x` â†’ âœ… oui | â–                | `x.M()` â†’ âŒ non    |
| Passable en argument  | âœ… oui : `g f` | âœ… oui : `g T.M`  | âŒ non : `g x.M` Â â‘  |

â‘  Alternative : wrappÃ©e dans lambda â†’ `g (fun x -> x.M())`

---

# PropriÃ©tÃ©s

â‰ƒ Sucre syntaxique masquant un *getter* et/ou un *setter*
â†’ Permet d'utiliser la propriÃ©tÃ© comme s'il s'agissait d'un champ

2 faÃ§ons de dÃ©clarer une propriÃ©tÃ© :
â€¢ DÃ©claration **explicite** : en relation avec un *backing field*
Â Â Â â†’ *Getter* : `member this.Property = expression`
Â Â Â â†’ Autres : verbeux *([dÃ©tails](https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/members/properties))* Â  ğŸ‘‰ PrÃ©fÃ©rer mÃ©thodes explicites
â€¢ DÃ©claration **automatique** : *backing field* implicite
Â Â Â â†’ *Read-only* :Â  `member val Property = value`
Â Â Â â†’ *Read/write* : `member val Property = value with get, set`

â˜ *Getter* Ã©valuÃ© Ã  chaque appel â‰  *Read-only* initialisÃ© Ã  la construction

---

# PropriÃ©tÃ©s - exemple

```fs
type Person = { First: string; Last: string } with
    member this.FullName = // Getter
        $"{this.Last.ToUpper()} {this.First}"

let joe = { First = "Joe"; Last = "Dalton" }
let s = joe.FullName  // "DALTON Joe"
```

---

# PropriÃ©tÃ©s et pattern matching

âš ï¸ Les propriÃ©tÃ©s ne sont pas dÃ©constructibles.
â†’ Peuvent participer Ã  un pattern matching que dans partie `when`

```fs
type Person = { First: string; Last: string } with
    member this.FullName = // Getter
        $"{this.Last.ToUpper()} {this.First}"

let joe = { First = "Joe"; Last = "Dalton" }
let { First = first } = joe  // val first : string = "Joe"
let { FullName = x } = joe
// ğŸ’¥ ~~~~~~~~ Error FS0039: undefined record label 'FullName'

let salut =
    match joe with
    | _ when joe.FullName = "DALTON Joe" -> "Salut, Joe !"
    | _ -> "Bonjour !"
// val salut : string = "Salut, Joe !"
```

---

# PropriÃ©tÃ©s indexÃ©es

Permet accÃ¨s par indice, comme si la classe Ã©tait un tableau : `instance.[index]`
â†’ IntÃ©ressant pour une collection ordonnÃ©e, pour masquer l'implÃ©mentation

Mise en place en dÃ©clarant membre `Item`

```fs
member self-identifier.Item
    with get(index) =
        get-member-body
    and set index value =
        set-member-body
```

ğŸ’¡ PropriÃ©tÃ© *read-only* (*write-only*) â†’ ne dÃ©clarer que le *getter* (*setter*)

â˜ ParamÃ¨tre en tuple pour *getter* â‰  paramÃ¨tres curryfiÃ©s *setter*

---

# PropriÃ©tÃ©s indexÃ©es : exemple

```fs
type Lang = En | Fr

type DigitLabel() =
    let labels = // Map<Lang, string[]>
        [| (En, [| "zero"; "one"; "two"; "three" |])
           (Fr, [| "zÃ©ro"; "un"; "deux"; "trois" |]) |] |> Map.ofArray

    member val Lang = En with get, set
    member me.Item with get(i) = labels.[me.Lang].[i]
    member _.En with get(i) = labels.[En].[i]

let digitLabel = DigitLabel()
let v1 = digitLabel.[1]     // "one"
digitLabel.Lang <- Fr
let v2 = digitLabel.[2]     // "deux"
let v3 = digitLabel.En(2)   // "two"
// ğŸ’¡ Notez la diffÃ©rence de syntaxe de l'appel Ã  la propriÃ©tÃ© `En`
```

---

# Slice

> Idem propriÃ©tÃ© indexÃ©e mais renvoie plusieurs valeurs

DÃ©finition : via mÃ©thode *(normale ou d'extension)* `GetSlice(?start, ?end)`

Usage : via opÃ©rateur `..`

```fs
type Range = { Min: int; Max: int } with
    member this.GetSlice(min, max) =
        { Min = System.Math.Max(defaultArg min this.Min, this.Min)
        ; Max = System.Math.Min(defaultArg max this.Max, this.Max) }

let range = { Min = 1; Max = 5 }
let slice1 = range.[0..3] // { Min = 1; Max = 3 }
let slice2 = range.[2..]  // { Min = 2; Max = 5 }
```

---

# Surcharge d'opÃ©rateur

OpÃ©rateur surchargÃ© Ã  2 niveaux possibles :

1. Dans un module, sous forme de fonction
   - `let [inline] (operator-symbols) parameter-list = ...`
   - ğŸ‘‰ Cf. session sur les fonctions
   - â˜ LimitÃ© : 1 seule surcharge possible
2. Dans un type, sous forme de membre
   - `static member (operator-symbols) (parameter-list) =`
   - MÃªmes rÃ¨gles que pour la forme de fonction
   - ğŸ‘ Plusieurs surcharges possibles (N types Ã— P *overloads*)

---

<!-- _footer: '' -->

# Surcharge d'opÃ©rateur : exemple

```fs
type Vector(x: float, y: float) =
    member _.X = x
    member _.Y = y

    override me.ToString() =
        let format n = (sprintf "%+.1f" n)
        $"Vector (X: {format me.X}, Y: {format me.Y})"

    static member (*)(a, v: Vector) = Vector(a * v.X, a * v.Y)
    static member (*)(v: Vector, a) = a * v
    static member (~-)(v: Vector) = -1.0 * v
    static member (+) (v: Vector, w: Vector) = Vector(v.X + w.X, v.Y + w.Y)

let v1 = Vector(1.0, 2.0)   // Vector (X: +1.0, Y: +2.0)
let v2 = v1 * 2.0           // Vector (X: +2.0, Y: +4.0)
let v3 = 0.75 * v2          // Vector (X: +1.5, Y: +3.0)
let v4 = -v3                // Vector (X: -1.5, Y: -3.0)
let v5 = v1 + v4            // Vector (X: -0.5, Y: -1.0)
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_alEchelle.png)

# 2.

## Extensions deÂ type

---

# Extension deÂ type

Membres d'un type dÃ©finis hors de son bloc `type` principal.

Chacun de ces membres est appelÃ© une **augmentation**.

3 catÃ©gories d'extension :

- Extension intrinsÃ¨que
- Extension optionnelle
- MÃ©thodes d'extension

---

# Extension intrinsÃ¨que

DÃ©finie dans mÃªme fichier et mÃªme namespace que le type
â†’ Membres intÃ©grÃ©s au type Ã  la compilation, visibles par *Reflection*

ğŸ’¡ **Cas d'usage**

DÃ©clarer successivement :
**1.** Type (ex : `type List`)
**2.** Module compagnon de ce type (ex : fonction `List.length list`)
**3.** Extension utilisant ce module compagnon (ex : membre `list.Length`)

ğŸ‘‰ FaÃ§on \+ propre en FP de sÃ©parer les fonctionnalitÃ©s des donnÃ©es
ğŸ’¡ InfÃ©rence de types marche mieux avec fonctions que membres

---

# Extension intrinsÃ¨que - Exemple

```fs
namespace Example

type Variant =
    | Num of int
    | Str of string

module Variant =
    let print v =
        match v with
        | Num n -> printf "Num %d" n
        | Str s -> printf "Str %s" s

// Add a member to Variant as an extension
type Variant with
    member x.Print() = Variant.print x
```

---

# Extension optionnelle

Extension dÃ©finie en-dehors du module/namespace/assembly du type Ã©tendu.

ğŸ’¡ Pratique pour les types dont la dÃ©claration n'est pas modifiable directement,
Â Â Â Â Â par exemple ceux issus d'une librairie.

```fs
module EnumerableExtensions

open System.Collections.Generic

type IEnumerable<'T> with
    /// Repeat each element of the sequence n times
    member xs.RepeatElements(n: int) =
        seq {
            for x in xs do
                for _ in 1 .. n -> x
        }
```

---

# Extension optionnelle (2)

**Compilation :** en mÃ©thode statique â†’ version simplifiÃ©e :

```cs
public static class Extensions
{
    public static IEnumerable<T> RepeatElements<T>(IEnumerable<T> xs, int n) {...}
}
```

**Usage :** comme un vrai membre, aprÃ¨s avoir importÃ© son module :

```fs
open Extensions

let x = [1..3].RepeatElements(2) |> List.ofSeq
// [1; 1; 2; 2; 3; 3]
```

---

# Extension optionnelle - Autre exemple

```fs
// File Person.fs
type Person = { First: string; Last: string }

// File PersonExtensions.fs
module PersonExtensions =
    type Person with
        member this.FullName =
            $"{this.Last.ToUpper()} {this.First}"

// Usage elsewhere
open PersonExtensions
let joe = { First = "Joe"; Last = "Dalton" }
let s = joe.FullName  // "DALTON Joe"
```

---

# Extension optionnelle - Limites

- Doit Ãªtre dÃ©clarÃ©e dans un module
- Pas compilÃ©e dans le type, pas visible par Reflection
- Membres visibles qu'en F#, invisibles en C#

---

# Extension de type et surcharges

â˜ ImplÃ©menter des surcharges :
â†’ RecommandÃ© dans la dÃ©claration initiale du type âœ…
â†’ DÃ©conseillÃ© dans une extension de type â›”

```fs
type Variant = Num of int | Str of string with
    override this.ToString() = ... âœ…

module Variant = ...

type Variant with
    override this.ToString() = ... âš ï¸
    // Warning FS0060: Override implementations in augmentations are now deprecated...
```

---

# Extension de type et alias de type

Sont incompatibles :

```fs
type i32 = System.Int32

type i32 with
    member this.IsEven = this % 2 = 0
// ğŸ’¥ Error FS0964: Les abrÃ©viations de type ne peuvent pas avoir d'augmentations
```

ğŸ’¡ **Solution :** il faut utiliser le vrai nom du type

```fs
type System.Int32 with
    member this.IsEven = this % 2 = 0
```

â˜ Les tuples F# tels que `int * int` ne peuvent pas Ãªtre augmentÃ©s ainsi.
â†’ Mais on peut avec une mÃ©thode d'extension Ã  la C# ğŸ“

---

# Extension de type - Limite

Extension autorisÃ©e sur type gÃ©nÃ©rique sauf quand contraintes diffÃ¨rent :

```fs
open System.Collections.Generic

type IEnumerable<'T> with
    member this.Sum() = Seq.sum this
// ğŸ’¥      ~~~~~~~~~~ Error FS0670
// Ce code n'est pas suffisamment gÃ©nÃ©rique. Impossible de gÃ©nÃ©raliser la variable de type
// ^T when ^T: (static member get_Zero: -> ^T) and ^T: (static member (+) : ^T * ^T -> ^T)

// â˜ Cette contrainte provient de `Seq.sum`
```

**Solution :** mÃ©thode d'extension Ã  la C# ğŸ“

---

<!-- _footer: '' -->

# MÃ©thode d'extension

MÃ©thode statique :
â€¢ DÃ©corÃ©e de `[<Extension>]`
â€¢ DÃ©finie dans classe `[<Extension>]`
â€¢ Type du 1er argument = type Ã©tendu *(`IEnumerable<'T>` ci-dessous)*

```fs
namespace Extensions

open System.Collections.Generic
open System.Runtime.CompilerServices

[<Extension>]
type EnumerableExtensions =
    [<Extension>]
    static member inline Sum(xs: IEnumerable<'T>) = Seq.sum xs

// ğŸ’¡ `inline` est nÃ©cessaire
```

---

<!-- _footer: '' -->

# MÃ©thode d'extension - Exemple simplifiÃ©

```fs
open System.Runtime.CompilerServices

[<Extension>]
type EnumerableExtensions =
    [<Extension>]
    static member inline Sum(xs: seq<_>) = Seq.sum xs

let x = [1..3].Sum()
//------------------------------
// Output en console FSI (syntaxe verbeuse) :
type EnumerableExtensions =
  class
    static member
      Sum : xs:seq<^a> -> ^a
              when ^a : (static member ( + ) : ^a * ^a -> ^a)
              and  ^a : (static member get_Zero : -> ^a)
  end
val x : int = 6
```

---

# MÃ©thode d'extension - DÃ©compil' en C#

Pseudo-Ã©quivalent en C# :

```cs
using System.Collections.Generic;

namespace Extensions
{
    public static class EnumerableExtensions
    {
        public static TSum Sum<TItem, TSum>(this IEnumerable<TItem> source) {...}
    }
}
```

â˜ **Note :** en vrai, il y a plein de `Sum()` dans LINQ pour chaque type : `int`, `float`â€¦
â†’ [*Code source*](https://github.com/dotnet/runtime/blob/main/src/libraries/System.Linq/src/System/Linq/Sum.cs)

---

# MÃ©thode d'extension - Tuples

On peut ajouter une mÃ©thode d'extension Ã  tout tuple F# :

```fs
open System.Runtime.CompilerServices

[<Extension>]
type EnumerableExtensions =
    [<Extension>]
    // static member IsDuplicate : ('a * 'a) -> bool when 'a : equality
    static member inline IsDuplicate((x, y)) =
        x = y

let b1 = (1, 1).IsDuplicate()  // true
let b2 = ("a", "b").IsDuplicate()  // false
```

---

# Extensions - Comparatif

| FonctionnalitÃ©      | Extension de type            | MÃ©thode d'extension    |
|---------------------|------------------------------|------------------------|
| MÃ©thodes            | âœ… instance, âœ… statique       | âœ… instance, âŒ statique |
| PropriÃ©tÃ©s          | âœ… instance, âœ… statique       | âŒ *Non supportÃ©*       |
| Constructeurs       | âœ… intrinsÃ¨que, âŒ optionnelle | âŒ *Non supportÃ©*       |
| Ã‰tendre contraintes | âŒ *Non supportÃ©*             | âœ… *Supporte SRTP*      |

---

# Extensions - Limites

Ne participent pas au polymorphisme :

- Pas dans table virtuelle
- Pas de membre `virtual`, `abstract`
- Pas de membre `override` *(mais surcharges ğŸ‘Œ)*

---

# Extensions *vs* classe partielle Câ™¯

| FonctionnalitÃ©      | Multi-fichiers | CompilÃ© dans type | Tout type             |
|---------------------|----------------|-------------------|-----------------------|
| Classe partielle Câ™¯ | âœ… Oui         | âœ… Oui            | Que `partial class`  |
| ExtensÂ° intrinsÃ¨que | âŒ Non         | âœ… Oui            | âœ… Oui               |
| ExtensÂ° optionnelle | âœ… Oui         | âŒ Non            | âœ… Oui               |

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_travail.png)

# 3.

## Classe & Structure

---

# Classe

Classe en Fâ™¯ â‰¡ classe en Câ™¯
â†’ Brique de base pour l'orientÃ©-objet
â†’ Constructeur d'objets contenant des donnÃ©es de type dÃ©fini et des mÃ©thodes

DÃ©finition d'une classe
â†’ Commence par `type` *(comme tout type en Fâ™¯)*
â†’ Nom de la classe gÃ©nÃ©ralement suivi du **constructeur primaire**

```fs
type CustomerName(firstName: string, lastName: string) =
    // Corps du constructeur primaire
    // Membres...
```

â˜ ParamÃ¨tres `firstName` et `lastName` visibles dans tout le corps de la classe

---

# Classe gÃ©nÃ©rique

ParamÃ¨tres gÃ©nÃ©riques Ã  spÃ©cifier car non infÃ©rÃ©s

```fs
type Tuple2_KO(item1, item2) = // âš ï¸ 'item1' et 'item2': type 'obj' !
    // ...

type Tuple2<'T1, 'T2>(item1: 'T1, item2: 'T2) =  // ğŸ‘Œ
    // ...
```

---

# Classe : constructeur secondaire

Syntaxe pour dÃ©finir un autre constructeur :
`new(argument-list) = constructor-body`

â˜ Doit appeler le constructeur primaire !

```fs
type Point(x: float, y: float) =
    new() = Point(0, 0)
    // Membres...
```

â˜ ParamÃ¨tres des constructeurs : que en tuple, pas curryfiÃ©s !

---

# Instanciation

Appel d'un des constructeurs, avec arguments en tuple
â†’ Ne pas oublier `()` si aucun argument, sinon on obtient une fonction !

Dans un `let` binding : `new` optionnel et non recommandÃ©
â†’ `let v = Vector(1.0, 2.0)` ğŸ‘Œ
â†’ `let v = new Vector(1.0, 2.0)` âŒ

Dans un `use` binding : `new` obligatoire
â†’ `use d = new Disposable()`

---

# Initialisation des propriÃ©tÃ©s

On peut initialiser des propriÃ©tÃ©s avec setter Ã  l'instanciation
â†’ Les spÃ©cifier en tant que **arguments nommÃ©s** dans l'appel au constructeur
â†’ Les placer aprÃ¨s les Ã©ventuels arguments du constructeur :

```fs
type PersonName(first: string) =
    member val First = first with get, set
    member val Last = "" with get, set

let p1 = PersonName("John")
let p2 = PersonName("John", Last="Paul")
let p3 = PersonName(first="John", Last="Paul")
```

ğŸ’¡ Ã‰quivalent de la syntaxe Câ™¯ `new PersonName("John") { Last = "Paul" }`

---

# Classe abstraite

AnnotÃ©e avec `[<AbstractClass>]`

Un des membres est **abstrait** :

1. DÃ©clarÃ© avec mot clÃ© `abstract`
2. Pas d'implÃ©mentation par dÃ©faut avec mot clÃ© `default`
   *(Sinon le membre est virtuel)*

HÃ©ritage via mot clÃ© `inherit`
â†’ Suivi de l'appel au constructeur de la classe de base

---

<!-- _footer: '' -->

# Classe abstraite : exemple

```fs
[<AbstractClass>]
type Shape2D() =
    member val Center = (0.0, 0.0) with get, set
    member this.Move(?deltaX: float, ?deltaY: float) =
        let x, y = this.Center
        this.Center <- (x + defaultArg deltaX 0.0,
                        y + defaultArg deltaY 0.0)
    abstract GetArea : unit -> float
    abstract Perimeter : float  with get

type Square(side) =
    inherit Shape2D()
    member val Side = side
    override _.GetArea () = side * side
    override _.Perimeter = 4.0 * side

let o = Square(side = 2.0, Center = (1.0, 1.0))
printfn $"S={o.Side}, A={o.GetArea()}, P={o.Perimeter}"  // S=2, A=4, P=8
o.Move(deltaY = -2.0)
printfn $"Center {o.Center}"  // Center (1, -1)
```

---

# Champs

Convention de nommage : camelCase

2 types de champs : implicite ou explicite

- Implicite â‰ƒ Variable Ã  l'intÃ©rieur du constructeur primaire
- Explicite â‰¡ Champ classique d'une classe en Câ™¯ / Java

---

# Champ implicite

Syntaxe :
â€¢ VariableÂ  : `[static] let [ mutable ] variable-name = expression`
â€¢ Fonction : `[static] let [ rec ] function-name function-args = expression`

â˜ **Notes**

- DÃ©clarÃ© avant les membres de la classe
- Valeur initiale obligatoire
- PrivÃ©
- S'utilise sans devoir prÃ©fixer par le `self-identifier`

---

# Champ implicite d'instance : exemple

```fs
type Person(firstName: string, lastName: string) =
    let fullName = $"{firstName} {lastName}"
    member _.Hi() = printfn $"Hi, I'm {fullName}!"

let p = Person("John", "Doe")
p.Hi()  // Hi, I'm John Doe!
```

---

# Champ implicite statique : exemple

```fs
type K() =
    static let mutable count = 0

    // do binding exÃ©cutÃ© Ã  chaque construction
    do
        count <- count + 1

    member _.CreatedCount = count

let k1 = K()
let count1 = k1.CreatedCount  // 1
let k2 = K()
let count2 = k2.CreatedCount  // 2
```

---

# Champ explicite

DÃ©claration du type, sans valeur initiale :
`val [ mutable ] [ access-modifier ] field-name : type-name`

- `val mutable a: int` â†’ champ publique
- `val a: int` â†’ champ interne `a@` + propriÃ©tÃ© `a => a@`

---

<!-- _footer: '' -->

# Champ *vs* propriÃ©tÃ©

```fs
// Champs explicites readonly
type C1 =
    val a: int
    val b: int
    val mutable c: int
    new(a, b) = { a = a; b = b; c = 0 } // ğŸ’¡ Constructeur 2ndaire "compacte"

// VS propriÃ©tÃ©s readonly => ordre inversÃ© dans SharpLab : b avant a
type C2(a: int, b: int) =
    member _.A = a
    member _.B = b
    member _.C = 0

// VS propriÃ©tÃ©s auto-implÃ©mentÃ©es
type C3(a: int, b: int) =
    member val A = a
    member val B = b with get
    member val C = 0 with get, set
```

---

# Champ explicite ou implicite ou propriÃ©tÃ©

Champ explicite **peu utilisÃ©** :
â†’ Ne concerne que les classes et structures
â†’ Utile avec fonction native manipulant la mÃ©moire directement
Â Â Â Â *(Car ordre des champs prÃ©servÃ©s - cf. [SharpLab](https://sharplab.io/#v2:DYLgZgzgNAJiDUAfA9MgBAYQBYEMC2ADhGgKYAeBwAlgMZUAuJxATiTjAPYB2wAngLAAoerwIlMARjQBeIWnloAbjmBocINFS705C5aoBGGrTsEK0XEgHcAFDihoDAShloA3mtc4A3I9cHfAF80VDRAXg3AQp3Mbgh6ZgBXGkZ45jQAJi4YHCpWNAAiGg5CHCSSPKEhUIA1AGU0AmYOBqoAS/oWljZOHgFhUXEMNLtjbQcjTW0XWTMFPBI8AxJUgH0AOgBBL115OYWltDWAIX8hIA===))*
â†’ Besoin d'une variable `[<ThreadStatic>]`
â†’ Interaction avec classe Fâ™¯ de code gÃ©nÃ©rÃ© sans constructeur primaire

Champ implicite - `let` binding
â†’ Variable intermÃ©diaire lors de la construction

Autres cas d'usages â†’ propriÃ©tÃ© auto-implÃ©mentÃ©e
â†’ Exposer une valeur â†’ `member val`
â†’ Exposer un "champ" mutable â†’ `member val ... with get, set`

---

# Structures

Alternatives aux classes mais \+ limitÃ©s / hÃ©ritage et rÃ©cursivitÃ©

MÃªme syntaxe que pour les classes mais avec en plus :

- Soit attribut `[<Struct>]`
- Soit bloc `struct...end` *(frÃ©quent)*

```fs
type Point =
    struct
        val mutable X: float
        val mutable Y: float
        new(x, y) = { X = x; Y = y }
    end

let x = Point(1.0, 2.0)
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_frontEnd_orange.png)

# 4.

## LesÂ Â Â Â  Interfaces

---

# Interface - Syntaxe

Idem classe abstraite avec :
â€¢ Que des membres abstraits, dÃ©finis par signature
â€¢ Sans l'attribut `[<AbstractClass>]`

```fs
type [accessibility-modifier] interface-name =
    abstract memberN : [ argument-typesN -> ] return-typeN
```

â€¢ Nom d'une interface commence par `I` pour suivre convention .NET
â€¢ Les arguments peuvent Ãªtre nommÃ©s *(sans parenthÃ¨ses sinon ğŸ’¥)*

```fs
type IPrintable =
    abstract member Print : format:string -> unit
```

---

# Interface - ImplÃ©mentation

2 maniÃ¨res d'implÃ©menter une interface :

1. Dans un type *(comme en Câ™¯)*
2. Dans une expression objet ğŸ“

---

# ImplÃ©mentation dans un type

```fs
type IPrintable =
    abstract member Print : unit -> unit

type Range = { Min: int; Max: int } with
    interface IPrintable with
        member this.Print() = printfn $"[{this.Min}..{this.Max}]"
```

âš ï¸ **PiÃ¨ge :** mot clÃ© `interface` en Fâ™¯
Â â‰  mot clÃ© `interface` en Câ™¯, Java, TS
Â â‰¡ mot clÃ© `implements` Java, TS

---

# ImplÃ©mentation dans une expression objet

```fs
type IConsole =
    abstract ReadLine : unit -> string
    abstract WriteLine : string -> unit

let console =
    { new IConsole with
        member this.ReadLine () = Console.ReadLine ()
        member this.WriteLine line = printfn "%s" line }
```

---

# Interface - ImplÃ©mentation par dÃ©faut

Fâ™¯ 5.0 supporte les interfaces dÃ©finissant des mÃ©thodes avec implÃ©mentations par dÃ©faut Ã©crites en Câ™¯ 8+ mais ne permet pas de les dÃ©finir.

âš ï¸ Mot clÃ© `default` : supportÃ© que dans les classes, pas dans les interfaces !

---

# Une interface Fâ™¯ est explicite

ImplÃ©mentation d'une interface en Fâ™¯
â‰¡ ImplÃ©mentation explicite d'une interface en Câ™¯

â†’ Les mÃ©thodes de l'interface ne sont consommables que par *upcasting* :

```fs
type IPrintable =
    abstract member Print : unit -> unit

type Range = { Min: int; Max: int } with
    interface IPrintable with
        member this.Print() = printfn $"[{this.Min}..{this.Max}]"

let range = { Min = 1; Max = 5 }
(range :> IPrintable).Print()  // OpÃ©rateur `:>` de upcast ğŸ“
// [1..5]
```

---

# ImplÃ©mentation d'une interface gÃ©nÃ©rique

```fs
type IValue<'T> =
    abstract member Get : unit -> 'T

type BiValue() =
    interface IValue<int> with
        member _.Get() = 1
    interface IValue<string> with
        member _.Get() = "hello"

let o = BiValue()
let i = (o :> IValue<int>).Get() // 1
let s = (o :> IValue<string>).Get() // "hello"
```

---

<!-- _footer: '' -->

# HÃ©ritage

DÃ©fini avec mot clÃ© `inherit`

```fs
type Base(x: int) =
    do
        printf "Base: "
        for i in 1..x do printf "%d " i
        printfn ""

type Child(y: int) =
    inherit Base(y * 2)
    do
        printf "Child: "
        for i in 1..y do printf "%d " i
        printfn ""

let child = Child(1)

// Base: 1 2 3 4
// Child: 1
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_smiley4.png)

# 5.

## Expression Objet

---

# Expression objet

Expression permettant d'implÃ©menter Ã  la volÃ©e un type abstrait
â†’ Similaire Ã  une classe anonyme en Java

```fs
let makeResource (resourceName: string) =
    printfn $"create {resourceName}"
    { new System.IDisposable with
        member _.Dispose() =
            printfn $"dispose {resourceName}" }
```

â˜ La signature de `makeResource` est `string -> System.IDisposable`.

---

<!-- _footer: '' -->

# ImplÃ©menter 2 interfaces

Possible mais 2e interface non consommable facilement et sÃ»rement

```fs
let makeDelimiter (delim1: string, delim2: string, value: string) =
    { new System.IFormattable with
        member _.ToString(format: string, _: System.IFormatProvider) =
            if format = "D" then
                delim1 + value + delim2
            else
                value
      interface System.IComparable with
        member _.CompareTo(_) = -1 }

let o = makeDelimiter("<", ">", "abc")
// val o : System.IFormattable
let s = o.ToString("D", System.Globalization.CultureInfo.CurrentCulture)
// val s : string = "<abc>"
let i = (d :?> System.IComparable).CompareTo("cde")  // â— Dangereux
// val i : int = -1
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](./themes/soat/pictos/SOAT_pictos_diplome.png)

# 6.

## Recommandations pourÂ l'orientÃ©-objet

---

# Pas d'orientÃ©-objet lÃ  oÃ¹ Fâ™¯ est bon

InfÃ©rence marche mieux avec fonction(objet) que objet.membre

**HiÃ©rarchie simple d'objets**
âŒ Ã‰viter hÃ©ritage
âœ… PrÃ©fÃ©rer type *Union* et *pattern matching* exhaustif, \+ simple en gÃ©nÃ©ral
â†’ En particulier les types rÃ©cursifs comme les arbres, Ã©paulÃ©s par fonction `fold`
â†’ https://fsharpforfunandprofit.com/series/recursive-types-and-folds/

**Ã‰galitÃ© structurelle**
âŒ Ã‰viter classe *(Ã©galitÃ© par rÃ©fÃ©rence par dÃ©faut)*
âœ… PrÃ©fÃ©rer un *Record* ou une *Union*
â“ Envisager Ã©galitÃ© structurelle custom / performance
â†’ https://www.compositional-it.com/news-blog/custom-equality-and-comparison-in-f/

---

# OrientÃ©-objet recommandÃ©

1. Encapsuler Ã©tat mutable â†’ dans une classe
2. Grouper fonctionnalitÃ©s â†’ dans une interface
3. API expressive et user-friendly â†’ mÃ©thodes tuplifiÃ©es
4. API Fâ™¯ consommÃ©e en Câ™¯ â†’ membres d'extension
5. Gestion des dÃ©pendances â†’ injection dans constructeur
6. DÃ©passer limites des fonctions d'ordre supÃ©rieur

---

# Classe pour encapsuler Ã©tat mutable

```fs
// ğŸ˜• Encapsuler Ã©tat mutable dans une closure â†’ fonction impure contre-intuitif âš ï¸
let counter =
    let mutable count = 0
    fun () ->
        count <- count + 1
        count

let x = counter ()  // 1
let y = counter ()  // 2

// âœ… Encapsuler Ã©tat mutable dans une classe
type Counter() =
    let mutable count = 0   // Champ privÃ©
    member _.Next() =
        count <- count + 1
        count
```

---

# Interface pour grouper fonctionnalitÃ©s

```fs
let checkRoundTrip serialize deserialize value =
    value = (value |> serialize |> deserialize)
// val checkRoundTrip :
//   serialize:('a -> 'b) -> deserialize:('b -> 'a) -> value:'a -> bool
//     when 'a : equality
```

`serialize` et `deserialize` forment un groupe cohÃ©rent
â†’ Les grouper dans un objet

```fs
let checkRoundTrip serializer data =
    data = (data |> serializer.Serialize |> serializer.Deserialize)
```

---

# Interface pour grouper fonctionnalitÃ©s (2)

ğŸ’¡ PrÃ©fÃ©rer une interface Ã  un *Record*

```fs
// âŒ Ã‰viter : ce n'est pas un bon usage d'un Record
type Serializer<'T> = {
    Serialize: 'T -> string
    Deserialize: string -> 'T
}

// âœ… RecommandÃ©
type Serializer =
    abstract Serialize<'T> : value: 'T -> string
    abstract Deserialize<'T> : data: string -> 'T
```

â†’ ParamÃ¨tres sont nommÃ©s dans les mÃ©thodes
â†’ Objet facilement instanciable avec une expression objet

---

# API expressive

```fs
// âŒ Ã‰viter                        // âœ… PrÃ©fÃ©rer
                                    [<AbstractClass; Sealed>]
module Utilities =                  type Utilities =
    let name = "Bob"                    static member Name = "Bob"
    let add2 x y = x + y                static member Add(x,y) = x + y
    let add3 x y z = x + y + z          static member Add(x,y,z) = x + y + z
    let log x = ...                     static member Log(x, ?retryPolicy) = ...
    let log' x retryPolicy = ...
```

â†’ MÃ©thode `Add` surchargÃ©e *vs* `add2`, `add3`
â†’ Une seule mÃ©thode `Log` avec paramÃ¨tre optionnel `retryPolicy`

ğŸ”— [Fâ™¯ component design guidelines - Libraries used in Câ™¯](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/component-design-guidelines#guidelines-for-libraries-for-use-from-other-net-languages)

---

# API Fâ™¯ consommÃ©e en Câ™¯ - Type

Ne pas exposer ce type tel quel :

```fs
type RadialPoint = { Angle: float; Radius: float }

module RadialPoint =
    let origin = { Angle = 0.0; Radius = 0.0 }
    let stretch factor point = { point with Radius = point.Radius * factor }
    let angle (i: int) (n: int) = (float i) * 2.0 * System.Math.PI / (float n)
    let circle radius count =
        [ for i in 0..count-1 -> { Angle = angle i count; Radius = radius } ]
```

---

# API Fâ™¯ consommÃ©e en Câ™¯ - Type (2)

ğŸ’¡ Pour faciliter la dÃ©couverte du type et l'usage de ses fonctionnalitÃ©s en Câ™¯

- Mettre le tout dans un namespace
- Augmenter le type avec fonctionnalitÃ©s du module compagnon

```fs
namespace Fabrikam

type RadialPoint = {...}
module RadialPoint = ...

type RadialPoint with
    static member Origin = RadialPoint.origin
    static member Circle(radius, count) = RadialPoint.circle radius count |> List.toSeq
    member this.Stretch(factor) = RadialPoint.stretch factor this
```

---

# API Fâ™¯ consommÃ©e en Câ™¯ - Type (3)

ğŸ‘‰ L'API consommÃ©e en Câ™¯ est +/- Ã©quivalente Ã  :

```cs
namespace Fabrikam
{
    public static class RadialPointModule { ... }

    public sealed record RadialPoint(double Angle, double Radius)
    {
        public static RadialPoint Origin => RadialPointModule.origin;

        public static IEnumerable<RadialPoint> Circle(double radius, int count) =>
            RadialPointModule.circle(radius, count);

        public RadialPoint Stretch(double factor) =>
            new RadialPoint(Angle@, Radius@ * factor);
    }
}
```

---

# Gestion des dÃ©pendances - Technique FP

**ParamÃ©trisation des dÃ©pendances + application partielle**
â†’ Marche Ã  petite dose : peu de dÃ©pendances, peu de fonctions concernÃ©es
â†’ Sinon, vite pÃ©nible Ã  coder et Ã  utiliser ğŸ¥±

```fs
module MyApi =
    let function1 dep1 dep2 dep3 arg1 = doStuffWith dep1 dep2 dep3 arg1
    let function2 dep1 dep2 dep3 arg2 = doStuffWith' dep1 dep2 dep3 arg2
```

---

# Gestion des dÃ©pendances - Technique OO

**Injection de dÃ©pendances**
â†’ Injecter les dÃ©pendances dans le constructeur de la classe
â†’ Utiliser ces dÃ©pendances dans les mÃ©thodes

ğŸ‘‰ Offre une API \+ user-friendly ğŸ‘

```fs
type MyParametricApi(dep1, dep2, dep3) =
    member _.Function1 arg1 = doStuffWith dep1 dep2 dep3 arg1
    member _.Function2 arg2 = doStuffWith' dep1 dep2 dep3 arg2
```

âœ… ParticuliÃ¨rement recommandÃ© pour encapsuler des **effets de bord** :
â†’ Connexion Ã  une BDD, lecture de settings...

---

# Gestion des dÃ©pendances - Techniques FP++

*Dependency rejection* = pattern sandwich
â†’ Rejeter dÃ©pendances dans couche Application, hors de couche Domaine
â†’ Puissant et simple ğŸ‘
â†’ ... quand c'est adaptÃ© â—

Monade _Reader_
â†’ Pour fans de Haskell, sinon trop disruptif ğŸ˜±

Etc. https://fsharpforfunandprofit.com/posts/dependencies/

---

# Limites des fonctions d'ordre supÃ©rieur

Mieux vaut passer un objet plutÃ´t qu'une lambda
en paramÃ¨tre d'une fonction d'ordre supÃ©rieure quand :

1. Lambda est une **commande** `'T -> unit`
   âœ… PrÃ©fÃ©rer dÃ©clencher un effet de bord via un objet
   â†’ `type ICommand = abstract Execute : 'T -> unit`
2. Arguments de la lambda pas explicites
   âŒ `let test (f: float -> float -> string) =...`
   âœ… Solution 1 : type wrappant les 2 args `float`
   â†’ `f: Point -> string` avec `type Point = { X: float; Y: float }`
   âœ… Solution 2 : interface + mÃ©thode pour avoir paramÃ¨tres nommÃ©s
   â†’ `type IXxx = abstract Execute : x:float -> y:float -> string`

---

<!-- _footer: '' -->

# Limites des fonctions d'ordre supÃ©rieur (2)

3. Lambda "vraiment" gÃ©nÃ©rique

```fs
let test42 (f: 'T -> 'U) =
    f 42 = f "42"
// âŒ ^^     ~~~~
// ^^ Cette construction rend le code moins gÃ©nÃ©rique :
//    paramÃ¨tre de type 'T contraint de reprÃ©senter le type `int`
// ~~ Type `int` attendu != type `string` actuel
```

âœ… Solution : wrapper la fonction dans un objet

```fs
type Func2<'U> =
    abstract Invoke<'T> : 'T -> 'U

let test42 (f: Func2<'U>) =
    f.Invoke 42 = f.Invoke "42"
```

---

<!-- _class: end invert lead-->

# Merci ğŸ™
