---
marp: true
html: true
theme: 'd-edge'
title: 'F‚ôØ Training ‚Ä¢ Collections'
footer: 'F‚ôØ Training ‚Ä¢ Collections'
paginate: true
---

<!-- _class: title invert -->

# F‚ôØ Training

## *Les collections*

### 2025 April

---

<!-- _class: toc agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- Vue d'ensemble
- Types
- Fonctions

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_tendance.png)

# 1.

## Vue¬†¬†¬†¬†¬† d'ensemble

---

# üîç Types et Modules

5 collections usuelles en F‚ôØ avec leur module associ√©

| Module  | Type          | Alias BCL                     | Immutable | Tri√© par     |
|---------|---------------|-------------------------------|-----------|--------------|
| `Array` | `'T array`    | ‚â° `Array<T>`                  | ‚ùå         | Ordre ajout  |
| `List`  | `'T list`     | ‚âÉ `ImmutableList<T>`          | ‚úÖ         | Ordre ajout  |
| `Seq`   | `seq<'T>`     | ‚â° `IEnumerable<T>`            | ‚úÖ         | Ordre ajout  |
| `Set`   | `Set<'T>`     | ‚âÉ `ImmutableHashSet<T>`       | ‚úÖ         | Valeur       |
| `Map`   | `Map<'K, 'V>` | ‚âÉ `ImmutableDictionary<K, V>` | ‚úÖ         | Cl√©          |

---

# üëç Homog√©n√©it√© des fonctions

Communes aux 5 modules :
‚Üí `empty`/`isEmpty`, `exists`/`forall`
‚Üí `find`/`tryFind`, `pick`/`tryPick`, `contains` (`containsKey` pour `Map`)
‚Üí `map`/`iter`, `filter`, `fold`

Communes √† `Array`, `List`, `Seq` :
‚Üí `append`/`concat`, `choose`, `collect`
‚Üí `item`, `head`, `last`
‚Üí `take`, `skip`
‚Üí ... *une centaine de fonctions en tout !*

---

# üëç Homog√©n√©it√© de la syntaxe

| Type    | √âl√©ments       | *Range*        | *Comprehension* |
|---------|----------------|----------------|-----------------|
| `Array` | `[‚à£ 1; 2 ‚à£]`   | `[‚à£ 1..5 ‚à£]`   | ...             |
| `List`  | `[ 1; 2 ]`     | `[ 1..5 ]`     | ...             |
| `Seq`   | `seq { 1; 2 }` | `seq { 1..5 }` | ...             |
| `Set`   | `set [ 1; 2 ]` | `set [ 1..5 ]` | ...             |

---

# ‚ö†Ô∏è Pi√®ge de la syntaxe

Les crochets `[]` sont utilis√©s pour :

- *Valeur* : instance d'une liste `[ 1; 2 ]` (de type `int list`)
- *Type* : tableau `int []`, par ex. de `[| 1; 2 |]`

‚òù **Recommendations**

- Bien distinguer type *vs* valeur ‚ùó
- Pr√©f√©rer √©crire `int array` plut√¥t que `int []`
  - *N.B. En console FSI, le type affich√© est encore `int []`*

---

# Cr√©ation par *Comprehension*

- Syntaxe similaire √† boucle `for`
- M√™me principe que g√©n√©rateurs en C‚ôØ, JS
  - Mot cl√© `yield` mais souvent optionnel (F‚ôØ 4.7 / .NET Core 3)
  - Mot cl√© `yield!` ‚â° `yield*` JS
  - Fonctionne pour toutes les collections üëç

---

# *Comprehension* : exemples

```fs
// Syntaxes √©quivalentes
seq { for i in 1 .. 10 -> i * i }         // Plut√¥t obsol√®te
seq { for i in 1 .. 10 do yield i * i }   // 'yield' explicite
seq { for i in 1 .. 10 do i * i }         // 'yield' omis üëç

// Avec 'if'
let halfEvens =
    [ for i in [1..10] do
        if (i % 2) = 0 then i / 2 ]  // [1; 2; 3; 4; 5]

// 'for' imbriqu√©s
let pairs =
    [ for i in [1..3] do
      for j in [1..3] do
        (i, j) ]              // [(1, 1); (1; 2); (1; 3); (2, 1); ... (3, 3)]
```

---

# *Comprehension* : exemples (2)

```fs
// M√™me ici les 'yield' peuvent √™tre omis üëç
let twoToNine =
    [ for i in [1; 4; 7] do
        if i > 1 then i
        i + 1
        i + 2 ]  // [2; 3; 4; 5; 6; 7; 8; 9]
```

`yield!` permet d'aplatir des collections imbriqu√©es :

```fs
let oneToSix =
    [ for i in [1; 3; 5] do
        yield! set [i; i+1] ]
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_baseDonnees.png)

# 2.

## Les Types

---

# üí† Type `List`

Impl√©ment√©e sous forme de **liste simplement cha√Æn√©e** :
‚Üí 1 liste = 1 √©l√©ment *(Head)* + 1 sous-liste *(Tail)*
‚Üí Construction nomm√©e *Cons* et not√©e `::`

Pour √©viter r√©cursion infinie, besoin d'un cas de "sortie" :
‚Üí Liste vide nomm√©e *Empty* et not√©e `[]`

üëâ **Type union g√©n√©rique et r√©cursif** :

```fs
type List<'T> =
  | ( [] )
  | ( :: ) of head: 'T * tail: List<'T>
```

---

# `List` : litt√©raux

| Nb | Notation    | Notation explicite  | Signification                       |
|----|-------------|---------------------|-------------------------------------|
| 0  | `[]`        | `[]`                | Empty                               |
| 1  | `[1]`       | `1 :: []`           | Cons (1, Empty)                     |
| 2  | `[2; 1]`    | `2 :: 1 :: []`      | Cons (2, Cons (1, Empty))           |
| 3  | `[3; 2; 1]` | `3 :: 2 :: 1 :: []` | Cons (3, Cons (2, Cons (1, Empty))) |

V√©rification par d√©compilation avec [SharpLab.io](https://sharplab.io/#v2:DYLgZgzgPsCmAuACAbgBkQXkQbQLoFgAoOJZARkxzIOIRQCZLt6BuRaoklAZie7dbsaRIA==) :

```cs
//...
v1@2 = FSharpList<int>.Cons(1, FSharpList<int>.Empty);
v2@3 = FSharpList<int>.Cons(2, FSharpList<int>.Cons(1, FSharpList<int>.Empty));
//...
```

---

# `List` : immuable

Il n'est pas possible de modifier une liste existante.
‚Üí C'est cela qui permet de l'impl√©menter en liste cha√Æn√©e.

üí° L'id√©e est de cr√©er une nouvelle liste pour signifier un changement.
‚Üí Utiliser les op√©rateurs *Cons* (`::`) et *Append* (`@`) üìç

---

# `List` : initialisation

```fs
// Range: Start..End (Step=1)
let numFromOneToFive = [1..5]     // [1; 2; 3; 4; 5]

// Range: Start..Step..End
let oddFromOneToNine = [1..2..9]  // [1; 3; 5; 7; 9]

// Comprehension
let pairs =
    [ for i in [1..3] do
      for j in [1..3] do
        (i, j) ]              // [(1, 1); (1; 2); (1; 3); (2, 1); ... (3, 3)]
```

---

# `List` - Exercices üïπÔ∏è

### **1.** Impl√©menter la fonction `rev`

Inverse une liste : `rev [1; 2; 3]` ‚â° `[3; 2; 1]`

### **2.** Impl√©menter la fonction `map`

Transforme chaque √©l√©ment : `[1; 2; 3] |> map ((+) 1)` ‚â° `[2; 3; 4]`

üí° **Astuces**
¬†¬†¬†‚Üí Pattern matching liste vide `[]` ou *Cons* `head :: tail`
¬†¬†¬†‚Üí Sous-fonction *(tail-) recursive*

‚è± 5'

---

# `List` - Exercices - Solution üé≤

```fs
let rev list =
    let rec loop acc rest =
        match rest with
        | [] -> acc
        | x :: xs -> loop (x :: acc) xs
    loop [] list

let map f list =
    let rec loop acc rest =
        match rest with
        | [] -> acc
        | x :: xs -> loop (f x :: acc) xs
    list |> loop [] |> rev
```

üí° V√©rification avec [sharplab.io](https://sharplab.io/#v2:DYLgZgzgNAJiDUAfYBTALgAgE4oG4eAEsJMBeAWACgMaD1sUBjAgexYAcMBDR5nEjBWq0RAWy5pGACwYCA7oTRSqIkYgwBtALoYAtAD5uvFaprqAHhhAgM5iHsPA2nABSXrRxgEpbEEzScOTR0iEiogA) de la *tail recursion* compil√©e en boucle `while`

---

# `List` - Exercices - Tests ‚úÖ

```fs
// Tests en console FSI
let (=!) actual expected =
    if actual = expected
    then printfn $"‚úÖ {actual}"
    else printfn $"‚ùå {actual} != {expected}"

[1..3] |> rev =! [3; 2; 1];;
// ‚úÖ [3; 2; 1]

[1..3] |> map ((+) 1) =! [2; 3; 4];;
// ‚úÖ [2; 3; 4]
```

---

# üí† Type `Array`

- Diff√©rences / `List` : mutable, taille fixe, acc√®s index√© en O(1)
- Signature g√©n√©rique : `'T array` *(r√©cemment recommand√©e)* ou `'T []`
- Litt√©ral et *comprehension* : similaires √† `List`

```fs
// Litt√©ral
[| 1; 2; 3; 4; 5 |]  // val it : int [] = [|1; 2; 3; 4; 5|]

// Comprehension using range
[| 1 .. 5 |] = [| 1; 2; 3; 4; 5 |]  // true
[| 1 .. 3 .. 10 |] = [| 1; 4; 7; 10 |] // true

// Comprehension using generator
[| for a in 1 .. 5 do (a, a * 2) |]
// [|(1, 2); (2, 4); (3, 6); (4, 8); (5, 10)|]
```

---

# `Array` : acc√®s index√© & mutation

Acc√®s par index : `my-array.[my-index]`

‚ö†Ô∏è **Pi√®ge :** ne pas oublier le `.` avant les crochets `[]` ‚ùó
üéÅ **F‚ôØ 6.0** supporte sans le `.` : `my-array[my-index]`

```fs
let names = [| "Juliet"; "Monique"; "Rachelle"; "Tara"; "Sophia" |]
names.[4] <- "Kristen" // "Rachelle"
names    // [| "Juliet"; "Monique"; "Rachelle"; "Tara"; "Kristen" |]
         //                                              ^^^^^^^
```

---

# `Array` : *slicing*

Renvoie un sous-tableau entre les indices `start..end` optionnels

```fs
let names = [|"0: Juliet"; "1: Monique"; "2: Rachelle"; "3: Tara"; "4: Sophia"|]

names.[1..3]  // [|"1: Monique"; "2: Rachelle"; "3: Tara"|]
names.[2..]   // [|"2: Rachelle"; "3: Tara"; "4: Sophia"|]
names.[..3]   // [|"0: Juliet"; "1: Monique"; "2: Rachelle"; "3: Tara"|]
```

üí° Marche aussi avec une `string` : `"012345".[1..3]` ‚â° `"123"`

---

# üí† Type `Seq`

`type Seq<'T> = IEnumerable<'T>`
‚Üí S√©rie d'√©l√©ments de m√™me type

*Lazy* : s√©quence construite au fur et √† mesure lors de son it√©ration
‚â† `List` construite d√®s la d√©claration

‚Üí Peut offrir de meilleures performances qu'un `List` pour une collection avec beaucoup d'√©l√©ments et qu'on ne souhaite pas parcourir enti√®rement.

---

# `Seq` - Syntaxe

`seq { comprehension }`

```fs
seq { yield 1; yield 2 }   // 'yield' explicites üòï
seq { 1; 2; 3; 5; 8; 13 }  // 'yield' implicites üëç

// Range
seq { 1 .. 10 }       // seq [1; 2; 3; 4; ...]
seq { 1 .. 2 .. 10 }  // seq [1; 3; 5; 7; ...]

// G√©n√©rateur
seq { for a in 1 .. 5 do (a, a * 2) }
// seq [(1, 2); (2, 4); (3, 6); (4, 8); ...]
```

---

<!-- _footer: '' -->

# `Seq` - S√©quence infinie

**Option 1** : appeler la fonction `Seq.initInfinite` :
‚Üí `Seq.initInfinite : (initializer: (index: int) -> 'T) -> seq<'T>`
‚Üí Param√®tre `initializer` sert √† cr√©er l'√©l√©ment d'index (>= 0) sp√©cifi√©

**Option 2** : √©crire une fonction r√©cursive g√©n√©rant la s√©quence

```fs
// Option 1
let seqOfSquares = Seq.initInfinite (fun i -> i * i)

// Option 2
let seqOfSquares' =
    let rec loop n = seq { yield n * n; yield! loop (n+1) }
    loop 0

// Test
let firstTenSquares = seqOfSquares |> Seq.take 5 |> List.ofSeq // [0; 1; 4; 9; 16]
```

---

# üí† Type `Set`

Collection auto-ordonn√©e d'√©l√©ments uniques *(sans doublon)*
‚Üí Impl√©ment√©e sous forme d'arbre binaire

```fs
// Cr√©ation
set [ 2; 9; 4; 2 ]          // set [2; 4; 9]  // ‚òù √âl√©ment 2 d√©doublonn√©
Set.ofArray [| 1; 3 |]      // set [1; 3]
Set.ofList [ 1; 3 ]         // set [1; 3]
seq { 1; 3 } |> Set.ofSeq   // set [1; 3]

// Ajout/retrait d'√©l√©ment
Set.empty         // set []
|> Set.add 2      // set [2]
|> Set.remove 9   // set [2]    // ‚òù Pas d'exception
|> Set.add 9      // set [2; 9]
|> Set.remove 9   // set [2]
```

---

# `Set` : informations

‚Üí count, minElement, maxElement

```fs
let oneToFive = set [1..5]          // set [1; 2; 3; 4; 5]

// Nombre d'√©l√©ments : propri√©t√© `Count` ou fonction `Set.count` - ‚ö†Ô∏è O(N)
// ‚òù Ne pas confondre avec `Xxx.length` pour Array, List, Seq
let nb = Set.count oneToFive  // 5

// √âl√©ment min, max
let min = oneToFive |> Set.minElement   // 1
let max = oneToFive |> Set.maxElement   // 5
```

---

# `Set` : op√©rations

‚Üí **Union**, **Diff√©rence**, **Intersection** *(idem ensembles en Math)*

| Op√©ration    | ? | Op√©rateur | Fonction 2 sets  | Fonction N sets      |
|--------------|---|-----------|------------------|----------------------|
| Union        | ‚à™ | `+`       | `Set.union`      | `Set.unionMany`      |
| Diff√©rence   | ‚äñ | `-`       | `Set.difference` | `Set.differenceMany` |
| Intersection | ‚à© | √ó         | `Set.intersect`  | `Set.intersectMany`  |

---

# `Set` : op√©rations - exemples

```fs
let oneToFive = set [1..5]                 // A - set [1; 2; 3; 4; 5]
let evenToSix = set [2; 4; 6]              // B - set [2; 4; 6]

let union = oneToFive + evenToSix              // set [1; 2; 3; 4; 5; 6]
let diff  = oneToFive - evenToSix              // set [1; 3; 5]
let inter = Set.intersect oneToFive evenToSix  // set [2; 4]
```

| Valeur | Union             | Diff√©rence        | Intersection      |
|--------|-------------------|-------------------|-------------------|
| A      | `[ 1 2 3 4 5 ¬† ]` | `[ 1 2 3 4 5 ¬† ]` | `[ 1 2 3 4 5 ¬† ]` |
| B      | `[ ¬† 2 ¬† 4 ¬† 6 ]` | `[ ¬† 2 ¬† 4 ¬† 6 ]` | `[ ¬† 2 ¬† 4 ¬† 6 ]` |
| A ? B  | `[ 1 2 3 4 5 6 ]` | `[ 1 ¬† 3 ¬† 5 ¬† ]` | `[ ¬† 2 ¬† 4 ¬† ¬† ]` |

---

# üí† Type `Map`

Tableau associatif { *Cl√©* ‚Üí *Valeur* } ‚âÉ `Dictionary` immutable en C‚ôØ

```fs
// Cr√©ation : depuis collection de tuples (key, val)
// ‚Üí Fonction `Map.ofXxx` (Array, List, Seq)
let map1 = seq { (2, "A"); (1, "B") } |> Map.ofSeq
// ‚Üí Constructeur `Map(tuples)`
let map2 = Map [ (2, "A"); (1, "B"); (3, "C"); (3, "D") ]
// map [(1, "B"); (2, "A"); (3, "D")]
// üëâ Ordonn√©s par cl√©s (1, 2, 3) et d√©doublonn√©s en last win - cf. { 3 ‚Üí "D" }

// Ajout/retrait d'√©l√©ment
Map.empty         // map []
|> Map.add 2 "A"  // map [(2, "A")]
|> Map.remove 5   // map [(2, "A")] // ‚òù Pas d'exception si cl√© absente
|> Map.add 9 "B"  // map [(2, "A"); (9, "B")]
|> Map.remove 2   // map [(9, "B")]
```

---

# `Map` : acc√®s par cl√©

```fs
let table = Map [ (2, "A"); (1, "B"); (3, "D") ]

// Syntaxe `.[key]`
table.[1]  // "B"  // ‚ö†Ô∏è `1` est bien une cl√© et pas un indice
table.[0]  // üí• KeyNotFoundException

// Fonction `Map.find` : renvoie valeur ou üí• si cl√© absente
table |> Map.find 3     // "D"

// Fonction `Map.tryFind` : renvoie `'V option`
table |> Map.tryFind 3  // Some "D"
table |> Map.tryFind 9  // None
```

---

# `Map` : performance des lookups (`find`)

üîó *High Performance Collections in F#* https://kutt.it/dxDOi7 (Jan 2021)

## `Map` vs `Dictionary`

Fonction `readOnlyDict` permet de cr√©er rapidement un `IReadOnlyDictionary`
‚Üí √† partir d'une s√©quence de tuples `key, item`
‚Üí tr√®s performant : 10x plus rapide que `Map` pour le *lookup*

## `Dictionary` vs `Array`

‚Üí `Array` suffit si peu de lookups (< 100) et peu d'√©l√©ments (< 100)
‚Üí `Dictionary` sinon

---

# ‚òù Types `Set` et `Map` *vs* `IComparable`

Ne marchent que si √©l√©ments (d'un `Set`) / cl√©s (d'une `Map`) sont **comparables** !

üéâ Compatibles avec tous les types F‚ôØ *(cf. √©galit√© structurelle)*

‚ö†Ô∏è Pour les classes : impl√©menter `IComparable`

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_travail.png)

# 3.

## Les¬†¬†¬†¬† Fonctions

---

# Acc√®s √† un √©l√©ment

| ‚Üì Acc√®s \ Renvoie ‚Üí | `'T` ou üí•     | `'T option`     |
|---------------------|----------------|-----------------|
| Par index           | `list.[index]` |                 |
|                     | `item index`   | `tryItem index` |
| Premier √©l√©ment     | `head`         | `tryHead`       |
| Dernier √©l√©ment     | `last`         | `tryLast`       |

‚Üí Fonctions √† pr√©fixer par le module associ√© : `Array`, `List` ou `Seq`
‚Üí Dernier param√®tre, la "collection", omis par concision
‚Üí üí• `ArgumentException` ou `IndexOutOfRangeException`

```fs
[1; 2] |> List.tryHead    // Some 1
[1; 2] |> List.tryItem 2  // None
```

---

# Acc√®s √† un √©l√©ment : co√ªt ‚ö†Ô∏è

| Fonction \ Module | `Array` | `List` | `Seq`  |
|-------------------|---------|--------|--------|
| `head`            | O(1)    | O(1)   | O(1)   |
| `item`            | O(1)    | O(n) ‚ùó | O(n) ‚ùó |
| `last`            | O(1)    | O(n) ‚ùó | O(n) ‚ùó |
| `length`          | O(1)    | O(n) ‚ùó | O(n) ‚ùó |

---

# Combiner des collections

| Fonction       | Param√®tre(s)                      | Taille finale      |
|----------------|-----------------------------------|--------------------|
| `append` / `@` | 2 collections de tailles N1 et N2 | N1 + N2            |
| `concat`       | K collections de tailles N1..Nk   | N1 + N2 + ... + Nk |
| `zip`          | 2 collections de m√™me taille N ‚ùó  | N tuples (x1, x2)  |

üí° `@` = op√©rateur infixe alias de `List.append` uniquement ~~(Array, Seq)~~

```fs
List.append [1;2;3] [4;5;6]  // [1; 2; 3; 4; 5; 6]
[1;2;3] @ [4;5;6]            // idem

List.concat [ [1]; [2; 3] ]  // [1; 2; 3]

List.zip [1; 2] ['a'; 'b']   // [(1, 'a'); (2, 'b')]
```

---

# `List` : `::` *vs* `@`

*Cons* `1 :: [2; 3]`
‚Üí √âl√©ment ajout√© en t√™te de liste ‚Üí liste para√Æt en ordre inverse üòï
‚Üí Mais op√©ration en **O(1)** üëç -- *(Tail conserv√©e)*

*Append* `[1] @ [2; 3]`
‚Üí Liste en ordre normal
‚Üí Mais op√©ration en **O(n)** ‚ùó -- *(Nouvelle Tail √† chaque niveau)*

---

# Recherche d'un √©l√©ment

Via un pr√©dicat `f : 'T -> bool` :

| Quel √©l√©ment \ Renvoie | `'T` ou üí•      | `'T option`        |
|------------------------|-----------------|--------------------|
| Premier trouv√©         | `find`          | `tryFind`          |
| Dernier trouv√©         | `findBack`      | `tryFindBack`      |
| Index du 1er trouv√©    | `findIndex`     | `tryFindIndex`     |
| Index du der trouv√©    | `findIndexBack` | `tryFindIndexBack` |

```fs
[1; 2] |> List.find (fun x -> x < 2)      // 1
[1; 2] |> List.tryFind (fun x -> x >= 2)  // Some 2
[1; 2] |> List.tryFind (fun x -> x > 2)   // None
```

---

# Recherche d'√©l√©ments

| Recherche        | Combien d'√©l√©ments | M√©thode          |
|------------------|--------------------|------------------|
| Par valeur       | Au moins un        | `contains value` |
| Par pr√©dicat `f` | Au moins un        | `exists f`       |
| "                | Tous               | `forall f`       |

```fs
[1; 2] |> List.contains 0      // false
[1; 2] |> List.contains 1      // true
[1; 2] |> List.exists (fun x -> x >= 2)  // true
[1; 2] |> List.forall (fun x -> x >= 2)  // false
```

---

# S√©lection d'√©l√©ments

| Quels √©l√©ments    | Par nombre   | Par pr√©dicat `f` |
|-------------------|--------------|------------------|
| Tous ceux trouv√©s |              | `filter f`       |
| Premiers ignor√©s  | `skip n`     | `skipWhile f`    |
| Premiers trouv√©s  | `take n`     | `takeWhile f`    |
|                   | `truncate n` |                  |

‚òù **Notes :**

- Avec `skip` et `take`, üí• exception si `n > list.Length` ; pas avec `truncate`
- Alternative pour `Array` : s√©lection par *Range* `arr.[2..5]`

---

# Mapping d'√©l√©ments

Fonction prenant en entr√©e :
‚Üí Une fonction de mapping `f`
‚Üí Une collection d'√©l√©ments de type `'T`

| Fonction  | Mapping `f`       | Retour      | Quel(s) √©l√©ment(s) |
|-----------|-------------------|-------------|--------------------|
| `map`     | `'T -> 'U`        | `'U list`   | Autant d'√©l√©ments  |
| `mapi`    | `int -> 'T -> 'U` | `'U list`   | idem               |
| `collect` | `'T -> 'U list`   | `'U list`   | *flatMap*          |
| `choose`  | `'T -> 'U option` | `'U list`   | Moins d'√©l√©ments   |
| `pick`    | `'T -> 'U option` | `'U`        | 1er √©l√©ment ou üí•  |
| `tryPick` | `'T -> 'U option` | `'U option` | 1er √©l√©ment        |

---

## `map` *vs* `mapi`

`mapi` ‚â° `map` *with index*

`map` : mapping `'T -> 'U` 
‚Üí Op√®re sur valeur de chaque √©l√©ment

`mapi` : mapping `int -> 'T -> 'U` 
‚Üí Op√®re sur index et valeur de chaque √©l√©ment

```fs
["A"; "B"]
|> List.mapi (fun i x -> $"{i+1}. {x}")
// ["1. A"; "2. B"]
```

---

## Alternative √† `mapi`

Hormis `map` et `iter`, aucune fonction `xxx` n'a de variante en `xxxi`.

üí° Utiliser `indexed` pour obtenir les √©l√©ments avec leur index

```fs
let isOk (i, x) = i >= 1 && x <= "C"

["A"; "B"; "C"; "D"]
|> List.indexed       // [ (0, "A"); (1, "B"); (2, "C"); (3, "D") ]
|> List.filter isOk   //           [ (1, "B"); (2, "C") ]
|> List.map snd       //               [ "B" ; "C" ]
```

---

## `map` *vs* `iter`

`iter` ‚â° `map` sans *mapping* : `f: 'T -> unit` *(= `Action` en C#)*

‚òù M√™me si `map` marche, utiliser `iter` pour la compr√©hension du code
‚Üí R√©v√®le intention d'it√©rer/parcourir la liste plut√¥t que de mapper ses √©l√©ments

```fs
// ‚ùå √Ä √©viter
["A"; "B"; "C"] |> List.mapi (fun i x -> printfn $"Item #{i}: {x}")

// ‚úÖ Recommand√©
["A"; "B"; "C"] |> List.iteri (fun i x -> printfn $"Item #{i}: {x}")
// Item #0: A
// Item #1: B
// Item #2: C
```

---

## `choose`, `pick`, `tryPick`

Mapping `'T -> 'U option`
‚Üí Peut √©chouer en fonction des √©l√©ments
‚Üí Renvoie `Some value` pour indiquer le succ√®s du mapping
‚Üí Exemple : `tryParseInt: string -> int option`

`choose` et `pick` *unwrap* la/les valeurs dans les `Some`

`pick` √©met une exception üí• si aucun `Some` (= que des `None`)
`tryPick` renvoie tel quel le 1er `Some`

---

## `choose`, `pick`, `tryPick` - Exemples

```fs
let tryParseInt (s: string) =
    match System.Int32.TryParse(s) with
    | true,  i -> Some i
    | false, _ -> None

["1"; "2"; "?"] |> List.choose tryParseInt   // [1; 2]
["1"; "2"; "?"] |> List.pick tryParseInt     // 1
["1"; "2"; "?"] |> List.tryPick tryParseInt  // Some 1
```

---

# S√©lection *vs* mapping

- `filter` ou `choose` ?
- `find`/`tryFind` ou `pick`/`tryPick` ?

`filter`, `find`/`tryFind` op√®rent avec un **pr√©dicat** `'T -> bool`, sans mapping

`choose`, `pick`/`tryPick` op√®rent avec un **mapping** `'T -> 'U option`

---

# S√©lection *vs* mapping (2)

- `filter` ou `find`/`tryFind` ?
- `choose` ou `pick`/`tryPick` ?

`filter`, `choose` renvoient **tous** les √©l√©ments trouv√©s/mapp√©s

`find`, `pick` ne renvoient que le **1er** √©l√©ment trouv√©/mapp√©

---

# Agr√©gation : fonctions sp√©cialis√©es

| Op√©ration | Sur √©l√©ment | Sur projection `'T -> 'U` |
|-----------|-------------|---------------------------|
| Maximum   | `max`       | `maxBy projection`        |
| Minimum   | `min`       | `minBy projection`        |
| Somme     | `sum`       | `sumBy projection`        |
| Moyenne   | `average`   | `averageBy projection`    |
| D√©compte  | `length`    | `countBy projection`      |

```fs
[1; 2; 3] |> List.max  // 3
[ (1,"a"); (2,"b"); (3,"c") ] |> List.sumBy fst  // 6
[ (1,"a"); (2,"b"); (3,"c") ] |> List.map fst |> List.sum  // Equivalent explicite
```

---

# Agr√©gation : fonctions g√©n√©riques

- `fold¬†¬†¬†¬†¬†¬† (f: 'U -> 'T -> 'U) (seed: 'U) list`
- `foldBack¬†¬† (f: 'T -> 'U -> 'U) list (seed: 'U)`
- `reduce¬†¬†¬†¬† (f: 'T -> 'T -> 'T) list`
- `reduceBack (f: 'T -> 'T -> 'T) list`

‚òù `f` prend 2 param√®tres : un "accumulateur" `acc` et l'√©l√©ment courant `x`

‚ö†Ô∏è Fonctions `xxxBack` : tout est invers√© / fonctions `xxx` !
¬†¬†¬†‚Üí Parcours des √©l√©ments en sens inverse : dernier ‚Üí 1er √©l√©ment
¬†¬†¬†‚Üí Param√®tres `seed` et `list` invers√©s (pour `foldBack` *vs* `fold`)
¬†¬†¬†‚Üí Param√®tres `acc` et `x` de `f` invers√©s

üí• `reduceXxx` plante si liste vide car 1er √©l√©ment utilis√© en tant que *seed*

---

# Agr√©gation : fonctions g√©n√©riques (2)

Exemples :

```fs
["a";"b";"c"] |> List.reduce (+)  // "abc"
[ 1; 2; 3 ] |> List.reduce ( * )  // 6

[1;2;3;4] |> List.reduce     (fun acc x -> 10 * acc + x)  // 1234
[1;2;3;4] |> List.reduceBack (fun x acc -> 10 * acc + x)  // 4321

("", [1;2;3;4]) ||> List.fold     (fun acc x -> $"{acc}{x}")  // "1234"
([1;2;3;4], "") ||> List.foldBack (fun x acc -> $"{acc}{x}")  // "4321"
```

---

# Changer l'ordre des √©l√©ments

| Op√©ration        | Sur √©l√©ment              | Sur projection `'T -> 'U` |
|------------------|--------------------------|---------------------------|
| Inversion        | `rev list`               |                           |
| Tri ascendant    | `sort list`              | `sortBy f list`           |
| Tri descendant   | `sortDescending list`    | `sortDescendingBy f list` |
| Tri personnalis√© | `sortWith comparer list` |                           |

```fs
[1..5] |> List.rev // [5; 4; 3; 2; 1]
[2; 4; 1; 3; 5] |> List.sort // [1..5]
["b1"; "c3"; "a2"] |> List.sortBy (fun x -> x.[0]) // ["a2"; "b1"; "c3"] cf. a < b < c
["b1"; "c3"; "a2"] |> List.sortBy (fun x -> x.[1]) // ["b1"; "a2"; "c3"] cf. 1 < 2 < 3
```

---

# S√©parer

üí° Les √©l√©ments sont r√©partis en groupes.

| Op√©ration       | R√©sultat *(`;` omis)*                        | Remarque              |
|-----------------|----------------------------------------------|-----------------------|
| `[1..10]`       | `[¬†1¬†¬†¬†2¬†¬†¬†3¬†¬†¬†4¬†¬†¬†5¬†¬†¬†6¬†¬†¬†7¬†¬†¬†8¬†¬†¬†9¬†¬†¬†10¬†]` | `length = 10`         |
| `chunkBySize 3` | `[[1¬†¬†¬†2¬†¬†¬†3]¬†[4¬†¬†¬†5¬†¬†¬†6]¬†[7¬†¬†¬†8¬†¬†¬†9]¬†[10]]` | `forall: length <= 3` |
| `splitInto 3`   | `[[1¬†¬†¬†2¬†¬†¬†3¬†¬†¬†4]¬†[5¬†¬†¬†6¬†¬†¬†7]¬†[8¬†¬†¬†9¬†¬†¬†10]]` | `length <= 3`         |
| `splitAt 3`     | `([1¬†¬†¬†2¬†¬†¬†3],[4¬†¬†¬†5¬†¬†¬†6¬†¬†¬†7¬†¬†¬†8¬†¬†¬†9¬†¬†¬†10])` | Tuple ‚ùó               |

---

# Grouper les √©l√©ments - Par taille

üí° Les √©l√©ments peuvent √™tre **dupliqu√©s** dans diff√©rents groupes.

| Op√©ration    | R√©sultat *(`'` et `;` omis)*           | Remarque                 |
|--------------|----------------------------------------|--------------------------|
| `[1..5]`     | `[¬†1¬†¬†¬†¬†¬†¬†¬†2¬†¬†¬†¬†¬†¬†¬†3¬†¬†¬†¬†¬†¬†¬†4¬†¬†¬†¬†¬†¬†5¬†]` |                          |
| `pairwise`   | `[(1,2)¬†¬†¬†(2,3)¬†¬†¬†(3,4)¬†¬†¬†(4,5)]`      | Tuple ‚ùó                  |
| `windowed 2` | `[[1 2]¬†¬†¬†[2 3]¬†¬†¬†[3 4]¬†¬†¬†[4 5]]`      | Tableau de tableaux de 2 |
| `windowed 3` | `[[1 2 3] [2 3 4] [3 4 5]]`            | Tableau de tableaux de 3 |

---

# Grouper les √©l√©ments - Par crit√®re

| Op√©ration   | Crit√®re                  | Retour                                    |
|-------------|--------------------------|-------------------------------------------|
| `partition` | `predicate:¬† 'T -> bool` | `('T list * 'T list)`                     |
|             |                          | ‚Üí 1 tuple `([OKs], [KOs])`                |
| `groupBy`   | `projection: 'T -> 'K`   | `('K * 'T list) list`                     |
|             |                          | ‚Üí N tuples `[(cl√©, [√©l√©ments associ√©s])]` |

```fs
let isOdd i = (i % 2 = 1)
[1..10] |> List.partition isOdd // (        [1; 3; 5; 7; 9] ,         [2; 4; 6; 8; 10]  )
[1..10] |> List.groupBy isOdd   // [ (true, [1; 3; 5; 7; 9]); (false, [2; 4; 6; 8; 10]) ]

let firstLetter (s: string) = s.[0]
["apple"; "alice"; "bob"; "carrot"] |> List.groupBy firstLetter
// [('a', ["apple"; "alice"]); ('b', ["bob"]); ('c', ["carrot"])]
```

---

# Changer de type de collection

Au choix : `Dest.ofSource` ou `Source.toDest`

| De / vers | `Array`        | `List`         | `Seq`         |
|-----------|----------------|----------------|---------------|
| `Array`   | √ó              | `List.ofArray` | `Seq.ofArray` |
|           | √ó              | `Array.toList` | `Array.toSeq` |
| `List`    | `Array.ofList` | √ó              | `Seq.ofList`  |
|           | `List.toArray` | √ó              | `List.toSeq`  |
| `Seq`     | `Array.ofSeq`  | `List.ofSeq`   | √ó             |
|           | `Seq.toArray`  | `Seq.toList`   | √ó             |

---

# Fonctions *vs* compr√©hension

Les fonctions de `List`/`Array`/`Seq` peuvent souvent √™tre remplac√©es par une compr√©hension :

```fs
let list = [ 0..99 ]

list |> List.map f                   <->  [ for x in list do f x ]
list |> List.filter p                <->  [ for x in list do if p x then x ]
list |> List.filter p |> List.map f  <->  [ for x in list do if p x then f x ]
list |> List.collect g               <->  [ for x in list do yield! g x ]
```

---

# Module `Map` : fonctions sp√©cifiques

## `Map.change` : modification intelligente

Signature : `Map.change key (f: 'T option -> 'T option) table`

Selon la fonction `f` pass√©e en argument, on peut :
‚Üí Ajouter, modifier ou supprimer l'√©l√©ment d'une cl√© donn√©e

| Entr√©e                               | Renvoie `None`                               | Renvoie `Some newVal`                                             |
|--------------------------------------|----------------------------------------------|-------------------------------------------------------------------|
| `None`<br>*(√âl√©ment absent)*         | Ignore cette cl√©                             | Ajoute l'√©l√©ment *(key, newVal)*<br> ‚â° `Map.add key newVal table` |
| `Some value`<br>*(√âl√©ment existant)* | Supprime la cl√©<br> ‚â° `Map.remove key table` | Passe la valeur √† *newVal*<br> ‚â° `Map.add key newVal table`       |

---

## `Map` : `containsKey` *vs* `exists` *vs* `filter`

```txt
Fonction      Signature                        Commentaire                                             
------------+--------------------------------+---------------------------------------------------------
containsKey   'K -> Map<'K,'V> -> bool         Indique si la cl√© est pr√©sente                          
exists         f -> Map<'K,'V> -> bool         Indique si un couple cl√©/valeur satisfait le pr√©dicat   
filter         f -> Map<'K,'V> -> Map<'K,'V>   Conserve les couples cl√©/valeur satisfaisant le pr√©dicat

Avec pr√©dicat f: 'K -> 'V -> bool
```

```fs
let table = Map [ (2, "A"); (1, "B"); (3, "D") ]

table |> Map.containsKey 0  // false
table |> Map.containsKey 2  // true

let isEven i = i % 2 = 0
let isFigure (s: string) = "AEIOUY".Contains(s)

table |> Map.exists (fun k v -> (isEven k) && (isFigure v))  // true
table |> Map.filter (fun k v -> (isEven k) && (isFigure v))  // map [(2, "A")]
```

---

# Module `String`

`string` ‚â° `Seq<char>` ‚Üí Module `(FSharp.Core.)String` (‚â† `System.String`) 
‚Üí Propose quelques fonctions similaires √† celles de `Seq` en \+ performantes :

```fs
String.concat (separator: string) (strings: seq<string>) : string
String.init (count: int) (f: (index: int) -> string) : string
String.replicate (count: int) (s: string) : string

String.exists (predicate: char -> bool) (s: string) : bool
String.forall (predicate: char -> bool) (s: string) : bool
String.filter (predicate: char -> bool) (s: string) : string

String.collect (mapping: ¬†¬†¬†¬†¬†¬†¬†char -> string) (s: string) : string
String.map¬†¬†¬†¬† (mapping: ¬†¬†¬†¬†¬†¬†¬†char -> char)¬†¬† (s: string) : string
String.mapi¬†¬†¬† (mapping: int -> char -> char)¬†¬† (s: string) : string
// Idem iter/iteri qui renvoie unit
```

---

# Module `String` - Exemples

```fs
let a = String.concat "-" ["a"; "b"; "c"]  // "a-b-c"
let b = String.init 3 (fun i -> $"#{i}")   // "#0#1#2"
let c = String.replicate 3 "0"             // "000"

let d = "abcd" |> String.exists (fun c -> c >= 'b')  // true
let e = "abcd" |> String.forall (fun c -> c >= 'b')  // false
let f = "abcd" |> String.filter (fun c -> c >= 'b')  // "bcd"

let g = "abcd" |> String.collect (fun c -> $"{c}{c}")  // "aabbccdd"

let h = "abcd" |> String.map (fun c -> (int c) + 1 |> char)  // "bcde"
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 4.

## üçî Quiz

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# Question 1

```fs
type Address = { City: string; Country: string }

let format address = $"{address.City}, {address.Country}"

let addresses: Address list = ...
```

**Quelle fonction de `List` utiliser sur `addresses`
pour appliquer `format` aux √©l√©ments ?**

**A.** `List.iter()`
**B.** `List.map()`
**C.** `List.sum()`

‚è± 10‚Äô‚Äô

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# R√©ponse 1

**A.** `List.iter()` ‚ùå

**B.** `List.map()` ‚úÖ

**C.** `List.sum()` ‚ùå

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# Question 2

#### Que vaut `[1..4] |> List.head` ?

**A.** `[2; 3; 4]`

**B.** `1`

**C.** `4`

‚è± 10‚Äô‚Äô

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# R√©ponse 2

#### `[1..4] |> List.head =`

**A.** `[2; 3; 4]` ‚ùå
¬†¬†¬†¬†*(Ne pas confondre avec `List.tail`)*

**B.** `1` ‚úÖ

**C.** `4` ‚ùå
¬†¬†¬†¬†*(Ne pas confondre avec `List.last`)*

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# Question 3

#### Quelle est la bonne mani√®re d'obtenir la moyenne d'une liste ?

**A.** `[2; 4] |> List.average`

**B.** `[2; 4] |> List.avg`

**C.** `[2.0; 4.0] |> List.average`

‚è± 10‚Äô‚Äô

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# R√©ponse 3

#### Bonne mani√®re d'obtenir la moyenne d'une liste :

**A.** `[2; 4] |> List.average` ‚ùå
¬†¬†¬†¬†üí• Error FS0001: Le type `int` ne prend pas en charge l'op√©rateur `DivideByInt`

**B.** `[2; 4] |> List.avg`
¬†¬†¬†¬†üí• Error FS0039: La valeur [...] `avg` n'est pas d√©finie.

**C.** `[2.0; 4.0] |> List.average` ‚úÖ
¬†¬†¬†¬†`val it : float = 3.0`

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 5.

## Le¬†¬†¬† R√©cap‚Äô

---

# Types

5 collections dont 4 fonctionnelles/immutables

- `List` : choix par d√©faut
  - *Passe-partout*
  - *Pratique* : pattern matching, op√©rateurs *Cons* `::` et *Append* `@`...
- `Array` : mutabilit√© / performance
- `Seq` : √©valuation diff√©r√©e (*Lazy*), s√©quence infinie
- `Set` : unicit√© des √©l√©ments
- `Map` : classement des √©l√©ments par cl√©

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_note.png)

# API

**Riche** ‚Üí Centaine de fonctions >> Cinquantaine pour LINQ

**Homog√®ne** ‚Üí Syntaxe et fonctions communes

**S√©mantique** ‚Üí Nom des fonctions proche du JS

---

## API - Comparatif C‚ôØ / F‚ôØ / JS

| C‚ôØ LINQ                       | F‚ôØ                    | JS `Array`           |
|-------------------------------|-----------------------|----------------------|
| `Select()`, `SelectMany()`    | `map`, `collect`      | `map()`, `flatMap()` |
| `Any(predicate)`, `All()`     | `exists`, `forall`    | `some()`, `every()`  |
| `Where()`, √ó                  | `filter`, `choose`    | `filter()`, √ó        |
| `First()`, `FirstOrDefault()` | `find`, `tryFind`     | √ó, `find()`          |
| √ó                             | `pick`, `tryPick`     | √ó                    |
| `Aggregate([seed]])`          | `fold`, `reduce`      | `reduce()`           |
| `Average()`, `Sum()`          | `average`, `sum`      | √ó                    |
| `ToList()`, `AsEnumerable()`  | `List.ofSeq`, `toSeq` | √ó                    |
| `Zip()`                       | `zip`                 | √ó                    |

---

# Exercices

Sur [exercism.io](https://exercism.io/tracks/fsharp) *(se cr√©er un compte)*

| Exercice            | Niveau | Sujets                 |
|---------------------|--------|------------------------|
| High Scores         | Facile | `List`                 |
| Protein Translation | Moyen+ | `Seq`/`List` üí°        |
| ETL                 | Moyen  | `Map` de `List`, Tuple |
| Grade School        | Moyen+ | `Map` de `List`        |

üí° **Astuces :**
‚Üí `string` est une `Seq<char>`
‚Üí Quid de `Seq.chunkBySize` ?

---

# Ressources compl√©mentaires

*Toutes les fonctions, avec leur co√ªt en O(?)*
https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/fsharp-collection-types#table-of-functions

*Choosing between collection functions* (2015)
https://fsharpforfunandprofit.com/posts/list-module-functions/

*An F# Primer for curious C# developers - Work with collections* (2020)
https://laenas.github.io/posts/01-fs-primer.html#work-with-collections

*Formatage des collections*
https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting#formatting-lists-and-arrays

---

<!-- _class: end invert lead-->

# Thanks üôè
