---
marp: true
html: true
theme: 'd-edge'
title: 'F‚ôØ Training ‚Ä¢ Asynchronous programming'
footer: 'F‚ôØ Training ‚Ä¢ Asynchronous programming'
paginate: true
---

<!-- _class: title invert -->

# F‚ôØ Training

## _Asynchronous programming_

### 2025 April

---

<!-- _class: agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- Asynchronous Workflow
- Interop with .NET TPL

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_software.png)

# 1.

## Asynchronous Workflow

---

# Asynchronous Workflow : Purpose

1. Do not block the current thread while waiting for a long calculation
2. Allow parallel calculations
3. Indicate that a calculation may take some time

---

# `Async<'T>` type

Represents an asynchronous calculation

üìÜ Similar to the `async/await` pattern way before C‚ôØ and JS

- 2007: `Async<'T>` F‚ôØ
- 2012: `Task<T>` .NET and pattern `async`/`await`
- 2017: `Promise` JavaScript and pattern `async`/`await`

---

# Methods returning an `Async` object

`Async.AwaitTask(task : Task or Task<'T>) : Async<'T>`
‚Üí Convert a `Task` (.NET) to `Async` (F‚ôØ)

`Async.Sleep(milliseconds or TimeSpan) : Async<unit>`
‚âÉ `await Task.Delay()` ‚â† `Thread.Sleep` ‚Üí does not block current thread

FSharp.Control `CommonExtensions` module: extends the `System.IO.Stream` type ([doc](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-commonextensions.html))
‚Üí `AsyncRead(buffer: byte[], ?offset: int, ?count: int) : Async<int>`
‚Üí `AsyncWrite(buffer: byte[], ?offset: int, ?count: int) : Async<unit>`

FSharp.Control `WebExtensions` module: extends type `System.Net.WebClient` ([doc](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-webextensions.html))
‚Üí `AsyncDownloadData(address : Uri) : Async<byte[]>`
‚Üí `AsyncDownloadString(address : Uri) : Async<string`

---

# Run an async calculation

`Async.RunSynchronously(calc: Async<'T>, ?timeoutMs: int, ?cancellationToken) : 'T`
‚Üí Waits for the calculation to end, blocking the calling thread! (‚â† `await` C‚ôØ) ‚ö†Ô∏è

`Async.Start(operation: Async<unit>, ?cancellationToken) : unit`
‚Üí Perform the operation in background _(without blocking calling thread)_
‚ö†Ô∏è If an exception occurs, it is "swallowed"!

`Async.StartImmediate(calc: Async<'T>, ?cancellationToken) : unit`
‚Üí Perform the calculation in the calling thread!
üí° Useful in a GUI to update it: progress bar...

`Async.StartWithContinuations(calc, continuations..., ?cancellationToken)`
‚Üí Ditto `Async.RunSynchronously` ‚ö†Ô∏è ... with 3 _callbacks_ of continuation :
‚Üí on success ‚úÖ, exception üí• and cancellation üõë

---

# `async { expression }` block

_A.k.a. Async workflow_

Syntax for sequentially writing an asynchronous calculation
‚Üí The result of the calculation is wrapped in an `Async` object

**Key words**
‚Ä¢ `return` ‚Üí final value of calculation ‚Ä¢ `unit` if omitted
‚Ä¢ `let!` ‚Üí access to the result of an async sub-calculation _(‚âÉ `await` in C‚ôØ)_
‚Ä¢ `use!` ‚Üí ditto `use` _(management of an `IDisposable`)_ + `let!`
‚Ä¢ `do!` ‚Üí ditto `let!` for async calculation without return (`Async<unit>`)

---

## `async` block - Examples

```fsharp
let repeat (computeAsync: int -> Async<string>) times = async {
    for i in [ 1..times ] do
        printf $"Start operation #{i}... "
        let! result = computeAsync i
        printfn $"Result: {result}"
}

let basicOp (num: int) = async {
    do! Async.Sleep 150
    return $"{num} * ({num} - 1) = {num * (num - 1)}"
}

repeat basicOp 5 |> Async.RunSynchronously

// Start operation #1... Result: 1 * (1 - 1) = 0
// Start operation #2... Result: 2 * (2 - 1) = 2
// Start operation #3... Result: 3 * (3 - 1) = 6
// Start operation #4... Result: 4 * (4 - 1) = 12
// Start operation #5... Result: 5 * (5 - 1) = 20
```

---

# Inappropriate use of `Async.RunSynchronously`

`Async.RunSynchronously` runs the calculation and returns the result BUT blocks the calling thread! Use it only at the "end of the chain" and not to _unwrap_ intermediate asynchronous calculations! Use an `async` block instead.

```fsharp
// ‚ùå Avoid
let a = calcA |> Async.RunSynchronously
let b = calcB a |> Async.RunSynchronously
calcC b

// ‚úÖ Favor
async {
    let! a = calcA
    let! b = calcB a
    return calcC b
}
|> Async.RunSynchronously
```

---

# Parallel calculations

**1.** `Async.Parallel(computations: seq<Async<'T>>, ?maxBranches) : Async<'T[]>`

‚âÉ `Task.WhenAll` : [Fork-Join model](https://en.wikipedia.org/wiki/Fork%E2%80%93join_model)

- _Fork_: calculations run in parallel
- Wait for all calculations to finish
- _Join_: aggregation of results _(which are of the same type)_
  - in the same order as calculations

‚ö†Ô∏è All calculations must return the same type!

---

## `Async.Parallel` - Example

```fsharp
let downloadSite (site: string) = async {
    do! Async.Sleep (100 * site.Length)
    printfn $"{site} ‚úÖ"
    return site.Length
}

[ "google"; "msn"; "yahoo" ]
|> List.map downloadSite  // string list
|> Async.Parallel         // Async<string[]>
|> Async.RunSynchronously // string[]
|> printfn "%A"

// msn ‚úÖ
// yahoo ‚úÖ
// google ‚úÖ
// [|6; 3; 5|]
```

---

# Parallel calculations (2)

**2.** `Async.StartChild(calc: Async<'T>, ?timeoutMs: int) : Async<Async<'T>>`

Allows several calculations to be run in parallel
‚Üí ... whose results are of different types _(‚â† `Async.Parallel`)_

Used in `async` block with 2 `let!` per child calculation _(cf. `Async<Async<'T>>`)_

**Shared cancellation** üìç
‚Üí Child calculation shares cancellation token with its parent calculation

---

## `Async.StartChild` - Example part 1

Let's first define a function `delay`
‚Üí which returns the specified value `x`
‚Üí after `ms` milliseconds

```fsharp
let delay (ms: int) x = async {
    do! Async.Sleep ms
    return x
}

// üí° Timing with FSI directive `#time` - üîó https://kutt.it/Zbp6ot
#time "on" // --> Timer start
"a" |> delay 100 |> Async.RunSynchronously // Real: 00:00:00.111, CPU...
#time "off" // --> Timer stop
```

---

<!-- _footer: "" -->

## `Async.StartChild` - Example part 2

```fsharp
let inSeries = async {
    let! result1 = "a" |> delay 100
    let! result2 = 123 |> delay 200
    return (result1, result2)
}

let inParallel = async {
    let! child1 = "a" |> delay 100 |> Async.StartChild
    let! child2 = 123 |> delay 200 |> Async.StartChild
    let! result1 = child1
    let! result2 = child2
    return (result1, result2)
}

#time "on"
inSeries |> Async.RunSynchronously    // Real: 00:00:00.317, ...
#time "off"
#time "on"
inParallel |> Async.RunSynchronously  // Real: 00:00:00.205, ...
#time "off"
```

---

# Cancelling a task

Based on a default or explicit `CancellationToken/Source`:
‚Ä¢ `Async.RunSynchronously(computation, ?timeout, ?cancellationToken)`
‚Ä¢ `Async.Start(computation, ?cancellationToken)`

Trigger cancellation
‚Ä¢ Explicit token + `cancellationTokenSource.Cancel()`
‚Ä¢ Explicit token with timeout `new CancellationTokenSource(timeout)`
‚Ä¢ Default token: `Async.CancelDefaultToken()` ‚Üí `OperationCanceledException` üí£

Check cancellation
‚Ä¢ Implicit: at each keyword in async block: `let`, `let!`, `for`...
‚Ä¢ Explicit local: `let! ct = Async.CancellationToken` then `ct.IsCancellationRequested`.
‚Ä¢ Explicit global: `Async.OnCancel(callback)`

---

## Cancelling a task - Example Part 1

```fsharp
let sleepLoop = async {
    let stopwatch = System.Diagnostics.Stopwatch()
    stopwatch.Start()
    let log message = printfn $"""   [{stopwatch.Elapsed.ToString("s\.fff")}] {message}"""

    use! __ = Async.OnCancel (fun () ->
        log $"  Cancelled ‚ùå")

    for i in [ 1..5 ] do
        log $"Step #{i}..."
        do! Async.Sleep 500
        log $"  Completed ‚úÖ"
}

// ...
```

---

## Cancelling a task - Example Part 2

```fsharp
// ...

open System.Threading

printfn "1. RunSynchronously:"
Async.RunSynchronously(sleepLoop)

printfn "2. Start with CancellationTokenSource + Sleep + Cancel"
use manualCancellationSource = new CancellationTokenSource()
Async.Start(sleepLoop, manualCancellationSource.Token)
Thread.Sleep(1200)
manualCancellationSource.Cancel()

printfn "3. Start with CancellationTokenSource with timeout"
use cancellationByTimeoutSource = new CancellationTokenSource(1200)
Async.Start(sleepLoop, cancellationByTimeoutSource.Token)
```

---

<!-- _footer: '' -->

## Cancelling a task - Example Outputs

```txt
1. RunSynchronously:
   [0.009] Step #1...
   [0.532]   Completed ‚úÖ
   [0.535] Step #2...
   [1.037]   Completed ‚úÖ
   [1.039] Step #3...
   [1.543]   Completed ‚úÖ
   [1.545] Step #4...
   [2.063]   Completed ‚úÖ
   [2.064] Step #5...
   [2.570]   Completed ‚úÖ
2. Start with CancellationTokenSource + Sleep + Cancel
   [0.000] Step #1...
   [0.505]   Completed ‚úÖ
   [0.505] Step #2...
   [1.011]   Completed ‚úÖ
   [1.013] Step #3...
   [1.234]   Cancelled ‚ùå
3. Start with CancellationTokenSource with timeout
... idem 2.
```

---

<!-- _class: blue chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_entretien.png)

# 2.

## Interop with .NET TPL

**TPL:** Task Parallel Library

---

# Interaction with .NET libraries

Asynchronous libraries in .NET and the `async`/`await` C‚ôØ pattern:
‚Üí Based on **TPL** and the `Task` type

Gateways with asynchronous worflow F‚ôØ :

- `Async.AwaitTask` and `Async.StartAsTask` functions
- `task {}` block

---

# Gateway functions

`Async.AwaitTask: Task<'T> -> Async<'T>`
‚Üí Consume an asynchronous .NET library in `async` block

`Async.StartAsTask: Async<'T> -> Task<'T>`
‚Üí Launch an async calculation as a `Task`

```fsharp
let getValueFromLibrary param = async {
    let! value = DotNetLibrary.GetValueAsync param |> Async.AwaitTask
    return value
}

let computationForCaller param =
    async {
        let! result = getAsyncResult param
        return result
    }
    |> Async.StartAsTask
```

---

# `task {}` block

> Allows to consume an asynchronous .NET library directly, using a single `Async.AwaitTask` rather than 1 for each async method called.

üí° Available since F‚ôØ 6 _(before: [Ply](https://github.com/crowded/ply) package nuget)_

```fsharp
#r "nuget: Ply"
open FSharp.Control.Tasks

task {
    use client = new System.Net.Http.HttpClient()
    let! response = client.GetStringAsync("https://www.google.fr/")
    response.Substring(0, 300) |> printfn "%s"
}  // Task<unit>
|> Async.AwaitTask  // Async<unit>
|> Async.RunSynchronously
```

---

# `Async` *vs* `Task`

#### 1. Calculation start mode

`Task` = _hot tasks_ ‚Üí calculations started immediately‚ùó

`Async` = _task generators_ = calculation specification, independent of startup
‚Üí Functional approach: no side-effects or mutations, composability
‚Üí Control of startup mode: when and how üëç

#### 2. Cancellation support

`Task`: by adding a `CancellationToken` parameter to async methods
‚Üí Forces manual testing if token is canceled = tedious + _error prone‚ùó_

`Async`: automatic support in calculations - token to be provided at startup üëç

---

# Recommendation for async function in F‚ôØ

C‚ôØ `async` applied at a method level
‚â† F‚ôØ `async` defines an async block, not an async function

‚òù **Recommendation:**
¬ª Put the entire body of the async function in an `async` block.

```fsharp
// ‚ùå Avoid
let workThenWait () =
    Thread.Sleep(1000)
    async { do! Async.Sleep(1000) } // Async only in this block üßê

// ‚úÖ Prefer
let workThenWait () = async {
    Thread.Sleep(1000)
    printfn "work"
    do! Async.Sleep(1000)
}
```

---

# Pitfalls of the `async`/`await` C‚ôØ pattern

1. Really asynchronous?
2. Omit the `await`

---

## Pitfall 1 - Really asynchronous?

In C‚ôØ: method `async` remains on the calling thread until the 1st `await`
‚Üí Misleading feeling of being asynchronous throughout the method

```csharp
async Task WorkThenWait() {
    Thread.Sleep(1000); // ‚ö†Ô∏è Blocks calling thread !
    await Task.Delay(1000); // Really async from here ü§î
}
```

---

## Pitfall 2 - Omit the `await`

```csharp
async Task PrintAfterOneSecond(string message) {
    await Task.Delay(1000);
    Console.WriteLine($"[{DateTime.Now:T}] {message}");
}

async Task Main() {
    PrintAfterOneSecond("Before"); // ‚ö†Ô∏è Missing `await`‚Üí warning CS4014
    Console.WriteLine($"[{DateTime.Now:T}] After");
    await Task.CompletedTask;
}
```

Compiles but returns unexpected result: _After_ before _Before_‚ùó

```txt
[11:45:27] After
[11:45:28] Before
```

---

## Pitfall 2 - In F‚ôØ too üò¢

```fsharp
let printAfterOneSecond message = async {
    do! Async.Sleep 1000
    printfn $"[{DateTime.Now:T}] {message}"
}

async {
    printAfterOneSecond "Before" // ‚ö†Ô∏è Missing `do!` ‚Üí warning FS0020
    printfn $"[{DateTime.Now:T}] After"
} |> Async.RunSynchronously
```

Compiles but returns another unexpected result: no _Before_ at all ‚ÅâÔ∏è

```txt
[11:45:27] After
```

---

# Pitfall 2 - Compilation warnings

The previous examples compile but with big _warnings_!

C‚ôØ [_warning CS4014_](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/compiler-messages/cs4014) message:
‚ùù _Because this call is not awaited, execution of the current method continues
¬†¬†¬†before the call is completed. Consider applying the `await` operator..._ ‚ùû

F‚ôØ _warning FS0020_ message:
‚ùù _The result of this expression has type `Async<unit>` and is implicitly ignored.
¬†¬†¬†Consider using `ignore` to discard this value explicitly..._ ‚ùû

‚òù **Recommendation:** be sure to **always** handle this type of _warnings_!
    _This is even more crucial in F‚ôØ where compilation is tricky._

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 3.

## The¬†¬†¬† Recap

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_note.png)

# Asynchronous programming in F‚ôØ

Via `async {}` block in pure F‚ôØ
‚Üí Similar to C‚ôØ `async`/`await` pattern but prior
‚Üí Avoids some of the pitfalls of the `async`/`await` pattern
‚Üí Requires manual start of calculation
‚Üí But compilation prevents forgetting it

Via `task {}` block
‚Üí Facilitates interaction with asynchronous .NET library

---

# üîó Additional resources

https://docs.microsoft.com/en-us/dotnet/fsharp/tutorials/async

---

<!-- _class: end invert lead-->

# Thanks üôè
