---
marp: true
html: true
theme: 'd-edge'
title: 'Fâ™¯ Training â€¢ Programmation asynchrone'
footer: 'Fâ™¯ Training â€¢ Programmation asynchrone'
paginate: true
---

<!-- _class: title invert -->

# Fâ™¯ Training

## *Programmation asynchrone*

### 2025 April

---

<!-- _class: toc agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- *Workflow* asynchrone
- Interop avec la TPL .NET

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_software.png)

# 1.

## Workflow asynchrone

---

# Workflow asynchrone : Besoins

1. Ne pas bloquer le thread courant en attendant un calcul long
2. Permettre calculs en parallÃ¨le
3. Indiquer qu'un calcul peut prendre du temps

---

# Type `Async<'T>`

- ReprÃ©sente un calcul asynchrone
- Similaire au pattern `async/await` avant l'heure ğŸ“†
  - 2007 : `Async<'T>` Fâ™¯
  - 2012 : `Task<T>` .NET et pattern `async`/`await`
  - 2017 : `Promise` JavaScript et pattern `async`/`await`

---

# MÃ©thodes renvoyant un objet `Async`

`Async.AwaitTask(task: Task or Task<'T>) : Async<'T>`
â†’ Conversion d'une `Task` (.NET) en `Async` (Fâ™¯)

`Async.Sleep(milliseconds or TimeSpan) : Async<unit>`
â‰ƒ `await Task.Delay()` â‰  `Thread.Sleep` â†’ ne bloque pas le thread courant

[FSharp.Control.CommonExtensions](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-commonextensions.html) : Ã©tend le type `System.IO.Stream`
â†’ `AsyncRead(buffer: byte[], ?offset: int, ?count: int) : Async<int>`
â†’ `AsyncWrite(buffer: byte[], ?offset: int, ?count: int) : Async<unit>`

[FSharp.Control.WebExtensions](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-webextensions.html) : Ã©tend le type `System.Net.WebClient`
â†’ `AsyncDownloadData(address: Uri) : Async<byte[]>`
â†’ `AsyncDownloadString(address: Uri) : Async<string`

---

# Lancement d'un calcul async

`Async.RunSynchronously(calc: Async<'T>, ?timeoutMs: int, ?cancellationToken) : 'T`
â†’ Attend la fin du calcul mais bloque le thread appelant ! (â‰  `await` Câ™¯) âš ï¸

`Async.Start(operation: Async<unit>, ?cancellationToken) : unit`
â†’ ExÃ©cute l'opÃ©ration en background *(sans bloquÃ© le thread appelant)*
âš ï¸ Si une exception survient, elle est "avalÃ©e" !

`Async.StartImmediate(calc: Async<'T>, ?cancellationToken) : unit`
â†’ ExÃ©cute le calcul dans le thread appelant !
ğŸ’¡ Pratique dans une GUI pour la mettre Ã  jour : barre de progression...

`Async.StartWithContinuations(calc, continuations..., ?cancellationToken)`
â†’ Idem `Async.RunSynchronously` âš ï¸ ... avec 3 *callbacks* de continuation :
â†’ en cas de succÃ¨s âœ…, d'exception ğŸ’¥ et d'annulation ğŸ›‘

---

# Bloc `async { expression }`

*A.k.a. Async workflow*

Syntaxe pour Ã©crire de maniÃ¨re sÃ©quentielle un calcul asynchrone
â†’ Le rÃ©sultat du calcul est wrappÃ© dans un objet `Async`

**Mots clÃ©s**
â€¢ `return` â†’ valeur finale du calcul - `unit` si omis
â€¢ `let!` *(prononcer Â« let bang Â»)*
Â Â Â â†’ accÃ¨s au rÃ©sultat d'un sous-calcul async *(â‰ƒ `await` en Câ™¯)*
â€¢ `use!` â†’ idem `use` *(gestion d'un `IDisposable`)* + `let!`
â€¢ `do!` â†’ idem `let!` pour calcul async sans retour (`Async<unit>`)

---

## Bloc `async` - Exemples

```fs
let repeat (computeAsync: int -> Async<string>) times = async {
    for i in [ 1..times ] do
        printf $"Start operation #{i}... "
        let! result = computeAsync i
        printfn $"Result: {result}"
}

let basicOp (num: int) = async {
    do! Async.Sleep 150
    return $"{num} * ({num} - 1) = {num * (num - 1)}"
}

repeat basicOp 5 |> Async.RunSynchronously

// Start operation #1... Result: 1 * (1 - 1) = 0
// Start operation #2... Result: 2 * (2 - 1) = 2
// Start operation #3... Result: 3 * (3 - 1) = 6
// Start operation #4... Result: 4 * (4 - 1) = 12
// Start operation #5... Result: 5 * (5 - 1) = 20
```

---

# Usage inappropriÃ© de `Async.RunSynchronously`

`Async.RunSynchronously` lance le calcul et renvoie son rÃ©sultat MAIS en bloquant le thread appelant ! Ne l'utiliser qu'en Â« bout de chaÃ®ne Â» et pas pour *unwrap* des calculs asynchrones intermÃ©diaires ! Utiliser plutÃ´t un bloc `async`.

```fs
// âŒ Ã€ Ã©viter
let a = calcA |> Async.RunSynchronously
let b = calcB a |> Async.RunSynchronously
calcC b

// âœ… Ã€ prÃ©fÃ©rer
async {
    let! a = calcA
    let! b = calcB a
    return calcC b
} |> Async.RunSynchronously
```

---

# Calculs en parallÃ¨le

**1.** `Async.Parallel(computations: seq<Async<'T>>, ?maxBranches) : Async<'T[]>`

â‰ƒ `Task.WhenAll` : modÃ¨le [Fork-Join](https://en.wikipedia.org/wiki/Fork%E2%80%93join_model)
- *Fork* : calculs lancÃ©s en parallÃ¨le
- Attente de la terminaison de tous les calculs
- *Join* : agrÃ©gation des rÃ©sultats *(qui sont du mÃªme type)*
  - dans le mÃªme ordre que les calculs

---

## `Async.Parallel` - Exemple


```fs
let downloadSite (site: string) = async {
    do! Async.Sleep (100 * site.Length)
    printfn $"{site} âœ…"
    return site.Length
}

[ "google"; "msn"; "yahoo" ]
|> List.map downloadSite  // string list
|> Async.Parallel         // Async<string[]>
|> Async.RunSynchronously // string[]
|> printfn "%A"

// msn âœ…
// yahoo âœ…
// google âœ…
// [|6; 3; 5|]
```

---

# Calculs en parallÃ¨le (2)

**2.** `Async.StartChild(calc: Async<'T>, ?timeoutMs: int) : Async<Async<'T>>`

Permet de lancer en parallÃ¨le plusieurs calculs
â†’ ... dont les rÃ©sultats sont de types diffÃ©rents _(â‰  `Async.Parallel`)_

S'utilise dans bloc `async` avec 2 `let!` par calcul enfant *(cf. `Async<Async<'T>>`)*

Annulation conjointe ğŸ“
â†’ Calcul enfant partage jeton dâ€™annulation du calcul parent

---

## `Async.StartChild` - Exemple partie 1

Soit le fonction `delay`
â†’ qui renvoie la valeur spÃ©cifiÃ©e `x`
â†’ au bout de `ms` millisecondes

```fs
let delay (ms: int) x = async {
    do! Async.Sleep ms
    return x
}

// ğŸ’¡ Minutage avec la directive FSI `#time` â€¢ ğŸ”— https://kutt.it/Zbp6ot
#time "on"  // --> Minutage activÃ©
"a" |> delay 100 |> Async.RunSynchronously // RÃ©el : 00:00:00.111, Proc...
#time "off" // --> Minutage dÃ©sactivÃ©
```

---

<!-- _footer: "" -->

## `Async.StartChild` - Exemple partie 2

```fs
let inSeries = async {
    let! result1 = "a" |> delay 100
    let! result2 = 123 |> delay 200
    return (result1, result2)
}

let inParallel = async {
    let! child1 = "a" |> delay 100 |> Async.StartChild
    let! child2 = 123 |> delay 200 |> Async.StartChild
    let! result1 = child1
    let! result2 = child2
    return (result1, result2)
}

#time "on"
inSeries |> Async.RunSynchronously    // RÃ©el : 00:00:00.317, ...
#time "off"
#time "on"
inParallel |> Async.RunSynchronously  // RÃ©el : 00:00:00.205, ...
#time "off"
```

---

# Annulation d'une tÃ¢che

Se base sur un `CancellationToken/Source` par dÃ©faut ou explicite :
â€¢ `Async.RunSynchronously(computation, ?timeout, ?cancellationToken)`
â€¢ `Async.Start(computation, ?cancellationToken)`

DÃ©clencher l'annulation
â€¢ Token explicite + `cancellationTokenSource.Cancel()`
â€¢ Token explicite avec timeout `new CancellationTokenSource(timeout)`
â€¢ Token par dÃ©faut : `Async.CancelDefaultToken()` â†’ `OperationCanceledException`ğŸ’¥

VÃ©rifier l'annulation
â€¢ Implicite : Ã  chaque mot clÃ© dans bloc async : `let`, `let!`, `for`...
â€¢ Explicite local : `let! ct = Async.CancellationToken` puis `ct.IsCancellationRequested`
â€¢ Explicite global : `Async.OnCancel(callback)`

---

## Annulation d'une tÃ¢che - Exemple - Partie 1

```fs
let sleepLoop = async {
    let stopwatch = System.Diagnostics.Stopwatch()
    stopwatch.Start()
    let log message = printfn $"""   [{stopwatch.Elapsed.ToString("s\.fff")}] {message}"""

    use! __ = Async.OnCancel (fun () ->
        log $"  Cancelled âŒ")

    for i in [ 1..5 ] do
        log $"Step #{i}..."
        do! Async.Sleep 500
        log $"  Completed âœ…"
}
```

---

## Annulation d'une tÃ¢che - Exemple - Partie 2

```fs
open System.Threading

printfn "1. RunSynchronously:"
Async.RunSynchronously(sleepLoop)

printfn "2. Start with CancellationTokenSource + Sleep + Cancel"
use manualCancellationSource = new CancellationTokenSource()
Async.Start(sleepLoop, manualCancellationSource.Token)
Thread.Sleep(1200)
manualCancellationSource.Cancel()

printfn "3. Start with CancellationTokenSource with timeout"
use cancellationByTimeoutSource = new CancellationTokenSource(1200)
Async.Start(sleepLoop, cancellationByTimeoutSource.Token)
```

---

<!-- _footer: '' -->

## Annulation d'une tÃ¢che - Exemple - Outputs

```txt
1. RunSynchronously:
   [0.009] Step #1...
   [0.532]   Completed âœ…
   [0.535] Step #2...
   [1.037]   Completed âœ…
   [1.039] Step #3...
   [1.543]   Completed âœ…
   [1.545] Step #4...
   [2.063]   Completed âœ…
   [2.064] Step #5...
   [2.570]   Completed âœ…
2. Start with CancellationTokenSource + Sleep + Cancel
   [0.000] Step #1...
   [0.505]   Completed âœ…
   [0.505] Step #2...
   [1.011]   Completed âœ…
   [1.013] Step #3...
   [1.234]   Cancelled âŒ
3. Start with CancellationTokenSource with timeout
... idem 2.
```

---

<!-- _class: blue chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_entretien.png)

# 2.

## Interop avec TPL .NET

TPL : Task Parallel Library

---

# Interaction avec librairie .NET

Librairies asynchrones en .NET et pattern `async`/`await` Câ™¯ :
â†’ BasÃ©s sur **TPL** et le type `Task`

Passerelles avec worflow asynchrone Fâ™¯ :

- Fonctions `Async.AwaitTask` et `Async.StartAsTask`
- Bloc `task {}`

---

# Fonctions passerelles

`Async.AwaitTask: Task<'T> -> Async<'T>`
â†’ Consommer une librairie .NET asynchrone dans bloc `async`

`Async.StartAsTask: Async<'T> -> Task<'T>`
â†’ Lancer un calcul async sous forme de `Task`

```fs
let getValueFromLibrary param = async {
    let! value = DotNetLibrary.GetValueAsync param |> Async.AwaitTask
    return value
}

let computationForCaller param =
    async {
        let! result = getAsyncResult param
        return result
    } |> Async.StartAsTask
```

---

# Bloc `task {}`

> Permet de consommer directement une librairie .NET asynchrone en ne faisant qu'un seul `Async.AwaitTask` plutÃ´t que 1 Ã  chaque mÃ©thode appelÃ©e

ğŸ’¡ Disponible en Fâ™¯ 6 ou via package nuget [Ply](https://github.com/crowded/ply)

```fs
#r "nuget: Ply"
open FSharp.Control.Tasks

task {
    use client = new System.Net.Http.HttpClient()
    let! response = client.GetStringAsync("https://www.google.fr/")
    response.Substring(0, 300) |> printfn "%s"
}  // Task<unit>
|> Async.AwaitTask  // Async<unit>
|> Async.RunSynchronously
```

---

# `Async` *vs* `Task`

#### 1. Mode de dÃ©marrage du calcul

`Task` = *hot tasks* â†’ calculs dÃ©marrÃ©s immÃ©diatementâ—

`Async` = *task generators* = spÃ©cification de calculs, indÃ©pendante du dÃ©marrage
â†’ Approche fonctionnelle : sans effet de bord ni mutation, composabilitÃ©
â†’ ContrÃ´le du mode de dÃ©marrage : quand et comment ğŸ‘

#### 2. Support de l'annulation

`Task` : en ajoutant un paramÃ¨tre `CancellationToken` aux mÃ©thodes async
â†’ Oblige Ã  tester manuellement si token est annulÃ© = fastidieux + _error proneâ—_

`Async` : support automatique dans les calculs - token Ã  fournir au dÃ©marrage ğŸ‘

---

# PiÃ¨ges du pattern `async`/`await` en Câ™¯

#### PiÃ¨ge 1 - Vraiment asynchrone ?

En Câ™¯ : mÃ©thode `async` reste sur le thread appelant jusqu'au 1er `await`
â†’ Sentiment trompeur d'Ãªtre asynchrone dans toute la mÃ©thode

```cs
async Task WorkThenWait() {
    Thread.Sleep(1000);           // âš ï¸ Bloque thread appelant !
    await Task.Delay(1000);       // Vraiment async Ã  partir d'ici ğŸ¤”
}
```

En Fâ™¯ : `async` ne dÃ©finit pas une fonction mais un **bloc**

```fs
let workThenWait () =
    Thread.Sleep(1000)
    async { do! Async.Sleep(1000) }   // Async que dans ce bloc ğŸ§
```

---

## PrÃ©conisation pour fonction asynchrone en Fâ™¯

Fonction asynchrone = renvoyant un `Async<_>`
â†’ On s'attend Ã  ce qu'elle soit **totalement** asynchrone
â†’ Fonction prÃ©cÃ©dente `workThenWait` ne respecte pas cette attente

â˜ **PrÃ©conisation :**
Â» Mettre tout le corps de la fonction asynchrone dans un bloc `async`

```fs
let workThenWait () = async {
    Thread.Sleep(1000)
    printfn "work"
    do! Async.Sleep(1000)
}
```

---

# PiÃ¨ge 2 - Omettre le `await` en Câ™¯

```cs
async Task PrintAfterOneSecond(string message) {
    await Task.Delay(1000);
    Console.WriteLine($"[{DateTime.Now:T}] {message}");
}

async Task Main() {
    PrintAfterOneSecond("Before"); // âš ï¸ Manque `await`â†’ warning CS4014
    Console.WriteLine($"[{DateTime.Now:T}] After");
    await Task.CompletedTask;
}
```

Cela compile ğŸ“ et produit un rÃ©sultat inattendu _After_ avant _Beforeâ—_

```txt
[11:45:27] After
[11:45:28] Before
```

---

# PiÃ¨ge 2 - Ã‰quivalent en Fâ™¯

```fs
let printAfterOneSecond message = async {
    do! Async.Sleep 1000
    printfn $"[{DateTime.Now:T}] {message}"
}

async {
    printAfterOneSecond "Before" // âš ï¸ Manque `do!` â†’ warning FS0020
    printfn $"[{DateTime.Now:T}] After"
} |> Async.RunSynchronously
```

Cela compile aussi ğŸ“ et produit un autre rÃ©sultat inattendu : pas de _Beforeâ—_

```txt
[11:45:27] After
```

---

# PiÃ¨ge 2 - Ã‰tude des warnings

Les exemples prÃ©cÃ©demment compilent mais avec des gros *warnings* !

En Câ™¯, le [*warning CS4014*](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/compiler-messages/cs4014) indique :
â *Because this call is not awaited, execution of the current method continues
Â Â Â before the call is completed. Consider applying the `await` operator...* â

En Fâ™¯, le *warning FS0020* est accompagnÃ© du message :
â *The result of this expression has type `Async<unit>` and is implicitly ignored.
Â Â Â Consider using `ignore` to discard this value explicitly...* â

â˜ **PrÃ©conisation :** veillez Ã  **toujours** traiter ce type de *warning* !
Â Â Â Â *C'est encore \+ crucial en Fâ™¯ oÃ¹ la compilation est \+ dÃ©licate.*

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 3.

## LeÂ Â Â  RÃ©capâ€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_note.png)

# Programmation asynchrone en Fâ™¯

Via bloc `async {}` en Fâ™¯ pur
â†’ Similaire mais antÃ©rieur au pattern `async`/`await`
â†’ Permet d'Ã©viter quelques piÃ¨ges du `async`/`await`
â†’ Oblige Ã  dÃ©marrer manuellement calcul
â†’ Mais compilation empÃªche d'oublier

Via bloc `task {}`
â†’ Facilite interactions avec librairie .NET asynchrone

---

# Ressources complÃ©mentaires

ğŸ”— https://docs.microsoft.com/en-us/dotnet/fsharp/tutorials/async

---

<!-- _class: end invert lead-->

# Merci ğŸ™
