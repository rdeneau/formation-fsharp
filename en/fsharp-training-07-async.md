---
marp: true
html: true
theme: 'd-edge'
title: 'F‚ôØ Training ‚Ä¢ Programmation asynchrone'
footer: 'F‚ôØ Training ‚Ä¢ Programmation asynchrone'
paginate: true
---

<!-- _class: title invert -->

# F‚ôØ Training

## *Programmation asynchrone*

### 2025 April

---

<!-- _class: toc agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- *Workflow* asynchrone
- Interop avec la TPL .NET

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_software.png)

# 1.

## Workflow asynchrone

---

# Workflow asynchrone : Besoins

1. Ne pas bloquer le thread courant en attendant un calcul long
2. Permettre calculs en parall√®le
3. Indiquer qu'un calcul peut prendre du temps

---

# Type `Async<'T>`

- Repr√©sente un calcul asynchrone
- Similaire au pattern `async/await` avant l'heure üìÜ
  - 2007 : `Async<'T>` F‚ôØ
  - 2012 : `Task<T>` .NET et pattern `async`/`await`
  - 2017 : `Promise` JavaScript et pattern `async`/`await`

---

# M√©thodes renvoyant un objet `Async`

`Async.AwaitTask(task: Task or Task<'T>) : Async<'T>`
‚Üí Conversion d'une `Task` (.NET) en `Async` (F‚ôØ)

`Async.Sleep(milliseconds or TimeSpan) : Async<unit>`
‚âÉ `await Task.Delay()` ‚â† `Thread.Sleep` ‚Üí ne bloque pas le thread courant

[FSharp.Control.CommonExtensions](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-commonextensions.html) : √©tend le type `System.IO.Stream`
‚Üí `AsyncRead(buffer: byte[], ?offset: int, ?count: int) : Async<int>`
‚Üí `AsyncWrite(buffer: byte[], ?offset: int, ?count: int) : Async<unit>`

[FSharp.Control.WebExtensions](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-webextensions.html) : √©tend le type `System.Net.WebClient`
‚Üí `AsyncDownloadData(address: Uri) : Async<byte[]>`
‚Üí `AsyncDownloadString(address: Uri) : Async<string`

---

# Lancement d'un calcul async

`Async.RunSynchronously(calc: Async<'T>, ?timeoutMs: int, ?cancellationToken) : 'T`
‚Üí Attend la fin du calcul mais bloque le thread appelant ! (‚â† `await` C‚ôØ) ‚ö†Ô∏è

`Async.Start(operation: Async<unit>, ?cancellationToken) : unit`
‚Üí Ex√©cute l'op√©ration en background *(sans bloqu√© le thread appelant)*
‚ö†Ô∏è Si une exception survient, elle est "aval√©e" !

`Async.StartImmediate(calc: Async<'T>, ?cancellationToken) : unit`
‚Üí Ex√©cute le calcul dans le thread appelant !
üí° Pratique dans une GUI pour la mettre √† jour : barre de progression...

`Async.StartWithContinuations(calc, continuations..., ?cancellationToken)`
‚Üí Idem `Async.RunSynchronously` ‚ö†Ô∏è ... avec 3 *callbacks* de continuation :
‚Üí en cas de succ√®s ‚úÖ, d'exception üí• et d'annulation üõë

---

# Bloc `async { expression }`

*A.k.a. Async workflow*

Syntaxe pour √©crire de mani√®re s√©quentielle un calcul asynchrone
‚Üí Le r√©sultat du calcul est wrapp√© dans un objet `Async`

**Mots cl√©s**
‚Ä¢ `return` ‚Üí valeur finale du calcul - `unit` si omis
‚Ä¢ `let!` *(prononcer ¬´ let bang ¬ª)*
¬†¬†¬†‚Üí acc√®s au r√©sultat d'un sous-calcul async *(‚âÉ `await` en C‚ôØ)*
‚Ä¢ `use!` ‚Üí idem `use` *(gestion d'un `IDisposable`)* + `let!`
‚Ä¢ `do!` ‚Üí idem `let!` pour calcul async sans retour (`Async<unit>`)

---

## Bloc `async` - Exemples

```fs
let repeat (computeAsync: int -> Async<string>) times = async {
    for i in [ 1..times ] do
        printf $"Start operation #{i}... "
        let! result = computeAsync i
        printfn $"Result: {result}"
}

let basicOp (num: int) = async {
    do! Async.Sleep 150
    return $"{num} * ({num} - 1) = {num * (num - 1)}"
}

repeat basicOp 5 |> Async.RunSynchronously

// Start operation #1... Result: 1 * (1 - 1) = 0
// Start operation #2... Result: 2 * (2 - 1) = 2
// Start operation #3... Result: 3 * (3 - 1) = 6
// Start operation #4... Result: 4 * (4 - 1) = 12
// Start operation #5... Result: 5 * (5 - 1) = 20
```

---

# Usage inappropri√© de `Async.RunSynchronously`

`Async.RunSynchronously` lance le calcul et renvoie son r√©sultat MAIS en bloquant le thread appelant ! Ne l'utiliser qu'en ¬´ bout de cha√Æne ¬ª et pas pour *unwrap* des calculs asynchrones interm√©diaires ! Utiliser plut√¥t un bloc `async`.

```fs
// ‚ùå √Ä √©viter
let a = calcA |> Async.RunSynchronously
let b = calcB a |> Async.RunSynchronously
calcC b

// ‚úÖ √Ä pr√©f√©rer
async {
    let! a = calcA
    let! b = calcB a
    return calcC b
} |> Async.RunSynchronously
```

---

# Calculs en parall√®le

**1.** `Async.Parallel(computations: seq<Async<'T>>, ?maxBranches) : Async<'T[]>`

‚âÉ `Task.WhenAll` : mod√®le [Fork-Join](https://en.wikipedia.org/wiki/Fork%E2%80%93join_model)
- *Fork* : calculs lanc√©s en parall√®le
- Attente de la terminaison de tous les calculs
- *Join* : agr√©gation des r√©sultats *(qui sont du m√™me type)*
  - dans le m√™me ordre que les calculs

---

## `Async.Parallel` - Exemple


```fs
let downloadSite (site: string) = async {
    do! Async.Sleep (100 * site.Length)
    printfn $"{site} ‚úÖ"
    return site.Length
}

[ "google"; "msn"; "yahoo" ]
|> List.map downloadSite  // string list
|> Async.Parallel         // Async<string[]>
|> Async.RunSynchronously // string[]
|> printfn "%A"

// msn ‚úÖ
// yahoo ‚úÖ
// google ‚úÖ
// [|6; 3; 5|]
```

---

# Calculs en parall√®le (2)

**2.** `Async.StartChild(calc: Async<'T>, ?timeoutMs: int) : Async<Async<'T>>`

Permet de lancer en parall√®le plusieurs calculs
‚Üí ... dont les r√©sultats sont de types diff√©rents _(‚â† `Async.Parallel`)_

S'utilise dans bloc `async` avec 2 `let!` par calcul enfant *(cf. `Async<Async<'T>>`)*

Annulation conjointe üìç
‚Üí Calcul enfant partage jeton d‚Äôannulation du calcul parent

---

## `Async.StartChild` - Exemple partie 1

Soit le fonction `delay`
‚Üí qui renvoie la valeur sp√©cifi√©e `x`
‚Üí au bout de `ms` millisecondes

```fs
let delay (ms: int) x = async {
    do! Async.Sleep ms
    return x
}

// üí° Minutage avec la directive FSI `#time` ‚Ä¢ üîó https://kutt.it/Zbp6ot
#time "on"  // --> Minutage activ√©
"a" |> delay 100 |> Async.RunSynchronously // R√©el : 00:00:00.111, Proc...
#time "off" // --> Minutage d√©sactiv√©
```

---

<!-- _footer: "" -->

## `Async.StartChild` - Exemple partie 2

```fs
let inSeries = async {
    let! result1 = "a" |> delay 100
    let! result2 = 123 |> delay 200
    return (result1, result2)
}

let inParallel = async {
    let! child1 = "a" |> delay 100 |> Async.StartChild
    let! child2 = 123 |> delay 200 |> Async.StartChild
    let! result1 = child1
    let! result2 = child2
    return (result1, result2)
}

#time "on"
inSeries |> Async.RunSynchronously    // R√©el : 00:00:00.317, ...
#time "off"
#time "on"
inParallel |> Async.RunSynchronously  // R√©el : 00:00:00.205, ...
#time "off"
```

---

# Annulation d'une t√¢che

Se base sur un `CancellationToken/Source` par d√©faut ou explicite :
‚Ä¢ `Async.RunSynchronously(computation, ?timeout, ?cancellationToken)`
‚Ä¢ `Async.Start(computation, ?cancellationToken)`

D√©clencher l'annulation
‚Ä¢ Token explicite + `cancellationTokenSource.Cancel()`
‚Ä¢ Token explicite avec timeout `new CancellationTokenSource(timeout)`
‚Ä¢ Token par d√©faut : `Async.CancelDefaultToken()` ‚Üí `OperationCanceledException`üí•

V√©rifier l'annulation
‚Ä¢ Implicite : √† chaque mot cl√© dans bloc async : `let`, `let!`, `for`...
‚Ä¢ Explicite local : `let! ct = Async.CancellationToken` puis `ct.IsCancellationRequested`
‚Ä¢ Explicite global : `Async.OnCancel(callback)`

---

## Annulation d'une t√¢che - Exemple - Partie 1

```fs
let sleepLoop = async {
    let stopwatch = System.Diagnostics.Stopwatch()
    stopwatch.Start()
    let log message = printfn $"""   [{stopwatch.Elapsed.ToString("s\.fff")}] {message}"""

    use! __ = Async.OnCancel (fun () ->
        log $"  Cancelled ‚ùå")

    for i in [ 1..5 ] do
        log $"Step #{i}..."
        do! Async.Sleep 500
        log $"  Completed ‚úÖ"
}
```

---

## Annulation d'une t√¢che - Exemple - Partie 2

```fs
open System.Threading

printfn "1. RunSynchronously:"
Async.RunSynchronously(sleepLoop)

printfn "2. Start with CancellationTokenSource + Sleep + Cancel"
use manualCancellationSource = new CancellationTokenSource()
Async.Start(sleepLoop, manualCancellationSource.Token)
Thread.Sleep(1200)
manualCancellationSource.Cancel()

printfn "3. Start with CancellationTokenSource with timeout"
use cancellationByTimeoutSource = new CancellationTokenSource(1200)
Async.Start(sleepLoop, cancellationByTimeoutSource.Token)
```

---

<!-- _footer: '' -->

## Annulation d'une t√¢che - Exemple - Outputs

```txt
1. RunSynchronously:
   [0.009] Step #1...
   [0.532]   Completed ‚úÖ
   [0.535] Step #2...
   [1.037]   Completed ‚úÖ
   [1.039] Step #3...
   [1.543]   Completed ‚úÖ
   [1.545] Step #4...
   [2.063]   Completed ‚úÖ
   [2.064] Step #5...
   [2.570]   Completed ‚úÖ
2. Start with CancellationTokenSource + Sleep + Cancel
   [0.000] Step #1...
   [0.505]   Completed ‚úÖ
   [0.505] Step #2...
   [1.011]   Completed ‚úÖ
   [1.013] Step #3...
   [1.234]   Cancelled ‚ùå
3. Start with CancellationTokenSource with timeout
... idem 2.
```

---

<!-- _class: blue chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_entretien.png)

# 2.

## Interop avec TPL .NET

TPL : Task Parallel Library

---

# Interaction avec librairie .NET

Librairies asynchrones en .NET et pattern `async`/`await` C‚ôØ :
‚Üí Bas√©s sur **TPL** et le type `Task`

Passerelles avec worflow asynchrone F‚ôØ :

- Fonctions `Async.AwaitTask` et `Async.StartAsTask`
- Bloc `task {}`

---

# Fonctions passerelles

`Async.AwaitTask: Task<'T> -> Async<'T>`
‚Üí Consommer une librairie .NET asynchrone dans bloc `async`

`Async.StartAsTask: Async<'T> -> Task<'T>`
‚Üí Lancer un calcul async sous forme de `Task`

```fs
let getValueFromLibrary param = async {
    let! value = DotNetLibrary.GetValueAsync param |> Async.AwaitTask
    return value
}

let computationForCaller param =
    async {
        let! result = getAsyncResult param
        return result
    } |> Async.StartAsTask
```

---

# Bloc `task {}`

> Permet de consommer directement une librairie .NET asynchrone en ne faisant qu'un seul `Async.AwaitTask` plut√¥t que 1 √† chaque m√©thode appel√©e

üí° Disponible en F‚ôØ 6 ou via package nuget [Ply](https://github.com/crowded/ply)

```fs
#r "nuget: Ply"
open FSharp.Control.Tasks

task {
    use client = new System.Net.Http.HttpClient()
    let! response = client.GetStringAsync("https://www.google.fr/")
    response.Substring(0, 300) |> printfn "%s"
}  // Task<unit>
|> Async.AwaitTask  // Async<unit>
|> Async.RunSynchronously
```

---

# `Async` *vs* `Task`

#### 1. Mode de d√©marrage du calcul

`Task` = *hot tasks* ‚Üí calculs d√©marr√©s imm√©diatement‚ùó

`Async` = *task generators* = sp√©cification de calculs, ind√©pendante du d√©marrage
‚Üí Approche fonctionnelle : sans effet de bord ni mutation, composabilit√©
‚Üí Contr√¥le du mode de d√©marrage : quand et comment üëç

#### 2. Support de l'annulation

`Task` : en ajoutant un param√®tre `CancellationToken` aux m√©thodes async
‚Üí Oblige √† tester manuellement si token est annul√© = fastidieux + _error prone‚ùó_

`Async` : support automatique dans les calculs - token √† fournir au d√©marrage üëç

---

# Pi√®ges du pattern `async`/`await` en C‚ôØ

#### Pi√®ge 1 - Vraiment asynchrone ?

En C‚ôØ : m√©thode `async` reste sur le thread appelant jusqu'au 1er `await`
‚Üí Sentiment trompeur d'√™tre asynchrone dans toute la m√©thode

```cs
async Task WorkThenWait() {
    Thread.Sleep(1000);           // ‚ö†Ô∏è Bloque thread appelant !
    await Task.Delay(1000);       // Vraiment async √† partir d'ici ü§î
}
```

En F‚ôØ : `async` ne d√©finit pas une fonction mais un **bloc**

```fs
let workThenWait () =
    Thread.Sleep(1000)
    async { do! Async.Sleep(1000) }   // Async que dans ce bloc üßê
```

---

## Pr√©conisation pour fonction asynchrone en F‚ôØ

Fonction asynchrone = renvoyant un `Async<_>`
‚Üí On s'attend √† ce qu'elle soit **totalement** asynchrone
‚Üí Fonction pr√©c√©dente `workThenWait` ne respecte pas cette attente

‚òù **Pr√©conisation :**
¬ª Mettre tout le corps de la fonction asynchrone dans un bloc `async`

```fs
let workThenWait () = async {
    Thread.Sleep(1000)
    printfn "work"
    do! Async.Sleep(1000)
}
```

---

# Pi√®ge 2 - Omettre le `await` en C‚ôØ

```cs
async Task PrintAfterOneSecond(string message) {
    await Task.Delay(1000);
    Console.WriteLine($"[{DateTime.Now:T}] {message}");
}

async Task Main() {
    PrintAfterOneSecond("Before"); // ‚ö†Ô∏è Manque `await`‚Üí warning CS4014
    Console.WriteLine($"[{DateTime.Now:T}] After");
    await Task.CompletedTask;
}
```

Cela compile üìç et produit un r√©sultat inattendu _After_ avant _Before‚ùó_

```txt
[11:45:27] After
[11:45:28] Before
```

---

# Pi√®ge 2 - √âquivalent en F‚ôØ

```fs
let printAfterOneSecond message = async {
    do! Async.Sleep 1000
    printfn $"[{DateTime.Now:T}] {message}"
}

async {
    printAfterOneSecond "Before" // ‚ö†Ô∏è Manque `do!` ‚Üí warning FS0020
    printfn $"[{DateTime.Now:T}] After"
} |> Async.RunSynchronously
```

Cela compile aussi üìç et produit un autre r√©sultat inattendu : pas de _Before‚ùó_

```txt
[11:45:27] After
```

---

# Pi√®ge 2 - √âtude des warnings

Les exemples pr√©c√©demment compilent mais avec des gros *warnings* !

En C‚ôØ, le [*warning CS4014*](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/compiler-messages/cs4014) indique :
‚ùù *Because this call is not awaited, execution of the current method continues
¬†¬†¬†before the call is completed. Consider applying the `await` operator...* ‚ùû

En F‚ôØ, le *warning FS0020* est accompagn√© du message :
‚ùù *The result of this expression has type `Async<unit>` and is implicitly ignored.
¬†¬†¬†Consider using `ignore` to discard this value explicitly...* ‚ùû

‚òù **Pr√©conisation :** veillez √† **toujours** traiter ce type de *warning* !
¬†¬†¬†¬†*C'est encore \+ crucial en F‚ôØ o√π la compilation est \+ d√©licate.*

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 3.

## Le¬†¬†¬† R√©cap‚Äô

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_note.png)

# Programmation asynchrone en F‚ôØ

Via bloc `async {}` en F‚ôØ pur
‚Üí Similaire mais ant√©rieur au pattern `async`/`await`
‚Üí Permet d'√©viter quelques pi√®ges du `async`/`await`
‚Üí Oblige √† d√©marrer manuellement calcul
‚Üí Mais compilation emp√™che d'oublier

Via bloc `task {}`
‚Üí Facilite interactions avec librairie .NET asynchrone

---

# Ressources compl√©mentaires

üîó https://docs.microsoft.com/en-us/dotnet/fsharp/tutorials/async

---

<!-- _class: end invert lead-->

# Merci üôè
