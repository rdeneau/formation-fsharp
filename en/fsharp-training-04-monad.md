---
marp: true
html: true
theme: 'd-edge'
title: 'F‚ôØ Training ‚Ä¢ Monadic Types'
footer: 'F‚ôØ Training ‚Ä¢ Monadic Types'
paginate: true
---

<!-- _class: title invert -->

# F‚ôØ Training

## Monadic Types

### 2025 April

---

<!-- _class: agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- Type `Option`
- Type `Result`
- *Smart constructor*
- *Computation expression* üöÄ

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_baseDonnees.png)

# 1.

## Type `Option`

---

# üí† Type `Option`

A.k.a `Maybe` *(Haskell),* `Optional` *(Java 8)*

Models the absence of value
‚Üí Defined as a union with 2 *cases*

```fsharp
type Option<'Value> =
    | None              // Case without data ‚Üí when value is missing
    | Some of 'Value    // Case with data ‚Üí when value is present
```

---

# `Option` ¬ª Use cases

1. Modeling an optional field
2. Partial operation

---

## Case 1: Modeling an optional field

```fsharp
type Civility = Mr | Mrs

type User = { Name: string; Civility: Civility option } with
    static member Create(name, ?civility) = { Name = name; Civility = civility }

let joey  = User.Create("Joey", Mr)
let guest = User.Create("Guest")
```

‚Üí Make it explicit that `Name` is mandatory and `Civility` optional

‚òù **Warning:** this design does not prevent `Name = null` here *(BCL limit)*

---

## Case 2. Partial operation

Operation where no output value is possible for certain inputs.

#### Example 1: inverse of a number

```fsharp
let inverse n = 1.0 / n

let tryInverse n =
    match n with
    | 0.0 -> None
    | n   -> Some (1.0 / n)
```

| Function     | Operation | Signature               | `n = 0.5`  | `n = 0.0`    |
|--------------|-----------|-------------------------|------------|--------------|
| `inverse`    | Partial   | `float -> float`        | `2.0`      | `infinity` ‚ùì |
| `tryInverse` | Total     | `float -> float option` | `Some 2.0` | `None` üëå    |

---

## Case 2. Partial operation (2)

#### Example 2: find an element in a collection

- Partial operation: `find predicate` ‚Üí üí• when item not found
- Total operation: `tryFind predicate` ‚Üí `None` or `Some item`

#### Benefits üëç

- Explicit, honest / partial operation
  - No special value: `null`, `infinity`
  - No exception
- Forces calling code to handle all cases:
  - `Some value` ‚Üí output value given
  - `None¬†.....` ‚Üí output value missing

---

# `Option` ¬ª Control flow

To test for the presence of the value *(of type `'T`)* in the option

- ‚ùå Do not use `IsSome`, `IsNone` and `Value` (ü§ûüí•)
  - ~~if option.IsSome then option.Value...~~
- üëå By hand with *pattern matching*.
- ‚úÖ `Option.xxx` functions üìç

---

## Manual control flow with *pattern matching*

Example:

```fsharp
let print option =
    match option with
    | Some x -> printfn "%A" x
    | None   -> printfn "None"

print (Some 1.0)  // 1.0
print None        // None
```

---

## Control flow with `Option.xxx` helpers

*Mapping* of the inner value (of type `'T`) **if present**:
‚Üí `map f option` with `f` total operation `'T -> 'U`
‚Üí `bind f option` with `f` partial operation `'T -> 'U option`

Keep value **if present** and if conditions are met:
‚Üí `filter predicate option` with `predicate: 'T -> bool` called only if value present

üë®‚Äçüè´ **Demo**
‚Üí Implementation of `map`, `bind` and `filter` with *pattern matching*

---

## üë®‚Äçüè´ Demo ¬ª Solution

```fsharp
let map f option =             // (f: 'T -> 'U) -> 'T option -> 'U option
    match option with
    | Some x -> Some (f x)
    | None   -> None           // üéÅ 1. Why can't we write `None -> option`?

let bind f option =            // (f: 'T -> 'U option) -> 'T option -> 'U option
    match option with
    | Some x -> f x
    | None   -> None

let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    match option with
    | Some x when predicate x -> option
    | _ -> None                // üéÅ 2. Implement `filter` with `bind`?
```

---

## üéÅ Bonus questions ¬ª Answers

```fsharp
// üéÅ 1. Why can't we write `None -> option`?
let map (f: 'T -> 'U) (option: 'T option) : 'U option =
    match option with
    | Some x -> Some (f x)
    | None   -> (*None*) option  // üí• Type error: `'U option` given != `'T option` expected
```

```fsharp
// üéÅ 2. Implement `filter` with `bind`?
let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    option |> bind (fun x -> if predicate x then option else None)
```

---

## Integrated control flow ¬ª Example

```fsharp
// Question/answer console application
type Answer = A | B | C | D

let tryParseAnswer =
    function
    | "A" -> Some A
    | "B" -> Some B
    | "C" -> Some C
    | "D" -> Some D
    | _   -> None

/// Called when the user types the answer on the keyboard
let checkAnswer (expectedAnswer: Answer) (givenAnswer: string) =
    tryParseAnswer givenAnswer
    |> Option.filter ((=) expectedAnswer)
    |> Option.map (fun _ -> "‚úÖ")
    |> Option.defaultValue "‚ùå"

["X"; "A"; "B"] |> List.map (checkAnswer B)  // ["‚ùå"; "‚ùå"; "‚úÖ"]
```

---

## Integrated control flow ¬ª Advantages

Makes business logic more readable

- No `if hasValue then / else`
- Highlight the *happy path*
- Handle corner cases at the end

üí° The *computation expressions* üìç provide an alternative syntax \+ lightweight

---

# `Option`: comparison with other types

1. `Option` *vs* `List`
2. `Option` *vs* `Nullable`
3. `Option` *vs* `null`

---

## `Option` *vs* `List`

Conceptually closed
‚Üí Option ‚âÉ List of 0 or 1 items
‚Üí See `Option.toList` function: `'t option -> 't list` (`None -> []`, `Some x -> [x]`)

üí° `Option` & `List` modules: many functions with the same name
‚Üí `contains`, `count`, `exist`, `filter`, `fold`, `forall`, `map`

‚òù A `List` can have more than 1 element
‚Üí Type `Option` models absence of value better than type `List`

---

## `Option` *vs* `Nullable`

`System.Nullable<'T>` ‚âÉ `Option<'T>` but more limited

- ‚ùó Does not work for reference types
- ‚ùó Lacks monadic behavior i.e. `map` and `bind` functions
- ‚ùó Lacks built-in pattern matching `Some x | None`
- ‚ùó In F‚ôØ, no magic as in C‚ôØ / keyword `null`

üëâ C‚ôØ uses nullable types whereas F‚ôØ uses only `Option`

---

## `Option` *vs* `null`

Due to the interop with the BCL, F‚ôØ has to deal with `null` in some cases.

üëâ **Good practice**: isolate these cases and wrap them in an `Option` type.

```fsharp
let readLine (reader: System.IO.TextReader) =
    reader.ReadLine() // Can return `null`
    |> Option.ofObj   // `null` becomes None

    // Same than:
    match reader.ReadLine() with
    | null -> None
    | line -> Some line
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_delivery.png)

# 2.

## Type `Result`

---

# üí† Type `Result`

A.k.a `Either` *(Haskell)*

Models a *double-track* Success/Failure

```fsharp
type Result<'Success, 'Error> = // 2 generic parameters
    | Ok of 'Success  // Success Track
    | Error of 'Error // Failure Track
```

Functional way of dealing with business errors *(expected errors)*
‚Üí Allows exceptions to be used only for exceptional errors
‚Üí As soon as an operation fails, the remaining operations are not launched

üîó *Railway-oriented programming (ROP)* https://fsharpforfunandprofit.com/rop/

---

# Module `Result`

Contains less functions than `Option`‚ÅâÔ∏è

`map f result` : to map the success
‚Ä¢ `('T -> 'U) -> Result<'T, 'Error> -> Result<'U, 'Error>`

`mapError f result` : to map the error
‚Ä¢ `('Err1 -> 'Err2) -> Result<'T, 'Err1> -> Result<'T, 'Err2>`

`bind f result` : same as `map` with `f` returning a `Result`
‚Ä¢ `('T -> Result<'U, 'Error>) -> Result<'T, 'Error> -> Result<'U, 'Error>`
‚Ä¢ üí° The result is flattened, like the `flatMap` function on JS arrays
‚Ä¢ ‚ö†Ô∏è Same type of `'Error` for `f` and the input `result`.

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# Quiz *Result* üïπÔ∏è

Implement `Result.map` and `Result.bind`

üí° **Tips:**

- *Map* the *Success* track
- Access the *Success* value using pattern matching

---

## Quiz *Result* üé≤

**Solution:** implementation of `Result.map` and `Result.bind`

```fsharp
// ('T -> 'U) -> Result<'T, 'Error> -> Result<'U, 'Error>
let map f result =
    match result with
    | Ok x    -> Ok (f x)  // ‚òù Ok -> Ok
    | Error e -> Error e   // ‚ö†Ô∏è The 2 `Error e` don't have the same type!

// ('T -> Result<'U, 'Error>) -> Result<'T, 'Error>
//                            -> Result<'U, 'Error>
let bind f result =
    match result with
    | Ok x    -> f x       // ‚òù `f x` already returns a `Result`
    | Error e -> Error e
```

---

# `Result`: Success/Failure tracks

`map`: no track change

```txt
Track      Input          Operation      Output
Success ‚îÄ Ok x    ‚îÄ‚îÄ‚îÄ‚ñ∫ map( x -> y ) ‚îÄ‚îÄ‚îÄ‚ñ∫ Ok y
Failure ‚îÄ Error e ‚îÄ‚îÄ‚îÄ‚ñ∫ map(  ....  ) ‚îÄ‚îÄ‚îÄ‚ñ∫ Error e
```

`bind`: eventual routing to Failure track, but never vice versa

```txt
Track     Input              Operation           Output
Success ‚îÄ Ok x    ‚îÄ‚î¨‚îÄ‚ñ∫ bind( x -> Ok y     ) ‚îÄ‚îÄ‚îÄ‚ñ∫ Ok y
                   ‚îî‚îÄ‚ñ∫ bind( x -> Error e2 ) ‚îÄ‚îê
Failure ‚îÄ Error e ‚îÄ‚îÄ‚îÄ‚ñ∫ bind(     ....      ) ‚îÄ‚î¥‚îÄ‚ñ∫ Error ~
```

‚òù The *mapping/binding* operation is never executed in track Failure.

---

# `Result` *vs* `Option`

`Option` can represent the result of an operation that may fail
‚òù But if it fails, the option doesn't contain the error, just `None`

`Option<'T>` ‚âÉ `Result<'T, unit>`
‚Üí `Some x` ‚âÉ `Ok x`
‚Üí `None` ‚âÉ `Error ()`
‚Üí See `Result.toOption` *(built-in)* and `Result.ofOption` *(below)*

```fsharp
[<RequireQualifiedAccess>]
module Result =
    let ofOption error option =
        match option with
        | Some x -> Ok x
        | None -> Error error
```

---

# `Result` *vs* `Option` (2)

üìÖ **Dates:**
‚Ä¢ The `Option` type is part of F# from the get go
‚Ä¢ The `Result` type is more recent: introduced in F# 4.1 (2016)
¬†¬†‚Üí After numerous articles on *F# for fun and profit*

üìù **Memory:**
‚Ä¢ The `Option` type (alias: `option`) is a regular union: a reference type
‚Ä¢ The `Result` type is a *struct* union: a value type
‚Ä¢ The `ValueOption` type (alias: `voption`) is a *struct* union
¬†¬†‚Üí `ValueNone | ValueSome of 't`

---

## `Result` *vs* `Option` ¬ª Example

Let's change our previous `checkAnswer` to indicate the `Error`:

```fsharp
type Answer = A | B | C | D
type Error = InvalidInput of string | WrongAnswer of Answer

let tryParseAnswer =
    function
    | "A" -> Ok A
    | "B" -> Ok B
    | "C" -> Ok C
    | "D" -> Ok D
    | s   -> Error(InvalidInput s)

let checkAnswerIs expected actual =
    if actual = expected then Ok actual else Error(WrongAnswer actual)

// ...
```

---

## `Result` *vs* `Option` ¬ª Example (2)

```fsharp
// ...

let printAnswerCheck (givenAnswer: string) =
    tryParseAnswer givenAnswer
    |> Result.bind (checkAnswerIs B)
    |> function
       | Ok x                  -> printfn $"%A{x}: ‚úÖ Correct"
       | Error(WrongAnswer x)  -> printfn $"%A{x}: ‚ùå Wrong Answer"
       | Error(InvalidInput s) -> printfn $"%s{s}: ‚ùå Invalid Input"

printAnswerCheck "X";;  // X: ‚ùå Invalid Input
printAnswerCheck "A";;  // A: ‚ùå Wrong Answer
printAnswerCheck "B";;  // B: ‚úÖ Correct
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_atelier.png)

# 3.

## *Smart constructor*

---

# Smart constructor: Purpose

> Making illegal states unrepresentable

üîó https://kutt.it/MksmkG *F‚ôØ for fun and profit, Jan 2013*

- Design to prevent invalid states
  - Encapsulate state *(all primitives)* in an object
- *Smart constructor* guarantees a valid initial state
  - Validates input data
  - If Ko, returns "nothing" (`Option`) or an error (`Result`)
  - If Ok, returns the created object wrapped in an `Option` / a `Result`

---

# Encapsulate the state in a type

‚Üí *Single-case (discriminated) union* üëå : `Type X = private X of a: 'a...`
üîó https://kutt.it/mmMXCo *F‚ôØ for fun and profit, Jan 2013*

‚Üí *Record* üëç : `Type X = private { a: 'a... }`
üîó https://kutt.it/cYP4gY *Paul Blasucci, Mai 2021*

‚òù `private` keyword:
‚Üí Hide object content
‚Üí Fields and constructor no longer visible from outside
‚Üí Smart constructor defined in companion module or static method

---

# *Smart constructor* ¬ª Example #1

Smart constructor :
‚Üí `tryCreate` function in companion module
‚Üí Returns an `Option`

```fsharp
type Latitude = private { Latitude: float } // üëà A single field, named like the

[<RequireQualifiedAccess>]                  // üëà Optional
module Latitude =
    let tryCreate (latitude: float) =
        if latitude >= -90. && latitude <= 90. then
            Some { Latitude = latitude }    // üëà Constructor accessible here
        else
            None

let lat_ok = Latitude.tryCreate 45.  // Some { Latitude = 45.0 }
let lat_ko = Latitude.tryCreate 115. // None
```

---

# *Smart constructor* ¬ª Example #2

Smart constructor:
‚Üí Static method `Of`
‚Üí Returns `Result` with error of type `string`

```fsharp
type Tweet =
    private { Tweet: string }

    static member Of tweet =
        if System.String.IsNullOrEmpty tweet then
            Error "Tweet shouldn't be empty"
        elif tweet.Length > 280 then
            Error "Tweet shouldn't contain more than 280 characters"
        else Ok { Tweet = tweet }

let tweet1 = Tweet.Of "Hello world" // Ok { Tweet = "Hello world" }
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_backEnd.png)

# 4.

## Computation expression üöÄ

---

# Computation expression (CE)

Syntactic sugar hiding a "machinery"
‚Üí Applies the _Separation of Concerns_ principle
‚Üí Code should be more readable inside the *computation expression*

Syntax: `builder { expr }`
‚Üí `builder` instance of a "Builder" üìç
‚Üí `expr` can contain `let`, `let!`, `do!`, `yield`, `yield!`, `return`, `return!`

üí° **Note:** `seq`, `async` and `task` are CEs

---

# Builder

A *computation expression* relies on an object called *Builder*.
‚Üí This object can be used to store a background state.

For each supported keyword (`let!`, `return`...), the *Builder* implements
one or more related methods. Examples:
‚Ä¢ `builder { return expr }` ‚Üí `builder.Return(expr)`
‚Ä¢ `builder { let! x = expr; cexpr }` ‚Üí `builder.Bind(expr, (fun x -> {| cexpr |}))`

The *builder* can also wrap the result in a type of its own:
‚Ä¢ `async { return x }` returns an `Async<'X>` type
‚Ä¢ `seq { yield x }` returns a type `Seq<'X>`

---

# Builder desugaring

The compiler translates to the *builder* methods.
‚Üí The CE hides the complexity of these calls, which are often nested:

```fsharp
seq {
    for n in list do
        yield n
        yield n * 10 }

// Desugared as:
seq.For(list, fun () ->
    seq.Combine(seq.Yield(n),
                seq.Delay(fun () -> seq.Yield(n * 10)) ) )
```

---

# Builder - Example : `logger`

Need: log the intermediate values of a calculation

```fsharp
let log value = printfn $"{value}"

let loggedCalc =
    let x = 42
    log x  // ‚ù∂
    let y = 43
    log y  // ‚ù∂
    let z = x + y
    log z  // ‚ù∂
    z
```

**Problems** ‚ö†Ô∏è
‚ë† Verbose: the `log x` interfere with reading
‚ë° *Error prone*: forget a `log`, log wrong value...

---

# Builder - Example : `logger` (2)

üí° Make logs implicit in a CE when `let!` / `Bind` :

```fsharp
type LoggingBuilder() =
    let log value = printfn $"{value}"; value
    member _.Bind(x, f) = x |> log |> f
    member _.Return(x) = x

let logger = LoggingBuilder()

//---

let loggedCalc = logger {
    let! x = 42
    let! y = 43
    let! z = x + y
    return z
}
```

---

# Builder - Example : `maybe`

Need: simplify the sequence of "trySomething" returning an `Option`

```fsharp
let tryDivideBy bottom top = // (bottom: int) -> (top: int) -> int option
    if (bottom = 0) or (top % bottom <> 0)
    then None
    else Some (top / bottom)

// W/o CE
let division =
    36
    |> tryDivideBy 2                // Some 18
    |> Option.bind (tryDivideBy 3)  // Some 6
    |> Option.bind (tryDivideBy 2)  // Some 3
```

---

# Builder - Example : `maybe` (2)

```fsharp
// With CE
type MaybeBuilder() =
    member _.Bind(x, f) = x |> Option.bind f
    member _.Return(x) = Some x

let maybe = MaybeBuilder()

let division' = maybe {
    let! v1 = 36 |> tryDivideBy 2
    let! v2 = v1 |> tryDivideBy 3
    let! v3 = v2 |> tryDivideBy 2
    return v3
}
```

**Result:** ‚úÖ Symmetry, ‚ùå Intermediate values

---

# Limit : nested CEs

‚úÖ Different CEs can be nested
‚ùå But code becomes difficult to understand

Example: combining `logger` and `maybe` ‚ùì

Alternative solution üöÄüöÄ:

```fsharp
// Define an operator for `bind`
let inline (>>=) x f = x |> Option.bind f

let logM value = printfn $"{value}"; Some value  // 'a -> 'a option

let division' =
    36 |> tryDivideBy 2 >>= logM
      >>= tryDivideBy 3 >>= logM
      >>= tryDivideBy 2 >>= logM
```

---

<!-- _footer: '' -->

# Limit: combining CEs

How to combine `Async` + `Option`/`Result` ? \
‚Üí `asyncResult` CE + helpers in [FsToolkit](https://demystifyfp.gitbook.io/fstoolkit-errorhandling/#a-motivating-example)

```fsharp
type LoginError =
    | InvalidUser | InvalidPassword
    | Unauthorized of AuthError | TokenErr of TokenError

let login username password =
    asyncResult {
        // tryGetUser: string -> Async<User option>
        let! user = username |> tryGetUser |> AsyncResult.requireSome InvalidUser
        // isPasswordValid: string -> User -> bool
        do! user |> isPasswordValid password |> Result.requireTrue InvalidPassword
        // authorize: User -> Async<Result<unit, AuthError>>
        do! user |> authorize |> AsyncResult.mapError Unauthorized
        // createAuthToken: User -> Result<AuthToken, TokenError>
        return! user |> createAuthToken |> Result.mapError TokenErr
    } // Async<Result<AuthToken, LoginError>>
```

---

# CE: the Swiss army knife ‚ú®

The *computation expressions* serve different purposes:
‚Ä¢ C‚ôØ `yield return` ‚Üí F‚ôØ `seq {}`
‚Ä¢ C‚ôØ `async/await` ‚Üí F‚ôØ `async {}`
‚Ä¢ C‚ôØ LINQ expressions `from... select` ‚Üí F‚ôØ `query {}`
‚Ä¢ ...

Underlying theoretical foundations :
‚Ä¢ Monoid
‚Ä¢ Monad
‚Ä¢ Applicative

---

# Monoid

‚âÉ Type `T` defining a set with:

1. Operation `(+): T -> T -> T`
   ‚Üí To combine sets and keep the same "type"
   ‚Üí Associative: `a + (b + c)` ‚â° `(a + b) + c`
2. Neutral element *(aka identity)* ‚âÉ empty set
   ‚Üí Combinable with any set without effect
   ‚Üí `a + e` ‚â° `e + a` ‚â° `a`

---

# CE monoidal

The builder of a monoidal CE *(such as `seq`)* has *at least* :
- `Yield` to build the set element by element
- `Combine` ‚â° `(+)` (`Seq.append`)
- Zero` ‚â° neutral element (`Seq.empty`)

Generally added (among others):
- `For` to support `for x in xs do ...`
- `YieldFrom` to support `yield!`

---

# Monad

‚âÉ Generic type `M<'T>` with:

1. `return` construction function
   - Signature : `(value: 'T) -> M<'T>`
   - ‚âÉ Wrap a value
2. Link function `bind` *(aka `>>=` operator)*
   - Signature : `(f: 'T -> M<'U>) -> M<'T> -> M<'U>`
   - Use wrapped value, map with `f` function
     ¬†¬†¬†¬†¬†¬†¬†to a value of another type and re-wrap the result

---

# Monad: laws

`return` ‚â° neutral element for `bind`

- Left: `return x |> bind f` ‚â° `f x`
- Right: `m |> bind return` ‚â° `m`

`bind` is associative

- `m |> bind f |> bind g` ‚â° `m |> bind (fun x -> f x |> bind g)`

---

# Monads and languages

**Haskell**
‚Ä¢ Monads used a lot. Common ones: `IO`, `Maybe`, `State`, `Reader`.
‚Ä¢ `Monad` is a *type class* for easily creating your own monads.

**F‚ôØ**
‚Ä¢ Some CEs allow monadic operations.
‚Ä¢ More rarely used directly _(except by Haskellers, OCamlers...)_

**C‚ôØ**
‚Ä¢ Monad implicit in LINQ
‚Ä¢ [LanguageExt](https://github.com/louthy/language-ext) library for functional programming

---

# Monadic CE

The builder of a monadic CE has `Return` and `Bind` methods.

The `Option` and `Result` types are monadic.
‚Üí We can create their own CE :

```fsharp
type OptionBuilder() =
    member _.Bind(x, f) = x |> Option.bind f
    member _.Return(x) = Some x

type ResultBuilder() =
    member _.Bind(x, f) = x |> Result.bind f
    member _.Return(x) = Ok x
```

---

# Monadic and generic CE

[FSharpPlus](http://fsprojects.github.io/FSharpPlus//computation-expressions.html) provides a `monad` CE
‚Üí Works for all monadic types: `Option`, `Result`, ... and even `Lazy`!

```fsharp
#r "nuget: FSharpPlus"
open FSharpPlus

let lazyValue = monad {
    let! a = lazy (printfn "I'm lazy"; 2)
    let! b = lazy (printfn "I'm lazy too"; 10)
    return a + b
} // System.Lazy<int>

let result = lazyValue.Value
// I'm lazy
// I'm lazy too
// val result : int = 12
```

---

# Monadic and generic CE (2)

Example with `Option` type:

```fsharp
#r "nuget: FSharpPlus"
open FSharpPlus

let addOptions x' y' = monad {
    let! x = x'
    let! y = y'
    return x + y
}

let v1 = addOptions (Some 1) (Some 2) // Some 3
let v2 = addOptions (Some 1) None     // None
```

---

# Monadic and generic CE (3)

‚ö†Ô∏è **Limit:** several monadic types cannot be mixed!

```fsharp
#r "nuget: FSharpPlus"
open FSharpPlus

let v1 = monad {
    let! a = Ok 2
    let! b = Some 10
    return a + b
} // üí• Error FS0043...

let v2 = monad {
    let! a = Ok 2
    let! b = Some 10 |> Option.toResult
    return a + b
} // val v2 : Result<int,unit> = Ok 12
```

---

# Specific monadic CE

[FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling/) library provides:
‚Ä¢ CE `option {}` specific to type `Option<'T>` _(example below)_
‚Ä¢ CE `result {}` specific to type `Result<'Ok, 'Err>`

‚òù Recommended as it is more explicit than `monad` CE.

```fsharp
#r "nuget: FSToolkit.ErrorHandling"
open FsToolkit.ErrorHandling

let addOptions x' y' = option {
    let! x = x'
    let! y = y'
    return x + y
}

let v1 = addOptions (Some 1) (Some 2) // Some 3
let v2 = addOptions (Some 1) None     // None
```

---

# Applicative _(a.k.a Applicative Functor)_

‚âÉ Generic type `M<'T>` -- 3 styles:

**Style A:** Applicative with `apply`/`<*>` and `pure`/`return`
‚Ä¢ ‚ùå Not easy to understand
‚Ä¢ ‚òù Not recommended by Don Syme in the [Nov. 2020 note](https://github.com/dsyme/fsharp-presentations/blob/master/design-notes/rethinking-applicatives.md)

**Style B:** Applications with `mapN`
‚Ä¢ `map2`, `map3`... `map5` combines 2 to 5 wrapped values

**Style C:** Applicatives with `let! ... and! ...` in a CE
‚Ä¢ Same principle: combine several wrapped values
‚Ä¢ Available from F‚ôØ 5 _([announcement Nov. 2020](https://devblogs.microsoft.com/dotnet/announcing-f-5/#applicative-computation-expressions))_

‚òù **Tip:** Styles B and C are equally recommended.

---

# Applicative CE

Library [FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling/) offers:
‚Ä¢ Type `Validation<'Ok, 'Err>` ‚â° `Result<'Ok, 'Err list>`
‚Ä¢ CE `validation {}` supporting `let!...and!...` syntax.

Allows errors to be accumulated ‚Üí Uses:
‚Ä¢ Parsing external inputs
‚Ä¢ *Smart constructor* *(Example code slide next...)*

---

<!-- _footer: '' -->

# Applicative CE: example

```fsharp
#r "nuget: FSToolkit.ErrorHandling"
open FsToolkit.ErrorHandling

type [<Measure>] cm
type Customer = { Name: string; Height: int<cm> }

let validateHeight height =
    if height <= 0<cm>
    then Error "Height must me positive"
    else Ok height

let validateName name =
    if System.String.IsNullOrWhiteSpace name
    then Error "Name can't be empty"
    else Ok name

module Customer =
    let tryCreate name height : Result<Customer, string list> =
        validation {
            let! validName = validateName name
            and! validHeight = validateHeight height
            return { Name = validName; Height = validHeight }
        }

let c1 = Customer.tryCreate "Bob" 180<cm>  // Ok { Name = "Bob"; Height = 180 }
let c2 = Customer.tryCreate "Bob" 0<cm> // Error ["Height must me positive"]
let c3 = Customer.tryCreate "" 0<cm>    // Error ["Name can't be empty"; "Height must me positive"]
```

---

<!-- _footer: '' -->

# Applicative _vs_ Monad

The `Result` type is "monadic": on the 1st error, we "unplug".

There is another type called `Validation` that is "applicative": it allows to accumulate errors.

- ‚âÉ `Result<'ok, 'error list>`\
- Handy for validating user input and reporting all errors

üîó **Ressources**

- [FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling)
- [Validation with F# 5 and FsToolkit](https://www.compositional-it.com/news-blog/validation-with-f-5-and-fstoolkit/)

---

# Applicative _vs_ Monad (2)

Example: `Validation.map2` to combine 2 results and get the list of their eventual errors.

```fsharp
module Validation =
    // f : 'T -> 'U -> Result<'V, 'Err list>
    // x': Result<'T, 'Err list>
    // y': Result<'U, 'Err list>
    //  -> Result<'V, 'Err list>
    let map2 f x' y' =
        match x', y' with
        | Ok x, Ok y -> f x y
        | Ok _, Error errors | Error errors, Ok _ -> Error errors
        | Error errors1, Error errors2 -> Error (errors1 @ errors2) // üëà ‚ë°
```

---

# Other CE

We've seen 2 libraries that extend F‚ôØ and offer their CEs:

- FSharpPlus ‚Üí `monad`
- FsToolkit.ErrorHandling ‚Üí `option`, `result`, `validation`

Many libraries have their own DSL *(Domain Specific Language.)*
Some are based on CE:

- [Expecto](https://github.com/haf/expecto): Testing library (`test "..." {...}`)
- [Farmer](https://github.com/compositionalit/farmer): Infra as code for Azure (`storageAccount {...}`)
- [Saturn](https://saturnframework.org/): Web framework on top of ASP.NET Core (`application {...}`)

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 5.

## Wrap up

---

# Union types: `Option` and `Result`

- What they are used for:
  - Model absence of value and business errors
  - Partial operations made total `tryXxx`
    - *Smart constructor* `tryCreate`
- How to use them:
  - Chaining: `map`, `bind`, `filter` ‚Üí *ROP*
  - Pattern matching
- Their benefits:
  - `null` free, `Exception` free ‚Üí no guard clauses Cluttering the code
  - Makes business logic and *happy path* more readable

---

# *Computation expression (CE)*

- Syntactic sugar: inner syntax standard or "banged" (`let!`)
- *Separation of Concerns*: business logic *vs* "machinery"
- Compiler is linked to *builder*
  - Object storing a state
  - Builds an output value of a specific type
- Can be nested but not easy to combine!
- Underlying theoretical concepts
  - Monoid ‚Üí `seq` *(of composable elements and with a "zero "*)
  - Monad ‚Üí `async`, `option`, `result`
  - Applicative ‚Üí `validation`/`Result<'T, 'Err list>`
- Libraries: FSharpPlus, FsToolkit, Expecto, Farmer, Saturn

---

# üîó Additional ressources

- Compositional IT *(Isaac Abraham)*
  - [*Writing more succinct C# ‚Äì in F#! (Part 2)*](https://kutt.it/gpIgfD) ‚Ä¢ 2020
- F# for Fun and Profit *(Scott Wlaschin)*
  - [*The Option type*](https://kutt.it/e78rNj) ‚Ä¢ 2012
  - [*Making illegal states unrepresentable*](https://kutt.it/7J5Krc) ‚Ä¢ 2013
  - [*The "Map and Bind and Apply, Oh my!" series*](https://kutt.it/ebfGNA) ‚Ä¢ 2015
  - [*The "Computation Expressions" series*](https://kutt.it/drchkQ) ‚Ä¢ 2013
- Extending F# through Computation Expressions
  - üìπ [Video](https://youtu.be/bYor0oBgvws)
  - üìú [Article](https://panesofglass.github.io/computation-expressions/#/)
- [Computation Expressions Workshop](https://github.com/panesofglass/computation-expressions-workshop)
- [Applicatives IRL](https://thinkbeforecoding.com/post/2020/10/03/applicatives-irl) by Jeremie Chassaing

---

<!-- _class: end invert lead-->

# Thanks üôè
