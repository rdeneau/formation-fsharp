---
marp: true
html: true
theme: 'd-edge'
title: 'Fâ™¯ Training â€¢ Les types'
footer: 'Fâ™¯ Training â€¢ Les types'
paginate: true
---

<!-- Notes :
F# ne se limite pas aux fonctions ; le puissant systÃ¨me de types est un autre ingrÃ©dient clÃ©. Et comme pour les fonctions, il est essentiel de comprendre le systÃ¨me de types pour Ãªtre Ã  l'aise dans le langage.

En plus des types courants de .NET, F# possÃ¨de d'autres types qui sont trÃ¨s courants dans les langages fonctionnels mais qui ne sont pas disponibles dans les langages impÃ©ratifs comme C# ou Java.
-->

<!-- _class: title invert -->

# Fâ™¯ Training

## *Types composites*

### 2025 April

---

<!-- _class: toc agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

1. GÃ©nÃ©ralitÃ©s
2. Tuples
3. Records
4. Unions
5. Enums
6. Records anonymes
7. Types valeur

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_idee.png)

# 1.

## GÃ©nÃ©ralitÃ©s surÂ les types

---

# Vue d'ensemble

Classifications des types .NET :

1. Types valeur *vs* types rÃ©fÃ©rence -- abrÃ©gÃ©s *TVal* et *TRef*
2. Types primitifs *vs* types composites
3. Types gÃ©nÃ©riques
4. Types crÃ©Ã©s Ã  partir de valeurs littÃ©rales
5. Types algÃ©briques : somme *vs* produit

---

<!-- _footer: '' -->

# Types composites

CrÃ©Ã©s par combinaison d'autres types

| CatÃ©gorie      | *Version* | Nom                    | *TRef*   | *TVal*    |
|----------------|-----------|------------------------|----------|-----------|
| Types .NET     |           | `class`                | âœ…        | âŒ         |
|                |           | `struct`, `enum`       | âŒ        | âœ…         |
| SpÃ©cifiques Câ™¯ | Câ™¯ 3.0    | Type anonyme           | âœ…        | âŒ         |
|                | Câ™¯ 7.0    | *Value tuple*          | âŒ        | âœ…         |
|                | Câ™¯ 9.0    | `record (class)`       | âœ…        | âŒ         |
|                | Câ™¯ 10.0   | `record struct`        | âŒ        | âœ…         |
| SpÃ©cifiques Fâ™¯ |           | *Tuple, Record, Union* | *Opt-in* | *Opt-out* |
|                | Fâ™¯ 4.6    | *Record* anonyme       | *Opt-in* | *Opt-out* |

---

# Types composites (2)

Peuvent Ãªtre gÃ©nÃ©riques (sauf `enum`)

Localisation :

- *Top-level* : `namespace`, *top-level* module Fâ™¯
- *Nested* : `class` (Câ™¯), `module` (Fâ™¯)
- Non dÃ©finissables dans mÃ©thode (Câ™¯) ou valeur simple / fonction (Fâ™¯) !

En Fâ™¯ toutes les dÃ©finitions de type se font avec mot clÃ© `type`
â†’ y compris les classes, les enums et les interfaces !
â†’ mais les tuples n'ont pas besoin d'une dÃ©finition de type

---

# ParticularitÃ© des types Fâ™¯ / types .NET

*Tuple, Record, Union* sont :

- Immuables
- Non nullables
- Ã‰galitÃ© et comparison structurelles *(sauf avec champ fonction)*
- `sealed` : ne peuvent pas Ãªtre hÃ©ritÃ©s
- DÃ©construction, avec mÃªme syntaxe que construction ğŸ“

ReflÃ¨te approches diffÃ©rentes selon paradigme :

- FP : focus sur les donnÃ©es organisÃ©es en types
- OOP : focus sur les comportements, possiblement polymorphiques

---

# Types Ã  valeurs littÃ©rales

Valeurs littÃ©rales = instances dont le type est infÃ©rÃ©

- Types primitifs : Â Â Â Â Â Â Â Â  `true` (`bool`) â€¢ `"abc"` (`string`) â€¢ `1.0m` (`decimal`)
- Tuples Câ™¯ / Fâ™¯ : Â Â Â Â Â Â Â Â Â  `(1, true)`
- Types anonymes Câ™¯ : `new { Name = "Joe", Age = 18 }`
- Records Fâ™¯ : Â Â Â Â Â Â Â Â Â Â Â Â Â Â  `{ Name = "Joe"; Age = 18 }`

â˜ **Note :**

- Les types doivent avoir Ã©tÃ© dÃ©finis au prÃ©alable â—
- Sauf tuples et types anonymes Câ™¯

---

# Types algÃ©briques

> Types composites, combinant d'autres types par produit ou par somme.

Soit les types `A` et `B`, alors on peut crÃ©er :

- Le type produit `A Ã— B` :
  - Contient 1 composante de type `A` ET 1 de type `B`
  - Composantes anonymes ou nommÃ©es
- Le type somme `A + B` :
  - Contient 1 composante de type `A` OU 1 de type `B`

Idem par extension les types produit / somme de N types

---

## Pourquoi les termes "Somme" et "Produit" ?

Soit `N(T)` le nombre de valeurs dans le type `T`, par exemples :

- `bool` â†’ 2 valeurs : `true` et `false`
- `unit` â†’ 1 valeur `()`

Alors :

- Le nombre de valeurs dans le type somme `A + B` est `N(A) + N(B)`.
- Le nombre de valeurs dans le type produit `A Ã— B` est `N(A) * N(B)`.

---

# Types algÃ©briques *vs* Types composites

| Type *custom*                    | Somme | Produit | Composantes nommÃ©es |
|----------------------------------|-------|---------|---------------------|
| `enum`                           | âœ…     | âŒ       | â–                   |
| *Union* Fâ™¯                       | âœ…     | âŒ       | â–                   |
| `class` â­, `interface`, `struct` | âŒ     | âœ…       | âœ…                   |
| *Record* Fâ™¯                      | âŒ     | âœ…       | âœ…                   |
| *Tuple* Fâ™¯                       | âŒ     | âœ…       | âŒ                   |

â­ Classes + variations Câ™¯ : type anonyme, *Value tuple* et `record`

ğŸ‘‰ En Câ™¯, pas de type somme sauf enum, trÃ¨s limitÃ© / type union ğŸ“

---

# AbrÃ©viation de type

**Alias** d'un autre type : `type [name] = [existingType]`

DiffÃ©rents usages :

```fs
// Documenter le code voire Ã©viter rÃ©pÃ©titions
type ComplexNumber = float * float
type Addition<'num> = 'num -> 'num -> 'num  // ğŸ‘ˆ Marche aussi avec les gÃ©nÃ©riques

// DÃ©coupler (en partie) usages / implÃ©mentation pour faciliter son changement
type ProductCode = string
type CustomerId = int
```

âš ï¸ EffacÃ©e Ã  la compilation â†’ ~~*type safety*~~
â†’ Compilateur autorise de passer `int` Ã  la place de `CustomerId` !

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_produit.png)

# 2.

## Les Tuples

---

# Tuples : points clÃ©s

- Types Ã  valeurs littÃ©rales
- Types "anonymes" mais on peut leur dÃ©finir des alias
- Types produit par excellence
  - Signe `*` dans signature `A * B`
  - **Produit cartÃ©sien** des ensembles de valeurs de A et de B
- Nombre d'Ã©lÃ©ments:
  - ğŸ‘Œ 2 ou 3 (`A * B * C`)
  - âš ï¸ \> 3 : possible mais prÃ©fÃ©rer *Record*
- Ordre des Ã©lÃ©ments est important
  - Si `A` â‰  `B`, alors `A * B` â‰  `B * A`

---

# Tuples : construction

- Syntaxe des littÃ©raux : `a,b` ou `a, b` ou `(a, b)`
  - Virgule `,` caractÃ©ristique des tuples
  - Espaces optionnels
  - ParenthÃ¨ses `()` peuvent Ãªtre nÃ©cessaires
- âš ï¸ PiÃ¨ge : sÃ©parateur diffÃ©rent entre un littÃ©ral et sa signature
  - `,` pour littÃ©ral
  - `*` pour signature
  - Ex : `true, 1.2` â†’ `bool * float`

---

# Tuples : dÃ©construction

- MÃªme syntaxe que construction
  - mais Â« de l'autre cÃ´tÃ© du `=` Â»
- Tous les Ã©lÃ©ments doivent apparaÃ®tre dans la dÃ©construction
  - Utiliser la discard `_` pour ignorer l'un des Ã©lÃ©ments

```fs
let point = 1.0,2.5
let x, y = point

let x, y = 1, 2, 3 // ğŸ’¥ Erreur FS0001: IncompatibilitÃ© de type...
                   // ... Les tuples ont des longueurs diffÃ©rentes de 2 et 3

let result = System.Int32.TryParse("123") // (bool * int)
let _, value = result // Ignore le "bool"
```

---

# Tuples en pratique

Utiliser un tuple pour une structure de donnÃ©es :

- Petite : 2 Ã  3 Ã©lÃ©ments
- LÃ©gÃ¨re : pas besoin de nom pour les Ã©lÃ©ments
- Locale : Ã©change local de donnÃ©es qui n'intÃ©resse pas toute la *codebase*
  - Renvoyer plusieurs valeurs - cf. `Int32.TryParse`

Tuple immuable : les modifications se font en crÃ©ant un nouveau tuple

```fs
let addOneToTuple (x,y,z) = (x+1,y+1,z+1)
```

---

# Tuples en pratique (2)

**Ã‰galitÃ© structurelle**, mais uniquement entre 2 tuples de mÃªme signature !

```fs
(1,2) = (1,2)       // true
(1,2) = (0,0)       // false
(1,2) = (1,2,3)     // ğŸ’¥ Erreur FS0001: IncompatibilitÃ© de type...
                    // ... Les tuples ont des longueurs diffÃ©rentes de 2 et 3
(1,2) = (1,(2,3))   // ğŸ’¥ Erreur FS0001: Cette expression Ã©tait censÃ©e avoir le type `int`
                    // ... mais elle a ici le type `'a * 'b`
```

**Imbrication** de tuples grÃ¢ce aux `()`

```fs
let doublet = (true,1), (false,"a")     // (bool * int) * (bool * string) â†’ pair de pairs
let quadruplet = true, 1, false, "a"    // bool * int * bool * string     â†’ quadruplet
doublet = quadruplet                    // ğŸ’¥ Erreur FS0001: IncompatibilitÃ© de type...
```

---

# Tuples : pattern matching

Patterns reconnus avec les tuples :

```fs
let print move =
    match move with
    | 0, 0 -> "No move"                     // Constante 0
    | 0, y -> $"Vertical {y}"               // Variable y (!= 0)
    | x, 0 -> $"Horizontal {x}"
    | x, y when x = y -> $"Diagonal {x}"    // Condition x et y Ã©gaux
  // `x, x` n'est pas un pattern reconnu â—
    | x, y -> $"Other ({x}, {y})"
```

â˜ **Notes :**

- Les patterns sont Ã  ordonner du \+ spÃ©cifique au \+ gÃ©nÃ©rique
- Le dernier pattern `(x, y)` correspond au pattern par dÃ©faut (obligatoire)

---

# Paires

- Tuples Ã  2 Ã©lÃ©ments
- Tellement courant que 2 helpers leur sont associÃ©s :
  - `fst` comme *first* pour extraire le 1Â° Ã©lÃ©ment de la paire
  - `snd` comme *second* pour extraire le 2Â° Ã©lÃ©ment de la paire
  - âš ï¸ Ne marche que pour les paires

```fs
let pair = ('a', "b")
fst pair  // 'a' (char)
snd pair  // "b" (string)
```

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# Pair Quiz ğŸ•¹ï¸

#### **1.** Comment implÃ©menter soi-mÃªme `fst` et `snd` ?

```fs
let fst ... ?
let snd ... ?
```

#### **2.** Quelle est la signature de cette fonction ?

```fs
let toList (x, y) = [x; y]
```

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# Pair Quiz ğŸ²

#### **1.** ImplÃ©menter soi-mÃªme `fst` et `snd` ?

```fs
let inline fst (x, _) = x  // Signature : 'a * 'b -> 'a
let inline snd (_, y) = y  // Signature : 'a * 'b -> 'b
```

- DÃ©construction avec *discard*, le tout entre `()`
- Fonctions peuvent Ãªtre `inline`

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# Pair Quiz ğŸ²ğŸ²

#### **2.** Signature de `toList` ?

```fs
let inline toList (x, y) = [x; y]
```

- Renvoie une liste avec les 2 Ã©lÃ©ments de la paire
- Les Ã©lÃ©ments sont donc du mÃªme type
- Ce type est quelconque â†’ gÃ©nÃ©rique `'a`

**RÃ©ponse :** `x: 'a * y: 'a -> 'a list` 
â†’ Soit le type `'a * 'a -> 'a list`

---

# Tuple `struct`

- LittÃ©ral : `struct(1, 'b', "trois")`
- Signature : `struct (int * char * string)`
- Usage : optimiser performance

ğŸ”— [https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/conventions
#performance](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/conventions#performance)

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_note.png)

# 3.

## LesÂ Â  *Records*

---

# Record : points clÃ©s

Type produit, alternative au Tuple quand type imprÃ©cis sous forme de tuple

- Exemple : `float * float`
  - Point dans un plan ?
  - CoordonnÃ©es gÃ©ographiques ?
  - Parties rÃ©elle et imaginaire d'un nombre complexe ?
- *Record* permet de lever le doute en nommant le type et ses Ã©lÃ©ments

```fs
type Point = { X: float; Y: float }
type Coordinate = { Latitude: float; Longitude: float }
type ComplexNumber = { Real: float; Imaginary: float }
```

---

# Record : dÃ©claration

- Membres nommÃ©s en PascalCase, pas en ~~camelCase~~
- Membres sÃ©parÃ©s `;` ou retours Ã  la ligne
- Saut de ligne aprÃ¨s `{` qu'en cas de membre additionnels

```fs
type PostalAddress =        â”†     type PostalAddress =
    { Address: string       â”†         {
      City: string          â”†             Address: string
      Zip: string }         â”†             City: string
                            â”†             Zip: string
                            â”†         }
                            â”†         member x.ZipAndCity = $"{x.Zip} {x.City}"
```

ğŸ”— https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting
Â Â Â Â  - [#use-pascalcase-for-type-declarations-members-and-labels](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting#use-pascalcase-for-type-declarations-members-and-labels)
Â Â Â Â  - [#formatting-record-declarations](https://docs.microsoft.com/en-us/dotnet/fsharp/style-guide/formatting#formatting-record-declarations)

---

<!-- _footer: '' -->

# Record : instanciation

- MÃªme syntaxe qu'un objet anonyme Câ™¯ sans `new`
  - Mais le record doit avoir Ã©tÃ© dÃ©clarÃ© au-dessus !
- Membres peuvent Ãªtre renseignÃ©s dans n'importe quel ordre
  - Mais doivent tous Ãªtre renseignÃ©s â†’ pas de membres optionnels !

```fs
type Point = { X: float; Y: float }
let point1 = { X = 1.0; Y = 2.0 }
let point2 = { Y = 2.0; X = 1.0 }   // ğŸ‘ˆ Possible mais confusant ici
let pointKo = { Y = 2.0 }           // ğŸ’¥ Error FS0764
//            ~~~~~~~~~~~ Aucune assignation spÃ©cifiÃ©e pour le champ 'X' de type 'Point'
```

âš ï¸ **PiÃ¨ge :** Syntaxe similaire mais pas identique Ã  celle de la dÃ©claration
Â Â Â Â Â â†’ `:` pour dÃ©finir le type du membre
Â Â Â Â Â â†’ `=` pour dÃ©finir la valeur du membre

---

# Record : instanciation (2)

- Les instances "longues" devraient Ãªtre Ã©crites sur plusieurs lignes
  - On peut aligner les `=` pour aider la lecture
- Les `{}` peuvent apparaÃ®tre sur leur propre ligne
  - \+ facile Ã  rÃ©-indenter et Ã  rÃ©-ordonner (`Lackeys` avant `Boss`)

```fs
let rainbow =
    { Boss = "Jeffrey"
      Lackeys = ["Zippy"; "George"; "Bungle"] }

let rainbow =
    {
        Boss    = "Jeffrey"
        Lackeys = ["Zippy"; "George"; "Bungle"]
    }
```

---

# Record : dÃ©construction

MÃªme syntaxe pour dÃ©construire un *Record* que pour l'instancier

ğŸ’¡ On peut ignorer certains champs
â†’ Rend explicite les champs utilisÃ©s ğŸ‘

```fs
let { X = x1 } = point1
let { X = x2; Y = y2 } = point1

// On peut aussi accÃ©der aux champs via le point '.'
let x3 = point1.X
let y3 = point1.Y
```

---

# Record : dÃ©construction (2)

âš ï¸ On ne peut pas dÃ©construire les membres additionnels *(propriÃ©tÃ©s)* !

```fs
type PostalAddress =
    {
        Address: string
        City: string
        Zip: string
    }
    member x.CityLine = $"{x.Zip} {x.City}"

let address = { Address = ""; City = "Paris"; Zip = "75001" }

let { CityLine = cityLine } = address   // ğŸ’¥ Error FS0039
//    ~~~~~~~~ L'Ã©tiquette d'enregistrement 'CityLine' n'est pas dÃ©finie
let cityLine = address.CityLine         // ğŸ‘Œ OK
```

---

# Record : infÃ©rence

- L'infÃ©rence de type ne marche pas quand on *"dot"* une `string`
- ... mais elle marche avec un *Record* ?!

```fs
type PostalAddress =
    { Address: string
      City   : string
      Zip    : string }

let department address =
    address.Zip.Substring(0, 2) |> int
    //     ^^^^ ğŸ’¡ Permet d'infÃ©rer que address est de type `PostalAddress`

let departmentKo zip =
    zip.Substring(0, 2) |> int
//  ~~~~~~~~~~~~~ Error FS0072
```

---

# Record : pattern matching

Fonction `inhabitantOf` donnant le nom des habitants Ã  une adresse :

```fs
type Address = { Street: string; City: string; Zip: string }

let department { Zip = zip } = zip.Substring(0, 2) |> int

let inIleDeFrance departmentNumber =
    [ 75; 77; 78 ] @ [ 91..95 ] |> List.contains departmentNumber

let inhabitantOf address =
    match address with
    | { Street = "PÃ´le"; City = "Nord" } -> "PÃ¨re NoÃ«l"
    | { City = "Paris" } -> "Parisien"
    | _ when department address = 78 -> "Yvelinois"
    | _ when department address |> inIleDeFrance -> "Francilien"
    | _ -> "FranÃ§ais"   // Le discard '_' sert de pattern par dÃ©faut (obligatoire)
```

---

<!-- _footer: '' -->

# Record : conflit de noms

En Fâ™¯, typage est nominal, pas structurel comme en TypeScript
â†’ Les mÃªmes Ã©tiquettes `First` et `Last` ci-dessous donnent 2 Records â‰ 
â†’ Mieux vaut Ã©crire des types distincts ou les sÃ©parer dans â‰  modules

```fs
type Person1 = { First: string; Last: string }
type Person2 = { First: string; Last: string }
let alice = { First = "Alice"; Last = "Jones"}  // val alice: Person2...
// (car Person2 est le type le + proche qui correspond aux Ã©tiquettes First et Last)

// âš ï¸ DÃ©construction
let { First = firstName } = alice   // Warning FS0667
//  ~~~~~~~~~~~~~~~~~~~~~  Les Ã©tiquettes et le type attendu du champ de ce Record
//                         ne dÃ©terminent pas de maniÃ¨re unique un type Record correspondant

let { Person2.Last = lastName } = alice     // ğŸ‘Œ OK avec type en prÃ©fixe
let { Person1.Last = lastName } = alice     // ğŸ’¥ Error FS0001
//                                ~~~~~ Type 'Person1' attendu, 'Person2' reÃ§u
```

---

# Record : modification

Record immuable mais facile de crÃ©er nouvelle instance ou copie modifiÃ©e
â†’ Expression de _**copy and update**_ d'un *Record*
â†’ Syntaxe spÃ©ciale pour ne modifier que certains champs
â†’ Multi-lignes si expression longue

```fs
let address2 = { address with Street = "Rue Vivienne" }

let { City = city; Zip = zip } = address
let address2' = { Street = "Rue Vivienne"; City = city; Zip = zip }
// address2 = address2'

let address3 =                  â”†      let address3' =
    { address with              â”†          { Street = address.Street
        City = "Lyon"           â”†            City   = "Lyon"
        Zip  = "69001" }        â”†            Zip    = "69001" }
// address3 = address3'
```

---

# Record *copy-update* : Câ™¯ / Fâ™¯ / JS

```cs
// Record Câ™¯ 9.0
address with { Street = "Rue Vivienne" }
```

```fs
// Fâ™¯ copy and update
{ address with Street = "Rue Vivienne" }
```

```js
// Object destructuring with spread operator
{ ...address, street: "Rue Vivienne" }
```

---

# Record *copy-update* : limites ğŸ›‘

LisibilitÃ© rÃ©duite quand plusieurs niveaux imbriquÃ©s

```fs
type Street = { Num: string; Label: string }
type Address = { Street: Street }
type Person = { Address: Address }

let person = { Address = { Street = { Num = "15"; Label = "rue Neuf" } } }

let person' =
    { person with
        Address =
          { person.Address with
              Street =
                { person.Address.Street with
                    Num = person.Address.Street.Num + " bis" } } }
```

---

# Record `struct`

Attribut `[<Struct>]` permet de passer d'un type rÃ©fÃ©rence Ã  un type valeur :

```fs
[<Struct>]
type Point = { X: float; Y: float; Z: float }
```

âš–ï¸ **Pros/Cons d'une `struct` :**

- âœ… Performant car ne nÃ©cessite pas de *garbage collection*
- âš ï¸ PassÃ©e par valeur â†’ pression sur la mÃ©moire

ğŸ‘‰ AdaptÃ© Ã  un type "petit" en mÃ©moire (~2-3 champs)

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_baseDonnees.png)

# 4.

## LesÂ Â  Unions

---

# Unions : points clÃ©s

- Terme exacte : Â« Union discriminÃ©e Â», *Discriminated Union (DU)*
- Types Somme : reprÃ©sente un **OU**, un **choix** entre plusieurs *Cases*
  - MÃªme principe que pour une `enum` mais gÃ©nÃ©ralisÃ©
- Chaque *case* doit avoir un *Tag* *(a.k.a Label)* -- en PascalCase â—
  - C'est le **discriminant** de l'union pour identifier le *case*
- Chaque *case* **peut** contenir des donnÃ©es
  - Si Tuple, ses Ã©lÃ©ments peuvent Ãªtre nommÃ©s -- en camelCase

```fs
type Billet =
    | Adulte                 // aucune donnÃ©e -> â‰ƒ singleton stateless
    | Senior of int          // contient un 'int' (mais on ne sait pas ce que c'est)
    | Enfant of age: int     // contient un 'int' de nom 'age'
    | Famille of Billet list // contient une liste de billet
                             // type rÃ©cursif -- pas besoin de 'rec'
```

---

# Unions : dÃ©claration

Sur plusieurs lignes : 1 ligne / *case*
â†’ â˜ Ligne indentÃ©e et commenÃ§ant par `|`

Sur une seule ligne -- si dÃ©claration reste **courte** â—
â†’ ğŸ’¡ Pas besoin du 1er `|`

```fs
open System

type IntOrBool =
    | Int32 of Int32                        // ğŸ’¡ Tag de mÃªme nom que ses donnÃ©es
    | Boolean of Boolean

type OrderId = OrderId of int               // ğŸ‘ˆ Single-case union
                                            // ğŸ’¡ Tag de mÃªme nom que l'union parent
type Found<'T> = Found of 'T | NotFound     // ğŸ’¡ Type gÃ©nÃ©rique
```

---

# Unions : instanciation

*Tag* â‰ƒ **constructeur**
â†’ Fonction appelÃ©e avec les Ã©ventuelles donnÃ©es du *case*

```fs
type Shape =
    | Circle of radius: int
    | Rectangle of width: int * height: int

let circle = Circle 12          // Type: 'Shape', Valeur: 'Circle 12'
let rect = Rectangle (4, 3)     // Type: 'Shape', Valeur: 'Rectangle (4, 3)'

let circles = [1..4] |> List.map Circle     // ğŸ‘ˆ Tag employÃ© comme fonction
```

---

# Unions : conflit de noms

Quand 2 unions ont des tags de mÃªme nom
â†’ Qualifier le tag avec le nom de l'union

```fs
type Shape =
    | Circle of radius: int
    | Rectangle of width: int * height: int

type Draw = Line | Circle       // 'Circle' sera en conflit avec le tag de 'Shape'

let draw = Circle              // Type='Draw' (type le + proche) -- âš ï¸ Ã  Ã©viter car ambigu

// Tags qualifiÃ©s par leur type union
let shape = Shape.Circle 12
let draw' = Draw.Circle
```

---

# Unions : accÃ¨s aux donnÃ©es

Uniquement via *pattern matching*
Matching d'un type Union est **exhaustif**

```fs
type Shape =
    | Circle of radius: float
    | Rectangle of width: float * height: float

let area shape =
    match shape with
    | Circle r -> Math.PI * r * r   // ğŸ’¡ MÃªme syntaxe que instanciation
    | Rectangle (w, h) -> w * h

let isFlat = function 
    | Circle 0.                     // ğŸ’¡ Constant pattern
    | Rectangle (0., _)
    | Rectangle (_, 0.) -> true     // ğŸ’¡ OR pattern
    | Circle _
    | Rectangle _ -> false
```

---

# Unions : *single-case*

Union avec un seul cas encapsulant un type (gÃ©nÃ©ralement primitif)

```fs
type CustomerId = CustomerId of int
type OrderId = OrderId of int

let fetchOrder (OrderId orderId) =    // ğŸ’¡ DÃ©construction directe sans 'match'
    ...
```

Assure *type safety* contrairement au simple type alias
â†’ Impossible de passer un `CustomerId` Ã  une fonction attendant un `OrderId` ğŸ‘

Permet d'Ã©viter *Primitive Obsession* Ã  coÃ»t minime

---

# Unions : style "enum"

Tous les *cases* sont vides = dÃ©pourvus de donnÃ©es
â†’ â‰  `enum` .NET ğŸ“

L'instanciation et le pattern matching se font juste avec le *tag*
â†’ Le *tag* n'est plus une ~~fonction~~ mais une valeur *([singleton](https://fsharpforfunandprofit.com/posts/fsharp-decompiled/#enum-style-unions))*

```fs
type Answer = Yes | No | Maybe
let answer = Yes

let print answer =
    match answer with
    | Yes   -> printfn "Oui"
    | No    -> printfn "Non"
    | Maybe -> printfn "Peut-Ãªtre"
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_innovation.png)

# 5.

## LesÂ Â Â  Enums

Vraies `enum` .NET

---

# Enum : dÃ©claration

- Ensemble de constantes de type entier (`byte`, `int`...)
- Syntaxe â‰  union

```fs
type Color  = Red | Green | Blue        // Union
type ColorN = Red=1 | Green=2 | Blue=3  // Enum

type AnswerChar = Yes='Y' | No='N'  // ğŸ’¡ enum basÃ©e sur 'char'
type AnswerChar = Yes="Y" | No="N"  // ğŸ’¥ Error FS0951
//   ~~~~~~~~~~ LittÃ©raux Ã©numÃ©rÃ©s doivent Ãªtre de type 'int'...

type File = a='a' | b='b' | c='c'  // ğŸ’¡ Membres d'enum peuvent Ãªtre en camelCase
```

---

# Enum : usage

âš ï¸ Contrairement aux unions, l'emploi d'un littÃ©ral d'enum est forcÃ©ment qualifiÃ©

```fs
let answerKo = Yes            // ğŸ’¥ Error FS0039
//             ~~~ La valeur ou le constructeur 'Yes' n'est pas dÃ©fini.
let answer = AnswerChar.Yes   // ğŸ‘Œ OK
```

Cast via helpers `int` et `enum` (mais pas `char`) :

```fs
let redValue = int ColorN.Red         // enum -> int
let redAgain = enum<ColorN> redValue  // int -> enum via type gÃ©nÃ©rique
let red: ColorN = enum redValue       // int -> enum via annotation

// âš ï¸ Ne marche pas avec char enum
let ko = char AnswerChar.No   // ğŸ’¥ Error FS0001
let no: AnswerChar = enum 'N' // ğŸ’¥ Error FS0001
```

---

# Enum : matching

âš ï¸ Contrairement aux unions, le *pattern matching* n'est pas exhaustif

```fs
type ColorN = Red=1 | Green=2 | Blue=3  // Enum

let toHex color =
    match color with
    | ColorN.Red   -> "FF0000"
    | ColorN.Green -> "00FF00"
    | ColorN.Blue  -> "0000FF"
    // âš ï¸ Warning FS0104: Les enums peuvent accepter des valeurs en dehors des cas connus.
    // Par exemple, la valeur 'enum<ColorN> (0)' peut indiquer un cas non traitÃ©.

    // ğŸ’¡ Pour enlever le warning, il faut ajouter un pattern gÃ©nÃ©rique
    | _ -> invalidArg (nameof color) $"Color {color} not supported"
```

---

# Enum : flags

MÃªme principe qu'en Câ™¯ :

```fs
open System

[<FlagsAttribute>]
type PermissionFlags =
    | Read    = 1
    | Write   = 2
    | Execute = 4

let permission = PermissionFlags.Read ||| PermissionFlags.Write

let canRead = permission.HasFlag PermissionFlags.Read
```

ğŸ’¡ Noter l'opÃ©rateur `|||` : OU binaire *(`|` en Câ™¯)*

---

# Enum *vs* Union

| Type  | DonnÃ©es     | Qualification        | ExhaustivitÃ© |
|-------|-------------|----------------------|--------------|
| Enum  | EntiÃ¨res    | Obligatoire          | âŒ Non        |
| Union | Quelconques | Qu'en cas de conflit | âœ… Oui        |

â˜ **Recommandation :**

- PrÃ©fÃ©rer une Union dans la majoritÃ© des cas
- Choisir une Enum pour :
  - Interop .NET
  - Besoin de lier donnÃ©es entiÃ¨res

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_rechercheProfil.png)

# 6.

## Record anonyme

---

# Record anonyme

- Depuis Fâ™¯ 4.6 *(mars 2019)*
- Syntaxe : idem *Record* avec accolades "larges" `{| fields |}`
  - `{| Age: int |}` â†’ signature
  - `{| Age = 15 |}` â†’ instance
- Typage *inline* : pas besoin de prÃ©-dÃ©finir un `type` nommÃ©
  - Alternative aux *Tuples*
- AutorisÃ© en entrÃ©e/sortie de fonction
  - â‰  Type anonyme Câ™¯

---

# Record anonyme : bÃ©nÃ©fices âœ…

â€¢ RÃ©duire *boilerplate*
â€¢ AmÃ©liorer interop avec systÃ¨mes externes (JavaScript, SQL...)

Exemples *(dÃ©taillÃ©s ensuite)* :

- Projection LINQ
- Personnalisation d'un record existant
- SÃ©rialisation JSON
- Signature *inline*
- Alias par module

---

## âœ… Projection LINQ

ğŸ’¡ SÃ©lectionner un sous-ensemble de propriÃ©tÃ©s

```fs
let names =
    query {
        for p in persons do
        select {| Name = p.FirstName |}
    }
```

En Câ™¯, on utiliserait un type anonyme :

```cs
var names =
    from p in persons
    select new { Name = p.FirstName };
```

*ğŸ”— https://queil.net/2019/10/fsharp-vs-csharp-anonymous-records/*

---

## âœ… Personnalisation d'un record existant

ğŸ’¡ Un record anonyme peut Ãªtre instanciÃ© Ã  partir d'une instance de record

```fs
type Person = { Age: int; Name: string }
let william = { Age = 12; Name = "William" }

// Ajout d'un champ (Gender)
let william' = {| william with Gender = "Male" |}
            // {| Age = 12; Name = "William"; Gender = "Male" |}

// Modification de champs (Name, Age: int => float)
let jack = {| william' with Name = "Jack"; Age = 16.5 |}
        // {| Age = 16.5; Name = "Jack"; Gender = "Male" |}
```

---

## âœ… SÃ©rialisation JSON

ğŸ˜• Unions sÃ©rialisÃ©es dans un format pas pratique

```fs
#r "nuget: Newtonsoft.Json"
let serialize obj = Newtonsoft.Json.JsonConvert.SerializeObject obj

type CustomerId = CustomerId of int
type Customer = { Id: CustomerId; Age: int; Name: string; Title: string option }

serialize { Id = CustomerId 1; Age = 23; Name = "Abc"; Title = Some "Mr" }
```

```json
{
  "Id": { "Case": "CustomerId", "Fields": [ 1 ] }, // ğŸ‘€
  "Age": 23,
  "Name": "Abc",
  "Title": { "Case": "Some", "Fields": [ "Mr" ] }  // ğŸ‘€
}
```

---

## âœ… SÃ©rialisation JSON (2)

ğŸ’¡ DÃ©finir un record anonyme pour sÃ©rialiser un *customer*

```fs
let serialisable customer =
    let (CustomerId customerId) = customer.Id
    {| customer with
         Id = customerId
         Title = customer.Title |> Option.toObj |}

serialize (serialisable { Id = CustomerId 1; Age = 23; Name = "Abc"; Title = Some "Mr" })
```

```json
{
  "Id": 1, // âœ…
  "Age": 23,
  "Name": "Abc",
  "Title": "Mr"  // âœ…
}
```

---

## âœ… Signature *inline*

ğŸ’¡ Utiliser un record anonyme *inline* pour rÃ©duire charge cognitive

```fs
type Title = Mr | Mrs
type Customer =
    { Age  : int
      Name : {| First: string; Middle: string option; Last: string |} // ğŸ‘ˆ
      Title: Title option }
```

---

## âœ… Alias par module

```fs
module Api =
    type Customer = // â˜ Customer est un alias
        {| Id   : System.Guid
           Name : string
           Age  : int |}

module Dto =
    type Customer =
        {| Id   : System.Guid
           Name : string
           Age  : int |}

let (customerApi: Api.Customer) = {| Id = Guid.Empty; Name = "Name"; Age = 34 |}
let (customerDto: Dto.Customer) = customerApi // ğŸ‰ Pas besoin de convertir
```

ğŸ’¡ Instant t : mÃªme type dans 2 modules
ğŸ’¡ Plus tard : facilite personnalisation des types par module

---

# Record anonyme : limites ğŸ›‘

```fs
// InfÃ©rence limitÃ©e
let nameKo x = x.Name  // ğŸ’¥ Error FS0072: Lookup on object of indeterminate type...
let nameOk (x: {| Name:string |}) = x.Name

// Pas de dÃ©construction
let x = {| Age = 42 |}
let {  Age = age  } = x  // ğŸ’¥ Error FS0039: The record label 'Age' is not defined
let {| Age = age |} = x  // ğŸ’¥ Error FS0010: Unexpected symbol '{|' in let binding

// Pas de fusion
let banana = {| Fruit = "Banana" |}
let yellow = {| Color = "Yellow" |}
let banYelKo = {| banana with yellow |} // ğŸ’¥ Error FS0609...
let banYelOk = {| banana with Color = "Yellow" |}

// Pas d'omission
let ko = {| banYelOk without Color |}  // ğŸ’¥ Mot clÃ© 'without' n'existe pas
```

---

# Record anonyme : limites ğŸ›‘

```fs
// Pas du typage structurel => tous les champs sont requis
let capitaliseFruit (x: {| Fruit: string |}) = x.Fruit.ToUpper()
capitaliseFruit {| Fruit = "Banana" |}                      // ğŸ‘Œ "BANANA"
capitaliseFruit {| Fruit = "Banana"; Origin = "RÃ©union" |}  // ğŸ’¥ Too much fields... [Origin]
```

---

<!-- _class: purple chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_entretien.png)

# 7.

## Types valeur

---

# Type composite *struct*

Type composite : peut Ãªtre dÃ©clarÃ© en tant que type valeur
â†’ Instances stockÃ©es dans la **pile** *(stack)* plutÃ´t que dans le tas *(heap)*
â†’ Permet parfois de gagner en performance
â†’ PlutÃ´t adaptÃ© aux types compacts : peu de champs, peu de comportements

- Attribut `[<Struct>]`
- Mot clÃ© `struct`
- Structure

---

# Attribut `[<Struct>]`

Pour *Record* et *Union*

Ã€ placer avant ou aprÃ¨s le mot cle `type`

```fs
type [<Struct>] Point = { X: float; Y: float }

[<Struct>]
type SingleCase = Case of string
```

---

# Mot clÃ© `struct`

Pour littÃ©ral de Tuple et *Record* anonyme

```fs
let t = struct (1, "a")
// struct (int * string)

let a = struct {| Id = 1; Value = "a" |}
// struct {| Id: int; Value: string |}
```

---

# Structures

Alternatives aux classes ğŸ“ mais \+ limitÃ©s / hÃ©ritage et rÃ©cursivitÃ©

ğŸ‘‰ Cf. session sur l'orientÃ©-objet et les classes...

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 8.

## LeÂ Â Â  RÃ©capâ€™

---

<!-- _footer: '' -->
![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# ğŸ•¹ï¸ RÃ©cap' Quiz

```fs
// Relier types et concepts
type Color1 = int * int * int
type Color2 = Red | Green | Blue
type Color3 = Red=1 | Green=2 | Blue=3
type Color4 = { Red: int; Green: int; Blue: int }
type Color5 = {| Red: int; Green: int; Blue: int |}
type Color6 = Color of Red: int * Green: int * Blue: int
type Color7 =
    | RGB of { Red: int; Green: int; Blue: int }
    | HSL of { Hue: int; Saturation: int; Lightness: int }

// A. Alias
// B. Enum
// C. Record
// D. Record anonyme
// E. Single-case union
// F. Union
// G. Union enum-like
// H. Tuple
```

---

# ğŸ² RÃ©cap' Quiz

| Types                                       | Concepts                                |
|---------------------------------------------|-----------------------------------------|
| `type Color1 = int * int * int`             | **H.** Tuple + **A.** Alias             |
| `type Color2 = Red âˆ£ Green âˆ£ Blue`           | **G.** Union enum-like                  |
| `type Color3 = Red=1 âˆ£ Green=2 âˆ£ Blue=3`     | **B.** Enum                             |
| `type Color4 = { Red: int; Green: intâ€¦ }`   | **C.** Record                           |
| `type Color5 = {âˆ£ Red: int; Green: intâ€¦ âˆ£}`  | **D.** Record anonyme + **A.** Alias    |
| `type Color6 = Color of Red: int * â€¦`       | **E.** Single-case union + **H.** Tuple |
| `type Color7 = RGB of { Red: intâ€¦ } âˆ£ HSLâ€¦`  | **F.** Union + **C.** Record            |

---

# Composition de types

CrÃ©ation de nouveaux types ?
â†’ âŒ Les types algÃ©briques ne supportent pas l'hÃ©ritage.
â†’ âœ… Par composition, dans *sum/product type*
â†’ ğŸ’¡ Extension d'un *Record* en un *Record* anonyme avec champs en \+

Combiner 2 unions ?
â†’ âŒ Pas "aplatissable" comme en TypeScript â‘ 
â†’ âœ… Nouveau type union â‘¡

```fs
type Noir = Pique | Trefle
type Rouge = Coeur | Carreau
type CouleurKo = Noir | Rouge  // (1) âŒ â‰  Pique | Trefle | Coeur | Carreau 
type Couleur = Noir of Noir | Rouge of Rouge // (2) âœ…
let c1 = Noir Pique
```

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_cocktail.png)

# Conclusion

Beaucoup de faÃ§ons de modÃ©liser !

De quoi s'adapter :

- Ã€ tous les goÃ»ts ?
- En fait surtout au domaine mÃ©tier !

---

<!-- _class: end invert lead-->

# Thanks ğŸ™
