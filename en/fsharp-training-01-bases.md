---
marp: true
html: true
theme: 'd-edge'
title: 'F‚ôØ Training ‚Ä¢ Bases'
footer: 'F‚ôØ Training ‚Ä¢ Bases'
paginate: true
---

<!-- _class: title invert -->

# F‚ôØ Training

## *Bases*

### 2025 April

---

<!-- _class: agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- What is F‚ôØ?
- Syntax: fundamentals, indentation
- Language design traits
  ¬†¬†¬†¬†¬†¬†¬†- Everything is an expression!
  ¬†¬†¬†¬†¬†¬†¬†- Type inference

---

# ‚òù Preliminary notes

1. Symbol üìç : indicates a concept we will see later.

2. Code is displayed using the **Fira Code** font
   üîó [github.com/tonsky/FiraCode](https://github.com/tonsky/FiraCode):

```js
'->' = '-' + '>'    '<=' = '<' + '='    '==' = '=' + '='
'=>' = '=' + '>'    '>=' = '>' + '='    '!=' = '!' + '='
'<>' = '<' + '>'    '|>' = '|' + '>'    '<|' = '<' + '|'
'[|' = '[' + '|'    '|]' = '|' + ']'
```

üí° Setting in VsCode to enable ligatures: `"editor.fontLigatures": true`

üí° In Rider, same with **JetBrains Mono** font
üîó [jetbrains.com/lp/mono/](https://www.jetbrains.com/lp/mono/)

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_probleme.png)

# 1.

## What is F‚ôØ?

---

# Key points

Microsoft language family - **.NET** platform

- Designer: Don Syme @ Microsoft Research
- ‚âÉ OCaml implementation for .NET
- ‚âÉ Inspired by Haskell *(Version 1.0 in 1990)*
- dotnet new -lang F#`
- Interoperability between C‚ôØ and F‚ôØ projects/assemblies

Multi-paradigm __*Functional-first*__ and very concise language

Where C‚ôØ is *imperative/object-oriented-first* and rather verbose
*(even if it's inspired by F‚ôØ to become more succinct and functional)*

---

# History

| Date | C‚ôØ         | F‚ôØ         | .NET               | Visual Studio |
|------|------------|------------|--------------------|---------------|
| 2002 | C‚ôØ 1.0     |            | .NET Framework 1.0 | VS .NET 2002  |
| 2005 | C‚ôØ 2.0     | **F‚ôØ 1.x** | .NET Framework ?.? | VS 2005       |
| 2007 | **C‚ôØ 3.0** |            | .NET Framework 3.5 | VS 2008       |
| 2010 | C‚ôØ 4.0     | **F‚ôØ 2.0** | .NET Framework 4   | VS 2010       |
| ...  | ...        | ...        | ...                | ...           |
| 2019 | C‚ôØ 8.0     | F‚ôØ 4.7     | .NET Core 3.x      | VS 2019       |
| 2020 | C‚ôØ 9.0     | **F‚ôØ 5.0** | **.NET 5.0**       | VS 2019       |
| ...  | ...        | ...        | ...                | ...           |
| 2024 | C‚ôØ 13.0    | F‚ôØ 9.0     | .NET 9.0           | VS 2022       |

---

# Editors / IDE

VsCode + [Ionide](https://marketplace.visualstudio.com/items?itemName=Ionide.Ionide-fsharp)
‚Üí ‚òù More a boosted text editor than a full IDE
‚Üí ‚òù Permissive: does not always report all compilation errors
‚Üí üîó [Fantastic F# and Azure Developer Extensions for VSCode](https://www.compositional-it.com/news-blog/fantastic-f-and-azure-developer-extensions-for-vscode/) by Compositional IT

Visual Studio / Rider
‚Üí ‚òù Less refactoring capabilities for F‚ôØ than for C‚ôØ

Try F#: https://try.fsharp.org/
‚Üí Online [REPL](https://en.wikipedia.org/wiki/Read%E2%80%93eval%E2%80%93print_loop) with some examples

---

## F‚ôØ interactive _(FSI)_

- REPL available in VS, Rider, vscode + `dotnet fsi`
- Usage : instantly test a snippet
  - üí° In the FSI console, enter `;;` at the end of an expression to evaluate it

‚òùÔ∏è **Notes:**

- _C‚ôØ interactive_ is more recent (VS 2015 Update 1). The FSI was there from the get go.
- Alternative worth trying, also working for C#: [LINQPad](https://www.linqpad.net/)

üë®‚Äçüè´ **Demo**

---

# File types

4 file types: `.fs`, `.fsi`, `.fsx`, `.fsproj`

> ‚ö†Ô∏è Single language : for F‚ôØ only

---

## Standalone file

- Script file `.fsx`
  - Executable _(hence the final **x**)_ using the FSI
  - Independent but can reference other files, DLLs, NuGet packages

---

## Project files

- In C‚ôØ : `.sln` contains `.csproj` projects that contains `.cs` files
- In F‚ôØ : `.sln` contains `.fsproj` projects that contains `.fs(i)` code files

üí° **Easy Interop** = Combine both `.csproj` and `.fsproj` projects in the same `.sln`

`.fsi` are signature files _(**i** for interface)_

- Associated with a `.fs` file of the same name
- Optional and rather rare in codebases
- Usages
  - Reinforces encapsulation _(like `.h` in C)_
  - Separate long documentation _(xml-doc)_

---

## F‚ôØ Project

Creation in the IDE or using the CLI `dotnet`:

- `dotnet new -l` : list supported project types
- `dotnet new console --language F# -o MyFSharpApp`
  - Cr√©ation of a console project named `MyFSharpApp`
  - `--language F#` is key to specify the language, by default in C#
- `dotnet build` : to build the project
- `dotnet run` : to build the project and run the underlying executable

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_backEnd.png)

# 2.

## Syntax - Fundamentals

---

# Syntax

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_idee.png)

F# **succinct** syntax is its first key point üí™

*(But, **explicit** is more important)*

---

# Comments

```fs
(* This is block
   comment *)


// And this is line comment


/// XML doc summary


/// <summary>
/// Full XML doc
/// </summary>
```

---

# Variables / Values

- Keyword: `let` to declare/name a value
- No need for `;` at the end of the declaration
- Creates a _Binding_ that is immutable by default
  - ‚âÉ `const` in JS, `readonly` members in C‚ôØ

```fsharp
let x = 1
let y = "abc"
```

---

## Variables: Mutable

Mutable binding with `let mutable`

- ‚âÉ `let` en JS, `var` en C‚ôØ
- ‚ö†Ô∏è The assignment operator is `<-`, not `=` used for equality
- Use it sparingly, on a limited scope

```fsharp
let x = 1
x <- 2 // üí• error FS0027: This value is not mutable. Consider using the mutable keyword...

let mutable y = 1
y <- 2 // ‚úÖ OK
```

---

# Names

- Same constraints on variable naming than in C‚ôØ
- ... except the apostrophe `'`  _(tick)_
  - allowed in the middle or at the end, but not at the ~~beginning~~ !
  - at the end ‚Üí indicates a variant _(code convention)_
- Between double _backticks_
  - allow any character, in particular whitespaces, except line breaks

```fsharp
let x = 1
let x' = x + 1

// Works on keyword too! But avoid it because it's confusing!
let if' b t f = if b then t else f

let ``123 456`` = "123 456"
// üí° no need to enter the ``, just the 123 to get the auto-completion
```

---

# *Shadowing*

- Use to redefine a value with a name already used above
- The previous value is no longer accessible in the current scope
- Not allowed at `module` level but allowed in a sub-scope
- Convenient but can be misleading

```fsharp
let a = 2

let a = "ko"  // üí• Error FS0037: Duplicate definition of value 'a'

let b =
    let a = "ok" // üëå No compilation error
    // `a` is bound to the "ok" string (not the previous value: 2)
    // in all the rest of the b expression
    let a = "ko" // üëå Consecutive shadowings are possible!
    ...
```

---

# Type Annotation

- Optional thanks to inference
- The type is declared after the name `name: type` _(like in TypeScript)_
- The value is mandatory, even with `mutable`
  - good constraint for the code quality üëç

```fsharp
let x = 1       // Type inferred (int)
let y: int = 2  // Type explicit

let z1: int
// üí• Error FS0010: Incomplete structured construct at or before this point
// in binding. Expected '=' or other token. 

let mutable z2: int
// üí• Same error
```

---

# Constant

_What:_ Variable erased during compilation, every usage is replaced by the value
‚âÉ `const` C‚ôØ - same idea than `const enum` in TypeScript

_How:_ Value decorated with the `Literal` attribute

Recommended naming convention : **PascalCase**

```fsharp
[<Literal>] // Line break required before the `let`
let AgeOfMajority = 18

let [<Literal>] Pi = 3.14 // Also possible but not recommended by MS/Fantomas formatter
```

‚ö†Ô∏è **Attributes** are between `[< >]`
‚Üí Frequent beginner error to use `[ ]` _(like in C‚ôØ)_

---

# Number

```fsharp
let pi = 3.14             // val pi    : float   = 3.14  ‚Ä¢ System.Double
let age = 18              // val age   : int     = 18    ‚Ä¢ System.Int32
let price = 5.95m         // val price : decimal = 5.95M ‚Ä¢ System.Decimal
```

‚ö†Ô∏è No implicit conversion between number types
‚Üí üí° use `int`, `float`, `decimal` helper functions to do this conversion
‚Üí ‚òùÔ∏è rule relaxed in some cases in [F# 6](https://learn.microsoft.com/en-us/dotnet/fsharp/whats-new/fsharp-6#additional-implicit-conversions)

```fsharp
let i = 1
i * 1.2;;    // üí£ Error FS0001: The type 'float' does not match the type 'int'
‚Äã
float 3;;    // val it : float   = 3.0
decimal 3;;  // val it : decimal = 3M
int 3.6;;    // val it : int     = 3
int "2";;    // val it : int     = 2
```

---

# String

```fsharp
let name = "Bob"                          // val name : string = "Bob"

// String formatting (available from the get go)
let name2 = sprintf "%s Marley" name      // val name2 : string = "Bob Marley"

// String interpolation (F‚ôØ 5)
let name3 = $"{name} Marley"              // val name3 : string = "Bob Marley"

// Type safe string interpolation
let rank = 1
let name4 = $"%s{name} Marley, #%i{rank}" // val name4: string = "Bob Marley, #1"

// Access to a character by its index (>= 0) (F‚ôØ 6)
let initial = name2[0]                    // val initial :  char = 'B'
let initial = name2.[0]                   // Dot syntax, still supported

// String slicing (F‚ôØ 6) (alternative to x.Substring(index [, length]) method)
let firstName = name2[0..2]               // val firstName : string = "Bob"
let lastName  = name2[4..]                // val lastName: string = "Marley"
```

---

# String (2)

```fsharp
// Verbatim string: idem C‚ôØ
let verbatimXml = @"<book title=""Paradise Lost"">"

// Triple-quoted string : no need to esapce the double-quotes `"`
let tripleXml = """<book title="Paradise Lost">"""

// Regular strings accept line breaks but do not trim whitespaces
let poemIndented = "
    The lesser world was daubed
    By a colorist of modest skill
    A master limned you in the finest inks
    And with a fresh-cut quill."
```

---

# String (3)

```fsharp
// Solution: backslash strings
// - Whitespaces (space and line break) are ignored between
//   the \ terminating a line and the following non-whitespace character
// - hence the \n to add line breaks
let poem = "\
    The lesser world was daubed\n\
    By a colorist of modest skill\n\
    A master limned you in the finest inks\n\
    And with a fresh-cut quill."

// We can also combine line breaks and backslash strings ü§∑
let poemWithoutBackslashN = "\
    The lesser world was daubed
\
    By a colorist of modest skill
\
    A master limned you in the finest inks
\
    And with a fresh-cut quill."
```

---

# String interpolation in F‚ôØ 8

Interpolated string cannot contain braces unless doubled: `$"{{xxx}}"`

Since F‚ôØ 8, the `$` character is doubled (`$$`) or tripled (`$$`) to indicate
the number of braces for interpolation, respectively `{{ }}` and `{{{ }}}`

```fsharp
let classAttr = "bold"
let cssNew = $$""".{{classAttr}}:hover {background-color: #eee;}"""
```

---

# Character encoding

String literals are encoded in **Unicode**:

```fsharp
let unicodeString1 = "abc"  // val unicodeString1: string = "abc"
let unicodeString2 = "ab‚úÖ" // val unicodeString2: string = "ab‚úÖ"
```

We can work in **ASCII** using the `B` suffix, but in this case we get a `byte array`:

```fsharp
let asciiBytes = "abc"B
// val asciiBytes1: byte array = [|97uy; 98uy; 99uy|]

let asciiBytesKO = "abüö´"B
// üí• Error FS1140: This byte array literal contains characters
//    that do not encode as a single byte
```

üí° Works also for character: `'a'B`

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

# Collections

- Lists
- Arrays
- Sequences

---

## Lists

A list is an immutable collection of elements of the same type.
‚â† `System.Collection.Generic.List<T>` BCL type
Implemented internally as a linked list.

Creation with `[]` ‚Ä¢ Items separated by `;` or line breaks + indentation

```fs
let abc = [ 'a'; 'b'; 'c' ] // val abc : char list = ['a'; 'b'; 'c']
let a =
  [ 2
    3 ]  // val a : int list = [2; 3]
```

‚ö†Ô∏è **Trap:** using `,` to separate items = single item: a tuple üìç

‚òù **ML style type annotation:** `int list` = `List<int>`
‚Üí Idiomatic only for some `FSharp.Core` types: `array`, `list`, `option` üìç

---

## List operators

`::`  _Cons_ _(for "construction")_: add an item to the top of the list
`..` _Range_ of numbers between `min..max` *(included)* or `min..step..max`
`@`¬† _Append_ 2 lists

```fsharp
let ints = [2..5]                 // val ints : int list = [2; 3; 4; 5]
let ints' = 1 :: ints             // val ints' : int list = [1; 2; 3; 4; 5]
let floats = [ 2. .. 5. ]         // val floats: float list = [2.0; 3.0; 4.0; 5.0]

let chars = [ 'a' .. 'd' ]        // val chars : char list = ['a'; 'b'; 'c'; 'd']
let chars' = chars @ [ 'e'; 'f' ] // val chars' : char list = ['a'; 'b'; 'c'; 'd'; 'e'; 'f']
let e = chars'[4]                 // val e: char = 'e'
```

‚ö†Ô∏è **Space** required before `[]` to create a list; otherwise: access by index

---

## Arrays

Mutable fixed-size collections of elements of the same type.
`array 't` = `'t[]`: BCL type

Creation with `[||]` ‚Ä¢ Items separated by `;` or line breaks + indentation

```fsharp
let a1 = [| 'a'; 'b'; 'c' |]
// val a1: char array = [|'a'; 'b'; 'c'|]

let a2 =
    [| 2
       3 |]
// val a2: int array = [|2; 3|]
```

‚ö†Ô∏è **Trap:** `[]` used for list creation and array type!

---

## Sequences

Series of elements of the same type
`'t seq` = alias for `System.Collections.Generic.IEnumerable<'t>` BCL type

Creation with `seq { }`

```fsharp
let seq1 = seq { 'a'; 'b'; 'c' }
// val seq1: char seq

let seq2 =
    seq {
        2
        3
    }
// val seq2: int seq
```

‚ö†Ô∏è **Lazy**: possible multiple enumeration ‚Ä¢ hence not evaluated in FSI console

---

## Collections functions

Each type has its own module containing dedicated functions.

Common functions:

| F‚ôØ collections     | C‚ôØ LINQ (`IEnumerable<_>`) | JS `Array`           |
|--------------------|----------------------------|----------------------|
| `map`, `collect`   | `Select()`, `SelectMany()` | `map()`, `flatMap()` |
| `exists`, `forall` | `Any(predicate)`, `All()`  | `some()`, `every()`  |
| `filter`           | `Where()`                  | `filter()`           |
| `find`, `tryFind`  | √ó                          | `find()`             |
| `fold`, `reduce`   | `Aggregate([seed]])`       | `reduce()`           |
| `average`, `sum`   | `Average()`, `Sum()`       | √ó                    |

üîó Full documentation on fsharp.github.io: [Array](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-collections-arraymodule.html) ‚Ä¢ [List](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-collections-listmodule.html) ‚Ä¢ [Seq](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-collections-seqmodule.html)

---

# Named functions

‚Ä¢ Declared in a `let` binding _(like a variable)_
‚Ä¢ Naming convention: **camelCase**
‚Ä¢ No `return` keyword: always returns the last expression in the body
‚Ä¢ No `()` around all parameters, no `,` between parameters
‚Ä¢ `()` required around parameter with type annotation (1) or deconstruction (2)

```fsharp
let square x = x * x  // Function with 1 parameter
let res = square 2    // Returns 4

// (1) Parentheses required for annotations of type
let square' (x: int) : int = x * x

// (2) Brackets required when deconstructing an object
//     (here it's a single-case discriminated union üìç
let hotelId (HotelId value) = value
```

---

## Functions of 2 or more parameters

Separate parameters and arguments with **spaces**:

```fsharp
// Function with 2 parameters
let add x y = x + y  // val add: x: int -> y: int -> int

// Call with the 2 arguments
let res = add 1 2    // val res: int = 3
```

‚ö†Ô∏èÔ∏è `,` creates another kind of functions using tuples üìç

```fsharp
let addByPair (x, y) = x + y
// val addByPair: x: int * y: int -> int
```

---

## Functions without parameter

Use `()` _(like in C#)_

```fsharp
let printHello () = printfn "Hello"
// val printHello: unit -> unit
printHello ();;
// Hello

let notAFunction = printfn "Hello"
// Hello
// val notAFunction: unit = ()
```

‚òùÔ∏è `unit` means "nothing" üìç

---

## Multi-line function

**Indentation** required, but no need for `{}` \
Can contain sub-function

```fsharp
let evens list =
    let isEven x =  // üëà Sub-function
        x % 2 = 0   // üí° `=` equality operator - No `==` operator in F#
    List.filter isEven list
// val evens: list: int list -> int list

let res = evens [1;2;3;4;5]
// val res: int list = [2; 4]
```

---

# Anonymous function

*A.k.a. **Lambda**, arrow function*

- Syntax: `fun {parameters} -> body` *(‚â† in C‚ôØ `{parameters} ‚áí body`)*
- In general, `()` required all around, for precedence reason

```fsharp
let evens' list = List.filter (fun x -> x % 2 = 0) list
```

---

## \_.Member shorthand (F‚ôØ 8)

```fsharp
type Person = { Name: string; Age: int }

let people =
    [ { Name = "Alice"; Age = 30 }
      { Name = "Billy"; Age =  5 } ]

// Regular lambda (Shorthand not possible)
let adults = people |> List.filter (fun person -> person.Age >= 18)
// val adults: Person list = [{ Name = "Alice"; Age = 30 }]

// Member chain shorthand
let uppercaseNames = people |> List.map _.Name.ToUpperInvariant() // üëàüëà
// val uppercaseNames: string list = ["ALICE"; "BILLY"]
```

---

# Naming convention related to functions

It's usual in F‚ôØ to use short names:
‚Ä¢ `x`, `y`, `z` : parameters for values of the same type
‚Ä¢ `f`, `g`, `h` : parameters for input functions
‚Ä¢ `_` : *discard* an element not used *(like in C‚ôØ 7.0)*
‚Ä¢ `xs` : list of `x`

‚òùÔ∏è Suited for a short function body or for a generic function:

```fsharp
// Function that simply returns its input parameter, whatever its type
let id x = x

// Composition of 2 functions
let compose f g = fun x -> g (f x)
```

üîó [When x, y, and z are great variable names](https://blog.ploeh.dk/2015/08/17/when-x-y-and-z-are-great-variable-names/) *by Mark Seemann*

---

# Piping

_Pipe_ operator `|>` : same idea that in UNIX with `|` \
‚Üí `value |> function` send a value to a function \
‚Üí match left-to-right reading order: "subject verb" \
‚Üí same order than when we dot an object: `object.Method`

```fsharp
let a = 2 |> add 3  // to read "2 + 3"

// We pipe a list to the "List.filter predicate" function
let evens = [1;2;3;4;5] |> List.filter (fun x -> x % 2 = 0)
```

```csharp
// ‚âÉ C‚ôØ
var a = 2.Add(3);
var nums = new[] { 1, 2, 3, 4, 5 };
var evens = nums.Where(x => x % 2 == 0);
```

---

## Pipeline: chain of pipings

Style of coding to emphasize the data flowing from functions to functions
‚Üí without intermediary variable üëç

Similar to a built-in *fluent API*
‚Üí no need to return the object at the end of each method üëç

```fsharp
// Short syntax: in a single line fitting the screen width
let res = [1;2;3;4;5] |> List.filter (fun x -> x % 2 = 0) |> List.sum

// More readable with line breaks
let res' =
    [1; 2; 3; 4; 5]
    |> List.filter isOdd  // With `let isOdd x = x % 2 <> 0`
    |> List.map square    //      `let square x = x * x`
    |> List.map addOne    //      `let addOne x = x + 1`
```

---

# If/then/else expression

In F‚ôØ, `if/then(/else)` is an expression, not a statement
‚Üí every branch (`then` and `else`) should return a value
‚Üí all returned values should be type-compatible

```fsharp
let isEven n =
    if n % 2 = 0 then
        "Even"
    else
        "Odd"
```

üí° `if b then x else y` ‚âÉ C‚ôØ ternary operator `b ? x : y`

---

# If/then/else expression (2)

‚òù When `then` returns "nothing" (`unit`üìç), `else` is optional:

```fsharp
let printIfEven n msg =
    if n |> isEven then
        printfn msg
```

üí° We can use `elif` keyword instead of `else if`.

---

# Match expression

```fs
let translateInFrench civility =
    match civility with
    | "Mister" -> "Monsieur"
    | "Madam"  -> "Madame"
    | "Miss"   -> "Mademoiselle"
    | _        -> ""   // üëà wilcard `_`
```

Equivalent in C‚ôØ 8 :

```cs
public static string TranslateInFrench(string civility) =>
    civility switch {
        "Mister" => "Monsieur"
        "Madam"  => "Madame"
        "Miss"   => "Mademoiselle"
        _        => ""
    }
```

---

# Handling Exception

‚Üí `try/with` expression

```fs
let tryDivide x y =
   try
       Some (x / y)
   with :? System.DivideByZeroException ->
       None
```

‚ö†Ô∏è **Trap**: the keyword used is `with`, not `catch`, contrary to C#.

üí° There is no `try/with/finally` expression, only `try/finally`
¬†¬†¬†¬† ‚Üí Nest a `try/finally` in a `try/with`

---

# Throwing Exception

‚Üí Helpers `failwith`, `invalidArg`, `nullArg`

```fs
let fn arg =
    if arg = null then nullArg (nameof arg)
    failwith "Not implemented"

let divide x y =
    if y = 0
    then invalidArg (nameof y) "Divisor cannot be zero"
    else x / y
```

üîó Handling Errors Elegantly https://devonburriss.me/how-to-fsharp-pt-8/

---

![bg h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

# Syntax rules

- Declarations order
- Indentation

---

# Declarations order

In a file, the declarations are ordered, from top to bottom.\
‚Üí Declaration comes before the usages.

In a `.fsproj`, the files are ordered too.\
‚Üí We can import only something previously declared.

Type inference works by proximity: the closest match will be used.

üëç Pros:
‚Üí no cyclic dependencies
‚Üí faster and more predictable compilation
‚Üí code easier de reason about

‚òùÔ∏è Cons:
‚Üí need more coding discipline

---

## Declarations order: example

We try to use the `fn` before its declaration:

```fsharp
let result = fn 2
//           ~~ üí• Error FS0039: The value or constructor 'fn' is not defined.

let fn i = i + 1
```

---

# Indentation

In general, indentation is very important for code readability:
‚Üí It creates visual structures that match the logical structure, the hierarchy.

In C‚ôØ: indentation is optional; logical blocks defined with `{ }` and `;`
‚Üí It's the indentation that matters for readability, then `{ }` can help
‚Üí A code not properly indented can be mis-interpreted, that can lead to bugs!

In F‚ôØ, indentation is required to define code blocks and nesting level.
‚Üí Compiler ensures indentation is correct
‚Üí Reader can really trust the indentation to understand the code structure

üëâ **Conclusion:**
F‚ôØ forces us to do what matters the most for the code readability üëç

---

## Vertical line of indentation

Concept related to the way F‚ôØ understands the indentation.
‚Ä¢ In general, a block starts in a new line, at a greater indentation level.
‚Ä¢ But sometimes a block can start in a middle of a line.
¬†¬†‚Üí This position defines the expected vertical indentation line.

```fsharp
let f =
   let x=1
   x+1
// ^ Vertical line here

let f = let x=1
        x+1
//      ^ Vertical line here!
```

---

## Vertical line of indentation (2)

There are some exceptions to this rule, for instance with operators.
üîó [F‚ôØ syntax: indentation and verbosity](https://fsharpforfunandprofit.com/posts/fsharp-syntax/) | F‚ôØ for fun and profit.

‚òùÔ∏è Indentation rules have been relaxed in [F‚ôØ 6](https://learn.microsoft.com/en-us/dotnet/fsharp/whats-new/fsharp-6#indentation-syntax-revisions).

---

## Indentation Guideline

F‚ôØ allows to have different:
‚Üí whitespace **characters**: tabs and spaces.
‚Üí **number** of whitespaces per indentation level.

Recommendations:
‚Ä¢ In temporary `fsx`, writing speed > code readability
‚Ä¢ In `fsproj`, proper and consistent formatting is important for maintainability
¬†¬†‚Üí Use consistently only spaces
¬†¬†‚Üí Use the same number of spaces for all indentation level
¬†¬†‚Üí 4 spaces is idiomatic; exception: vertical indentation line *(next slide)*
¬†¬†‚Üí Use a code formatter like [**Fantomas**](https://github.com/fsprojects/fantomas) to ensure this consistency.

---

## Indentation Guideline (2)

Avoid naming-sensible indentation a.k.a *Vanity Alignment*:

- They can break compilation after a renaming.
- Blocks too far at the right: less readable *(left-to-right language)*

```fsharp
// üëå OK
let myLongValueName =
    someExpression
    |> anotherExpression

// ‚ö†Ô∏è To avoid
let myLongValueName = someExpression
                      |> anotherExpression  // üëà Depend on the length of `myLongValueName`
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_idee.png)

# 3.

## *F‚ôØ language* design traits

---

# Expression *vs* Statement

> A **statement** will produce a side effect.\
> An **expression** will produce a value... and an eventual side-effect.

- F‚ôØ is a functional, based on expressions only
- In comparison, C‚ôØ is an imperative language, based on statements,...
- ... including more and more syntactic sugar based on expressions:
  - Ternary operator `b ? x : y`
  - [Null-coalescing operator](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/operators/null-coalescing-operator) `??` in C‚ôØ 8 : `label ?? "(Empty)"`
  - [Expression-bodied members](https://docs.microsoft.com/en-us/dotnet/csharp/programming-guide/statements-expressions-operators/expression-bodied-members) in C‚ôØ 6 and 7
  - `switch` expression in C‚ôØ 8

---

## ‚öñÔ∏è Benefits of expressions over instructions

- **Conciseness**: less visual clutters ‚Üí more readable
- **Composability**: composing expressions is like composing values
- **Understanding**: no need to know the previous instructions to understand the current one
- **Testability**: pure expressions (\*) ‚Üí easier to test
  - *Predictable*: same inputs mean same outputs
  - *Isolated*: shorter *Arrange/Setup* phase in tests, no need for mocks...

(\*) **Pure:** with no side-effects

---

# In F‚ôØ ¬´¬†Everything is an expression¬†¬ª

- A function is declared and behaves like a value
  - We can pass it as parameter or return it from another function (1)
- The *control flow* building blocks are also expressions
  - `if ‚Ä¶ then/else` , `match ‚Ä¶ with`
  - `for ‚Ä¶ in`, `for ‚Ä¶ to`, `while ‚Ä¶ do` just return "nothing" (2)

‚òùÔ∏è **Notes:**
¬†¬†‚Ä¢ (1) See *1st-class citizens, high-order functions* üìç
¬†¬†‚Ä¢ (2) Except in *collection comprehensions* üìç

---

## Everything is an expression¬†¬ª Consequences

No `void`
‚Üí Best replaced by the `unit` type üìç

No *early exit*
‚Ä¢ In C#, you can exit a function with `return` and exit a `for/while` loop with `break`.
‚Ä¢ In F‚ôØ, these keywords do not exist. *(Alternatives: next slides)*

---

## Early exit alternatives ¬ª Imperative style

üí© `BreakException`
¬†¬†¬†‚Üí *See code in* [StackOverflow](https://stackoverflow.com/a/42018355/8634147)

üòï **Mutable variables**

```fsharp
let firstItemOr defaultValue predicate (items: 't array) =
    let mutable result = None
    let mutable i = 0
    while i < items.Length && result.IsNone do
        let item = items[i]
        if predicate item then
            result <- Some item
        i <- i + 1

    result
    |> Option.defaultValue defaultValue

let test1' = firstItemOr -1 (fun x -> x > 5) [| 1 |]     // -1
```

---

## Early exit alternatives ¬ª Functional style

‚úÖ **Recursive function** üìç

```fsharp
[<TailCall>]
let rec firstItemOr defaultValue predicate list =
    let loop list =
        firstItemOr defaultValue predicate list

    match list with
    | [] -> defaultValue            // üëà Exit
    | x :: _ when predicate x -> x  // üëà Exit
    | _ :: rest -> loop rest        // üëà Continue recursion

// Tests
let test1 = [1]    |> firstItemOr -1 (fun x -> x > 5)  // -1
let test2 = [1..7] |> firstItemOr -1 (fun x -> x > 5)  // 6
```

---

# Typing, inference and ceremony

The ceremony is correlated to the typing weakness
üîó [Zone of Ceremony](https://blog.ploeh.dk/2019/12/16/zone-of-ceremony/) _by Mark Seemann_

| Lang | Typing strength                  | Inference | Ceremony |
|------|----------------------------------|-----------|----------|
| JS   | Low (dynamic)                    | √ó         | Low      |
| C‚ôØ   | Medium (static nominal)          | Low       | Strong   |
| TS   | Strong (static structural + ADT) | Medium    | Medium   |
| F‚ôØ   | Strong (static nominal + ADT)    | √âlev√©e    | Low      |

ADT = *Algebraic Data Types* üìç

---

# Type inference

Goal: write type annotations as little as possible

- Less code to write
- Compiler ensures consistency
- IntelliSense helps with coding and reading

---

## Type inference in C‚ôØ

- Method parameters and return value ‚ùå‚ùå
- Variable declaration: `var o = new { Name = "John" }` ‚úîÔ∏è
- Lambda as argument: `list.Find(i => i == 5)` ‚úîÔ∏è
- Lambda declaration in C# 10: `var f3 = () => 1;` ‚úîÔ∏è *(limited)*
- Array initialisation: `var a = new[] { 1, 2 };` ‚úîÔ∏è
- Generic classes:
  - constructor: `new Tuple<int, string>(1, "a")` ‚ùå
  - static helper class: `Tuple.Create(1, "a")` ‚úîÔ∏è
- C‚ôØ 9 target-typed expression `StringBuilder sb = new();` ‚úîÔ∏è

---

## Type inference in F‚ôØ

[Hindley‚ÄìMilner](https://en.wikipedia.org/wiki/Hindley%E2%80%93Milner_type_system) method

- Able to deduce the type of variables, expressions and functions
  - without any type annotation
- Based on both the implementation and the usage

**Example:**

```fsharp
let helper instruction source =
    if instruction = "inc" then // 1. `instruction` has the same type than `"inc"` => `string`
      source + 1                // 2. `source` has the same type than `1` => `int`
    elif instruction = "dec" then
      source - 1
    else
      source                    // 3. `return` has the same type than `source` => `int`
```

---

## F‚ôØ inference - Automatic generalization

If something can be inferred as generic, it will be\
‚Üí Open to more cases ü•≥

```fsharp
// Generic value
let a = [] // 'a list

// Generic function with both parameters generic
let listOf2 x y = [x; y]
// val listOf2: x: 'a -> y: 'a -> 'a list

// Generic type constraint inference: 'a must be "comparable"
let max x y = if x > y then x else y
```

‚òù In F‚ôØ, a generic type starts with an apostrophe `'` *(a.k.a. tick)*
¬†¬†¬† ‚Ä¢ Can be in camelCase (`'a`) or PascalCase (`'T`)
¬†¬†¬† ‚Ä¢ C‚ôØ `TXxx` ‚Üí F‚ôØ `'xxx` or `'Xxx`

---

## Inference *vs* type annotation

**Pros** üëç
‚Üí code terser
‚Üí automatic generalization

**Cons** ‚ö†Ô∏è
‚Üí we can break code in cascade
‚Üí inference limited:
¬†¬†¬†¬†‚Ä¢ an object type cannot be determine by the call to one of its members (1)
¬†¬†¬†¬†‚Ä¢ sensible to the instructions order (2)

(1)(2) Example on next slides

---

## Inference *vs* type annotation - Limit #1

‚ö†Ô∏è No inference from *"object dotting"* *(Exception: records üìç)*

```fsharp
let helperKO instruction source =
    match instruction with
    | 'U' -> source.ToUpper()
    //       ~~~~~~~~~~~~~~~~ üí•
    // Error FS0072: Lookup on object of indeterminate type based on information prior to this program point.
    // A type annotation may be needed prior to this program point to constrain the type of the object.
    | _   -> source

let helperOk instruction (source: string) = [...]
// Type annotation needed here  : ^^^^^^

// If there is a function equalivalent to the method, it will work
let info list = if list.Length = 0 then "Vide" else "..." // üí• Error FS0072...
let info list = if List.length list = 0 then "Vide" else $"{list.Length} √©l√©ments" // üëå
```

---

## Inference *vs* type annotation - Limit #2

‚ö†Ô∏è Sensitivity to the instructions order

```fsharp
let listKo = List.sortBy (fun x -> x.Length) ["three"; "two"; "one"]
//                                 ~~~~~~~~ üí• Error FS0072: Lookup on object of indeterminate type...

// Solution 1: reverse the order by piping the list
let listOk = ["three"; "two"; "one"] |> List.sortBy (fun x -> x.Length)

// Solution 2: use a named function  instead of a lambda
let listOk' = List.sortBy String.length ["three"; "two"; "one"]
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 4.

## üçî Quiz

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

## 1. Who is the father of the F‚ôØ? ‚è± 10‚Äô‚Äô

**A.** Anders Hejlsberg

**B.** Don Syme

**C.** Scott Wlaschin

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

## 1. Who is the father of the F‚ôØ ?

**A.** Anders Hejlsberg ‚ùå

‚Üí Father of C‚ôØ and TypeScript!

**B.** Don Syme ‚úÖ

‚Üí [dsymetweets](https://twitter.com/dsymetweets) ‚Ä¢ üé• [F‚ôØ Code I Love](https://www.youtube.com/watch?v=1AZA1zoP-II)

**C.** Scott Wlaschin ‚ùå

‚Üí Famous blog [F‚ôØ for Fun and Profit](https://fsharpforfunandprofit.com/), a gold mine for F‚ôØ

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

## 2. What is the name of the `::` operator? ‚è± 10‚Äô‚Äô

**A.** Append

**B.** Concat

**C.** Cons

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

## 2. What is the name of the `::` operator?

**A.** Append ‚ùå

`List.append` : concatenation of 2 lists

**B.** Concat ‚ùå

`List.concat` : concatenation of a set of lists

**C.** Cons ‚úÖ

`newItem :: list` is the fasted way to add an item at the top of a list

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

## 3. Find the intruder! ‚è± 15‚Äô‚Äô

**A.** `let a = "a"`

**B.** `let a () = "a"`

**C.** `let a = fun () -> "a"`

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

## 3. Find the intruder!

B and C are functions, while A is a simple value: a `string`.

**A.** `let a = "a"` ‚úÖ

**B.** `let a () = "a"` ‚ùå

**C.** `let a = fun () -> "a"` ‚ùå

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

## 4. What line does not compile? ‚è± 20‚Äô‚Äô

```fs
(* 1 *) let evens list =
(* 2 *)     let isEven x =
(* 3 *)     x % 2 = 0
(* 4 *)     List.filter isEven list
```

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

## 4. What line does not compile?

```fs
(* 1 *) let evens list =
(* 2 *)     let isEven x =
(* 3 *)     x % 2 = 0 // üí• Error FS0058: Unexpected syntax or possible incorrect indentation
(* 4 *)     List.filter isEven list
```

Line **3.** `x % 2 = 0` : an indentation is missing

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

## 5. What is the name of `|>` operator? ‚è± 10‚Äô‚Äô

**A.** Compose

**B.** Chain

**C.** Pipeline

**D.** Pipe

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

## 5. What is the name of `|>` operator?

**A.** Compose ‚ùå - Composition operator is `>>` üìç

**B.** Chain ‚ùå

**C.** Pipeline ‚ùå

**D.** Pipe ‚úÖ

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

## 6. Which expression compiles? ‚è± 20‚Äô‚Äô

**A.** `a == "a" && b != "*"`

**B.** `a == "a" && b <> "*"`

**C.** `a = "a" && b <> "*"`

**D.** `a = "a" && b != "*"`

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

## 6. Which expression compiles?

**A.** `a == "a" && b != ""` ‚ùå

**B.** `a == "a" && b <> ""` ‚ùå

**C.** `a = "a" && b <> ""` ‚úÖ

**D.** `a = "a" && b != ""` ‚ùå

| Operator   | C‚ôØ             | F‚ôØ             |
|------------|----------------|----------------|
| Equality   | `==`           | `=`            |
| Inequality | `!=` (`!`‚Äâ`=`) | `<>` (`<`‚Äâ`>`) |

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 5.

## Wrap up

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_note.png)

# Recap

- F‚ôØ Syntax
- F‚ôØ Language design traits
  - Everything is an expression!
  - Type inference

---

# Addendum

üîó [F‚ôØ Cheatsheet](https://fsprojects.github.io/fsharp-cheatsheet/)

üîó [Troubleshooting F‚ôØ - Why won't my code compile?](https://fsharpforfunandprofit.com/troubleshooting-fsharp/)

---

<!-- _class: end invert lead-->

# Thanks üôè
