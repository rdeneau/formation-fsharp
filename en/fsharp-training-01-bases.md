---
marp: true
html: true
theme: 'd-edge'
title: 'Fâ™¯ Training â€¢ Bases'
footer: 'Fâ™¯ Training â€¢ Bases'
paginate: true
---

<!-- RDE

Terminal commands :
- HTML: marp fsharp-training-01-bases.md --watch --theme themes/d-edge.css
- PDF : marp fsharp-training-01-bases.md --pdf --allow-local-files --theme themes/d-edge.css

VSCode:
- [F11] Full screen
- [Ctrl]+[KeyPad+/-] Zoom
- [F1] + "Screencast mode" â†’ show key stroke
- [Ctrl]+[Ã¹] â†’ show terminal
- [Ctrl]+[L] â†’ clear terminal
- Terminal tab > Right click : "Move panel right"

-->

<!-- _class: title invert -->

# Fâ™¯ Training

## *Bases*

### 2025 April

---

<!-- _class: toc agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- Le Fâ™¯, c'est quoi ?
- Syntaxe : fondamentaux, indentation
- Premiers concepts
  Â Â Â Â Â Â Â â€¢ Currification et application partielle
  Â Â Â Â Â Â Â â€¢ Tout est expression
  Â Â Â Â Â Â Â â€¢ InfÃ©rence de type

---

# â˜ Notes prÃ©alables

1. Symbole ğŸ“ : indique notion abordÃ©e + tard

2. Code utilise la police **Fira Code** - ğŸ”— [github.com/tonsky/FiraCode](https://github.com/tonsky/FiraCode) :

```js
'->' = '-' + '>'    '<=' = '<' + '='    '==' = '=' + '='
'=>' = '=' + '>'    '>=' = '>' + '='    '!=' = '!' + '='
'<>' = '<' + '>'    '|>' = '|' + '>'    '<|' = '<' + '|'
'[|' = '[' + '|'    '|]' = '|' + ']'
```

ğŸ’¡ Setting dans VsCode pour activer la ligature : `"editor.fontLigatures": true`

ğŸ’¡ Dans Rider, idem avec police **JetBrains Mono** - ğŸ”— [jetbrains.com/lp/mono/](https://www.jetbrains.com/lp/mono/)

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_probleme.png)

# 1.

## Le Fâ™¯, c'est quoi ?

---

# Points clÃ©s

Famille des langages Microsoft - Plateforme **.NET**

- Son concepteur : Don Syme @ Microsoft Research
- â‰ƒ ImplÃ©mentation de OCaml pour .NET
- â‰ƒ InspirÃ©e par Haskell *(Version 1.0 en 1990)*
- `dotnet new -lang F#`
- Inter-opÃ©rabilitÃ© entre projets/assemblies Câ™¯ et Fâ™¯

Langage multi-paradigme __*Functional-first*__ et trÃ¨s concis

LÃ  oÃ¹ Câ™¯ est *imperative/object-oriented-first* et plutÃ´t verbeux
*(mÃªme sâ€™il s'inspire de Fâ™¯ pour Ãªtre \+ succinct)*

---

# Historique

| Date     | Câ™¯     | Fâ™¯     | .NET                              | Visual Studio |
|----------|--------|--------|-----------------------------------|---------------|
| 2002     | Câ™¯ 1.0 |        | .NET Framework 1.0                | VS .NET 2002  |
| **2005** |        | Fâ™¯ 1.x | .NET Framework 1.0                | VS 2005 ?     |
| 2010     | Câ™¯ 4.0 | Fâ™¯ 2.0 | .NET Framework 4                  | VS 2010       |
| 2015     | Câ™¯ 6.0 | Fâ™¯ 4.0 | .NET Framework 4.6, .NET Core 1.x | VS 2015       |
| 2018     | Câ™¯ 7.3 | Fâ™¯ 4.5 | .NET Framework 4.8, .NET Core 2.x | VS 2017       |
| 2019     | Câ™¯ 8.0 | Fâ™¯ 4.7 | .NET Core 3.x                     | VS 2019       |
| 2020     | Câ™¯ 9.0 | Fâ™¯ 5.0 | .NET 5.0                          | VS 2019       |

---

# Ã‰diteurs / IDE

VsCode + [Ionide](https://marketplace.visualstudio.com/items?itemName=Ionide.Ionide-fsharp)
â†’ â˜ Permissif : ne remonte pas toujours toutes les erreurs de compilation

Visual Studio / Rider
â†’ â˜ Moins de refacto que pour Câ™¯

https://try.fsharp.org/
â†’ Online [REPL](https://en.wikipedia.org/wiki/Read%E2%80%93eval%E2%80%93print_loop) avec exemples

---

# Rappel : setup du poste

[https://docs.microsoft.com/en-us/learn/modules/fsharp-first-steps/
4-set-up-development-environment-exercise](https://docs.microsoft.com/en-us/learn/modules/fsharp-first-steps/4-set-up-development-environment-exercise)

- Installation du SDK .NET (5.0 min, 6.0 si dispo)
- Installation de VScode
- Ajout de l'extension Ionide-fsharp

*(Optionnel)* extensions complÃ©mentaires : https://www.compositional-it.com/news-blog/fantastic-f-and-azure-developer-extensions-for-vscode/

---

# Fâ™¯ interactive *(FSI)*

- REPL disponible dans VS, Rider, vscode + `dotnet fsi`
- Usage : vÃ©rifier en live un bout de code
  - ğŸ’¡ Terminer expression par `;;` pour l'Ã©valuer
- Existe depuis le dÃ©part *(cf. aspect scripting du F#)*
  - *Câ™¯ interactive* + rÃ©cent (VS 2015 Update 1)
- Alternative : [LINQPad](https://www.linqpad.net/)

ğŸ‘¨â€ğŸ« **DÃ©mo**

---

# Types de fichier

4 types de fichier : `.fs`, `.fsi`, `.fsx`, `.fsproj`

- Mono langage : purement pour/en Fâ™¯
- Standalone vs Projet

## Fichier standalone

- Fichier de script `.fsx`
  - ExÃ©cutable *(d'oÃ¹ le **x**)* dans la console FSI
  - IndÃ©pendant mais peut rÃ©fÃ©rencer autre fichier, DLL, package NuGet.

---

## Fichiers de projet

- En Câ™¯ : `.sln` contient `.csproj` qui contient `.cs`
- En Fâ™¯ : `.sln` contient `.fsproj` qui contient `.fs` et `.fsi`
  - Fichier projet `.fsproj`
  - Fichier de code `.fs`
  - Fichier de signature `.fsi` *(**i** comme interface)*
    - AssociÃ© Ã  un fichier `.fs` de mÃªme nom
    - Optionnel et plutÃ´t rare -- + d'info : [MSDN](https://docs.microsoft.com/fr-fr/dotnet/fsharp/language-reference/signature-files)
    - Renforcer encapsulation *(idem `.h` en C)*
    - SÃ©parer longue documentation (xml-doc)

ğŸ’¡ **Interop Câ™¯ - Fâ™¯** = Mixer `.csproj` et `.fsproj` dans `.sln`... ğŸ“

---

## Projet Fâ™¯

CrÃ©ation dans un IDE ou avec la CLI `dotnet` :

- `dotnet new -l` : lister les types de projet supportÃ©s
- `dotnet new console --language F# -o MyFSharpApp`
  - CrÃ©ation d'un projet console nommÃ© `MyFSharpApp`
  - `--language F#` Ã  spÃ©cifier ; sinon C#
- `dotnet build` : builder le projet
- `dotnet run` : builder le projet et lancer l'exÃ©cutable rÃ©sultant

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_backEnd.png)

# 2.

## Syntaxe - Fondamentaux

---

# Syntaxe - ClÃ©

1er point fort de Fâ™¯ : langage succinct

Pour sans rendre compte :

1. Passons rapidement en revue sa syntaxe
2. Ensuite vous pourrez commencer Ã  jouer avec
   - â˜ C'est Ã  l'usage que l'on mesure le cÃ´tÃ© succinct de Fâ™¯

---

# Commentaires

```fs
(* This is block
   comment *)


// And this is line comment


/// XML doc summary


/// <summary>
/// Full XML doc
/// </summary>
```

---

# ~~Variables~~ Valeurs

- Mot clÃ© `let` pour dÃ©clarer/nommer une valeur
- Pas besoin de `;` en fin de dÃ©claration
- Liaison/*Binding* est immutable par dÃ©faut
  - â‰ƒ `const` en JS, `readonly` pour un membre en Câ™¯
- Mutable avec `let mutable` et opÃ©rateur d'assignation `<-`
  - â‰ƒ `let` en JS, `var` en Câ™¯
  - Avec parcimonie, sur *scope* limitÃ©

```fs
let x = 1
x <- 2 // âŒ Error FS0027: Cette valeur n'est pas mutable.

let mutable x = 1
x <- 2 // âœ… AutorisÃ©
```

---

# Noms

- MÃªmes contraintes qu'en Câ™¯
- Sauf apostrophe `'`
  - permise dans nom au milieu ou Ã  la fin *(mais pas au dÃ©but)*
  - en fin de nom â†’ indique une variante *(convention)*
- Entre doubles *backticks* â†’ acceptent tout char (sauf saut de ligne)

```fs
let x = 1
let x' = x + 1  // Se prononce "x prime" ou "x tick"

let if' b t f = if b then t else f

let ``123 456`` = "123 456"
// ğŸ’¡ Auto-complÃ©tion : pas besoin de taper les ``, directement 123 (quand Ã§a veut marcher)
```

---

# *Shadowing*

- Consiste Ã  redÃ©finir une valeur avec un nom existant
- En Fâ™¯, interdit dans un mÃªme *scope* mais autorisÃ© dans un sous-scope
  - Mais pas recommandÃ©, sauf cas particulier

```fs
let a = 2

let a = "ko"  // ğŸ’¥ Error FS0037: DÃ©finition dupliquÃ©e de value 'a'

let b =
    let a = "ok" // ğŸ‘Œ Pas d'erreur de compilation
    // `a` vaut "ok" (et pas 2) dans tout le reste de `b`
    // ...
```

---

# Annotation de type

- Optionnelle grÃ¢ce Ã  l'infÃ©rence
- Type dÃ©clarÃ© aprÃ¨s nom `name: type` *(comme en TypeScript)*
- Valeur obligatoire, mÃªme si `mutable`

```fs
let x = 1       // InfÃ©rÃ© (int)
let y: int = 2  // Explicite

let z1: int           // ğŸ’¥ Error FS0010: Construction structurÃ©e incomplÃ¨te Ã  cet emplacement...
let mutable z2: int   // ğŸ’¥ ... ou avant dans la liaison. '=' ou autre jeton attendu.
```

---

# Constante

- *What:* Variable effacÃ©e Ã  la compilation, remplacÃ©e par sa valeur
  - â‰ƒ `const` Câ™¯ - mÃªme idÃ©e en TS que `const enum`
- *How:* Valeur dÃ©corÃ©e avec attribut `Literal`
- Convention de nommage : PascalCase

```fs
[<Literal>] // Saut de ligne nÃ©cessaire car avant le `let`
let AgeOfMajority = 18

let [<Literal>] Pi = 3.14 // Possible aussi aprÃ¨s le `let`
```

---

# Nombre

```fs
let pi = 3.14             // val pi : float = 3.14       â€¢ System.Double
let age = 18              // val age : int = 18          â€¢ System.Int32
let price = 5.95m         // val price : decimal = 5.95M â€¢ System.Decimal
```

âš ï¸ Pas de conversion implicite entre nombre

â†’ ğŸ’¡ Utiliser fonctions `int`, `float`, `decimal`

```fs
float 3;;             // val it : float = 3.0
decimal 3;;           // val it : decimal = 3M
int 3.6;;             // val it : int = 3
int "2";;             // val it : int = 2
```

---

# String

```fs
let name = "Bob"              // val name : string = "Bob"
let name2 = $"{name} Marley"  // val name' : string = "Bob Marley"
let initial = name2.[0]       // val initial :  char = 'B'
let firstName = name2.[0..2]  // val firstName : string = "Bob"
```

- `$"{val}"` chaÃ®ne avec interpolation (âš ï¸ Fâ™¯ 5)
  - Avant Fâ™¯ 5 â†’ utiliser `sprintf`
- `.[0]` : accÃ¨s par index â†’ caractÃ¨re
- `.[0..2]` : accÃ¨s par plage â†’ sous-chaÃ®ne
  - Alternative Ã  mÃ©thode `Substring(index [, length])`

---

# String (2)

```fs
// Verbatim string: idem Câ™¯
let verbatimXml = @"<book title=""Paradise Lost"">"

// Triple-quoted string : pas besoin d'Ã©chapper les guillemets `"`
let tripleXml = """<book title="Paradise Lost">"""

// Backslash strings : trim les espaces Ã  gauche
let poem = 
    "The lesser world was daubed\n\
     By a colorist of modest skill\n\
     A master limned you in the finest inks\n\
     And with a fresh-cut quill."
```

---

# Listes

Liste immuable â†’ type spÃ©cial Fâ™¯ â‰  `System.Collection.Generic.List<T>`

```fs
let abc = [ 'a'; 'b'; 'c' ] // val abc : char list = ['a'; 'b'; 'c']
let a =
  [ 2
    3 ]  // val a : int list = [2; 3]
```

- CrÃ©ation avec `[]`, Ã©lÃ©ments sÃ©parÃ©s par `;` ou saut de ligne + indentation
  - âš ï¸ PiÃ¨ge : ne pas utiliser `,` sinon on a 1! Ã©lÃ©ment : un tuple ğŸ“
- Notation ML du type `int list` = `List<int>`
  - â˜ Idiomatique que pour `list` et `option` ğŸ“

---

# Listes - OpÃ©rateurs

```fs
let nums = [2..5]                 // val nums : int list = [2; 3; 4; 5]
let nums' = 1 :: nums             // val nums' : int list = [1; 2; 3; 4; 5]

let chars = [ 'a' .. 'd' ]        // val chars : char list = ['a'; 'b'; 'c'; 'd']
let chars' = chars @ [ 'e'; 'f' ] // val chars' : char list = ['a'; 'b'; 'c'; 'd'; 'e'; 'f']
let c = chars.[0]                 // val c : char = 'a'
```

- `min..max` plage d'entiers entre min et max inclus
  - `min..step..max` pour un Ã©cart > 1
- `::` opÃ©rateur *Cons* (signifiant construction)
  - Ajoute un Ã©lÃ©ment en tÃªte de liste
- `@` opÃ©rateur *Append* = ConcatÃ¨ne 2 listes
- Point `.` nÃ©cessaire pour accÃ¨s par index `.[index]`

---

# Listes - Module `List`

| Fâ™¯ `List`          | Câ™¯ LINQ                    | JS `Array`           |
|--------------------|----------------------------|----------------------|
| `map`, `collect`   | `Select()`, `SelectMany()` | `map()`, `flatMap()` |
| `exists`, `forall` | `Any(predicate)`, `All()`  | `some()`, `every()`  |
| `filter`           | `Where()`                  | `filter()`           |
| `find`, `tryFind`  | Ã—                          | `find()`             |
| `fold`, `reduce`   | `Aggregate([seed]])`       | `reduce()`           |
| `average`, `sum`   | `Average()`, `Sum()`       | Ã—                    |

```txt
â˜ Autres fonctions : cf. documentation
   https://fsharp.github.io/fsharp-core-docs/reference/fsharp-collections-listmodule.html
```

---

# Fonctions

- Fonction nommÃ©e : dÃ©clarÃ©e avec `let`
- Convention de nommage : **camelCase**
- Pas de `return` : renvoie toujours derniÃ¨re expression
- Pas de `()` autour de tous les paramÃ¨tres
  - `()` autour d'un paramÃ¨tre avec type explicite ou dÃ©construit ğŸ“

```fs
let square x = x * x  // Fonction Ã  1 paramÃ¨tre
let res = square 2    // Vaut 4

// Avec types explicites - juste pour l'exemple - pas idiomatique !
let square' (x: int) : int = x * x
```

---

# Fonctions de 0-n paramÃ¨tres

- ParamÃ¨tres et arguments sÃ©parÃ©s par **espace**
  - âš ï¸ PiÃ¨ge : `,` sert Ã  instancier 1 tuple ğŸ“ = 1! paramÃ¨tre
- `()` : fonction sans paramÃ¨tre, sans argument
- Sans `()`, on dÃ©clare une valeur "vide", pas une fonction :

```fs
let add x y = x + y  // Fonction Ã  2 paramÃ¨tres
let res = add 1 2    // `res` vaut 3

let printHello () = printfn "Hello"  // Fonction sans paramÃ¨tre
printHello ()                        // Affiche "Hello" en console

let notAFunction = printfn "Hello"   // Affiche "Hello" en console et renvoie "vide"
```

---

# Fonction multi-ligne

**Indentation** nÃ©cessaire, mais pas de `{}`

```fs
let evens list =
    let isEven x =  // ğŸ‘ˆ Sous-fonction
        x % 2 = 0   // ğŸ’¡ `=` opÃ©rateur d'Ã©galitÃ© - Pas de `==`
    List.filter isEven list

let res = evens [1;2;3;4;5] // Vaut [2;4]
```

---

# Fonction anonyme

A.k.a. **Lambda**, arrow function

- DÃ©clarÃ©e avec `fun` et `->`
- En gÃ©nÃ©ral entre `()` pour question de prÃ©cÃ©dence

```fs
let evens' list = List.filter (fun x -> x % 2 = 0) list
```

â˜ **Note** : taille de la flÃ¨che

- Fine `->` en Fâ™¯, Java
- Large / *fat* `=>` en Câ™¯, JS

---

# Convention de noms courts

- `x`, `y`, `z` : paramÃ¨tres de type valeurs simples
- `f`, `g`, `h` : paramÃ¨tres de type fonction
- `xs` : liste de `x` â†’ `x::xs` (ou `h::t`) = *head* et *tail* d'une liste non vide
- `_` : *discard* / Ã©lÃ©ment ignorÃ© car non utilisÃ© *(comme en Câ™¯ 7.0)*

Bien adaptÃ© quand fonction courte ou trÃ¨s gÃ©nÃ©rique :

```fs
// Fonction qui renvoie son paramÃ¨tre d'entrÃ©e, quel qu'il soit
let id x = x

// Composition de 2 fonctions
let compose f g = fun x -> g (f x)
```

---

# *Piping*

OpÃ©rateur *pipe* `|>` : mÃªme idÃ©e que `|` UNIX
â†’ Envoyer la valeur Ã  gauche dans une fonction Ã  droite
â†’ Ordre naturel "sujet verbe" - idem appel mÃ©thode d'un objet

```fs
let a = 2 |> add 3  // Se lit comme "2 + 3"

let nums = [1;2;3;4;5]
let evens = nums |> List.filter (fun x -> x % 2 = 0)
// Idem     List.filter (fun x -> x % 2 = 0) nums
```

```cs
// â‰ƒ Câ™¯
var a = 2.Add(3);
var nums = new[] { 1, 2, 3, 4, 5 };
var evens = nums.Where(x => x % 2 == 0);
```

---

<!-- _footer: '' -->

# Chainage de *pipes* - *Pipeline*

Comme fluent API en Câ™¯ mais natif : pas besoin de mÃ©thode d'extension ğŸ‘

FaÃ§on naturelle de reprÃ©senter le flux de donnÃ©es entre diffÃ©rentes opÃ©rations
â†’ Sans variable intermÃ©diaire ğŸ‘

Ã‰criture :

```fs
// Sur une seule ligne (courte)
let res = [1;2;3;4;5] |> List.filter (fun x -> x % 2 = 0) |> List.sum

// Sur plusieurs lignes => fait ressortir les diffÃ©rentes opÃ©rations
let res' =
    [1; 2; 3; 4; 5]
    |> List.filter isOdd  // Avec `let isOdd x = x % 2 <> 0`
    |> List.map square    //      `let square x = x * x`
    |> List.map addOne    //      `let addOne x = x + 1`
```

---

# Expression `if/then/else`

ğŸ’¡ `if b then x else y` â‰ƒ OpÃ©rateur ternaire Câ™¯ `b ? x : y`

```fs
let isEven n =
    if n % 2 = 0 then
        "Even"
    else
        "Odd"
```

â˜ Si `then` ne renvoie pas de valeur, `else` facultatif

```fs
let printIfEven n msg =
    if n |> isEven then
        printfn msg
```

---

# Pattern matching avec `match/with`

```fs
let translate civility =
    match civility with
    | "Mister" -> "Monsieur"
    | "Madam"  -> "Madame"
    | "Miss"   -> "Mademoiselle"
    | _        -> ""   // ğŸ‘ˆ wilcard `_`
```

Ã‰quivalent en Câ™¯ 8 :

```cs
public static string Translate(string civility) =>
    civility switch {
        "Mister" => "Monsieur"
        "Madam"  => "Madame"
        "Miss"   => "Mademoiselle"
        _        => ""
    }
```

---

# Exception

## Handling Exception

â†’ Bloc `try/with` -- âš ï¸ PiÃ¨ge : ~~try/catch~~

```fs
let tryDivide x y =
   try
       Some (x / y)
   with :? System.DivideByZeroException ->
       None
```

---

## Throwing Exception

â†’ Helpers `failwith`, `invalidArg`, `nullArg`

```fs
let fn arg =
    if arg = null then nullArg (nameof arg)
    failwith "Not implemented"

let divide x y =
    if y = 0
    then invalidArg (nameof y) "Divisor cannot be zero"
    else x / y
```

â˜ Pour erreurs mÃ©tier i.e. cas prÃ©vus, non exceptionnels :
Â Â Â Â Â PrÃ©fÃ©rer type `Result` / *Railway-oriented programming* ğŸ“

ğŸ”— Handling Errors Elegantly https://devonburriss.me/how-to-fsharp-pt-8/

---

# Ordre des dÃ©clarations

âš ï¸ DÃ©clarations ordonnÃ©es de haut en bas

- DÃ©claration prÃ©cÃ¨de usage
- Au sein d'un fichier
- Entre fichiers dÃ©pendants
  - â˜ Importance de l'ordre des fichiers dans un `.fsproj`
- BÃ©nÃ©fice : pas de dÃ©pendances cycliques ğŸ‘

```fs
let result = fn 2
//           ~~ ğŸ’¥ Error FS0039: La valeur ou le constructeur 'fn' n'est pas dÃ©fini

let fn i = i + 1 // â˜ Doit Ãªtre dÃ©clarÃ©e avant `result`
```

---

# Indentation

- TrÃ¨s importante pour lisibilitÃ© du code
  - CrÃ©e struct. visuelle qui reflÃ¨te struct. logique / hiÃ©rarchie
  - `{}` alignÃ©es verticalement (Câ™¯) = aide visuelle mais < indentation
- Essentielle en Fâ™¯ :
  - FaÃ§on de dÃ©finir des blocs de code
  - Compilateur assure que indentation est correcte

ğŸ‘‰ **Conclusion :**

- Fâ™¯ force Ã  bien indenter
- Mais c'est pour notre bien
- Car c'est bÃ©nÃ©fique pour lisibilitÃ© du code ğŸ‘

---

# Ligne verticale d'indentation

â†’ DÃ©marre aprÃ¨s `let ... =`, `(`, `then`/`else`,
 `try`/`finally`, `do`, `->` (dans clause `match`) mais pas `fun` !
â†’ Commence au 1er caractÃ¨re non *whitespace* qui suit
â†’ Tout le reste du bloc doit s'aligner verticalement
â†’ L'indentation peut varier d'un bloc Ã  l'autre

```fs
let f =
  let x=1     // ligne d'indentation fixÃ©e en column 3 (indÃ©pendamment des lignes prÃ©cÃ©dentes)
  x+1         // ğŸ‘‰ cette ligne (du mÃªme bloc) doit commencer en column 3 ; ni 2, ni 4 â—

let f = let x=1  // Indentation en column 10
        x+1      // ğŸ‘‰ alignement vertical des autres lignes du bloc en column 10
```

ğŸ”— https://fsharpforfunandprofit.com/posts/fsharp-syntax/

---

# Indentation - *Guideline*

â€¢ Utiliser des **espaces**, pas des ~~tabulations~~

â€¢ Utiliser **4 espaces** par indentation

- Facilite la dÃ©tection visuelle des blocs
- ... qui ne peut se baser sur les `{ }` comme en Câ™¯

---

# Indentation - *Guideline* (2)

â€¢ Ã‰viter un alignement sensible au nom, a.k.a *vanity alignment*

- Risque de rupture de l'alignement aprÃ¨s renommage â†’ ğŸ’¥ Compilation
- Bloc trop dÃ©calÃ© Ã  droite â†’ nuit Ã  la lisibilitÃ©

```fs
// ğŸ‘Œ OK
let myLongValueName =
    someExpression
    |> anotherExpression

// âš ï¸ Ã€ Ã©viter
let myLongValueName = someExpression
                      |> anotherExpression  // ğŸ‘ˆ DÃ©pend de la longueur de `myLongValueName`
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_idee.png)

# 3.

## Premiers concepts

---

# Curryfication

Consiste Ã  transformer :

- une fonction prenant N paramÃ¨tres
  - `Func<T1, T2...Tn, TReturn>` en Câ™¯
- en une chaÃ®ne de N fonctions prenant 1 paramÃ¨tre
  - `Func<T1, Func<Tn, ...Func<Tn, TReturn>>>`

---

# Application partielle

Appel d'une fonction avec moins d'arguments que son nombre de paramÃ¨tres

- Possible grÃ¢ce Ã  la curryfication
- Renvoie fonction prenant en paramÃ¨tre le reste d'arguments Ã  fournir

```fs
// Template Ã  2 paramÃ¨tres
let insideTag (tagName: string) (content: string) =
    $"<{tagName}>{content}</{tagName}>"

// Helpers Ã  1! paramÃ¨tre `content`, `tagName` Ã©tant fixÃ© par application partielle
let emphasize = insideTag "em"     // `tagName` fixÃ© Ã  la valeur "em"
let strong    = insideTag "strong" // `tagName` fixÃ© Ã  la valeur "strong"

// Equivalent - Ã©lÃ©gant mais + explicite
let em content = insideTag "em" content
```

---

# Expression *vs* Instruction *(Statement)*

> Une **instruction** produit un effet de bord.
> Une **expression** produit une valeur et un Ã©ventuel effet de bord **(Ã  Ã©viter)**.

- Fâ™¯ est un langage fonctionnel, Ã  base **d'expressions** uniquement.
- Câ™¯ est un langage impÃ©ratif, Ã  base **d'instructions** *(statements)*
  mais comporte de + en + de sucre syntaxique Ã  base d'expressions :
  - OpÃ©rateur ternaire `b ? x : y`
  - [Null-conditional operator](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/operators/member-access-operators#null-conditional-operators--and-) `?.` en Câ™¯ 6 : `model?.name`
  - [Null-coalescing operator](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/operators/null-coalescing-operator) `??` en Câ™¯ 8 : `label ?? '(Vide)'`
  - Expression lambda en Câ™¯ 3 avec LINQ : `numbers.Select(x => x + 1)`
  - [Expression-bodied members](https://docs.microsoft.com/en-us/dotnet/csharp/programming-guide/statements-expressions-operators/expression-bodied-members) en Câ™¯ 6 et 7
  - Expression `switch` en Câ™¯ 8

---

## âš–ï¸ Avantages des expressions / instructions

- **Concision** : code + compact == + lisible
- **ComposabilitÃ©** : composer expressions == composer valeurs
  - Addition, multiplication... de nombres,
  - ConcatÃ©nation dans une chaÃ®ne,
  - Collecte dans une liste...
- **ComprÃ©hension** : pas besoin de connaÃ®tre les instructions prÃ©cÃ©dentes
- **TestabilitÃ©** : expressions pures *(sans effet de bord)* + facile Ã  tester
  - *PrÃ©dictible* : mÃªme inputs produisent mÃªme outputs
  - *IsolÃ©e* : phase *arrange/setup* allÃ©gÃ©e *(pas de mock...)*

---

# En Fâ™¯ Â«Â Tout est expressionÂ Â»

- Une fonction se dÃ©clare et se comporte comme une valeur
  - En param ou en sortie d'une autre fonction *(dite high-order function)*
- Ã‰lÃ©ments du *control flow* sont aussi des expressions
  - Branches des `if/else` et `match/with` (`~=switch`) renvoient une valeur.
  - Sauf bloc `for` mais style impÃ©ratif, pas fonctionnel â†’ cas particuliers

**Remarques**

- ğŸ” `let a = expression` ressemble Ã  instruction de dÃ©claration/affectation
- ğŸ **DÃ©bogage** : quasi mÃªme expÃ©rience de dÃ©bogage pas Ã  pas qu'en Câ™¯

---

## Tout est expression â€¢ ConsÃ©quences

### Pas de `void`

- RemplacÃ© par le type `unit` ayant 1! valeur notÃ©e `()` ğŸ“
- `else` optionnel si `if` renvoie `unit`

### Pas de *early exit*

- Pas de `return` pour court-circuiter fonction âŒ
- Pas de `break` pour sortir d'une boucle `for/while` âŒ

---

### Pas de *early exit* - Solutions

- ğŸ’© `throw BreakException` (cf. [rÃ©ponse StackOverflow](https://stackoverflow.com/a/42018355/8634147))
- ğŸ˜• ImpÃ©ratif : `while (ok)` avec `ok` mutable
- âœ… Fonctionnel via fonction rÃ©cursive ğŸ“
  - *DÃ©cide ou non de continuer la "boucle" en s'appelant*

```fs
let rec firstOr defaultValue predicate list =
    match list with
    | [] -> defaultValue                                // ğŸ‘ˆ Sortie
    | x :: _ when predicate x -> x                      // ğŸ‘ˆ Sortie
    | _ :: rest -> firstOr defaultValue predicate rest  // ğŸ‘ˆ Appel rÃ©cursif â†’ continue

let test1 = firstOr -1 (fun x -> x > 5) [1]     // -1
let test2 = firstOr -1 (fun x -> x > 5) [1; 6]  // 6
```

---

### Pas de *early exit* - InconvÃ©nients

âš ï¸ Risque de `if` imbriquÃ©s *(+ difficile Ã  comprendre)*
ğŸ’¡ Solutions *(les mÃªmes qu'en Câ™¯)* :

- DÃ©composer en sous fonctions -- *"Do one thing"* de *Clean Code*
- Aplatir : rÃ©unir valeurs + *Pattern matching*
  - `match x, y with ...` oÃ¹ `x, y` est un tuple ğŸ“

---

# Typage, infÃ©rence et cÃ©rÃ©monie

Poids de la cÃ©rÃ©monie â‰  Force du typage
â†’ Cf. https://blog.ploeh.dk/2019/12/16/zone-of-ceremony/

| Lang | Force du typage                  | InfÃ©rence | CÃ©rÃ©monie |
|------|----------------------------------|-----------|-----------|
| JS   | Faible (dynamique)               | Ã—         | Faible    |
| Câ™¯   | Moyen (statique nominal)         | Faible    | Fort      |
| TS   | Fort (statique structurel + ADT) | Moyenne   | Moyen     |
| Fâ™¯   | Fort (statique nominal + ADT)    | Ã‰levÃ©e    | Faible    |

ADT = *Algebraic Data Types* = product types + sum types

---

# InfÃ©rence de type

Objectif : Typer explicitement le moins possible

- Moins de code Ã  Ã©crire ğŸ‘
- Compilateur garantit la cohÃ©rence
- IntelliSense aide le codage et la lecture
  - Importance du nommage pour lecture hors IDE âš ï¸

---

## InfÃ©rence de type en Câ™¯ : plutÃ´t faible

- DÃ©claration dâ€™une mÃ©thode â†’ paramÃ¨tres et retour âŒ
- Argument lambda : `list.Find(i => i == 5)` âœ”ï¸
- Variable, y.c. objet anonyme : `var o = new { Name = "John" }` âœ”ï¸
  - Sauf lambda : `Func<int, int> fn = (x: int) => x + 1;` â†’ KO avec `var`
    - ğŸ’¡ LanguageExt : `var fn = fun( (x: int) => x + 1 );` âœ”ï¸
    - ğŸ’¡ AutorisÃ© en Câ™¯ 10 : `var fn = f(x: int) => x + 1;` âœ”ï¸
- Initialisation d'un tableau : `new[] { 1, 2 }` âœ”ï¸
- Appel Ã  une mÃ©thode gÃ©nÃ©rique avec argument, sauf constructeur :
  - `Tuple.Create(1, "a")` âœ”ï¸
  - `new Tuple<int, string>(1, "a")` âŒ
- Câ™¯ 9 *target-typed expression* `StringBuilder sb = new();` âœ”ï¸

---

## InfÃ©rence en TypeScript - *The good parts* ğŸ‘

ğŸ‘‰ Code pur JavaScript *(modulo `as const` qui reste Ã©lÃ©gant)*

```ts
const obj1 = { a: 1 };                // { a: number }
const obj2 = Object.freeze({ a: 1 }); // { readonly a: number }
const obj3 = { a: 1 } as const;       // { readonly a: 1 }

const arr1 = [1, 2, null]; // (number | null)[]
const arr2 = [1, 2, 3]; // number[]
const arr3 = arr2.map(x => x * x); // âœ”ï¸ Pure lambda

// Type littÃ©ral
let s   = 'a';   // string
const a = 'a';   // "a"
```

---

## InfÃ©rence en TypeScript - Limites ğŸ›‘

```ts
// 1. Combinaison de littÃ©raux
const a  = 'a';   // "a"
const aa = a + a; // string (et pas "aa")

// 2. Tuple, immuable ou non
const tupleMutableKo = [1, 'a']; // âŒ (string | number)[]
const tupleMutableOk: [number, string] = [1, 'a'];

const tupleImmutKo = Object.freeze([1, 'a']); // âŒ readonly (string | number)[]
const tupleImmutOk = [1, 'a'] as const; // readonly [1, "a"]

// 3. ParamÃ¨tres d'une fonction â†’ gÃªne *Extract function* ğŸ˜”
// => Refacto de `arr2.map(x => x * x)` en `arr2.map(square)`
const square = x => x * x; // âŒ Sans annotation
//             ~ Parameter 'x' implicitly has an 'any' type.(7006)
const square = (x: number) => x * x; // (x: number) => number
```

---

## InfÃ©rence de type en Fâ™¯ : forte ğŸ’ª

MÃ©thode [Hindleyâ€“Milner](https://en.wikipedia.org/wiki/Hindley%E2%80%93Milner_type_system)

- Capable de dÃ©duire le type de variables, expressions et fonctions
  d'un programme dÃ©pourvu de toute annotation de type
- Se base sur implÃ©mentation et usage

```fs
let helper instruction source =
    if instruction = "inc" then // 1. `instruction` a mÃªme type que `"inc"` => `string`
      source + 1                // 2. `source` a mÃªme type que `1` => `int`
    elif instruction = "dec" then
      source - 1
    else
      source                    // 3. `return` a mÃªme type que `source` => `int`
```

---

### InfÃ©rence en Fâ™¯ - GÃ©nÃ©ralisation automatique

```fs
// Valeurs gÃ©nÃ©riques
let a = [] // 'a list

// Fonctions gÃ©nÃ©riques : 2 param 'a, renvoie 'a list
let listOf2 x y = [x; y]

// Idem avec 'a "comparable"
let max x y = if x > y then x else y
```

- â˜ En Fâ™¯, type gÃ©nÃ©rique prÃ©cÃ©dÃ© d'une apostrophe : `'a`
  - Partie `when 'a : comparison` = contraintes sur type
- ğŸ’¡ GÃ©nÃ©ralisation rend fonction utilisable dans \+ de cas ğŸ¥³
  - `max` utilisable pour 2 args de type `int`, `float`, `string`...
- â˜ D'oÃ¹ l'intÃ©rÃªt de laisser l'infÃ©rence plutÃ´t que d'annoter les types

---

### InfÃ©rence en Fâ™¯ - RÃ©solution statique

**ProblÃ¨me :** type infÃ©rÃ© \+ restreint qu'attendu ğŸ˜¯

```fs
let sumOfInt x y = x + y // Seulement int
```

- Juste `int` ? Pourtant `+` marche pour les nombres et les chaÃ®nes ğŸ˜•

**Solution :** fonction `inline`

```fs
let inline sum x y = x + y // Full generic: 2 params ^a ^b, retour ^c
```

- ParamÃ¨tres ont un type rÃ©solu statiquement = Ã  la compilation
  - NotÃ© avec un *caret* : `^a`
  - â‰  Type gÃ©nÃ©rique `'a`, rÃ©solu au runtime

---

### InfÃ©rence en Fâ™¯ - Limites

âš ï¸ Type d'un objet non infÃ©rable depuis ses mÃ©thodes

```fs
let helperKo instruction source = // ğŸ’¥ Error FS0072: Recherche d'un objet de type indÃ©terminÃ©...
    match instruction with
    | 'U' -> source.ToUpper()
    | _   -> source

let helper instruction (source: string) = [...] // ğŸ‘ˆ Annotation nÃ©cessaire

let info list = if list.Length = 0 then "Vide" else "..." // ğŸ’¥ Error FS0072...
let info list = if List.length list = 0 then "Vide" else $"{list.Length} Ã©lÃ©ments" // ğŸ‘Œ
```

â˜ D'oÃ¹ l'intÃ©rÃªt de l'approche FP *(fonctions sÃ©parÃ©es des donnÃ©es)*
Â Â Â *Vs* approche OO *(donnÃ©es + mÃ©thodes ensemble dans objet)*

---

### InfÃ©rence en Fâ™¯ - Gestion de la prÃ©cÃ©dence

âš ï¸ Ordre des termes impacte infÃ©rence

```fs
let listKo = List.sortBy (fun x -> x.Length) ["three"; "two"; "one"]
  // ğŸ’¥ Error FS0072: Recherche d'un objet de type indÃ©terminÃ©...
```

ğŸ’¡ **Solutions**

1. Inverser ordre des termes en utilisant le *pipe*

```fs
let listOk = ["three"; "two"; "one"] |> List.sortBy (fun x -> x.Length)
```

2. Utiliser fonction plutÃ´t que mÃ©thode

```fs
let listOk' = List.sortBy String.length ["three"; "two"; "one"]
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 4.

## ğŸ” Quiz

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 1. Qui est le papa de Fâ™¯ ?

## **A.** Anders Hejlsberg

## **B.** Don Syme

## **C.** Scott Wlaschin

â± 10â€™â€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# 1. Qui est le papa de Fâ™¯ ?

## **A.** Anders Hejlsberg âŒ

Papa de Câ™¯ et de TypeScript

## **B.** Don Syme âœ…

![social-network w:30](../themes/d-edge/pictos/twitter-round.png) [dsymetweets](https://twitter.com/dsymetweets) â€¢ ğŸ¥ [Fâ™¯ Code I Love](https://www.youtube.com/watch?v=1AZA1zoP-II)

## **C.** Scott Wlaschin âŒ

Auteur du blog [Fâ™¯ for Fun and Profit](https://fsharpforfunandprofit.com/), mine d'or pour Fâ™¯

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 2. Comment se nomme l'opÃ©rateur `::` ?

## **A.** Append

## **B.** Concat

## **C.** Cons

â± 10â€™â€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# 2. Comment se nomme l'opÃ©rateur `::` ?

## **A.** Append âŒ

`List.append` : concatÃ¨ne 2 listes

## **B.** Concat âŒ

`List.concat` : concatÃ¨ne un ensemble de listes

## **C.** Cons âœ…

`newItem :: list` est la maniÃ¨re la + rapide de crÃ©er une nouvelle liste avec un nouvel Ã©lÃ©ment en tÃªte : `1 :: [2; 3]` renvoie `[1; 2; 3]`.

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 3. Cherchez l'intrus

## **A.** `let a = "a"`

## **B.** `let a () = "a"`

## **C.** `let a = fun () -> "a"`

â± 15â€™â€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# 3. Cherchez l'intrus

B et C sont des fonctions, A est juste une `string`.

## **A.** `let a = "a"` âœ…

## **B.** `let a () = "a"` âŒ

## **C.** `let a = fun () -> "a"` âŒ

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 4. Quelle ligne ne compile pas ?

```fs
let evens list =
    let isEven x =
    x % 2 = 0
    List.filter isEven list
```

### Ligne **1.** `let evens list =`

### Ligne **2.** `let isEven x =`

### Ligne **3.** `x % 2 = 0`

### Ligne **4.** `List.filter isEven list`

â± 15â€™â€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# 4. Quelle ligne ne compile pas ?

### Ligne **3.** `x % 2 = 0`

ProblÃ¨me d'indentation

```fs
let evens list =
    let isEven x =
    x % 2 = 0       // ğŸ‘ˆ Manque une indentation ici
    List.filter isEven list
```

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 5. Comment se nomme l'opÃ©rateur `|>` ?

## **A.** Compose

## **B.** Chain

## **C.** Pipeline

## **D.** Pipe

â± 10â€™â€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# 5. Comment se nomme l'opÃ©rateur `|>` ?

## **A.** Compose âŒ

L'opÃ©rateur de composition est `>>` ğŸ“

## **B.** Chain âŒ

## **C.** Pipeline âŒ

## **D.** Pipe âœ…

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# 6. Quelle expression compile ?

## **A.** `a == "a" && b != "*"`

## **B.** `a == "a" && b <> "*"`

## **C.** `a = "a" && b <> "*"`

## **D.** `a = "a" && b != "*"`

â± 15â€™â€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_solution.png)

# 6. Quelle expression compile ?

â˜ En Fâ™¯, les opÃ©rateurs d'Ã©galitÃ© et d'inÃ©galitÃ© sont respectivement `=` et `<>`.

## **A.** `a == b && b != ""` âŒ

## **B.** `a == b && b <> ""` âŒ

## **C.** `a = b && b <> ""` âœ…

## **D.** `a = b && b != ""` âŒ

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 5.

## LeÂ Â Â  RÃ©capâ€™

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_note.png)

# RÃ©capâ€™

- Syntaxe du Fâ™¯
  - AperÃ§u gÃ©nÃ©ral, dÃ©jÃ  copieux
  - Nous permettra de nous focaliser sur des dÃ©tails
- Concepts engrammÃ©s dans Fâ™¯
  - Curryfication, application partielle
  - Â« Une expression sinon rien ! Â»
  - InfÃ©rence de type

---

# ComplÃ©ment

https://blog.ploeh.dk/2015/08/17/when-x-y-and-z-are-great-variable-names

En Fâ™¯, les fonctions et variables ont souvent des noms courts : `f`, `x` et `y`.
Mauvais nommage ? Non, pas dans les cas suivants :

- Fonction hyper gÃ©nÃ©rique â†’ paramÃ¨tres avec nom gÃ©nÃ©rique
- PortÃ©e courte â†’ code \+ lisible avec nom court que nom long

---

<!-- _class: end invert lead-->

# Merci ğŸ™
