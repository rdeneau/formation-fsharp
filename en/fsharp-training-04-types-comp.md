---
marp: true
html: true
theme: 'd-edge'
title: 'Fâ™¯ Training â€¢ Types : ComplÃ©ments'
footer: 'Fâ™¯ Training â€¢ Types : ComplÃ©ments'
paginate: true
---

<!-- _class: title invert -->

# Fâ™¯ Training

## *ComplÃ©ments sur les types*

### 2025 April

---

<!-- _class: toc agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

1. Type `unit`
2. GÃ©nÃ©riques
3. Contraintes sur paramÃ¨tres de type
4. Type flexible
5. UnitÃ©s de mesure
6. *Casting* et conversion

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_objectifs.png)

# 1.

## Type `unit`

---

# Type `unit` : pourquoi ?

> **Contrainte :** l'Ã©valuation d'une expression doit produire une valeur.

Quid des expressions ne produisant pas de valeur significative ?
â†’ `void` ? Non, `void` en Câ™¯, Java n'est pas une valeur !
â†’ `null` ? Non, `null` n'est pas un type en .NET ! *(â‰  TypeScript)*

Il faut donc un type spÃ©cifique, avec une seule valeur signifiant par convention :
Â« Valeur non significative, Ã  ignorer. Â»

â†’ Ce type s'appelle `unit`.
â†’ Sa valeur est notÃ©e `()`.

---

# Type `unit` et fonctions

Fonction `unit -> 'T` ne prend pas de paramÃ¨tre.
â†’ Ex : `System.DateTime.Now` *(fonction cachÃ©e derriÃ¨re une propriÃ©tÃ©)*

Fonction `'T -> unit` ne renvoie pas de valeur.
â†’ Ex : `printf`

ğŸ‘‰ Fonctions impliquant un **effet de bord** !

---

# Type `unit` : ignorer une valeur

Fâ™¯ n'est pas une langage fonctionnel pur, sans effet de bord.
Mais il encourage l'Ã©criture de programme fonctionnel pur.

ğŸ‘‰ **RÃ¨gle :** Toute expression produisant une valeur doit Ãªtre utilisÃ©e.
â†’ Sinon, le compilateur Ã©met un warning *(sauf en console FSI).*
â†’ Exception: `()` est la seule valeur que le compilateur autorise Ã  ignorer.

â˜ **Avertissement :** ignorer une valeur est gÃ©nÃ©ralement un *code smell* en FP.

ğŸ‘‰ Une expression avec effet de bord doit le signaler avec type de retour `unit`

---

# Type `unit` : fonction `ignore`

> â“ Comment *(malgrÃ© tout)* ignorer la valeur produit par une expression ?

Avec la fonction `ignore` :
â†’ Prend un paramÃ¨tre d'entrÃ©e ignorÃ©, "avalÃ©"
â†’ Renvoie `unit`

```fs
let inline ignore _ = ()
// Signature: 'T -> unit
```

Usage : `expression |> ignore`

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_cooptation.png)

# 2.

## LesÂ Â Â Â Â Â  gÃ©nÃ©riques

---

# GÃ©nÃ©riques

Fonctions et types peuvent Ãªtre gÃ©nÃ©riques, avec \+ de flexibilitÃ© qu'en Câ™¯.

Par dÃ©faut, gÃ©nÃ©ricitÃ© **implicite**
â†’ InfÃ©rÃ©e
â†’ Voire gÃ©nÃ©ralisÃ©e, grÃ¢ce Ã  Â« gÃ©nÃ©ralisation automatique Â»

Sinon, gÃ©nÃ©ricitÃ© peut Ãªtre explicite ou rÃ©solue statiquement.

âš ï¸ Notations diffÃ©rentes :
â†’ `'T` : paramÃ¨tre de type gÃ©nÃ©rique
â†’ `^T` : paramÃ¨tre de type rÃ©solu statiquement *(SRTP)*

---

# GÃ©nÃ©ricitÃ© implicite

```fs
module ListHelper =
    let singleton x = [x]
    // val singleton : x:'a -> 'a list

    let couple x y = [x; y]
    // val couple : x:'a -> y:'a -> 'a list
```

ğŸ‘‰ **Explications :**

â€¢ `singleton` : son argument `x` est quelconque â†’ type gÃ©nÃ©rique `'a`
â†’ GÃ©nÃ©ralisation automatique

â€¢ `couple` : ses 2 arguments `x` et `y` doivent Ãªtre du mÃªme type
pour pouvoir Ãªtre dans une liste â†’ InfÃ©rence

---

# GÃ©nÃ©ricitÃ© explicite

```fs
let print2 x y = printfn "%A, %A" x y
// val print2 : x:'a -> y:'b -> unit
```

â†’ InfÃ©rence de la gÃ©nÃ©ricitÃ© de `x` et `y` ğŸ‘

â“ **Comment indiquer que `x` et `y` doivent avoir le mÃªme type ?**

â†’ Besoin de l'indiquer explicitement :

```fs
let print2<'T> (x: 'T) (y: 'T) = printfn "%A, %A" x y
// val print2 : x:'T -> y:'T -> unit
```

---

# GÃ©nÃ©ricitÃ© explicite - Forme inline

ğŸ’¡ **Astuce :** la convention en `'x` permet ici d'Ãªtre \+ concis :

```fs
// AVANT
let print2<'T> (x: 'T) (y: 'T) = printfn "%A, %A" x y

// APRES
let print2 (x: 'T) (y: 'T) = printfn "%A, %A" x y
```

---

# GÃ©nÃ©ricitÃ© explicite - Type

La dÃ©finition des types gÃ©nÃ©riques est explicite :

```fs
type Pair = { Item1: 'T ; Item2: 'T }
// ğŸ’¥                ~~          ~~
// Error FS0039: Le paramÃ¨tre de type `'T` n'est pas dÃ©fini.

// âœ… Records et unions avec 1 ou 2 paramÃ¨tres de type
type Pair<'T> = { Item1: 'T; Item2: 'T }

type Tuple<'T, 'U> = { Item1: 'T; Item2: 'U }

type Option<'T> = None | Some of 'T

type Result<'TOk, 'TErr> =
    | Ok of 'TOk
    | Error of 'TErr
```

---

# GÃ©nÃ©ricitÃ© ignorÃ©e

Le *wildcard* `_` permet de remplacer un paramÃ¨tre de type ignorÃ© :

```fs
let printSequence (sequence: seq<'T>) = sequence |> Seq.iteri (printfn "%i: %A")
// Versus
let printSequence (sequence: seq<_>) = ...
```

Encore \+ utile avec type flexibleğŸ“ :

```fs
let tap action (sequence: 'TSeq when 'TSeq :> seq<_>) =
    sequence |> Seq.iteri action
    sequence
// action:(int -> 'a -> unit) -> sequence:'TSeq -> 'TSeq when 'TSeq :> seq<'a>

// Versus
let tap action (sequence: #seq<_>) = ...
```

---

# SRTP

Fâ™¯ propose deux catÃ©gories de types de paramÃ¨tre :
â†’ `'X` : type de paramÃ¨tre gÃ©nÃ©rique *(vus jusqu'ici)*
â†’ `^X` : type de paramÃ¨tre rÃ©solu statiquement *(par le compilateur Fâ™¯)*

â˜ **SRTP :** abrÃ©viation frÃ©quente de *Statically Resolved Type Parameter*

---

<!-- _footer: '' -->

# SRTP - Le pourquoi

Sans SRTP :

```fs
let add x y = x + y
// val add : x:int -> y:int -> int
```

â†’ InfÃ©rence du type `int` pour `x` et `y`, sans gÃ©nÃ©ralisation (aux `float` par ex.) !

Avec SRTP, de pair avec fonction `inline` :

```fs
let inline add x y = x + y
// val inline add : x: ^a -> y: ^b -> ^c
//    when ( ^a or ^b ) : (static member (+) : ^a * ^b -> ^c)
//    â˜ Contrainte de membre ğŸ“

let x = add 1 2       // âœ… val x: int = 3
let y = add 1.0 2.0   // âœ… val y: float = 3.0
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_software.png)

# 3.

## Contraintes sur paramÃ¨tres de type

---

# Contraintes

MÃªme principe qu'en Câ™¯ avec quelques diffÃ©rences :

| Contrainte  | Syntaxe Fâ™¯                     | Syntaxe Câ™¯                      |
|-------------|--------------------------------|---------------------------------|
| Mots clÃ©s   | `when xxx and yyy`             | `where xxx, yyy`                |
| Emplacement | Juste aprÃ¨s type :             | Fin de ligne :                  |
|             | `fn (arg: 'T when 'T ...)`     | `Method<T>(arg: T) where T ...` |
|             | Dans chevrons :                |                                 |
|             | `fn<'T when 'T ...> (arg: 'T)` |                                 |

---

<!-- _footer: '' -->

# Contraintes : vue d'ensemble

| Contrainte              | Syntaxe Fâ™¯               | Syntaxe Câ™¯               |
|-------------------------|--------------------------|--------------------------|
| Type de base            | `'T :> my-base`          | `T : my-base`            |
| Type valeur             | `'T : struct`            | `T : struct`             |
| Type rÃ©fÃ©rence          | `'T : not struct`        | `T : class`              |
| Type rÃ©fÃ©rence nullable | `'T : null`              | `T : class?`             |
| Constructeur sans param | `'T : (new: unit -> 'T)` | `T : new()`              |
| Ã‰numÃ©ration             | `'T : enum<my-enum>`     | `T : System.Enum`        |
| Comparaison             | `'T : comparison`        | â‰ƒ `T : System.IComparable` |
| Ã‰galitÃ©                 | `'T : equality`          | *(pas nÃ©cessaire)*       |
| Membre explicite        | `^T : member-signature`  | *(pas d'Ã©quivalent)*     |

---

# Contraintes de type

Pour forcer le type de base : classe mÃ¨re ou interface

```fs
let check<'TError when 'TError :> System.Exception> condition (error: 'TError) =
    if not condition then raise error
```

â†’ Ã‰quivalent en C# :

```cs
static void check<TError>(bool condition, TError error) where TError : System.Exception
{
    if (!condition) throw error;
}
```

ğŸ’¡ Syntaxe alternative : `let check condition (error: #System.Exception)`
â†’ Cf. *Type flexible*ğŸ“

---

# Contrainte d'enum

```fs
open System

let getValues<'T when 'T : enum<int>>() =
    Enum.GetValues(typeof<'T>) :?> 'T array

type ColorEnum = Red = 1 | Blue = 2
type ColorUnion = Red | Blue

let x = getValues<ColorEnum>()   // [| Red; Blue |]
let y = getValues<ColorUnion>()  // ğŸ’¥ Exception ou erreur de compilation (1)
```

(1) La contrainte `when 'T : enum<int>` permet :
â€¢ D'Ã©viter la `ArgumentException` au runtime *(Type provided must be an Enum)*
â€¢ Au profit d'une erreur dÃ¨s la compilation *(The type 'ColorUnion' is not an enum)*

---

# Contrainte de comparaison

Syntaxe : `'T : comparison`

Indique que le type `'T` doit :
â€¢ soit implÃ©menter `IComparable` (1)
â€¢ soit Ãªtre un collection d'Ã©lÃ©ments comparables (2)

â˜ **Notes :**

1. `'T : comparison` > `'T : IComparable` â—
2. `'T : comparison` â‰  `'T : IComparable<'T>` â—
3. Pratique pour mÃ©thodes gÃ©nÃ©riques `compare` ou `sort` ğŸ’¡

---

# Contrainte de comparaison - Exemple

```fs
let compare (x: 'T) (y: 'T when 'T : comparison) =
    if   x < y then -1
    elif x > y then +1
    else 0

// Comparaison de nombres et de chaÃ®nes
let x = compare 1.0 2.0  // -1
let y = compare "a" "A"  // +1

// Comparaison de listes d'entier
let z = compare [ 1; 2; 3 ] [ 2; 3; 1 ]  // -1

// Comparaison de listes de fonctions
let a = compare [ id; ignore ] [ id; ignore ]
// ğŸ’¥             ~~
// error FS0001: Le type '('a -> 'a)' ne prend pas en charge la contrainte 'comparison'.
// Par exemple, il ne prend pas en charge l'interface 'System.IComparable'
```

---

# Contrainte de membre explicite

> **Pb :** Comment indiquer qu'un objet doit disposer d'un certain membre ?

â€¢ ManiÃ¨re classique en .NET : typage nominal
â†’ Contrainte spÃ©cifiant type de base (interface ou classe parent)

â€¢ Alternative en Fâ™¯ : typage structurel *(a.k.a duck-typing du TypeScript)*
â†’ Contrainte de membre explicite
â†’ UtilisÃ©e avec SRTP *(statically resolved type parameter)*

---

# Contrainte de membre explicite (2)

```fs
let inline add (value1 : ^T when ^T : (static member (+) : ^T * ^T -> ^T), value2: ^T) =
    value1 + value2

let x = add (1, 2)
// val x : int = 3
let y = add (1.0, 2.0)
// val y : float = 3.0
```

âš–ï¸ Pour et contre :

- ğŸ‘ Permet de rendre code gÃ©nÃ©rique pour types hÃ©tÃ©rogÃ¨nes
- ğŸ‘ Difficile Ã  lire, Ã  maintenir. Ralentit la compilation
- ğŸ‘‰ Ã€ utiliser dans une librairie, pas pour modÃ©liser un domaine

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_agilite.png)

# 4.

## Type flexible

---

# Type flexible - Besoin (1)

Lors de la crÃ©ation de certaines fonctions gÃ©nÃ©riques, il faut spÃ©cifier
qu'un paramÃ¨tre de type est un sous-type d'un certain autre type.

â†’ Illustration grÃ¢ce Ã  un exemple :

```fs
open System.Collections.Generic

// V1
let add item (collection: ICollection<_>) =
    collection.Add item
    collection

let a = List([1..3])    // List<int>
let b = a |> add 4      // ICollection<int> != List<int> â—
```

---

# Type flexible - Besoin (2)

Solutions :
â†’ **V2 :** indiquer une **contrainte de type**
â†’ **V3 :** indiquer un **type flexible**

```fs
(* V1  âŒ *)  let add item (collection: ICollection<_>) = â€¦
(* V2a ğŸ˜– *)  let add<'t, 'u when 'u :> ICollection<'t>> (item: 't) (collection: 'u) : 'u = â€¦
(* V2b ğŸ˜• *)  let add (item: 't) (collection: 'u when 'u :> ICollection<'t>) : 'u = â€¦
(* V3  âœ… *)  let add item (collection: #ICollection<_>) = â€¦
```

âš–ï¸ **Bilan :**
â†’ **V2a** : syntaxe similaire au Câ™¯ â†’ verbeux et pas trÃ¨s lisible ! ğŸ˜–
â†’ **V2b** : version amÃ©liorÃ©e en Fâ™¯ â†’ \+ lisible mais encore un peu verbeux ! ğŸ˜•
â†’ **V3**Â Â  : syntaxe proche de **V1** â†’ concision Â« dans l'esprit Fâ™¯ Â» âœ…

---

# Type flexible - Autres usages (1)

Faciliter l'usage de la fonction sans avoir besoin d'un *upcast*

```fs
let join separator (generate: unit -> seq<_>) =
    let items = System.String.Join (separator, generate() |> Seq.map (sprintf "%A"))
    $"[ {items} ]"

let s1 = join ", " (fun () -> [1..5])               // ğŸ’¥ Error FS0001
let s2 = join ", " (fun () -> [1..5] :> seq<int>)   // ğŸ˜• Marche mais pÃ©nible Ã  Ã©crire
```

Avec un type flexible :

```fs
let join separator (generate: unit -> #seq<_>) =
    // [...]

let s1 = join ", " (fun () -> [1..5])               // âœ… Marche naturellement
```

---

# Type flexible - Autres usages (2)

Dans l'exemple ci-dessous, `items` est infÃ©rÃ© avec la bonne contrainte :

```fs
let tap f items =
    items |> Seq.iter f
    items
// val tap : f:('a -> unit) -> items:'b -> 'b when 'b :> seq<'a>
```

ğŸ’¡ Quid de faciliter la lecture du code avec un type flexible ?

```fs
let tap f (items: #seq<_>) =
    // [...]
```

---

# Type flexible - Autres usages (3)

âš ï¸ Astuce prÃ©cÃ©dente ne marche pas toujours !

```fs
let max x y =
    if x > y then x else y
// val max : x:'a -> y:'a -> 'a when 'a : comparison
```

`x` et `y` doivent satisfaire 2 conditions

1. `'a : comparison` â‰ƒ les types de `x` et `y` implÃ©mentent `IComparable`
   â†’ `(x: #IComparable) (y: #IComparable)` ?
2. `x:'a` et `y:'a` â†’ `x` et `y` ont le mÃªme type
   â†’ Non exprimable sous forme de type flexible ! ğŸ˜

---

# Type flexible - RÃ©sumÃ©

Type flexible
â†’ UtilisÃ© dans la dÃ©claration de certaine fonction gÃ©nÃ©rique
â†’ Indique qu'un paramÃ¨tre de type est un sous-type d'un type spÃ©cifiÃ©
â†’ Sucre syntaxique au format `#super-type`
â†’ Ã‰quivalent de `'T when 'T :> super-type`

Autres usages :
â†’ Faciliter l'usage de la fonction sans avoir besoin d'un *upcast*
â†’ Faciliter la lecture du code ?

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_atelier.png)

# 5.

## UnitÃ©s de mesure

---

# UnitÃ©s de mesure : prÃ©sentation

Moyen d'associer un **type numÃ©rique** Ã  une unitÃ© de mesure
â†’ DurÃ©e : `s` *aka* `second`
â†’ Masse : `kg`
â†’ Longueur : `m` *aka* `metre`
â†’ ...

Les unitÃ©s sont **vÃ©rifiÃ©es Ã  la compilation**
â†’ EmpÃªche d'ajouter des ğŸ¥¦ Ã  des ğŸ¥• â†’ **code \+ sÃ»r**
â†’ Permet de les **combiner** : Vitesse = Distance / DurÃ©e â†’ `m/s`

---

# UnitÃ©s de mesure : dÃ©claration

Syntaxe basÃ©e sur attribut `[<Measure>]`

```fs
// ğŸ‘‰ Nouvelles unitÃ©s "from scratch"
[<Measure>] type kilogram
[<Measure>] type metre
[<Measure>] type second

// ğŸ‘‰ Alias d'unitÃ©s existantes
[<Measure>] type kg = kilogram
[<Measure>] type m  = metre
[<Measure>] type s  = second

// ğŸ‘‰ Combinaison d'unitÃ©s existantes
[<Measure>] type Hz = / s
[<Measure>] type N = kg m / s^2
```

---

# UnitÃ©s de mesure : SI

Les unitÃ©s du **SystÃ¨me International** sont prÃ©dÃ©finies dans les namespaces :

`FSharp.Data.UnitSystems.SI.UnitNames` :
â†’ `ampere`, `hertz`, `joule`, `kelvin`, `kilogram`, `metre`...
â†’ https://fsharp.github.io/fsharp-core-docs/reference/fsharp-data-unitsystems-si-unitnames.html

`FSharp.Data.UnitSystems.SI.UnitSymbols`
â†’ `A`, `Hz`, `J`, `K`, `kg`, `m`...
â†’ https://fsharp.github.io/fsharp-core-docs/reference/fsharp-data-unitsystems-si-unitsymbols.html

---

# UnitÃ©s de mesure : symbole

ğŸ’¡ **Astuce :** utilisation des *doubles back ticks*

```fs
[<Measure>] type ``Î©``
[<Measure>] type ``Â°C``
[<Measure>] type ``Â°F``

let waterFreezingAt = 0.0<``Â°C``>
// val waterFreezingAt : float<Â°C> = 0.0

let waterBoilingAt = 100.0<``Â°C``>
// val waterBoilingAt : float<Â°C> = 100.0
```

---

# UnitÃ©s de mesure : usage

```fs
// UnitÃ© dÃ©finie en annotant le nombre
let distance = 1.0<m>               // val distance : float<m> = 1.0
let time = 2.0<s>                   // val time : float<s> = 2.0

// UnitÃ© combinÃ©e, infÃ©rÃ©e
let speed = distance / time         // val speed : float<m/s> = 0.5

// UnitÃ© combinÃ©e, dÃ©finie par annotation
let [<Literal>] G = 9.806<m/s^2>    // val G : float<m/s ^ 2> = 9.806

// Comparaison
let sameFrequency = (1<Hz> = 1</s>)   // âœ… true
let ko1 = (distance = 1.0)            // âŒ Error FS0001: IncompatibilitÃ© de type.
                                      // ğŸ’¥ Attente de 'float<m>' mais obtention de 'float'
let ko2 = (distance = 1<m>)           // ğŸ’¥ Attente de 'float<m>' mais obtention de 'int<m>'
let ko3 = (distance = time)           // ğŸ’¥ Attente de 'float<m>' mais obtention de 'float<s>'

```

---

# UnitÃ©s de mesure : conversion

- Facteur multiplicatif avec une unitÃ© `<target/source>`
- Fonction de conversion utilisant ce facteur

```fs
[<Measure>] type m
[<Measure>] type cm
[<Measure>] type km

module Distance =
    let toCentimeter (x: float<m>) = // (x: float<m>) -> float<cm>
        x * 100.0<cm/m>

    let toKilometer (x: float<m>) = // (x: float<m>) -> float<km>
        x / 1000.0<m/km>

let a = Distance.toCentimeter 1.0<m>   // val a : float<cm> = 100.0
let b = Distance.toKilometer 500.0<m>  // val b : float<km> = 0.5
```

---

# UnitÃ©s de mesure : conversion (2)

Exemple 2 : degrÃ© Celsius (Â°C) â†’ degrÃ© Fahrenheit (Â°F)

```fs
[<Measure>] type ``Â°C``
[<Measure>] type ``Â°F``

module Temperature =
    let toFahrenheit ( x: float<``Â°C``> ) =  // (x: float<Â°C>) -> float<Â°F>
        9.0<``Â°F``> / 5.0<``Â°C``> * x + 32.0<``Â°F``>

let waterFreezingAt = Temperature.toFahrenheit 0.0<``Â°C``>
// val waterFreezingAt : float<Â°F> = 32.0

let waterBoilingAt = Temperature.toFahrenheit 100.0<``Â°C``>
// val waterBoilingAt : float<Â°F> = 212.0
```

---

# UnitÃ©s de mesure : ajouter/supprimer

Ajouter une unitÃ© Ã  un nombre nu :
â†’ âœ… `number * 1.0<target>`

Supprimer l'unitÃ© d'un nombre `number : float<source>` :
â†’ âœ… `number / 1.0<source>`
â†’ âœ… `float number`

CrÃ©er une liste de nombres avec unitÃ© :
â†’ âœ… `[1<m>; 2<m>; 3<m>]`
â†’ âŒ `[1<m>..3<m>]` *(un range nÃ©cessite des nombres nus)*
â†’ âœ… `[ for i in [1..3] -> i * 1<m> ]`

---

# UnitÃ©s de mesure : effacÃ©es au runtime â—

Les unitÃ©s de mesure sont propres au compilateur Fâ™¯.
â†’ Elles ne sont pas compilÃ©es en .NET

---

# Type avec unitÃ© gÃ©nÃ©rique

Besoin de distinguer d'un type gÃ©nÃ©rique classique
â†’ Annoter l'unitÃ© gÃ©nÃ©rique avec `[<Measure>]`

```fs
type Point<[<Measure>] 'u, 'data> =
    { X: float<'u>; Y: float<'u>; Data: 'data }

let point = { X = 10.0<m>; Y = 2.0<m>; Data = "abc" }
// val point : Point<m, string> = { X = 10.0; Y = 2.0; Data = "abc" }
```

---

# UnitÃ© pour primitive non numÃ©rique

ğŸ’¡ Nuget [FSharp.UMX](https://github.com/fsprojects/FSharp.UMX) *(Unit of Measure Extension)*
â†’ Pour autres primitives `bool`, `DateTime`, `Guid`, `string`, `TimeSpan`

```fs
open System

#r "nuget: FSharp.UMX"
open FSharp.UMX

[<Measure>] type ClientId
[<Measure>] type OrderId

type Order = { Id: Guid<OrderId>; ClientId: string<ClientId> }

let order = { Id = % Guid.NewGuid(); ClientId = % "RDE" }
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_webinar.png)

# 6.

## *Casting* et conversion

---

# Conversion de nombre

Types numÃ©riques :
â€¢ Entier : `byte`, `int16`, `int`/`int32`, `int64`
â€¢ Flottant : `float`/`double` (64b), `single` (32b), `decimal`
â€¢ Autres : `char`, `enum`

Conversion entre eux **explicite**
â†’ Fonction de mÃªme nom que le type cible

```fs
let x = 1               // val x : int = 1
let y = float x         // val y : float = 1.0
let z = decimal 1.2     // val z : decimal = 1.2M
let s = char 160        // val s : char = ' '
```

---

# Conversion entre nombre et enum

Il faut utiliser le nom de l'enum pour convertir un nombre en enum :
â€¢ Soit en paramÃ¨tre gÃ©nÃ©rique de la fonction `enum<my-enum>`, â‘ 
â€¢ Soit par annotation de type et la fonction `enum` sans paramÃ¨tre gÃ©nÃ©rique. â‘¡

L'opÃ©ration inverse utilise la fonction `int`. â‘¢

```fs
type Color =
    | Red   = 1
    | Green = 2
    | Blue  = 3

let color1 = enum<Color> 1      // (1)  val color1 : Color = Red
let color2 : Color = enum 2     // (2)  val color2 : Color = Green
let value3 = int Color.Blue     // (3)  val c1 : int = 3
```

---

# Casting d'objets

â†’ S'utilise pour un objet dont le type appartient Ã  une hiÃ©rarchie

| FonctionnalitÃ© | PrÃ©cision             | SÃ»r        | OpÃ©rateur | Fonction   |
|----------------|-----------------------|------------|-----------|------------|
| *Upcast*       | Vers type de base     | âœ… Oui      | `:>`      | `upcast`   |
| *Downcast*     | Vers type dÃ©rivÃ©      | âŒ Non (\*) | `:?>`     | `downcast` |
| Test de type   | Dans pattern matching | âœ… Oui      | `:?`      |            |

(\*) Le *downcast* peut Ã©chouer â†’ risque de `InvalidCastException` au runtime âš ï¸

---

# Upcasting d'objets

En Câ™¯ : *upcast* peut gÃ©nÃ©ralement Ãªtre implicite

```cs
object o = "abc";
```

En Fâ™¯ : *upcast* peut parfois Ãªtre implicite
mais en gÃ©nÃ©ral doit Ãªtre **explicite**, avec opÃ©rateur `:>`

```fs
let o1: obj = "abc"             // Implicite ğŸ’¥ Error FS0001...
let o2 = "abc" :> obj           // Explicite ğŸ‘Œ

let toObject x : obj = x        // obj -> obj
let o3 = "abc" |> toObject      // Implicite ğŸ‘Œ

let l1: obj list = [1; 2; 3]    // Implicite ğŸ‘Œ
let l2: int seq = [1; 2; 3]     // Implicite ğŸ’¥ Error FS0001...
```

---

# Upcasting d'objets (2)

RÃ¨gles Ã©largies/assouplies en Fâ™¯ 6

â†’ Exemple : upcast implicite de `int list` vers `int seq`

```fsharp
let l2: int seq = [1; 2; 3]  // ğŸ‘Œ OK en Fâ™¯ 6
```

---

<!-- _footer: '' -->

# Casting d'objets - Exemple

```fs
type Base() =
    abstract member F : unit -> string
    default _.F() = "F Base"

type Derived1() =
    inherit Base()
    override _.F() = "F Derived1"

type Derived2() =
    inherit Base()
    override _.F() = "F Derived2"

let d1 = Derived1()
let b1 = d1 :> Base         // val b1 : Base
let b1': Base = upcast d1   // val b1' : Base

let t1 = b1.GetType().Name  // val t1 : string = "Derived1"

let one = box 1  // val one : obj = 1
```

---

<!-- _footer: '' -->

# Casting d'objets - Exemple (2)

```fs
let d1' = b1 :?> Derived1           // val d1' : Derived1
let d2' = b1 :?> Derived2           // ğŸ’¥ System.InvalidCastException

let d1'': Derived1 = downcast b1    // val d1'' : Derived1

let f (b: Base) =
    match b with
    | :? Derived1 as derived1 -> derived1.F()
    | :? Derived2 as derived2 -> derived2.F()
    | _ -> b.F()

let x = f b1            // val x : string = "F Derived1"
let y = b1.F()          // val y : string = "F Derived1"
let z = f (Base())      // val z : string = "F Base"
let a = f (Derived2())  // val a : string = "F Derived2"
// â˜ï¸      ^^^^^^^^  Upcast implicite
```

---

# Test de type

L'opÃ©rateur `:?` rÃ©alise un test de type et renvoie un boolÃ©en.

```fs
let isDerived1 = b1 :? Derived1   // val isDerived1 : bool = true
let isDerived2 = b1 :? Derived2   // val isDerived2 : bool = false
```

â˜ï¸ Il faut *boxer* un nombre pour tester son type :

```fs
let isIntKo = 1 :? int          // ğŸ’¥ Error FS0016
let isInt32 = (box 1) :? int    // val isInt32 : bool = true
let isFloat = (box 1) :? float  // val isFloat : bool = false
```

ğŸ’¡ `box x` â‰ƒ `x :> obj`

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 7.

## LeÂ Â Â  RÃ©capâ€™

---

# RÃ©cap' - Type `unit`

Instance unique `()`

UtilitÃ© avec expressions :
â†’ LA valeur non significative Ã  renvoyer
â†’ Remplace `void`

Dans signature de fonctions :
â†’ Indique effets de bord

Appel d'une fonction sans paramÃ¨tres
â†’ MÃªme syntaxe qu'en Câ™¯ : `my-func()`

---

# RÃ©cap' - GÃ©nÃ©riques

GÃ©nÃ©ricitÃ© de fonctions et de types

GÃ©nÃ©ricitÃ© implicite
â†’ BasÃ©e sur infÃ©rence de type + gÃ©nÃ©ralisation automatique

GÃ©nÃ©ricitÃ© explicite
â†’ Annotation `'T`
â†’ Inline (`x: 'T`) ou globale (`my-func<'T> (x: 'T) = ...`, `type Abc<'T> = ...`)
â†’ Wilcard `_` pour accepter n'importe quel paramÃ¨tre de type : `seq<_>`

GÃ©nÃ©ricitÃ© statique
â†’ Annotation `^T` : paramÃ¨tre de type rÃ©solu statiquement *(SRTP)*
â†’ Typage structurel : puissant mais difficile Ã  lire et lent Ã  la compilation

---

# RÃ©cap' - Contraintes

Mots-clÃ©s `when`, `and` â‰  `where` en Câ™¯

Plusieurs familles de contraintes :
â†’ Type valeur `struct` ou rÃ©fÃ©rence `not struct` ou nullable `null`
â†’ Constructeur `'T : (new: unit -> 'T)`
â†’ Type de base `'T :> my-base` ou `#my-base` *(type flexible)*
â†’ Ã‰numÃ©ration `'T : enum<int>`
â†’ Ã‰galitÃ© `'T : equality` et comparaison `'T : comparison` structurelles
â†’ Membre explicite pour SRTP : `^T : member-signature`

---

# RÃ©cap' - UnitÃ© de mesure

DÃ©finition `[<Measure>] type kg`

Usage `let x = 1.0<kg>`

Apporte *type safety*
â†’ Mais qu'en Fâ™¯, effacÃ©es Ã  la compilation

Performant *(vs Single-Case Union)*

LimitÃ©e aux types numÃ©riques
â†’ Ã‰tendue aux autres primitives avec [FSharp.UMX](https://github.com/fsprojects/FSharp.UMX)

---

# RÃ©cap' - Conversion

- Conversion de type â†’ gÃ©nÃ©ralement explicite
- Conversion entre types numÃ©riques â†’ fonctions tq `int`
- Upcast `my-object :> base-type -> base-type`
- Downcast `my-object :?> derived-type -> derived-type | InvalidCastException`
- Test de type `my-object :? derived-type -> bool`

---

<!-- _class: end invert lead-->

# Thanks ğŸ™
