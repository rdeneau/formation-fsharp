---
marp: true
html: true
theme: 'd-edge'
title: 'Fâ™¯ Training â€¢ Types monadiques'
footer: 'Fâ™¯ Training â€¢ Types monadiques'
paginate: true
---

<!-- _class: title invert -->

# Fâ™¯ Training

## *Les types Â« monadiques Â»*

### 2025 April

---

<!-- _class: agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- Type `Option`
- Type `Result`
- *Smart constructor*
- *Computation expression*

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_baseDonnees.png)

# 1.

## Type `Option`

---

# ğŸ’  Type `Option`

A.k.a `Maybe` *(Haskell),* `Optional` _(Java 8)_

ModÃ©lise l'absence de valeur
â†’ DÃ©fini sous la forme d'une union avec 2 *cases*

```fs
type Option<'Value> =
    | None              // Case sans donnÃ©e â†’ quand valeur absente
    | Some of 'Value    // Case avec donnÃ©e â†’ quand valeur prÃ©sente
```

---

# `Option` Â» Cas d'utilisation

## Cas 1. ModÃ©liser un champ optionnel

```fs
type Civility = Mr | Mrs
type User = { Name: string; Civility: Civility option }

let joey  = { Name = "Joey"; Civility = Some Mr }
let guest = { Name = "Guest"; Civility = None }
```

â†’ Rend explicite le fait que `Name` est obligatoire et `Civility` facultatif

â˜ **Attention :** ce design n'empÃªche pas ici d'avoir `Name = null` *(limite BCL)*

---

## Cas 2. OpÃ©ration partielle

OpÃ©ration oÃ¹ aucune valeur de sortie n'est possible pour certaines entrÃ©es.

#### Exemple 1 : inverse d'un nombre

```fs
let inverse n = 1.0 / n

let tryInverse n =
    match n with
    | 0.0 -> None
    | n   -> Some (1.0 / n)
```

| Fonction     | OpÃ©ration | Signature               | `n = 0.5`  | `n = 0.0`    |
|--------------|-----------|-------------------------|------------|--------------|
| `inverse`    | Partielle | `float -> float`        | `2.0`      | `infinity` â“ |
| `tryInverse` | Totale    | `float -> float option` | `Some 2.0` | `None` ğŸ‘Œ    |

---

#### Exemple 2 : recherche d'un Ã©lÃ©ment dans une collection

- OpÃ©ration partielle : `find predicate` â†’ ğŸ’¥ quand Ã©lÃ©ment non trouvÃ©
- OpÃ©ration totale : `tryFind predicate` â†’ `None` ou `Some item`

#### Avantages ğŸ‘

- Explicite, honnÃªte / partialitÃ© de l'opÃ©ration
  - Pas de valeur spÃ©ciale : `null`, `infinity`
  - Pas d'exception
- Force le code appelant Ã  gÃ©rer la totalitÃ© des cas :
  - PrÃ©sence d'une valeur en sortie : `Some value`
  - Absence d'une valeur en sortie : `None`

---

# `Option` Â» Flux de contrÃ´le

Pour tester la prÃ©sence de la valeur *(de type `'T`)* dans l'option

- âŒ Ne pas utiliser `IsSome`, `IsNone` et `Value` (ğŸ¤ğŸ’¥)
  - ~~if option.IsSome then option.Value...~~
- ğŸ‘Œ A la main avec *pattern matching*
- âœ… Fonctions du module `Option`

---

## Flux de contrÃ´le manuel avec *pattern matching*

Exemple :

```fs
let print option =
    match option with
    | Some x -> printfn "%A" x
    | None   -> printfn "None"

print (Some 1.0)  // 1.0
print None        // None
```

---

## Flux de contrÃ´le intÃ©grÃ© au module `Option`

OpÃ©ration de *Mapping* de la valeur (de type `'T`) **si âˆƒ** :
â†’ `mapÂ  f option` avec `f` opÃ©ration totale `'T -> 'U`
â†’ `bind f option` avec `f` opÃ©ration partielle `'T -> 'U option`

Conserver la valeur **si âˆƒ** et si respecte condition :
â†’ `filter predicate option` avec `predicate: 'T -> bool` appelÃ© que si valeur âˆƒ

ğŸ‘¨â€ğŸ« **DÃ©mo**
â†’ ImplÃ©mentation de `map`, `bind` et `filter` avec *pattern matching*

---

## ğŸ‘¨â€ğŸ« DÃ©mo Â» Solution

```fs
let map f option =             // (f: 'T -> 'U) -> 'T option -> 'U option
    match option with
    | Some x -> Some (f x)
    | None   -> None           // ğŸ 1. Pourquoi on ne peut pas Ã©crire `None -> option` ?

let bind f option =            // (f: 'T -> 'U option) -> 'T option -> 'U option
    match option with
    | Some x -> f x
    | None   -> None

let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    match option with
    | Some x when predicate x -> option
    | _ -> None                // ğŸ 2. ImplÃ©menter `filter` avec `bind` ?
```

---

## ğŸ Questions bonus Â» RÃ©ponses

```fs
// ğŸ 1. Pourquoi on ne peut pas Ã©crire `None -> option` :
let map (f: 'T -> 'U) (option: 'T option) : 'U option =
    match option with
    | Some x -> Some (f x)
    | None   -> (*None*) option  // ğŸ’¥ Erreur de typage : `'U option` attendu != `'T option`
```

```fs
// ğŸ 2. ImplÃ©menter `filter` avec `bind` :
let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    let f x = if predicate x then option else None
    bind f option
```

---

## Flux de contrÃ´le intÃ©grÃ© Â» Exemple

```fs
// Application console de questions/rÃ©ponses
type Answer = A | B | C | D

let tryParseAnswer text =
    match text with
    | "A" -> Some A
    | "B" -> Some B
    | "C" -> Some C
    | "D" -> Some D
    | _   -> None

// Fonction appelÃ©e quand l'utilisateur saisit la rÃ©ponse au clavier Ã  la question posÃ©e
let checkAnswer (expectedAnswer: Answer) (givenAnswer: string) =
    tryParseAnswer givenAnswer
    |> Option.filter ((=) expectedAnswer)
    |> Option.map (fun _ -> "âœ…")
    |> Option.defaultValue "âŒ"

["X"; "A"; "B"] |> List.map (checkAnswer B)  // ["âŒ"; "âŒ"; "âœ…"]
```

---

## Flux de contrÃ´le intÃ©grÃ© Â» BÃ©nÃ©fices

Rend logique mÃ©tier \+ lisible

- Pas de `if hasValue then / else`
- Met en valeur le *happy path*
- Centralise Ã  la fin la gestion de l'absence de valeur

ğŸ’¡ Les *computation expressions* ğŸ“ fournissent une syntaxe alternative \+ lÃ©gÃ¨re

---

# `Option` *vs* `List`

Option â‰ƒ Liste de 0 ou 1 Ã©lÃ©ment
â†’ Cf. fonction `Option.toList`

```fs
let noneIsEmptyList       = Option.toList(None)   = []   // true
let someIsListWithOneItem = Option.toList(Some 1) = [1]  // true
```

â˜ Une `List` peut avoir \+ de 1 Ã©lÃ©ment
â†’ Type `Option` modÃ©lise mieux l'absence de valeur que type `List`

ğŸ’¡ Module `Option` : beaucoup de mÃªme fonctions que module `List`
â†’ `contains`, `count`, `exist`, `filter`, `fold`, `forall`, `map`

---

# `Option` *vs* `Nullable`

Type `System.Nullable<'T>` â‰ƒ `Option<'T>` en \+ limitÃ©

- â— Ne marche pas pour les types rÃ©fÃ©rences
- â— Manque comportement monadique i.e. fonctions `map` et `bind`
- â— En Fâ™¯, pas de magie comme en Câ™¯ / mot clÃ© `null`

ğŸ‘‰ `Option` est le type idiomatique en Fâ™¯

---

# `Option` *vs* `null`

De part ses interactions avec la BCL, Fâ™¯ autorise parfois la valeur `null`

ğŸ‘‰ **Bonne pratique** : isoler ces cas de figure et wrapper dans un type `Option`

```fs
let readLine (reader: System.IO.TextReader) =
    reader.ReadLine() |> Option.ofObj

    // Ã‰quivalent Ã  faire :
    match reader.ReadLine() with
    | null -> None
    | line -> Some line
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_delivery.png)

# 2.

## Type `Result`

---

# ğŸ’  Type `Result`

A.k.a `Either` *(Haskell)*

ModÃ©lise une *double-track* SuccÃ¨s/Ã‰chec

```fs
type Result<'Success, 'Error> =   // 2 paramÃ¨tres gÃ©nÃ©riques
    | Ok of 'Success    // Track "SuccÃ¨s"
    | Error of 'Error   // Track "Ã‰chec"
```

Gestion fonctionnelle des erreurs mÃ©tier *(les erreurs prÃ©visibles)*
â†’ Permet de limiter usage des exceptions aux erreurs exceptionnelles
â†’ DÃ¨s qu'une opÃ©ration Ã©choue, les opÃ©rations restantes ne sont pas lancÃ©es
â†’ *Railway-oriented programming* â€¢ https://fsharpforfunandprofit.com/rop/

---

# Module `Result`

*Ne contient que 3 fonctions*

`map f option` : sert Ã  mapper le rÃ©sultat
â€¢ `('T -> 'U) -> Result<'T, 'Error> -> Result<'U, 'Error>`

`mapError f option` : sert Ã  mapper l'erreur
â€¢ `('Err1 -> 'Err2) -> Result<'T, 'Err1> -> Result<'T, 'Err2>`

`bind f option` : idem `map` avec fonction `f` qui renvoie un `Result`
â€¢ `('T -> Result<'U, 'Error>) -> Result<'T, 'Error> -> Result<'U, 'Error>`
â€¢ ğŸ’¡ Le rÃ©sultat est aplati, comme la fonction `flatMap` sur les arrays JS
â€¢ âš ï¸ MÃªme type d'erreur `'Error` pour `f` et le `result` en entrÃ©e

---

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_question.png)

# Quiz *Result* ğŸ•¹ï¸

ImplÃ©menter `Result.map` et `Result.bind`

ğŸ’¡ **Tips :**

- *Mapping* sur la track *SuccÃ¨s*
- AccÃ¨s Ã  la valeur dans la track *SuccÃ¨s* :
  - Utiliser *pattern matching* (`match result with...`)
- Retour : simple `Result`, pas un `Result<Result>` !

---

# Quiz *Result* ğŸ²

**Solution :** implÃ©mentation de `Result.map` et `Result.bind`

```fs
// ('T -> 'U) -> Result<'T, 'Error> -> Result<'U, 'Error>
let map f result =
    match result with
    | Ok x    -> Ok (f x)  // â˜ Ok -> Ok
    | Error e -> Error e   // âš ï¸ Les 2 `Error e` n'ont pas le mÃªme type !

// ('T -> Result<'U, 'Error>) -> Result<'T, 'Error>
//                            -> Result<'U, 'Error>
let bind f result =
    match result with
    | Ok x    -> f x       // â˜ Ok -> Ok ou Error !
    | Error e -> Error e
```

---

# `Result` : tracks Success/Failure

`map` : pas de changement de track

```txt
Track      Input          Operation      Output
Success â”€ Ok x    â”€â”€â”€â–º map( x -> y ) â”€â”€â”€â–º Ok y
Failure â”€ Error e â”€â”€â”€â–º map(  ....  ) â”€â”€â”€â–º Error e
```

`bind` : routage possible vers track Failure mais jamais l'inverse

```txt
Track     Input              Operation           Output
Success â”€ Ok x    â”€â”¬â”€â–º bind( x -> Ok y     ) â”€â”€â”€â–º Ok y
                   â””â”€â–º bind( x -> Error e2 ) â”€â”
Failure â”€ Error e â”€â”€â”€â–º bind(     ....      ) â”€â”´â”€â–º Error ~
```

â˜ OpÃ©ration de *mapping/binding* jamais exÃ©cutÃ©e dans track Failure

---

# `Result` *vs* `Option`

`Option` peut reprÃ©senter le rÃ©sultat d'une opÃ©ration qui peut Ã©chouer
â˜ Mais en cas d'Ã©chec, l'option ne contient pas l'erreur, juste `None`

`Option<'T>` â‰ƒ `Result<'T, unit>`
â†’ `Some x` â‰ƒ `Ok x`
â†’ `None` â‰ƒ `Error ()`
â†’ Cf. fonctions `Option.toResult` et `Option.toResultWith error` de [FSharpPlus](http://fsprojects.github.io/FSharpPlus/reference/fsharpplus-option.html#toResult)

```fs
let toResultWith (error: 'Error) (option: 'T option) : Result<'T, 'Error> =
    match option with
    | Some x -> Ok x
    | None   -> Error error
```

---

<!-- _footer: '' -->

## `Result` *vs* `Option` Â» Exemple

Modification de la fonction `checkAnswer` prÃ©cÃ©dente pour indiquer l'erreur :

```fs
open FSharpPlus

type Answer = A | B | C | D
type Error = InvalidInput | WrongAnswer

let tryParseAnswer text = ... // string -> Answer option

let checkAnswer (expectedAnswer: Answer) (givenAnswer: string) =
    let check answer = if answer = expectedAnswer then Ok answer else Error WrongAnswer
    tryParseAnswer givenAnswer           // Answer option
    |> Option.toResultWith InvalidInput  // Result<Answer, Error>
    |> Result.bind check
    |> function
       | Ok _               -> "âœ…"
       | Error InvalidInput -> "âŒ Invalid Input"
       | Error WrongAnswer  -> "âŒ Wrong Answer"

["X"; "A"; "B"] |> List.map (checkAnswer B)  // ["âŒ Invalid Input"; "âŒ Wrong Answer"; "âœ…"]
```

---

# `Result` *vs* `Validation`

`Result` est "monadique" : Ã  la 1Ã¨re erreur, on "dÃ©branche"

`Validation` est "applicatif" : permet d'accumuler les erreurs
â†’ â‰ƒ `Result<'ok, 'error list>`
â†’ Pratique pour valider saisie utilisateur et remonter âˆ‘ erreurs
â†’ Dispo dans librairies [FSharpPlus](https://github.com/fsprojects/FSharpPlus), [FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling)

*Plus d'info :*
ğŸ”— https://kutt.it/pke2i1 *Validation with F# 5 and FsToolkit* - Dec 2020

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_atelier.png)

# 3.

## *Smart constructor*

---

# *Â« Making illegal states unrepresentable Â»*

ğŸ”— https://kutt.it/MksmkG *Fâ™¯ for fun and profit, Jan 2013*

- Avoir un design qui empÃªche d'avoir des Ã©tats invalides
  - Encapsuler Ã©tat *(âˆ‘ primitives)* dans un objet
- *Smart constructor* sert Ã  garantir un Ã©tat initial valide
   - Valide les donnÃ©es en entrÃ©e
   - Si Ko, renvoie "rien" (`Option`) ou l'erreur (`Result`)
   - Si Ok, renvoie l'objet crÃ©Ã© wrappÃ© dans l'`Option` / le `Result`

---

# Encapsuler Ã©tat dans un type

â†’ *Single-case (discriminated) union* ğŸ‘Œ : `Type X = private X of a: 'a...`
ğŸ”— https://kutt.it/mmMXCo *Fâ™¯ for fun and profit, Jan 2013*

â†’ *Record* ğŸ‘ : `Type X = private { a: 'a... }`
ğŸ”— https://kutt.it/cYP4gY *Paul Blasucci, Mai 2021*

â˜ Mot clÃ© `private` :
â†’ Cache contenu de l'objet
â†’ Champs et constructeur ne sont plus visibles de l'extÃ©rieur
â†’ Smart constructeur dÃ©fini dans module compagnon ğŸ‘ ou mÃ©thode statique

---

## *Smart constructor* Â» Exemple 1 

Smart constructeur :
â†’ Fonction `tryCreate` dans module compagnon
â†’ Renvoie une `Option`

```fs
type Latitude = private { Latitude: float } // ğŸ‘ˆ Un seul champ, nommÃ© comme le type

[<RequireQualifiedAccess>]                  // ğŸ‘ˆ Optionnel
module Latitude =
    let tryCreate (latitude: float) =
        if latitude >= -90. && latitude <= 90. then
            Some { Latitude = latitude }    // ğŸ‘ˆ Constructeur accessible ici
        else
            None

let lat_ok = Latitude.tryCreate 45.  // Some { Latitude = 45.0 }
let lat_ko = Latitude.tryCreate 115. // None
```

---

## *Smart constructor* Â» Exemple 2

Smart constructeur :
â†’ MÃ©thode statique `Of`
â†’ Renvoie `Result` avec erreur de type `string`

```fs
type Tweet =
    private { Tweet: string }

    static member Of tweet =
        if System.String.IsNullOrEmpty tweet then
            Error "Tweet shouldn't be empty"
        elif tweet.Length > 280 then
            Error "Tweet shouldn't contain more than 280 characters"
        else Ok { Tweet = tweet }

let tweet1 = Tweet.Of "Hello world" // Ok { Tweet = "Hello world" }
```

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_backEnd.png)

# 4.

## Computation expression

---

# Computation expression

Sucre syntaxique cachant une Â« machinerie Â»
â†’ Applique la _Separation of Concerns_
â†’ Code \+ lisible Ã  l'intÃ©rieur de la *computation expression* (CE)

Syntaxe : `builder { expr }`
â†’ `builder` instance d'un Â« Builder Â» ğŸ“
â†’ `expr` peut contenir `let`, `let!`, `do!`, `yield`, `yield!`, `return`, `return!`

ğŸ’¡ **Note :** `seq`, `async` et `task` sont des CE

---

# Builder

Une *computation expression* s'appuie sur un objet appelÃ© *Builder*.
â†’ Cet objet permet Ã©ventuellement de stocker un Ã©tat en background.

Pour chaque mot-clÃ© supportÃ© (`let!`, `return`...), le *Builder* implÃ©mente
une ou plusieurs mÃ©thodes associÃ©es. Exemples :
â€¢ `builder { return expr }` â†’ `builder.Return(expr)`
â€¢ `builder { let! x = expr; cexpr }` â†’ `builder.Bind(expr, (fun x -> {| cexpr |}))`

Le *builder* peut Ã©galement wrappÃ© le rÃ©sultat dans un type qui lui est propre :
â€¢ `async { return x }` renvoie un type `Async<'X>`
â€¢ `seq { yield x }` renvoie un type `Seq<'X>`

---

# Builder desugaring

Le compilateur opÃ¨re la traduction vers les mÃ©thodes du *builder*.
â†’ La CE masque la complexitÃ© de ces appels, souvent imbriquÃ©s :

```fs
seq {
    for n in list do
        yield n
        yield n * 10 }

// Traduit en :
seq.For(list, fun () ->
    seq.Combine(seq.Yield(n),
                seq.Delay(fun () -> seq.Yield(n * 10)) ) )
```

---

<!-- _footer: '' -->

# Builder - Exemple : `logger`

Besoin : logguer les valeurs intermÃ©diaires d'un calcul

```fs
let log value = printfn $"{value}"

let loggedCalc =
    let x = 42
    log x  // â¶
    let y = 43
    log y  // â¶
    let z = x + y
    log z  // â¶
    z
```

**ProblÃ¨mes**  âš ï¸
â‘  Verbeux : les `log x` gÃªnent lecture
â‘¡ *Error prone* : oublier un `log`, logguer mauvaise valeur...

---

# Builder - Exemple : `logger` (2)

ğŸ’¡ Rendre les logs implicites dans une CE lors du `let!` / `Bind` :

```fs
type LoggingBuilder() =
    let log value = printfn $"{value}"; value
    member _.Bind(x, f) = x |> log |> f
    member _.Return(x) = x

let logger = LoggingBuilder()

//---

let loggedCalc = logger {
    let! x = 42
    let! y = 43
    let! z = x + y
    return z
}
```

---

# Builder - Exemple : `maybe`

Besoin : simplifier enchaÃ®nement de "trySomething" renvoyant une `Option`

```fs
let tryDivideBy bottom top = // (bottom: int) -> (top: int) -> int option
    if (bottom = 0) or (top % bottom <> 0)
    then None
    else Some (top / bottom)

// Sans CE
let division =
    36
    |> tryDivideBy 2                // Some 18
    |> Option.bind (tryDivideBy 3)  // Some 6
    |> Option.bind (tryDivideBy 2)  // Some 3
```

---

# Builder - Exemple : `maybe` (2)

```fs
// Avec CE
type MaybeBuilder() =
    member _.Bind(x, f) = x |> Option.bind f
    member _.Return(x) = Some x

let maybe = MaybeBuilder()

let division' = maybe {
    let! v1 = 36 |> tryDivideBy 2
    let! v2 = v1 |> tryDivideBy 3
    let! v3 = v2 |> tryDivideBy 2
    return v3
}
```

**Bilan :** âœ… SymÃ©trie, âŒ Valeurs intermÃ©diaires

---

# Limite : imbrication de CE

âœ… On peut imbriquer des CE diffÃ©rentes
âŒ Mais code devient difficile Ã  comprendre

Exemple : combiner `logger` et `maybe` â“

Solution alternative :

```fs
let inline (>>=) x f = x |> Option.bind f

let logM value = printfn $"{value}"; Some value  // 'a -> 'a option

let division' =
    36 |> tryDivideBy 2 >>= logM
      >>= tryDivideBy 3 >>= logM
      >>= tryDivideBy 2 >>= logM
```

---

<!-- _footer: '' -->

# Limite : combinaison de CE

Combiner `Async` + `Option`/`Result` ?
â†’ Solution : CE `asyncResult` + helpers dans [FsToolkit](https://demystifyfp.gitbook.io/fstoolkit-errorhandling/#a-motivating-example)

```fs
type LoginError =
    | InvalidUser | InvalidPassword
    | Unauthorized of AuthError | TokenErr of TokenError

let login username password =
    asyncResult {
        // tryGetUser: string -> Async<User option>
        let! user = username |> tryGetUser |> AsyncResult.requireSome InvalidUser
        // isPasswordValid: string -> User -> bool
        do! user |> isPasswordValid password |> Result.requireTrue InvalidPassword
        // authorize: User -> Async<Result<unit, AuthError>>
        do! user |> authorize |> AsyncResult.mapError Unauthorized
        // createAuthToken: User -> Result<AuthToken, TokenError>
        return! user |> createAuthToken |> Result.mapError TokenErr
    } // Async<Result<AuthToken, LoginError>>
```

---

# CE : le couteau suisse âœ¨

Les *computation expressions* servent Ã  diffÃ©rentes choses :
â€¢ Câ™¯ `yield return` â†’ Fâ™¯ `seq {}`
â€¢ Câ™¯ `async/await` â†’ Fâ™¯ `async {}`
â€¢ Câ™¯ expressions LINQ `from... select` â†’ Fâ™¯ `query {}`
â€¢ ...

Fondements thÃ©oriques sous-jacents :
â€¢ MonoÃ¯de
â€¢ Monade
â€¢ Applicative

---

# MonoÃ¯de

â‰ƒ Type `T` dÃ©finissant un ensemble comportant :

1. OpÃ©ration `(+) : T -> T -> T`
   â†’ Pour combiner des ensembles et garder le mÃªme "type"
   â†’ Associative : `a + (b + c)` â‰¡ `(a + b) + c`
2. Ã‰lÃ©ment neutre *(aka identity)* â‰ƒ ensemble vide
   â†’ Combinable Ã  tout ensemble sans effet
   â†’ `a + e` â‰¡ `e + a` â‰¡ `a`

---

# CE monoÃ¯dale

Le builder d'une CE monoÃ¯dale *(telle que `seq`)* dispose *a minima* de :
- `Yield` pour construire l'ensemble Ã©lÃ©ment par Ã©lÃ©ment
- `Combine` â‰¡ `(+)` (`Seq.append`)
- `Zero` â‰¡ Ã©lÃ©ment neutre (`Seq.empty`)

S'y ajoute gÃ©nÃ©ralement (entre autres) :
- `For` pour supporter `for x in xs do ...`
- `YieldFrom` pour supporter `yield!`

---

# Monade

â‰ƒ Type gÃ©nÃ©rique `M<'T>` comportant :

1. Fonction `return` de construction
   - Signature : `(value: 'T) -> M<'T>`
   - â‰ƒ Wrap une valeur
2. Fonction `bind` de "liaison" *(aka opÃ©rateur `>>=`)*
   - Signature : `(f: 'T -> M<'U>) -> M<'T> -> M<'U>`
   - Utilise la valeur wrappÃ©e, la "map" avec la fonction `f`
     Â Â Â Â Â Â Â vers une valeur d'un autre type et "re-wrap" le rÃ©sultat

---

# Monade : lois

`return` â‰¡ Ã©lÃ©ment neutre pour `bind`
- Ã€ gauche : `return x |> bind f` â‰¡ `f x`
- Ã€ droite : `m |> bind return` â‰¡ `m`

`bind` est associatif
- `m |> bind f |> bind g` â‰¡ `m |> bind (fun x -> f x |> bind g)`

---

# Monades et langages

**Haskell**
â€¢ Monades beaucoup utilisÃ©es. Les \+ communes : `IO`, `Maybe`, `State`, `Reader`.
â€¢ `Monad` est une *classe de type* pour crÃ©er facilement ses propres monades.

**Fâ™¯**
â€¢ Certaines CE permettent des opÃ©rations monadiques.
â€¢ Plus rarement utilisÃ©es directement _(sauf par des Haskellers)_

**Câ™¯**
â€¢ Monade implicite dans LINQ
â€¢ Librairie [LanguageExt](https://github.com/louthy/language-ext) de programmation fonctionnelle

---

# CE monadique

Le builder d'une CE monadique dispose des mÃ©thodes `Return` et `Bind`.

Les types `Option` et `Result` sont monadiques.
â†’ On peut leur crÃ©er leur propre CE :

```fs
type OptionBuilder() =
    member _.Bind(x, f) = x |> Option.bind f
    member _.Return(x) = Some x

type ResultBuilder() =
    member _.Bind(x, f) = x |> Result.bind f
    member _.Return(x) = Ok x
```

---

# CE monadique et gÃ©nÃ©rique

[FSharpPlus](http://fsprojects.github.io/FSharpPlus//computation-expressions.html) propose une CE `monad`
â†’ Marche pour tous les types monadiques : `Option`, `Result`, ... et mÃªme `Lazy` !

```fs
#r "nuget: FSharpPlus"
open FSharpPlus

let lazyValue = monad {
    let! a = lazy (printfn "I'm lazy"; 2)
    let! b = lazy (printfn "I'm lazy too"; 10)
    return a + b
} // System.Lazy<int>

let result = lazyValue.Value
// I'm lazy
// I'm lazy too
// val result : int = 12
```

---

# CE monadique et gÃ©nÃ©rique (2)

Exemple avec le type `Option` :

```fs
#r "nuget: FSharpPlus"
open FSharpPlus

let addOptions x' y' = monad {
    let! x = x'
    let! y = y'
    return x + y
}

let v1 = addOptions (Some 1) (Some 2) // Some 3
let v2 = addOptions (Some 1) None     // None
```

---

# CE monadique et gÃ©nÃ©rique (3)

âš ï¸ **Limite :** on ne peut pas mÃ©langer plusieurs types monadiques !

```fs
#r "nuget: FSharpPlus"
open FSharpPlus

let v1 = monad {
    let! a = Ok 2
    let! b = Some 10
    return a + b
} // ğŸ’¥ Error FS0043...

let v2 = monad {
    let! a = Ok 2
    let! b = Some 10 |> Option.toResult
    return a + b
} // val v2 : Result<int,unit> = Ok 12
```

---

<!-- _footer: '' -->

# CE monadiques spÃ©cifiques

Librairie [FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling/) propose :
â€¢ CE `option {}` spÃ©cifique au type `Option<'T>` _(exemple ci-dessous)_
â€¢ CE `result {}` spÃ©cifique au type `Result<'Ok, 'Err>`

â˜ RecommandÃ© car \+ explicite que CE `monad`

```fs
#r "nuget: FSToolkit.ErrorHandling"
open FsToolkit.ErrorHandling

let addOptions x' y' = option {
    let! x = x'
    let! y = y'
    return x + y
}

let v1 = addOptions (Some 1) (Some 2) // Some 3
let v2 = addOptions (Some 1) None     // None
```

---

<!-- _footer: '' -->

# Applicative _(a.k.a Applicative Functor)_

â‰ƒ Type gÃ©nÃ©rique `M<'T>` -- 3 styles :

**Style A:** Applicatives avec `apply`/`<*>` et `pure`/`return`
â€¢ âŒ Pas facile Ã  comprendre
â€¢ â˜ DÃ©conseillÃ© par Don Syme dans cette [note de nov. 2020](https://github.com/dsyme/fsharp-presentations/blob/master/design-notes/rethinking-applicatives.md)

**Style B:** Applicatives avec `mapN`
â€¢ `map2`, `map3`... `map5` combine 2 Ã  5 valeurs wrappÃ©es

**Style C:** Applicatives avec `let! ... and! ...` dans une CE
â€¢ MÃªme principe : combiner plusieurs valeurs wrappÃ©es
â€¢ Disponible Ã  partir de Fâ™¯ 5 _([annonce de nov. 2020](https://devblogs.microsoft.com/dotnet/announcing-f-5/#applicative-computation-expressions))_

â˜ **Conseil :** Styles B et C sont autant recommandÃ©s l'un que l'autre.

---

# CE applicative

Librairie [FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling/) propose :
â€¢ Type `Validation<'Ok, 'Err>` â‰¡ `Result<'Ok, 'Err list>`
â€¢ CE `validation {}` supportant syntaxe `let!...and!...`

Permet d'accumuler les erreurs â†’ Usages :
â€¢ Parsing d'inputs externes
â€¢ *Smart constructor* *(Exemple de code slide suivante...)*

---

```fs
#r "nuget: FSToolkit.ErrorHandling"
open FsToolkit.ErrorHandling

type [<Measure>] cm
type Customer = { Name: string; Height: int<cm> }

let validateHeight height =
    if height <= 0<cm>
    then Error "Height must me positive"
    else Ok height

let validateName name =
    if System.String.IsNullOrWhiteSpace name
    then Error "Name can't be empty"
    else Ok name

module Customer =
    let tryCreate name height : Result<Customer, string list> =
        validation {
            let! validName = validateName name
            and! validHeight = validateHeight height
            return { Name = validName; Height = validHeight }
        }

let c1 = Customer.tryCreate "Bob" 180<cm>  // Ok { Name = "Bob"; Height = 180 }
let c2 = Customer.tryCreate "Bob" 0<cm> // Error ["Height must me positive"]
let c3 = Customer.tryCreate "" 0<cm>    // Error ["Name can't be empty"; "Height must me positive"]

```

---

<!-- _footer: '' -->

# Applicative _vs_ Monade

> Soit N opÃ©rations `tryXxx` renvoyant un `Option` ou `Result`

**Style monadique :**
â€¢ Avec `bind` ou CE `let! ... let! ...`
â€¢ **ChaÃ®ne** les opÃ©rations, exÃ©cutÃ©e 1 Ã  1, la N dÃ©pendant de la N-1
â€¢ S'arrÃªte Ã  1Ã¨re opÃ©ration KO â†’ juste 1Ã¨re erreur dans `Result` â‘ 
â€¢ *[Railway-oriented programming](https://fsharpforfunandprofit.com/rop/)* de Scott Wlaschin

```fs
module Result =
    // f : 'T -> Result<'U, 'Err>
    // x': Result<'T, 'Err>
    //  -> Result<'U, 'Err>
    let bind f x' =
        match x' with
        | Error e  -> Error e // ğŸ‘ˆ â‘ 
        | Ok value -> f value
```

---

# Applicative _vs_ Monade (2)

**Style applicatif :**
â€¢ Avec `mapN` ou CE `let! ... and! ...`
â€¢ **Combine** 2..N opÃ©rations indÃ©pendantes â†’ parallÃ©lisables ğŸ‘
â€¢ Permet de combiner les cas `Error` contenant une `List` â‘¡

```fs
module Validation =
    // f : 'T -> 'U -> Result<'V, 'Err list>
    // x': Result<'T, 'Err list>
    // y': Result<'U, 'Err list>
    //  -> Result<'V, 'Err list>
    let map2 f x' y' =
        match x', y' with
        | Ok x, Ok y -> f x y
        | Ok _, Error errors | Error errors, Ok _ -> Error errors
        | Error errors1, Error errors2 -> Error (errors1 @ errors2) // ğŸ‘ˆ â‘¡
```

---

# Autres CE

On a vu 2 librairies qui Ã©tendent Fâ™¯ et proposent leurs CE :

- FSharpPlus â†’ `monad`
- FsToolkit.ErrorHandling â†’ `option`, `result`, `validation`

Beaucoup de librairies ont leur propre DSL _(Domain Specific Language.)_
Certaines s'appuient alors sur des CE :

- Expecto
- Farmer
- Saturn

---

# Expecto

â Librairie de testing : assertions + runner â
ğŸ”— https://github.com/haf/expecto

```fs
open Expecto

let tests =
  test "A simple test" {
    let subject = "Hello World"
    Expect.equal subject "Hello World" "The strings should equal"
  }

[<EntryPoint>]
let main args =
  runTestsWithCLIArgs [] args tests
```

---

# Farmer

â *Infrastructure-as-code* pour Azure â
ğŸ”— https://github.com/compositionalit/farmer

```fs
// Create a storage account with a container
let myStorageAccount = storageAccount {
    name "myTestStorage"
    add_public_container "myContainer"
}

// Create a web app with application insights that's connected to the storage account
let myWebApp = webApp {
    name "myTestWebApp"
    setting "storageKey" myStorageAccount.Key
}

// [...]
```

---

# Farmer (2)

```fs
// [...]

// Create an ARM template (Azure Resource Manager)
let deployment = arm {
    location Location.NorthEurope
    add_resources [
        myStorageAccount
        myWebApp
    ]
}

// Deploy it to Azure!
deployment
|> Writer.quickDeploy "myResourceGroup" Deploy.NoParameters
```

---

# Saturn

â Framework Web au-dessus de ASP.NET Core, pattern MVC â
ğŸ”— https://saturnframework.org/

```fs
open Saturn
open Giraffe

let app = application {
    use_router (text "Hello World from Saturn")
}

run app
```

---

# CE : aller \+ loin

Extending F# through Computation Expressions
ğŸ“¹ https://youtu.be/bYor0oBgvws
ğŸ“œ https://panesofglass.github.io/computation-expressions/#/
Â Â Â Â â†’ RÃ©fÃ©rences en [slide 14](https://panesofglass.github.io/computation-expressions/#/14)

Computation Expressions Workshop
ğŸ”— https://github.com/panesofglass/computation-expressions-workshop

---

<!-- _class: chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 5.

## LeÂ Â Â  RÃ©capâ€™

---

# Types unions : `Option` et `Result`

- A quoi ils servent :
  - ModÃ©liser absence de valeur et erreurs mÃ©tier
  - OpÃ©rations partielles rendues totales `tryXxx`
    - *Smart constructor* `tryCreate`
- Comment on s'en sert :
  - ChaÃ®nage : `map`, `bind`, `filter` â†’ *ROP*
  - Pattern matching
- Leurs bÃ©nÃ©fices :
  - `null` free, `Exception` free â†’ pas de guard polluant code
  - Rend logique mÃ©tier et *happy path* \+ lisible

---

<!-- _footer: '' -->

# *Computation expression (CE)*

- Sucre syntaxique : syntaxe intÃ©rieure standard ou "bangÃ©e" (`let!`)
- *Separation of Concerns* : logique mÃ©tier *vs* Â« machinerie Â»
- Compilateur fait lien avec *builder*
  - Objet stockant un Ã©tat
  - Build une valeur en sortie, d'un type spÃ©cifique
- Imbricables mais pas faciles Ã  combiner !
- Concepts thÃ©oriques sous-jacents
  - MonoÃ¯de â†’ `seq` *(d'Ã©lÃ©ments composables et avec un "zÃ©ro"*)
  - Monade â†’ `async`, `option`, `result`
  - Applicative â†’ `validation`/`Result<'T, 'Err list>`
- Librairies : FSharpPlus, FsToolkit, Expecto, Farmer, Saturn

---

# ğŸ”— Ressources complÃ©mentaires

Compositional IT *(Isaac Abraham)*
â†’ https://kutt.it/gpIgfD â€¢ *Writing more succinct C# â€“ in F#! (Part 2)* â€¢ Jul 2020

F# for Fun and Profit *(Scott Wlaschin)*
â†’ https://kutt.it/e78rNj â€¢ *The Option type* â€¢ Jun 2012
â†’ https://kutt.it/7J5Krc â€¢ *Making illegal states unrepresentable* â€¢ Jan 2013
â†’ https://kutt.it/drchkQ â€¢ SÃ©rie de 11 articles sur les CE â€¢ Jan 2013
â†’ https://kutt.it/ebfGNA â€¢ SÃ©rie de 7 articles sur monades 'n co â€¢ Aug 2015

---

<!-- _class: end invert lead-->

# Thanks ğŸ™
