---
marp: true
html: true
theme: 'd-edge'
title: 'Fâ™¯ Training â€¢ Pattern matching'
footer: 'Fâ™¯ Training â€¢ Pattern matching'
paginate: true
---

<!-- _class: title invert -->

# Fâ™¯ Training

## *Pattern matching*

### 2025 April

---

<!-- _class: toc agenda invert lead -->

![bg right:30% h:300](../themes/d-edge/pictos/SOAT_pictos_formation.png)

## Table of contents

- Patterns
- Match expression
- Active patterns

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_architecture.png)

# 1.

## GÃ©nÃ©ralitÃ©s : *Patterns*

---

# Patterns

*Patterns* = rÃ¨gles pour dÃ©tecter la structure de donnÃ©es en entrÃ©e

UtilisÃ©s abondamment en Fâ™¯
â†’ Dans *match expression*, *let binding* de valeurs et de paramÃ¨tres de fonctions
â†’ TrÃ¨s pratiques pour manipuler les types algÃ©briques Fâ™¯ (tuple, record, union)
â†’ Composables : supporte plusieurs niveaux d'imbrication
â†’ Assemblables par ET/OU logiques
â†’ Supporte les littÃ©raux : `1.0`, `"test"`...

---

# Wildcard Pattern

ReprÃ©sentÃ© par `_`, seul ou combinÃ© avec un autre *pattern*

Toujours vrai
â†’ A placer en dernier dans une *match expression*

âš ï¸ Toujours chercher en 1er Ã  traiter exhaustivement/explicitement tous les cas
Â Â Â Â  Quand impossible, utiliser alors le `_`

```fs
match option with
| Some 1 -> ...
| _ -> ...              // âš ï¸ Non exhaustif

match option with
| Some 1 -> ...
| Some _ | None -> ...  // ğŸ‘Œ \+ exhaustif
```

---

# Constant Pattern

DÃ©tecte constantes, `null` et littÃ©raux de nombre, `char`, `string`, `enum`

```fs
[<Literal>]
let Three = 3   // Constante

let is123 num = // int -> bool
    match num with
    | 1 | 2 | Three -> true
    | _ -> false
```

â˜ **Notes :**

- Le pattern de `Three` est aussi classÃ© en tant que *Identifier Pattern* ğŸ“
- Pour le matching de `null`, on parle aussi de *Null Pattern*

---

# Identifier Pattern

DÃ©tecte les *cases* d'un type union ainsi que leur Ã©ventuel contenu

```fs
type PersonName =
    | FirstOnly of string
    | LastOnly  of string
    | FirstLast of string * string

let classify personName =
    match personName with
    | FirstOnly _ -> "First name only"
    | LastOnly  _ -> "Last name only"
    | FirstLast _ -> "First and last names"
```

---

# Variable Pattern

Assigne la valeur dÃ©tectÃ©e Ã  une "variable" pour l'utiliser ensuite

Exemple : variables `firstName` et `lastName` ci-dessous

```fs
type PersonName =
    | FirstOnly of string
    | LastOnly  of string
    | FirstLast of string * string

let confirm personName =
    match personName with
    | FirstOnly (firstName) -> printf "May I call you %s?" firstName
    | LastOnly  (lastName) -> printf "Are you Mr. or Ms. %s?" lastName
    | FirstLast (firstName, lastName) -> printf "Are you %s %s?" firstName lastName
```

---

# Variable Pattern (2)

âš ï¸ On ne peut pas lier Ã  plusieurs reprises vers la mÃªme variable

```fs
let elementsAreEqualKo tuple =
    match tuple with
    | (x,x) -> true  // ğŸ’¥ Error FS0038: 'x' est liÃ© Ã  deux reprises dans ce modÃ¨le
    | (_,_) -> false
```

Solutions : utiliser 2 variables puis vÃ©rifier l'Ã©galitÃ©

```fs
// 1. Guard clauseğŸ“
let elementsAreEqualOk = function
    | (x,y) when x = y -> true
    | (_,_) -> false

// 2. DÃ©construction
let elementsAreEqualOk' (x, y) = x = y
```

---

## Champs nommÃ©s de *case* d'union

Plusieurs possibilitÃ©s :
â‘  Pattern "anonyme" du tuple complet
â‘¡ Pattern d'un seul champ par son nom â†’ `Field = value`
â‘¢ Pattern de plusieurs champs par leur nom â†’ `F1 = v1; F2 = v2`

```fs
type Shape =
    | Rectangle of Height: int * Width: int
    | Circle of Radius: int

let describe shape =
    match shape with
    | Rectangle (0, _)                                              // â‘ 
    | Rectangle (Height = 0)            -> "Flat rectangle"         // â‘¡
    | Rectangle (Width = w; Height = h) -> $"Rectangle {w} Ã— {h}"   // â‘¢
    | Circle radius                     -> $"Circle âˆ… {2*radius}"
```

---

# Alias Pattern

`as` permet de nommer un Ã©lÃ©ment dont le contenu est dÃ©construit

```fs
let (x, y) as coordinate = (1, 2)
printfn "%i %i %A" x y coordinate  // 1 2 (1, 2)
```

ğŸ’¡ Marche aussi dans les fonctions :

```fs
type Person = { Name: string; Age: int }

let acceptMajorOnly ({ Age = age } as person) =
    if age < 18 then None else Some person
```

---

# OR et AND Patterns

Permettent de combiner deux patterns *(nommÃ©s `P1` et `P2` ci-aprÃ¨s)*
â€¢ `P1 | P2` â†’ P1 ou P2. Ex : `Rectangle (0, _) | Rectangle (_, 0)`
â€¢ `P1 & P2` â†’ P1 et P2. UtilisÃ© surtout avec *active patterns* ğŸ“

ğŸ’¡ Utiliser la mÃªme variable :

```fs
type Upload = { Filename: string; Title: string option }

let titleOrFile ({ Title = Some name } | { Filename = name }) = name

titleOrFile { Filename = "Report.docx"; Title = None }            // Report.docx
titleOrFile { Filename = "Report.docx"; Title = Some "Report+" }  // "Report+"
```

---

# Parenthesized Pattern

Usage des parenthÃ¨ses `()` pour grouper des patterns, pour gÃ©rer la prÃ©cÃ©dence

```fs
type Shape = Circle of Radius: int | Square of Side: int

let countFlatShapes shapes =
    let rec loop rest count =
        match rest with
        | (Square (Side = 0) | (Circle (Radius = 0))) :: tail -> loop tail (count + 1) // â‘ 
        | _ :: tail -> loop tail count
        | [] -> count
    loop shapes 0
```

â˜ **Note :** la ligne â‘  ne compilerait sans faire `() :: tail` 

---

# Parenthesized Pattern (2)

âš ï¸ Les parenthÃ¨ses compliquent la lecture
ğŸ’¡ Essayer de s'en passer quand c'est possible

```fs
let countFlatShapes shapes =
    let rec loop rest count =
        match rest with
        | Circle (Radius = 0) :: tail
        | Square (Side = 0) :: tail
          -> loop tail (count + 1)
        // [...]
```

---

# Construction Patterns

Reprennent syntaxe de construction d'un type pour le dÃ©construire

- Cons et List Patterns
- Array Pattern
- Tuple Pattern
- Record Pattern

---

# Cons et List Patterns

â‰ƒ Inverses de 2 types de construction d'une liste, avec mÃªme syntaxe

*Cons Pattern* : `head :: tail` â†’ dÃ©compose une liste *(avec >= 1 Ã©lÃ©ment)* en :
â€¢ *Head* : 1er Ã©lÃ©ment
â€¢ *Tail* : autre liste avec le reste des Ã©lÃ©ments - peut Ãªtre vide

*List Pattern* : `[items]` â†’ dÃ©compose une liste en 0..N Ã©lÃ©ments
â€¢ `[]` : liste vide
â€¢ `[x]` : liste avec 1 Ã©lÃ©ment mis dans la variable `x`
â€¢ `[x; y]` : liste avec 2 Ã©lÃ©ments mis dans les variables `x` et `y`
â€¢ `[_; _]` : liste avec 2 Ã©lÃ©ments ignorÃ©s

ğŸ’¡ `x :: []` â‰¡ `[x]`, `x :: y :: []` â‰¡ `[x; y]`...

---

# Cons et List Patterns (2)

La *match expression* par dÃ©faut combine les 2 patterns :
â†’ Une liste est soit vide `[]`, soit composÃ©e d'un item et du reste : `head :: tail`

Les fonctions rÃ©cursives parcourant une liste utilise le pattern `[]` pour stopper la rÃ©cursion :

```fs
let rec printList l =
    match l with
    | head :: tail ->
        printf "%d " head
        printList tail     // RÃ©cursion sur le reste
    | [] -> printfn ""     // Fin de rÃ©cursion : liste parcourue entiÃ¨rement
```

---

# Array Pattern

Syntaxe: `[| items |]` pour 0..N items entre `;`

```fs
let length vector =
    match vector with
    | [| x |] -> x
    | [| x; y |] -> sqrt (x*x + y*y)
    | [| x; y; z |] -> sqrt (x*x + y*y + z*z)
    | _ -> invalidArg (nameof vector) $"Vector with more than 4 dimensions not supported"
```

â˜ Il n'existe pas de pattern pour les sÃ©quences, vu qu'elles sont *"lazy"*.

---

# Tuple Pattern

Syntaxe : `items` ou `(items)` pour 2..N items entre `,`

ğŸ’¡ Pratique pour pattern matcher plusieurs valeurs en mÃªme temps

```fs
type Color = Red | Blue
type Style = Background | Text

let css color style =
    match color, style with
    | Red, Background -> "background-color: red"
    | Red, Text -> "color: red"
    | Blue, Background -> "background-color: blue"
    | Blue, Text -> "color: blue"
```

---

# Record Pattern

Syntaxe : `{ Fields }` pour 1..N `Field = variable` entre `;`
â†’ Pas obligÃ© de spÃ©cifier tous les champs du Record
â†’ En cas d'ambiguÃ¯tÃ©, qualifier le champ : `Record.Field`

ğŸ’¡ Marche aussi pour les paramÃ¨tres d'une fonction :

```fs
type Person = { Name: string; Age: int }

let displayMajority { Age = age; Name = name } =
    if age >= 18
    then printfn "%s is major" name
    else printfn "%s is minor" name

let john = { Name = "John"; Age = 25 }
displayMajority john // John is major
```

---

# Record Pattern (2)

âš ï¸ **Rappel :** il n'y a pas de pattern pour les *Records* anonymes !

```fs
type Person = { Name: string; Age: int }

let john = { Name = "John"; Age = 25 }
let { Name = name } = john  // ğŸ‘Œ val name : string = "John"

let john' = {| john with Civility = "Mister" |}
let {| Name = name' |} = john'  // ğŸ’¥
```

---

# Type Test Pattern

Syntaxe : `my-object :? sub-type` et renvoie un `bool`
â†’ â‰ƒ `my-object is sub-type` en Câ™¯

Usage : avec une hiÃ©rarchie de types

```fs
open System.Windows.Forms

let RegisterControl (control: Control) =
    match control with
    | :? Button as button -> button.Text <- "Registered."
    | :? CheckBox as checkbox -> checkbox.Text <- "Registered."
    | :? Windows -> invalidArg (nameof control) "Window cannot be registered"
    | _ -> ()
```

---

# Type Test Pattern - Bloc `try`/`with`

On rencontre frÃ©quemment ce pattern dans les blocs `try`/`with` :

```fs
try
    printfn "Difference: %i" (42 / 0)
with
| :? DivideByZeroException as x -> 
    printfn "Fail! %s" x.Message
| :? TimeoutException -> 
    printfn "Fail! Took too long"
```

---

# Type Test Pattern - Boxing

Le *Type Test Pattern* ne marche qu'avec des types rÃ©fÃ©rences.
â†’ Pour un type valeur ou inconnu, il faut le convertir en objet *(a.k.a boxing)*

```fs
let isIntKo = function :? int -> true | _ -> false
// ğŸ’¥ Error FS0008: test de type au moment de l'exÃ©cution du type 'a en int...

let isInt x =
    match box x with
    | :? int -> true
    | _ -> false
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_etudeCas.png)

# 2.

## *Match Expression*

---

# Match expression

Similaire Ã  une expression `switch` en Câ™¯ 8.0 en \+ puissant grÃ¢ce aux patterns

Syntaxe :

```fs
match test-expression with
| pattern1 [ when condition ] -> result-expression1
| pattern2 [ when condition ] -> result-expression2
| ...
```

Renvoie le rÃ©sultat de la 1Ã¨re branche dont le pattern "match" `test-expression`

â˜ **Note :** toutes les branches doivent renvoyer le mÃªme type !

---

# Match expression - ExhaustivitÃ©

Un `switch` doit toujours dÃ©finir un cas par dÃ©faut *(cf. pattern *wildcard* `_`)*
Sinon : warning Ã  la compilation, risque de ğŸ’¥ `MatchFailureException` au runtime

Pas nÃ©cessaire dans une *match expression* si les branches couvrent tous les cas
car le compilateur vÃ©rifie leur exhaustivitÃ© et les branches "mortes"

```fs
let fn x =
    match x with
    | Some true  -> "ok"
    | Some false -> "ko"
    | None       -> ""
    | _          -> "?"  // âš ï¸ Warning FS0026: Cette rÃ¨gle n'aura aucune correspondance
```

---

# Match expression - ExhaustivitÃ© (2)

â˜ **Conseil :** \+ les branches sont exhaustives, \+ le code est explicite et sÃ»r

Exemple : matcher tous les cases d'un type union permet de gÃ©rer
l'ajout d'un case par un warning Ã  la compilation :
`Warning FS0025: CritÃ¨res spÃ©ciaux incomplets dans cette expression`

- DÃ©tection d'un ajout accidentel
- Identification du code Ã  changer pour gÃ©rer le nouveau case

---

# Match expression - Guard

Syntaxe : `pattern1 when condition`
Usage : raffiner un pattern, via contraintes sur des variables

```fs
let classifyBetween low top value =
    match value with
    | x when x < low -> "Inf"  // ğŸ’¡ Alternative : `_ when value < low`
    | x when x = low -> "Low"
    | x when x = top -> "Top"
    | x when x > top -> "Sup"
    | _ -> "Between"

let test1 = 1 |> classifyBetween 1 5  // "Low"
let test2 = 6 |> classifyBetween 1 5  // "Sup"
```

ğŸ’¡ La *guard* n'est Ã©valuÃ©e que si le pattern est satisfait.

---

# Match expression - Guard et Pattern OR

Le pattern OR a une *precedence/prioritÃ©* plus Ã©levÃ©e que la *guard* :

```fs
type Parity = Even of int | Odd of int

let parityOf value =
    if value % 2 = 0 then Even value else Odd value

let hasSquare square value =
    match parityOf square, parityOf value with
    | Even x2, Even x
    | Odd  x2, Odd  x
        when x2 = x*x -> true  // ğŸ‘ˆ Porte sur les 2 patterns prÃ©cÃ©dents
    | _ -> false

let test1 = 2 |> hasSquare 4  // true
let test2 = 3 |> hasSquare 9  // true
```

---

# Match function

Syntaxe :

```fs
function
| pattern1 [ when condition ] -> result-expression1
| pattern2 [ when condition ] -> result-expression2
| ...
```

Equivalent Ã  une lambda prenant un paramÃ¨tre implicite qui est "matchÃ©" :

```fs
fun value ->
    match value with
    | pattern1 [ when condition ] -> result-expression1
    | pattern2 [ when condition ] -> result-expression2
    | ...
```

---

# Match function - IntÃ©rÃªts

1. Dans pipeline

```fs
value
|> is123
|> function
    | true  -> "ok"
    | false -> "ko"
```

2. Ã‰criture \+ succincte d'une fonction

```fs
// âš ï¸ ParamÃ¨tre implicite => peut rendre le code \+ difficile Ã  comprendre !
let is123 = function
    | 1 | 2 | 3 -> true
    | _ -> false
```

---

# Match function - Limites

âš ï¸ ParamÃ¨tre implicite => peut rendre le code \+ difficile Ã  comprendre !

Exemple : fonction dÃ©clarÃ©e avec d'autres paramÃ¨tres eux explicites
â†’ On peut se tromper sur le nombre de paramÃ¨tres et leur ordre :

```fs
let classifyBetween low high = function  // ğŸ‘ˆ 3 paramÃ¨tres : low, high + 1 implicite
    | x when x < low  -> "Inf"
    | x when x = low  -> "Low"
    | x when x = high -> "High"
    | x when x > high -> "Sup"
    | _ -> "Between"

let test1 = 1 |> classifyBetween 1 5  // "Low"
let test2 = 6 |> classifyBetween 1 5  // "Sup"
```

---

<!-- _footer: '' -->

# Fonction `fold`

Fonction associÃ©e Ã  un type union et masquant la logique de *matching*
Prend N+1 paramÃ¨tres pour un type union avec N *cases* `CaseI of 'DataI`
â†’ N fonctions `'DataI -> 'T` (1 / *case*), avec `'T` le type de retour de `fold`
â†’ En dernier, l'instance du type union Ã  matcher

```fs
type [<Measure>] C
type [<Measure>] F

type Temperature =
    | Celsius     of float<C>
    | Fahrenheint of float<F>

module Temperature =
    let fold mapCelsius mapFahrenheint temperature : 'T =
        match temperature with
        | Celsius x     -> mapCelsius x      // mapCelsius    : float<C> -> 'T
        | Fahrenheint x -> mapFahrenheint x  // mapFahrenheint: float<F> -> 'T
```

---

# Fonction `fold` : utilisation

```fs
module Temperature =
    // ...
    let [<Literal>] FactorC2F = 1.8<F/C>
    let [<Literal>] DeltaC2F = 32.0<F>

    let celsiusToFahrenheint x = (x * FactorC2F) + DeltaC2F  // float<C> -> float<F>
    let fahrenheintToCelsius x = (x - DeltaC2F) / FactorC2F  // float<F> -> float<C>

    let toggleUnit temperature =
        temperature |> fold
            (celsiusToFahrenheint >> Fahrenheint)
            (fahrenheintToCelsius >> Celsius)

let t1 = Celsius 100.0<C>
let t2 = t1 |> Temperature.toggleUnit  // Fahrenheint 212.0
```

---

# Fonction `fold` : intÃ©rÃªt

`fold` masque les dÃ©tails d'implÃ©mentation du type

Par exemple, on peut ajouter un *case* `Kelvin` et n'impacter que `fold`,
pas les fonctions qui l'appellent comme `toggleUnit` :

```fs
type [<Measure>] C
type [<Measure>] F
type [<Measure>] K  // ğŸŒŸ

type Temperature =
    | Celsius     of float<C>
    | Fahrenheint of float<F>
    | Kelvin      of float<K>  // ğŸŒŸ

// ...
```

---

# Fonction `fold` : intÃ©rÃªt (2)

```fs
// ...
module Temperature =
    let fold mapCelsius mapFahrenheint temperature : 'T =
        match temperature with
        | Celsius x     -> mapCelsius x      // mapCelsius: float<C> -> 'T
        | Fahrenheint x -> mapFahrenheint x  // mapFahrenheint: float<F> -> 'T
        | Kelvin x      -> mapCelsius (x * 1.0<C/K> + 273.15<C>)  // ğŸŒŸ

Kelvin 273.15<K>
|> Temperature.toggleUnit
|> Temperature.toggleUnit
// Celsius 0.0<C>
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_alEchelle.png)

# 3.

## *Active Patterns*

---

# Limitations du *Pattern Matching*

Nombre limitÃ© de patterns

ImpossibilitÃ© de factoriser l'action de patterns avec leur propre guard
â†’ `Pattern1 when Guard1 | Pattern2 when Guard2 -> do` ğŸ’¥
â†’ `Pattern1 when Guard1 -> do | Pattern2 when Guard2 -> do` ğŸ˜•

Patterns ne sont pas des citoyens de 1Ã¨re classe
*Ex : une fonction ne peut pas renvoyer un pattern*
â†’ Juste une sorte de sucre syntaxique

Patterns interagissent mal avec un style OOP

---

# Origine des *Active Patterns*

> ğŸ”— [*Extensible pattern matching via a lightweight language extension*](https://www.microsoft.com/en-us/research/publication/extensible-pattern-matching-via-a-lightweight-language-extension/)
> â„¹ï¸ Publication de 2007 de Don Syme, Gregory Neverov, James Margetson

IntÃ©grÃ© Ã  Fâ™¯ 2.0 (2010)

ğŸ’¡ **IdÃ©es**

- Permettre le *pattern matching* sur d'autres structures de donnÃ©es
- Faire de ces nouveaux patterns des citoyens de 1Ã¨re classe

---

# *Active Patterns* - Syntaxe

Syntaxe gÃ©nÃ©rale : `let (|Cases|) [arguments] valueToMatch = expression`

1. **Fonction** avec un nom spÃ©cial dÃ©fini dans une "banane" `(|...|)`
2. Ensemble de 1..N **cases** oÃ¹ ranger `valueToMatch`

ğŸ’¡ Sorte de fonction *factory* d'un **type union** "anonyme", dÃ©fini *inline*

---

# *Active Patterns* - Types

Il existe 4 types d'active patterns :

1. Pattern total simple
2. Pattern total multiple
3. Pattern partiel
4. Pattern paramÃ©trÃ©

ğŸ’¡ *Partiel* et *total* indique la faisabilitÃ© du Â« rangement dans le(s) case(s) Â»
Â Â Â Â Â de la valeur en entrÃ©e
Â Â Â Â Â â†’ **Partiel** : il n'existe pas toujours une case correspondante
Â Â Â Â Â â†’ **Total** : il existe forcÃ©ment une case correspondante â†’ pattern exhaustif

---

# Active pattern total simple

*A.k.a Single-case Total Pattern*

Syntaxe : `let (|Case|) [...parameters] value = Case [data]`
Usage : dÃ©construction en ligne

```fs
// Avec paramÃ¨tre => pas trÃ¨s lisible ğŸ˜•
let (|Default|) = Option.defaultValue  // 'T -> 'T option -> 'T

let (Default "unknown" name1) = Some "John"  // name1 = "John"
let (Default "unknown" name2) = None         // name2 = "unknown"

// Sans paramÃ¨tre => mieux ğŸ‘Œ
let (|ValueOrUnknown|) = Option.defaultValue "unknown"  // 'T option -> 'T

let (ValueOrUnknown name1) = Some "John"  // name1 = "John"
let (ValueOrUnknown name2) = None         // name2 = "unknown"
```

---

# Active pattern total simple (2)

Autre exemple : extraction de la forme polaire d'un nombre complexe

```fs
open System.Numerics

let (|Polar|) (x : Complex) =
    Polar (x.Magnitude, x.Phase)

let multiply (Polar (m1, p1)) (Polar (m2, p2)) =  // Complex -> Complex -> Complex
    Complex(m1 + m2, p1 + p2)
```

Sans l'active pattern, c'est un autre style mais de lisibilitÃ© Ã©quivalente :

```fs
let multiply x y =
    Complex (x.Magnitude + y.Magnitude, x.Phase + y.Phase)
```

---

<!-- _footer: '' -->

# Active pattern total multiple

*A.k.a Multiple-case Total Pattern*

Syntaxe : `let (|Case1|...|CaseN|) value = CaseI [dataI]`
â˜ Pas de paramÃ¨tre possibleâ—

```fs
// RÃ©-Ã©criture d'un exemple prÃ©cÃ©dent

// âŒ type Parity = Even of int | Odd of int
// âŒ let parityOf value = if value % 2 = 0 then Even value else Odd value

let (|Even|Odd|) x =  // int -> Choice<int, int>
    if x % 2 = 0 then Even x else Odd x

let hasSquare square value =
    // âŒ match parityOf square, parityOf value with
    match square, value with
    | Even x2, Even x | Odd x2, Odd x when x2 = x*x -> true
    | _ -> false
```

---

<!-- _footer: '' -->

# Active pattern partiel

Syntaxe : `let (|Case|_|) value = Some Case | Some data | None`
â†’ Renvoie type `'T option` si *Case* comprend des donnÃ©es, sinon `unit option`
â†’ Pattern matching est non exhaustif â†’ il faut un cas par dÃ©faut

```fs
let (|Integer|_|) (x: string) = // (x: string) -> int option
    match System.Int32.TryParse x with
    | true, i -> Some i
    | false, _ -> None

let (|Float|_|) (x: string) = // (x: string) -> float option
    match System.Double.TryParse x with
    | true, f -> Some f
    | false, _ -> None

let detectNumber = function
    | Integer i -> $"Integer {i}"   // detectNumber "10"
    | Float f -> $"Float {f}"       // detectNumber "1,1" = "Float 1,1" (en France)
    | s -> $"NaN {s}"               // detectNumber "abc" = "NaN abc"
```

---

# Active pattern partiel paramÃ©trÃ©

Syntaxe : `let (|Case|_|) ...arguments value = Some Case | Some data | None`

Exemple 1 : annÃ©e bissextile = multiple de 4 mais pas 100 sauf 400

```fs
let (|DivisibleBy|_|) factor x =  // (factor: int) -> (x: int) -> unit option
    match x % factor with
    | 0 -> Some DivisibleBy
    | _ -> None

let isLeapYear year =  // (year: int) -> bool
    match year with
    | DivisibleBy 400 -> true
    | DivisibleBy 100 -> false
    | DivisibleBy   4 -> true
    | _               -> false
```

---

# Active pattern partiel paramÃ©trÃ© (2)

Exemple 2 : Expression rÃ©guliÃ¨re

```fs
let (|Regexp|_|) pattern value =  // string -> string -> string list option
    let m = System.Text.RegularExpressions.Regex.Match(value, pattern)
    if not m.Success || m.Groups.Count < 1 then
        None
    else
        [ for g in m.Groups -> g.Value ]
        |> List.tail // drop "root" match
        |> Some
```

---

# Active pattern partiel paramÃ©trÃ© (3)

Exemple : Couleur hexadÃ©cimale

```fs
let hexToInt hex =  // string -> int // E.g. "FF" -> 255
    System.Int32.Parse(hex, System.Globalization.NumberStyles.HexNumber)

let (|HexaColor|_|) = function  // string -> (int * int * int) option
    // ğŸ’¡ Utilise l'active pattern prÃ©cÃ©dent
    // ğŸ’¡ La Regex recherche 3 groupes de 2 chars Ã©tant un chiffre ou une lettre A..F
    | Regexp "#([0-9A-F]{2})([0-9A-F]{2})([0-9A-F]{2})" [ r; g; b ] ->
        Some <| HexaColor ((hexToInt r), (hexToInt g), (hexToInt b))
    | _ -> None

match "#0099FF" with
| HexaColor (r, g, b) -> $"RGB: {r}, {g}, {b}"
| otherwise -> $"'{otherwise}' is not a hex-color"
// "RGB: 0, 153, 255"
```

---

# RÃ©capâ€™ des types dâ€™active patterns

| Type           | Syntaxe                    | Signature                            |
|----------------|----------------------------|--------------------------------------|
| Total multiple | `let (ï½œCase1ï½œâ€¦ï½œCaseNï½œ) x`  | `'T -> Choice<'U1, â€¦, 'Un>`          |
| Total simple   | `let (ï½œCaseï½œ) x`           | `'T -> 'U`                           |
| Partiel simple | `let (ï½œCaseï½œ_ï½œ) x`         | `'T -> 'U option`                    |
| ... paramÃ©trÃ©  | `let (ï½œCaseï½œ_ï½œ) p1 â€¦ pN x` | `'P1 -> â€¦ -> 'Pn -> 'T -> 'U option` |

---

# Comprendre un active pattern

> Comprendre comment utiliser un active pattern...
> peut s'avÃ©rer un vrai **jonglage intellectuel** !

ğŸ‘‰ Explications en utilisant les exemples prÃ©cÃ©dents

---

# Comprendre un active pattern total

- Active pattern total â‰ƒ Fonction *factory* d'un type union "anonyme"
- Usage : idem pattern matching d'un type union normal

```fs
// Single-case
let (|Cartesian|) (x: Complex) = Cartesian (x.Real, x.Imaginary)

let Cartesian (r, i) = Complex (1.0, 2.0)  // r = 1.0, i = 2.0

// Double-case
let (|Even|Odd|) x = if x % 2 = 0 then Even else Odd

let parityOf = function  // int -> string
    | Even -> "Pair"
    | Odd  -> "Impair"
```

---

# Comprendre un active pattern partiel

â˜ Bien distinguer les Ã©ventuels paramÃ¨tres des Ã©ventuelles donnÃ©es

Examiner la signature de l'active pattern : `[...params ->] value -> 'U option`
â€¢ Les 1..N-1 paramÃ¨tres = paramÃ¨tres de l'active pattern
â€¢ Son retour : `'U option` â†’ donnÃ©es de type `'U` ; si `'U` = `unit` â†’ pas de donnÃ©e

Ã€ l'usage : `match value with Case [params] [data]`
â€¢ `Case params` â‰ƒ **application partielle**, donnant active pattern sans paramÃ¨tre
â€¢ `CaseWithParams data` â‰ƒ dÃ©construction d'un case de type union

---

# Comprendre un active pattern partiel (2)

1. `let (|Integer|_|) (s: string) : int option`
   â†’ Usage `match s with Integer i`, avec `i: int` donnÃ©e en sortie

2. `let (|DivisibleBy|_|) (factor: int) (x: int) : unit option`
   â†’ Usage `match year with DivisibleBy 400`, avec `400` le paramÃ¨tre `factor`

3. `let (|Regexp|_|) (pattern: string) (value: string) : string list option`
   â†’ Usage `match s with Regexp "#([0-9...)" [ r; g; b ]`
   â†’ Avec `"#([0-9...)"` le paramÃ¨tre `pattern`
   â†’ Et `[ r; g; b ]` la liste en sortie dÃ©composÃ©e en 3 chaÃ®nes

---

# Exercice : fizz buzz avec active pattern

RÃ©-Ã©crire ce fizz buzz en utilisant un active pattern `DivisibleBy`

```fs
let isDivisibleBy factor number =
    number % factor = 0

let fizzBuzz = function
    | i when i |> isDivisibleBy 15 -> "FizzBuzz"
    | i when i |> isDivisibleBy  3 -> "Fizz"
    | i when i |> isDivisibleBy  5 -> "Buzz"
    | other -> string other

[1..15] |> List.map fizzBuzz
// ["1"; "2"; "Fizz"; "4"; "Buzz"; "Fizz";
//  "7"; "8"; "Fizz"; "Buzz"; "11";
//  "Fizz"; "13"; "14"; "FizzBuzz"]
```

---

<!-- _footer: '' -->

# Fizz buzz avec active pattern : solution

```fs
let isDivisibleBy factor number =
    number % factor = 0

let (|DivisibleBy|_|) factor number =
    if number |> isDivisibleBy factor
    then Some DivisibleBy // ğŸ’¡ Ou `Some ()`
    else None

let fizzBuzz = function
    | DivisibleBy 3 &
      DivisibleBy 5 -> "FizzBuzz"  // ğŸ’¡ Ou `DivisibleBy 15`
    | DivisibleBy 3 -> "Fizz"
    | DivisibleBy 5 -> "Buzz"
    | other -> string other

[1..15] |> List.map fizzBuzz
// ["1"; "2"; "Fizz"; "4"; "Buzz"; "Fizz";
//  "7"; "8"; "Fizz"; "Buzz"; "11";
//  "Fizz"; "13"; "14"; "FizzBuzz"]
```

---

<!-- _footer: '' -->

# Fizz buzz avec active pattern : alternative

```fs
let isDivisibleBy factor number =
    number % factor = 0

let boolToOption b =
    if b then Some () else None

let (|Fizz|_|) number = number |> isDivisibleBy 3 |> boolToOption
let (|Buzz|_|) number = number |> isDivisibleBy 5 |> boolToOption

let fizzBuzz = function
    | Fizz & Buzz -> "FizzBuzz"
    | Fizz -> "Fizz"
    | Buzz -> "Buzz"
    | other -> string other
```

â†’ Les 2 solutions se valent. C'est une question de style / de goÃ»t personnel.

---

# Cas d'utilisation des actives patterns

1. Factoriser une guard *(cf. exercice prÃ©cÃ©dent du fizz buzz)*
2. Wrapper une mÃ©thode de la BCL *(cf. `(|Regexp|_|)` et ci-dessous)*
3. AmÃ©liorer l'expressivitÃ©, aider Ã  comprendre la logique *(cf. aprÃ¨s)*

```fs
let (|ParsedInt|UnparsableInt|) (input: string) =
    match input with
    | _ when fst (System.Int32.TryParse input) -> ParsedInt(int input)
    | _ -> UnparsableInt

let addOneOrZero = function
    | ParsedInt i -> i + 1
    | UnparsableInt -> 0

let v1 = addOneOrZero "1"  // 2
let v2 = addOneOrZero "a"  // 0
```

---

# ExpressivitÃ© grÃ¢ce aux actives patterns

```fs
type Movie = { Title: string; Director: string; Year: int; Studio: string }

module Movie =
    let private boolToOption b =
        if b then Some () else None

    let (|Director|_|) director movie =
        movie.Director = director |> boolToOption

    let (|Studio|_|) studio movie =
        movie.Studio = studio |> boolToOption

    let private matchYear comparator year movie =
        (comparator movie.Year year) |> boolToOption

    let (|After|_|) = matchYear (>)
    let (|Before|_|) = matchYear (<)
    let (|In|_|) = matchYear (=)
```

---

# ExpressivitÃ© grÃ¢ce aux actives patterns (2)

```fs
open Movie

let ``Is anime rated 10/10`` = function
    | ((After 2001 & Before 2007) | In 2014) & Studio "Bones"
    | Director "Hayao Miyazaki" -> true
    | _ -> false
```

---

<!-- _footer: '' -->

# Active pattern : citoyen de 1Ã¨re classe

Un active pattern â‰ƒ fonction avec des mÃ©tadonnÃ©es
â†’ Citoyen de 1Ã¨re classe :

```fs
// 1. Renvoyer un active pattern depuis une fonction
let (|Hayao_Miyazaki|_|) movie =
    (|Director|_|) "Hayao Miyazaki" movie

// 2. Prendre un active pattern en paramÃ¨tre -- Un peu tricky 
let firstItems (|Ok|_|) list =
    let rec loop values = function
        | Ok (item, rest) -> loop (item :: values) rest
        | _ -> List.rev values
    loop [] list

let (|Even|_|) = function
    | item :: rest when (item % 2) = 0 -> Some (item, rest) | _ -> None

let test = [0; 2; 4; 5; 6] |> firstItems (|Even|_|)  // [0; 2; 4]
```

---

<!-- _class: green chapter invert -->

![bg-right h:300](../themes/d-edge/pictos/SOAT_pictos_diplome.png)

# 4.

## LeÂ Â Â  RÃ©capâ€™

---

# RÃ©capâ€™ - Pattern matching

- Brique fondamentale de Fâ™¯
- Combine "comparaison de structure de donnÃ©es" et "dÃ©construction"
- S'utilisent presque partout :
  - `match expression` et bloc `function`
  - bloc `try/with`
  - `let binding`, y.c. paramÃ¨tre de fonction
- Peut s'abstraire en fonction `fold` associÃ©e Ã  un type union

---

# RÃ©capâ€™ - Patterns

| Pattern                            | Exemple                           |
|------------------------------------|-----------------------------------|
| Constant â€¢ Identifier â€¢ Wilcard    | `1`, `Color.Red` â€¢ `Some 1` â€¢ `_` |
| *Collection* : Cons â€¢ List â€¢ Array | `head :: tail` â€¢ `[1; 2]`         |
| *Product type* : Record â€¢ Tuple    | `{ A = a }` â€¢ `a, b`              |
| Type Test                          | `:? Subtype`                      |
| *Logique* : OR, AND                | `1 \| 2`, `P1 & P2`               |
| Variables â€¢ Alias                  | `head :: _` â€¢ `(0, 0) as origin`  |

\+ Les guards `when` dans les match expressions

---

# RÃ©capâ€™ - Active Patterns

- Extension du pattern matching
- BasÃ©s sur fonction + metadata â†’ Citoyens de 1Ã¨re classe
- 4 types : total simple/multiple, partiel (simple), paramÃ©trÃ©
- Un peu tricky Ã  comprendre mais on s'habitue vite
- S'utilisent pour :
  - Ajouter de la sÃ©mantique sans recourir aux types union
  - Simplifier / factoriser des guards
  - Wrapper des mÃ©thodes de la BCL
  - Extraire un ensemble de donnÃ©es d'un objet
  - ...

---

# ComplÃ©ments

ğŸ“œ Match expressions
https://fsharpforfunandprofit.com/posts/match-expression/

ğŸ“œ Domain modelling et pattern matching
https://fsharpforfunandprofit.com/posts/roman-numerals/

ğŸ“œ Recursive types and folds *(6 articles)*
https://fsharpforfunandprofit.com/series/recursive-types-and-folds/

ğŸ“¹ A Deep Dive into Active Patterns
https://www.youtube.com/watch?v=Q5KO-UDx5eA
https://github.com/pblasucci/DeepDiveAP

---

# Exercices

Les exercices suivants sur https://exercism.org/tracks/fsharp 
peuvent se rÃ©soudre avec des active patterns :

- Collatz Conjecture *(easy)*
- Darts *(easy)*
- Queen Attack *(medium)*
- Robot Name *(medium)*

---

<!-- _class: end invert lead-->

# Merci ğŸ™
